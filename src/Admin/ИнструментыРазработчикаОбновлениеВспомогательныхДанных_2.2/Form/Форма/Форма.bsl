настройка.
	ДанныеПодсистемыБазоваяФункциональность = Истина;
	ДанныеПодсистемыПользователи = Истина;
	ДанныеПодсистемыУправлениеДоступом = Истина;
	ДанныеПодсистемыВариантыОтчетов = Истина;
	
	// Сложная настройка.
	
	// СтандартныеПодсистемы БазоваяФункциональность
	ПараметрыСлужебныхСобытий = Истина;
	ИдентификаторыОбъектовМетаданных = Истина;
	КэшПрограммныхИнтерфейсов = Истина;
	
	// СтандартныеПодсистемы Пользователи
	ПараметрыРаботыПользователей = Истина;
	СоставыГруппПользователей = Истина;
	
	// СтандартныеПодсистемы УправлениеДоступом
	ПраваРолей = Истина;
	ЗависимостиПрав = Истина;
	СвойстваВидовДоступа = Истина;
	ОписаниеПоставляемыхПрофилейГруппДоступа = Истина;
	ОписаниеВозможныхПравДляНастройкиПравОбъектов = Истина;
	ПоставляемыеПрофилиГруппДоступа = Истина;
	РолиПользователейИнформационнойБазы = Истина;
	ТаблицыГруппДоступа = Истина;
	ЗначенияГруппДоступа = Истина;
	НаследованиеНастроекПравОбъектов = Истина;
	НастройкиПравОбъектов = Истина;
	ГруппыЗначенийДоступа = Истина;
	НаборыЗначенийДоступа = Истина;
	
	// СтандартныеПодсистемы ВариантыОтчетов
	ПараметрыВариантовОтчетов = Истина;
	СправочникВариантыОтчетов = Истина;
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		
		Если ОбщегоНазначенияПовтИсп.СеансЗапущенБезРазделителей() Тогда
			ОбновитьНеразделенныеДанные = Истина;
		КонецЕсли;
	Иначе
		ОбновитьНеразделенныеДанные = Истина;
		ОбновитьРазделенныеДанные   = Истина;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначенияПовтИсп.РазделениеВключено()
	 ИЛИ НЕ ОбновитьНеразделенныеДанные Тогда
		
		Элементы.ОбластьДанных.Видимость                = Ложь;
		Элементы.ВойтиВУказаннуюОбластьДанных.Видимость = Ложь;
		Элементы.ТекущаяОбластьДанных.Видимость         = Ложь;
		Элементы.ВыйтиИзТекущейОбластиДанных.Видимость  = Ложь;
	КонецЕсли;
	
	ОбновитьТекущуюОбластьДанных();
	
	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		Элементы.ПодсистемаУправлениеДоступом.Видимость = Ложь;
		Элементы.ДанныеПодсистемыУправлениеДоступом.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		Элементы.ПодсистемаВариантыОтчетов.Видимость = Ложь;
		Элементы.ДанныеПодсистемыВариантыОтчетов.Видимость = Ложь;
	КонецЕсли;
	
	ОбновитьВидимостьПоРежимуНастройки();
	
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбновитьВидимостьПоРежимуНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимНастройкиПриИзменении(Элемент)
	
	ОбновитьВидимостьПоРежимуНастройки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВойтиВУказаннуюОбластьДанных(Команда)
	
	ОбновитьТекущуюОбластьДанных();
	
	Если ОбластьДанных = ТекущаяОбластьДанных Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Вход в указанную область данных уже выполнен.'"));
		Возврат;
	КонецЕсли;
	
	Если ТекущаяОбластьДанных <> Неопределено Тогда
		Отказ = Ложь;
		ВыйтиИзОбластиДанных(Отказ);
		Если Отказ Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не удалось выйти из текущей области данных.'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ВойтиВОбластьДанных();
	
	ОбновитьТекущуюОбластьДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыйтиИзТекущейОбластиДанных(Команда)
	
	ОбновитьТекущуюОбластьДанных();
	
	Если ТекущаяОбластьДанных = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Вход в область данных еще не выполнен.'"));
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ВыйтиИзОбластиДанных(Отказ);
	Если Отказ Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не удалось выйти из текущей области данных.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбновитьТекущуюОбластьДанных() Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Область данных не изменилась.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбновление(Команда)
	
	ДанныеОтмечены = Ложь;
	
	Если НЕ ЗначениеЗаполнено(РежимНастройки) Тогда
		ДанныеОтмечены = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы БазоваяФункциональность
	Если РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыБазоваяФункциональность
	 ИЛИ РежимНастройки = "СложнаяНастройка"
	     И (    ПараметрыСлужебныхСобытий        И Элементы.ПараметрыСлужебныхСобытий.Доступность
	        ИЛИ ИдентификаторыОбъектовМетаданных И Элементы.ИдентификаторыОбъектовМетаданных.Доступность
	        ИЛИ КэшПрограммныхИнтерфейсов        И Элементы.КэшПрограммныхИнтерфейсов.Доступность) Тогда
		
		ДанныеОтмечены = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы Пользователи
	Если РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыПользователи
	 ИЛИ РежимНастройки = "СложнаяНастройка"
	     И (    ПараметрыРаботыПользователей И Элементы.ПараметрыРаботыПользователей.Доступность
	        ИЛИ СоставыГруппПользователей    И Элементы.СоставыГруппПользователей.Доступность) Тогда
		
		ДанныеОтмечены = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы УправлениеДоступом
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		Если РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка"
		   И (    ПраваРолей                                    И Элементы.ПраваРолей.Доступность
		      ИЛИ ЗависимостиПрав                               И Элементы.ЗависимостиПрав.Доступность
		      ИЛИ СвойстваВидовДоступа                          И Элементы.СвойстваВидовДоступа.Доступность
		      ИЛИ ОписаниеПоставляемыхПрофилейГруппДоступа      И Элементы.ОписаниеПоставляемыхПрофилейГруппДоступа.Доступность
		      ИЛИ ОписаниеВозможныхПравДляНастройкиПравОбъектов И Элементы.ОписаниеВозможныхПравДляНастройкиПравОбъектов.Доступность
		      
		      ИЛИ ПоставляемыеПрофилиГруппДоступа     И Элементы.ПоставляемыеПрофилиГруппДоступа.Доступность
		      ИЛИ РолиПользователейИнформационнойБазы И Элементы.РолиПользователейИнформационнойБазы.Доступность
		      ИЛИ ТаблицыГруппДоступа                 И Элементы.ТаблицыГруппДоступа.Доступность
		      ИЛИ ЗначенияГруппДоступа                И Элементы.ЗначенияГруппДоступа.Доступность
		      ИЛИ НаследованиеНастроекПравОбъектов    И Элементы.НаследованиеНастроекПравОбъектов.Доступность
		      ИЛИ НастройкиПравОбъектов               И Элементы.НастройкиПравОбъектов.Доступность
		      ИЛИ ГруппыЗначенийДоступа               И Элементы.ГруппыЗначенийДоступа.Доступность
		      ИЛИ НаборыЗначенийДоступа               И Элементы.НаборыЗначенийДоступа.Доступность) Тогда
			
			ДанныеОтмечены = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы ВариантыОтчетов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		Если РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыВариантыОтчетов
		 ИЛИ РежимНастройки = "СложнаяНастройка"
		   И (    ПараметрыВариантовОтчетов И Элементы.ПараметрыВариантовОтчетов.Доступность
		      ИЛИ СправочникВариантыОтчетов И Элементы.СправочникВариантыОтчетов.Доступность) Тогда
		
			ДанныеОтмечены = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ДанныеОтмечены Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Отметьте данные, которые нужно обновить.'"));
		Возврат;
	КонецЕсли;
	
	// Установка обычного цвета всех пунктов.
	ВыделитьИзменения(
		"ДанныеПодсистемыБазоваяФункциональность,
		|ПараметрыСлужебныхСобытий,
		|ИдентификаторыОбъектовМетаданных,
		|КэшПрограммныхИнтерфейсов,
		|
		|ДанныеПодсистемыПользователи,
		|ПараметрыРаботыПользователей,
		|СоставыГруппПользователей,
		|
		|ДанныеПодсистемыВариантыОтчетов,
		|ПараметрыВариантовОтчетов,
		|СправочникВариантыОтчетов,
		|
		|ДанныеПодсистемыУправлениеДоступом,
		|ПраваРолей,
		|ЗависимостиПрав,
		|СвойстваВидовДоступа,
		|ОписаниеПоставляемыхПрофилейГруппДоступа,
		|ОписаниеВозможныхПравДляНастройкиПравОбъектов,
		|ПоставляемыеПрофилиГруппДоступа,
		|РолиПользователейИнформационнойБазы,
		|ТаблицыГруппДоступа,
		|ЗначенияГруппДоступа,
		|НаследованиеНастроекПравОбъектов,
		|НастройкиПравОбъектов,
		|ГруппыЗначенийДоступа,
		|НаборыЗначенийДоступа",
		Ложь);
	
	ЕстьИзменения = Ложь;
	ВыполнитьОбновлениеНаСервере(ЕстьИзменения);
	
	Если ЕстьИзменения = Неопределено Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'После открытия обработки изменилась текущая область данных.
			           |Если нужно проверьте настройки и повторите команду.'"));
		Возврат;
	КонецЕсли;
	
	Если ОбновитьНеразделенныеДанные И ОбновитьРазделенныеДанные Тогда
		
		Если ЕстьИзменения Тогда
			Текст = НСтр("ru = 'Обновление выполнено успешно.'");
		Иначе
			Текст = НСтр("ru = 'Обновление не требуется.'");
		КонецЕсли;
		
	ИначеЕсли ОбновитьНеразделенныеДанные Тогда
		
		Если ЕстьИзменения Тогда
			Текст = НСтр("ru = 'Обновление неразделенных данных выполнено успешно.'");
		Иначе
			Текст = НСтр("ru = 'Обновление неразделенных данных не требуется.'");
		КонецЕсли;
	Иначе
		Если ЕстьИзменения Тогда
			Текст = НСтр("ru = 'Обновление разделенных данных выполнено успешно.'");
		Иначе
			Текст = НСтр("ru = 'Обновление разделенных данных не требуется.'");
		КонецЕсли;
	КонецЕсли;
	
	ПоказатьПредупреждение(, Текст);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьОбновлениеНаСервере(ЕстьИзменения)
	
	Если ОбновитьТекущуюОбластьДанных() Тогда
		ЕстьИзменения = Неопределено;
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		ВыполнитьОбновлениеНеразделенныхДанных(ЕстьИзменения);
		ВыполнитьОбновлениеРазделенныхДанных(ЕстьИзменения);
	Иначе
		Если ОбновитьНеразделенныеДанные Тогда
			ВернутьсяВТекущуюОбластьДанных = Ложь;
			
			Если ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
				// Временный выход из области данных.
				ВернутьсяВТекущуюОбластьДанных = Истина;
				ОбщегоНазначения.УстановитьРазделениеСеанса(Ложь);
				ОбновитьПовторноИспользуемыеЗначения();
			КонецЕсли;
			
			Попытка
				// Обновление общих данных.
				ВыполнитьОбновлениеНеразделенныхДанных(ЕстьИзменения);
			Исключение
				Если ВернутьсяВТекущуюОбластьДанных Тогда
					ОбщегоНазначения.УстановитьРазделениеСеанса(Истина, ТекущаяОбластьДанных);
				КонецЕсли;
				ОбновитьПовторноИспользуемыеЗначения();
				ВызватьИсключение;
			КонецПопытки;
			
			Если ВернутьсяВТекущуюОбластьДанных Тогда
				ОбщегоНазначения.УстановитьРазделениеСеанса(Истина, ТекущаяОбластьДанных);
				ОбновитьПовторноИспользуемыеЗначения();
			КонецЕсли;
		КонецЕсли;
		
		Если ОбновитьРазделенныеДанные Тогда
			// Обновление данных области данных.
			ВыполнитьОбновлениеРазделенныхДанных(ЕстьИзменения);
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьТекущуюОбластьДанных();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбновлениеНеразделенныхДанных(ЕстьОбщиеИзменения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// СтандартныеПодсистемы БазоваяФункциональность
	Если РежимНастройки = ""
	 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыБазоваяФункциональность
	 ИЛИ РежимНастройки = "СложнаяНастройка" И ПараметрыСлужебныхСобытий Тогда
		
		ЕстьИзменения = Ложь;
		Константы.ПараметрыСлужебныхСобытий.СоздатьМенеджерЗначения().Обновить(ЕстьИзменения);
		
		Если ЕстьИзменения Тогда
			ЕстьОбщиеИзменения = Истина;
			ВыделитьИзменения("ДанныеПодсистемыБазоваяФункциональность, ПараметрыСлужебныхСобытий");
		КонецЕсли;
	КонецЕсли;
	
	Если РежимНастройки = ""
	 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыБазоваяФункциональность
	 ИЛИ РежимНастройки = "СложнаяНастройка" И ИдентификаторыОбъектовМетаданных Тогда
		
		ЕстьИзменения = Ложь;
		Справочники.ИдентификаторыОбъектовМетаданных.ОбновитьДанныеСправочника(ЕстьИзменения);
		
		Если ЕстьИзменения Тогда
			ЕстьОбщиеИзменения = Истина;
			ВыделитьИзменения("ДанныеПодсистемыБазоваяФункциональность, ИдентификаторыОбъектовМетаданных");
		КонецЕсли;
	КонецЕсли;
	
	Если РежимНастройки = ""
	 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыБазоваяФункциональность
	 ИЛИ РежимНастройки = "СложнаяНастройка" И КэшПрограммныхИнтерфейсов Тогда
		
		ЕстьИзменения = Ложь;
		ОчиститьКэшПрограммныхИнтерфейсов(ЕстьИзменения);
		
		Если ЕстьИзменения Тогда
			ЕстьОбщиеИзменения = Истина;
			ВыделитьИзменения("ДанныеПодсистемыБазоваяФункциональность, КэшПрограммныхИнтерфейсов");
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы Пользователи
	Если РежимНастройки = ""
	 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыПользователи
	 ИЛИ РежимНастройки = "СложнаяНастройка" И ПараметрыРаботыПользователей Тогда
		
		ЕстьИзменения = Ложь;
		Константы.ПараметрыРаботыПользователей.СоздатьМенеджерЗначения().ОбновитьОбщиеПараметры(ЕстьИзменения);
		
		Если ЕстьИзменения Тогда
			ЕстьОбщиеИзменения = Истина;
			ВыделитьИзменения("ДанныеПодсистемыПользователи, ПараметрыРаботыПользователей");
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
	
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ПраваРолей Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["ПраваРолей"].ОбновитьДанныеРегистра(ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, ПраваРолей");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ЗависимостиПрав Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["ЗависимостиПравДоступа"].ОбновитьДанныеРегистра(ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, ЗависимостиПрав");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И СвойстваВидовДоступа Тогда
			
			ЕстьИзменения = Ложь;
			Константы["ПараметрыОграниченияДоступа"].СоздатьМенеджерЗначения(
				).ОбновитьОписаниеСвойствВидовДоступа(ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, СвойстваВидовДоступа");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ОписаниеПоставляемыхПрофилейГруппДоступа Тогда
			
			ЕстьИзменения = Ложь;
			Справочники["ПрофилиГруппДоступа"].ОбновитьСоставПредопределенныхПрофилей(ЕстьИзменения);
			Справочники["ПрофилиГруппДоступа"].ОбновитьОписаниеПоставляемыхПрофилей(ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, ОписаниеПоставляемыхПрофилейГруппДоступа");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ОписаниеВозможныхПравДляНастройкиПравОбъектов Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["НастройкиПравОбъектов"].ОбновитьВозможныеПраваДляНастройкиПравОбъектов(
				ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, ОписаниеВозможныхПравДляНастройкиПравОбъектов");
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
	// СтандартныеПодсистемы ВариантыОтчетов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыВариантыОтчетов
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ПараметрыВариантовОтчетов Тогда
			
			ПараметрыОбновления = Новый Структура;
			ПараметрыОбновления.Вставить("МонопольныйРежим", Истина);
			ПараметрыОбновления.Вставить("РазделенныеОбработчики",
				ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления());
			
			ЕстьИзменения = Ложь;
			МодульВариантыОтчетов.ОбновитьОбщиеДанные(ПараметрыОбновления);
			МодульВариантыОтчетов.ОтложенноеПолноеОбновлениеОбщихДанных();
			ЕстьИзменения = Истина; // Не поддерживает анализ наличия фактических изменений.
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыВариантыОтчетов, ПараметрыВариантовОтчетов");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбновлениеРазделенныхДанных(ЕстьОбщиеИзменения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	// СтандартныеПодсистемы Пользователи
	Если РежимНастройки = ""
	 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыПользователи
	 ИЛИ РежимНастройки = "СложнаяНастройка" И СоставыГруппПользователей Тогда
		
		ЕстьИзменения = Ложь;
		РегистрыСведений["СоставыГруппПользователей"].ОбновитьДанныеРегистра(ЕстьИзменения);
		
		Если ЕстьИзменения Тогда
			ЕстьОбщиеИзменения = Истина;
			ВыделитьИзменения("ДанныеПодсистемыПользователи, СоставыГруппПользователей");
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступомСлужебный = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступомСлужебный");
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ПоставляемыеПрофилиГруппДоступа Тогда
			
			ЕстьИзменения = Ложь;
			Справочники["ГруппыДоступа"].ПометитьНаУдалениеГруппыДоступаПомеченныхПрофилей(ЕстьИзменения);
			Справочники["ПрофилиГруппДоступа"].ОбновитьПоставляемыеПрофили(ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, ПоставляемыеПрофилиГруппДоступа");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И РолиПользователейИнформационнойБазы Тогда
			
			ЕстьИзменения = Ложь;
			МодульУправлениеДоступомСлужебный.ОбновитьРолиПользователей(, , ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, РолиПользователейИнформационнойБазы");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ТаблицыГруппДоступа Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["ТаблицыГруппДоступа"].ОбновитьДанныеРегистра( , , ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, ТаблицыГруппДоступа");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ЗначенияГруппДоступа Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["ЗначенияГруппДоступа"].ОбновитьДанныеРегистра( , ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, ЗначенияГруппДоступа");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И НаследованиеНастроекПравОбъектов Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["НаследованиеНастроекПравОбъектов"].ОбновитьДанныеРегистра(, ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, НаследованиеНастроекПравОбъектов");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И НастройкиПравОбъектов Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["НастройкиПравОбъектов"].ОбновитьВспомогательныеДанныеРегистра(ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, НастройкиПравОбъектов");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И ГруппыЗначенийДоступа Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["ГруппыЗначенийДоступа"].ОбновитьДанныеРегистра(ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, ГруппыЗначенийДоступа");
			КонецЕсли;
		КонецЕсли;
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыУправлениеДоступом
		 ИЛИ РежимНастройки = "СложнаяНастройка" И НаборыЗначенийДоступа Тогда
			
			ЕстьИзменения = Ложь;
			РегистрыСведений["НаборыЗначенийДоступа"].ОбновитьДанныеРегистра(ЕстьИзменения);
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыУправлениеДоступом, НаборыЗначенийДоступа");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы ВариантыОтчетов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
		
		Если РежимНастройки = ""
		 ИЛИ РежимНастройки = "ПростаяНастройка" И ДанныеПодсистемыВариантыОтчетов
		 ИЛИ РежимНастройки = "СложнаяНастройка" И СправочникВариантыОтчетов Тогда
			
			ЕстьИзменения = Ложь;
			//МодульВариантыОтчетов.ОбновитьРазделенныеДанные();
			МодульВариантыОтчетов.ОперативноеОбновлениеРазделенныхДанныхКонфигурации();
			МодульВариантыОтчетов.ОтложенноеОбновлениеОбщихДанныхКонфигурацииПолное();
			ЕстьИзменения = Истина; // Не поддерживает анализ наличия фактических изменений.
			
			Если ЕстьИзменения Тогда
				ЕстьОбщиеИзменения = Истина;
				ВыделитьИзменения("ДанныеПодсистемыВариантыОтчетов, СправочникВариантыОтчетов");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭлементов(Форма)
	
	Элементы = Форма.Элементы;
	
	// СтандартныеПодсистемы БазоваяФункциональность
	Элементы.ПараметрыСлужебныхСобытий.Доступность                     = Форма.ОбновитьНеразделенныеДанные;
	Элементы.ИдентификаторыОбъектовМетаданных.Доступность              = Форма.ОбновитьНеразделенныеДанные;
	Элементы.КэшПрограммныхИнтерфейсов.Доступность                     = Форма.ОбновитьНеразделенныеДанные;
	
	// СтандартныеПодсистемы Пользователи
	Элементы.ПараметрыРаботыПользователей.Доступность                  = Форма.ОбновитьНеразделенныеДанные;
	Элементы.СоставыГруппПользователей.Доступность                     = Форма.ОбновитьРазделенныеДанные;
	
	// СтандартныеПодсистемы УправлениеДоступом
	Элементы.ПраваРолей.Доступность                                    = Форма.ОбновитьНеразделенныеДанные;
	Элементы.ЗависимостиПрав.Доступность                               = Форма.ОбновитьНеразделенныеДанные;
	Элементы.СвойстваВидовДоступа.Доступность                          = Форма.ОбновитьНеразделенныеДанные;
	Элементы.ОписаниеПоставляемыхПрофилейГруппДоступа.Доступность      = Форма.ОбновитьНеразделенныеДанные;
	Элементы.ОписаниеВозможныхПравДляНастройкиПравОбъектов.Доступность = Форма.ОбновитьНеразделенныеДанные;
	Элементы.ПоставляемыеПрофилиГруппДоступа.Доступность               = Форма.ОбновитьРазделенныеДанные;
	Элементы.РолиПользователейИнформационнойБазы.Доступность           = Форма.ОбновитьРазделенныеДанные;
	Элементы.ТаблицыГруппДоступа.Доступность                           = Форма.ОбновитьРазделенныеДанные;
	Элементы.ЗначенияГруппДоступа.Доступность                          = Форма.ОбновитьРазделенныеДанные;
	Элементы.НаследованиеНастроекПравОбъектов.Доступность              = Форма.ОбновитьРазделенныеДанные;
	Элементы.НастройкиПравОбъектов.Доступность                         = Форма.ОбновитьРазделенныеДанные;
	Элементы.ГруппыЗначенийДоступа.Доступность                         = Форма.ОбновитьРазделенныеДанные;
	Элементы.НаборыЗначенийДоступа.Доступность                         = Форма.ОбновитьРазделенныеДанные;
	
	// СтандартныеПодсистемы ВариантыОтчетов
	Элементы.ПараметрыВариантовОтчетов.Доступность                     = Форма.ОбновитьНеразделенныеДанные;
	Элементы.СправочникВариантыОтчетов.Доступность                     = Форма.ОбновитьРазделенныеДанные;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьПоРежимуНастройки()
	
	ОбновитьТекущуюОбластьДанных();
	
	Если РежимНастройки = "ПростаяНастройка" Тогда
		Элементы.ПростаяНастройка.Видимость = Истина;
		Элементы.СложнаяНастройка.Видимость = Ложь;
		
	ИначеЕсли РежимНастройки = "СложнаяНастройка" Тогда
		Элементы.ПростаяНастройка.Видимость = Ложь;
		Элементы.СложнаяНастройка.Видимость = Истина;
		
	Иначе // Без настройки
		Элементы.ПростаяНастройка.Видимость = Ложь;
		Элементы.СложнаяНастройка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыделитьИзменения(ИменаЭлементов, ЕстьИзменения = Истина)
	
	Если ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2 Тогда
		ЦветИзменения = Новый Цвет(0, 128, 0); // Зеленый Web.
	Иначе
		ЦветИзменения = Новый Цвет(0, 0, 255); // Синий Web.
	КонецЕсли;
	ЦветОбычный   = Элементы.ОбластьДанных.ЦветТекстаЗаголовка; // Авто.
	
	ОписаниеЭлементов = Новый Структура(ИменаЭлементов);
	Для каждого ОписаниеЭлемента Из ОписаниеЭлементов Цикл
		Элементы[ОписаниеЭлемента.Ключ].ЦветТекстаЗаголовка = ?(ЕстьИзменения, ЦветИзменения, ЦветОбычный);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьТекущуюОбластьДанных()
	
	Если НЕ ОбщегоНазначенияПовтИсп.РазделениеВключено()
	 ИЛИ НЕ ОбновитьНеразделенныеДанные Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	ЗначениеРазделителяСеанса = Неопределено;
	
	Если ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		ЗначениеРазделителяСеанса = ОбщегоНазначения.ЗначениеРазделителяСеанса();
	КонецЕсли;
	
	ТекущаяОбластьДанныхИзменена = ТекущаяОбластьДанных <> ЗначениеРазделителяСеанса;
	
	Если ТекущаяОбластьДанныхИзменена Тогда
		ТекущаяОбластьДанных = ЗначениеРазделителяСеанса;
	КонецЕсли;
	
	Если ТекущаяОбластьДанных = Неопределено Тогда
		ОбновитьРазделенныеДанные = Ложь;
	Иначе
		ОбновитьРазделенныеДанные = Истина;
	КонецЕсли;
	
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
	
	Возврат ТекущаяОбластьДанныхИзменена;
	
КонецФункции

&НаСервере
Процедура ОчиститьКэшПрограммныхИнтерфейсов(ЕстьИзменения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЗначениеИстина
	|ИЗ
	|	РегистрСведений.КэшПрограммныхИнтерфейсов КАК КэшПрограммныхИнтерфейсов";
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьИзменения = Истина;
	
	НаборЗаписей = РегистрыСведений.КэшПрограммныхИнтерфейсов.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Скопировано из ОбщаяФормы.ВходВОбластьДанных.

&НаКлиенте
Процедура ВойтиВОбластьДанных()
	
	Если НЕ УказаннаяОбластьДанныхЗаполнена(ОбластьДанных) Тогда
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ВойтиВОбластьДанныхЗавершение", ЭтотОбъект),
			НСтр("ru = 'Выбранная область данных не заполнена, продолжить вход?'"),
			РежимДиалогаВопрос.ДаНет,
			,
			КодВозвратаДиалога.Нет);
	Иначе
		ВойтиВОбластьДанныхЗавершение(Неопределено, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВойтиВОбластьДанныхЗавершение(Ответ, Неопределен) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ВойтиВОбластьДанныхНаСервере(ОбластьДанных);
	
	ОбновитьИнтерфейс();
	
	ОбработкаЗавершения = Новый ОписаниеОповещения(
		"ПродолжениеВходаВОбластьДанныхПослеДействийПередНачаломРаботыСистемы", ЭтотОбъект);
	
	СтандартныеПодсистемыКлиент.ПередНачаломРаботыСистемы(ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВходаВОбластьДанныхПослеДействийПередНачаломРаботыСистемы(Результат, Неопределен) Экспорт
	
	Если Результат.Отказ Тогда
		ВыйтиИзОбластиДанныхНаСервере();
		ОбновитьИнтерфейс();
		Возврат;
	КонецЕсли;
	
	ОбработкаЗавершения = Новый ОписаниеОповещения(
		"ПродолжениеВходаВОбластьДанныхПослеДействийПриНачалеРаботыСистемы", ЭтотОбъект);
	
	СтандартныеПодсистемыКлиент.ПриНачалеРаботыСистемы(ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВходаВОбластьДанныхПослеДействийПриНачалеРаботыСистемы(Результат, Неопределен) Экспорт
	
	Если Результат.Отказ Тогда
		ВыйтиИзОбластиДанныхНаСервере();
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	Активизировать();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыйтиИзОбластиДанных(Отказ)
	
	ОбработкаЗавершения = Новый ОписаниеОповещения(
		"ПродолжениеВыходаИзОбластиДанныхПослеДействийПередЗавершениемРаботыСистемы", ЭтотОбъект);
	
	СтандартныеПодсистемыКлиент.ПередЗавершениемРаботыСистемы(, ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеВыходаИзОбластиДанныхПослеДействийПередЗавершениемРаботыСистемы(Результат, Неопределен) Экспорт
	
	Если Результат.Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыйтиИзОбластиДанныхНаСервере();
	
	ОбновитьИнтерфейс();
	
	СтандартныеПодсистемыКлиент.УстановитьРасширенныйЗаголовокПриложения(Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УказаннаяОбластьДанныхЗаполнена(Знач ОбластьДанных)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОбластиДанных");
	ЭлементБлокировки.УстановитьЗначение("ОбластьДанныхВспомогательныеДанные", ОбластьДанных);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбластиДанных.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.ОбластиДанных КАК ОбластиДанных
	|ГДЕ
	|	ОбластиДанных.ОбластьДанныхВспомогательныеДанные = &ОбластьДанных";
	Запрос.УстановитьПараметр("ОбластьДанных", ОбластьДанных);
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		Результат = Запрос.Выполнить();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Статус = Перечисления["СтатусыОбластейДанных"].Используется;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ВойтиВОбластьДанныхНаСервере(Знач ОбластьДанных)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.УстановитьРазделениеСеанса(Истина, ОбластьДанных);
	
	НачатьТранзакцию();
	
	Попытка
		КлючОбласти = РегистрыСведений["ОбластиДанных"].СоздатьКлючЗаписи(
			Новый Структура("ОбластьДанныхВспомогательныеДанные", ОбластьДанных));
		
		ЗаблокироватьДанныеДляРедактирования(КлючОбласти);
		
		Блокировка = Новый БлокировкаДанных;
		Элемент = Блокировка.Добавить("РегистрСведений.ОбластиДанных");
		Элемент.УстановитьЗначение("ОбластьДанныхВспомогательныеДанные", ОбластьДанных);
		Элемент.Режим = РежимБлокировкиДанных.Разделяемый;
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = РегистрыСведений["ОбластиДанных"].СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ОбластьДанныхВспомогательныеДанные = ОбластьДанных;
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.ОбластьДанныхВспомогательныеДанные = ОбластьДанных;
			МенеджерЗаписи.Статус = Перечисления.СтатусыОбластейДанных.Используется;
			МенеджерЗаписи.Записать();
		КонецЕсли;
		РазблокироватьДанныеДляРедактирования(КлючОбласти);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыйтиИзОбластиДанныхНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.УстановитьРазделениеСеанса(Ложь);
	
КонецПроцедуры

#КонецОбласти
