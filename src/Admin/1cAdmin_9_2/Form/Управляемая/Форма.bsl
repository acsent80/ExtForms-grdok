
&НаКлиенте
Перем СтрокаРавно;
&НаСервере
Перем СтрокаРавно;

//+yuraos, 10.09.2015
&НаКлиенте
Перем спСвязанныеОкна Экспорт;
&НаСервере
Перем This;
//+yuraos, 10.09.2015

&НаСервере
Функция ОбъектОбработка()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

Функция ЭтоГлавныйУзел()
	Возврат ПланыОбмена.ГлавныйУзел() = Неопределено;
КонецФункции

// Для ВызоваОбработки из 1С после добавления её в справочник "Дополнительные отчеты и обработки", если используется Команда.Использование = "ВызовКлиентскогоМетода";
// В
// &НаСервере
// Функция СведенияОВнешнейОбработке() Экспорт
// ...
// Команда.Использование = "ВызовКлиентскогоМетода";
// ...
//
&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначения) Экспорт	// Синтаксис для вида внешней обработки "ЗаполнениеОбъекта".
	
	Объект.ОбъектБД = ОбъектыНазначения[0];
	
	Если ИдентификаторКоманды = "Администратор1С-ОткрытьОбъектВ" Тогда
		Открыть();
	КонецЕсли;	
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// ОСНОВНЫЕ ПРОЦЕДУРЫ: СОБЫТИЯ ФОРМЫ.
// 

// МО: Перед Открытием формы:
// - Определение ПараметровЗапускаОбработки
// - Добавление реквизитов в таблицу Ссылок на Объект БД и соответствующих элементов формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ)
	Перем ОбъектОбработка;
	
	Объект.УправляемаяФорма = Истина;
	Объект.ЭтоГлавныйУзел = ЭтоГлавныйУзел();
	ОбъектОбработка = ОбъектОбработка();
	
	СведенияОВнешнейОбработке = ОбъектОбработка.СведенияОВнешнейОбработке();
	Объект.Настройки = СведенияОВнешнейОбработке.Настройки;
	ЗаполнитьЗначенияСвойств(Объект, Объект.Настройки);
	
	Элементы.кнОМДомашняяСтраницаОбработки.Заголовок = Объект.ДомашняяСтраницаОбработки;
	
	//+yuraos, 10.09.2015 
	yuraosИнициализация(ОбъектОбработка); // ЭтотОбъект
	//сохраним значение параметра формы в добавленном реквизите формы "ПараметрыОбъектБД"
	//и присвоим его ОбъектуБД в обработчике ПриОткрытии
	Параметры.Свойство("ОбъектБД", ЭтаФорма.ПараметрыОбъектБД);
	//+yuraos, 10.09.2015 
	
	ЭтаФорма.ПроверкаПравПользователя = ОбъектОбработка.ОбработкаПроверитьПраваПользователяИРежимЗапуска(Объект.Настройки);
	
	// Добавляем в Реквизит Формы ТСсылкиСписок новые Колонки-Реквизиты.
	НовыеРеквизиты = Новый Массив;
	Реквизит = Новый РеквизитФормы("Данные"		, Новый ОписаниеТипов()			, "Объект.ТСсылкиСписок", , Ложь);
	НовыеРеквизиты.Добавить(Реквизит);
	Реквизит = Новый РеквизитФормы("Метаданные"	, Новый ОписаниеТипов("Строка")	, "Объект.ТСсылкиСписок", , Ложь);
	НовыеРеквизиты.Добавить(Реквизит);
	Реквизит = Новый РеквизитФормы("Ссылка"		, Новый ОписаниеТипов()			, "Объект.ТСсылкиСписок", , Ложь);
	НовыеРеквизиты.Добавить(Реквизит);
	
	ИзменитьРеквизиты(НовыеРеквизиты);
	
	// Добавляем в Элемент Формы ТСсылкиСписок новые Колонки-Реквизиты.
	НовоеПоле = Элементы.Добавить("Данные", Тип("ПолеФормы"), Элементы.ТСсылкиСписок);
	НовоеПоле.ПутьКДанным			= "Объект.ТСсылкиСписок.Данные";
	НовоеПоле.Вид					= ВидПоляФормы.ПолеВвода;
	НовоеПоле.РежимРедактирования	= РежимРедактированияКолонки.ВходПриВводе;
	НовоеПоле.ПропускатьПриВводе 	= Истина;
	НовоеПоле.Ширина				= 30;
	НовоеПоле.ВыбиратьТип			= Ложь;
	НовоеПоле.КнопкаВыбора			= Ложь;
	НовоеПоле.КнопкаСпискаВыбора	= Ложь;
	НовоеПоле.КнопкаОткрытия		= Истина;
	НовоеПоле.КнопкаОчистки			= Ложь;
	НовоеПоле.КнопкаРегулирования	= Ложь;
	НовоеПоле.РедактированиеТекста	= Ложь;

	НовоеПоле = Элементы.Добавить("МетаДанные", Тип("ПолеФормы"), Элементы.ТСсылкиСписок);
	НовоеПоле.ПутьКДанным			= "Объект.ТСсылкиСписок.МетаДанные";
	НовоеПоле.Вид					= ВидПоляФормы.ПолеНадписи;
	НовоеПоле.Ширина				= 20;
	
	ЭтаФорма.ВосстановлениеСсылкиКомментарий = "ПРОЦЕДУРА ВОССТАНОВЛЕНИЯ ""БИТОЙ"" ССЫЛКИ.
	|Основано на GUID (Globally Unique Identifier) — статистически уникальный 128-битный идентификатор.
	|
	|1. В сеансе Основной БД: Заполнить поле ""Битый"" Объект (ТЕКСТ) строкой подобно:
	|<Объект не найден> (51:8309001cc496175a11e17a512788a343).
	|2. В сеансе Основной БД: Выполнить <""Битый"" Объект: Получить GUID>. Получить GUID и Дату создания объекта.
	|3. В сеансе Архивной БД: По дате создания объекта открыть 1С:Предприятие по восстановленным данным из архивной копии.
	|4. В сеансе Архивной БД: Открыть Обработку ""1С:Администратор"".
	|5. В сеансе Архивной БД: По полученному ""Битый"" Объект (GUID) найти в архивной копии Ссылку на объект.
	|Таким образом однозначно определяется тип ""Битого"" объекта (Актуально для составных реквизитов).
	|6. В сеансе Основной БД: Выбрать ""Битый"" Объект (ТИП) в соответствии с данными Архивной БД или определенным по данным открытой ИБ.
	|(по-умолчанию присваивается тип текущего редактируемого объекта).
	|7. В сеансе Основной БД: Выполнить <""Битый"" Объект: Восстановить>. Создать новый Объект с GUID ""Битого"" Объекта.
	|8. В сеансе Основной БД: Выполнить <""Битый"" Объект: Редактировать>.
	|Данная Обработка в отличие от стандартной формы Конфигурации позволит Вам изменить нужные реквзиты Объекта.
	|9. В сеансе Основной БД: Заполнить поля объекта данными из Архивной БД.
	|
	|Записать изменения.
	|ОБЪЕКТ ВОССТАНОВЛЕН.";
	
	ЭтаФорма.ПланОбменаСправка1С 								= """О запрете загрузки данных""";
	ЭтаФорма.ПланОбменаСпециальноеПояснение 					= "Если использовалась другая возможность для отключения Главного Узла, то ... ";
	ЭтаФорма.ПланОбменаКомментарийУдалениеРегистрацииИзменений	= "- После удаления неподтвержденные изменения не будут посланы в текущий узел.";
	ЭтаФорма.ПланОбменаКомментарийОтключениеГлавногоУзла		= "- При необходимости в конфигураторе загрузить конфигурацию (CF) ГлавногоУзла.";
	
	УстановитьЗначениеПараметраНаВкладке("ДокументСтатистикаОрганизация"	, "Организация", "Организации", Объект.ЕстьСправочникОрганизации);
	УстановитьЗначениеПараметраНаВкладке("ДокументПроведениеОрганизация"	, "Организация", "Организации", Объект.ЕстьСправочникОрганизации);
	УстановитьЗначениеПараметраНаВкладке("ДокументДвиженияОрганизация"		, "Организация", "Организации", Объект.ЕстьСправочникОрганизации);
	УстановитьЗначениеПараметраНаВкладке("ДокументПрефиксОрганизация"		, "Организация", "Организации", Объект.ЕстьСправочникОрганизации);
	
	Элементы.ПолнотекстовыйПоискДанных.Видимость = (НЕ Объект.ВерсияПриложенияСокращенно = "8.2");

	ОМСоздатьЭлементыМенюСервисНаСервере("ПанельАдминистрирования", "", Элементы.пмНастройка.ПодчиненныеЭлементы.ГруппаПанелиАдминистрирования);
	ОМСоздатьЭлементыМенюСервисНаСервере("Панель", "ПанельАдминистрирования", Элементы.пмНастройка.ПодчиненныеЭлементы.ГруппаПанелиКонфигурации);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПараметраНаВкладке(ИмяРеквизитаФормы, ИмяРеквизитаОбъекта, ИмяСправочника, ЕстьСправочник)
	Перем НачальноеЗначение;
	
	Если НЕ ТипЗнч(ЭтаФорма[ИмяРеквизитаФормы]) = Тип("СписокЗначений") Тогда
		Попытка
			НачальноеЗначение = Объект.ОбъектБД[ИмяРеквизитаОбъекта];
		Исключение
			НачальноеЗначение = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Если ЕстьСправочник Тогда
		Элементы[ИмяРеквизитаФормы].Видимость = Истина;
		Элементы[ИмяРеквизитаФормы].ТолькоПросмотр = Ложь;
		Если ТипЗнч(ЭтаФорма[ИмяРеквизитаФормы]) = Тип("СписокЗначений") Тогда
			ОграничитьТипРеквизита(ИмяРеквизитаФормы, "СправочникСсылка." + ИмяСправочника);
		Иначе
			ОграничитьТипЭлемента(ИмяРеквизитаФормы, "СправочникСсылка." + ИмяСправочника);
		КонецЕсли;
	Иначе
		Элементы[ИмяРеквизитаФормы].Видимость = Ложь;
		Элементы[ИмяРеквизитаФормы].ТолькоПросмотр = Истина;
		ЭтаФорма[ИмяРеквизитаФормы] = Неопределено;
	КонецЕсли;
	
	Если Элементы[ИмяРеквизитаФормы].Видимость
		И НЕ ТипЗнч(ЭтаФорма[ИмяРеквизитаФормы]) = Тип("СписокЗначений") Тогда
		ЭтаФорма[ИмяРеквизитаФормы] = НачальноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

// Перед открытием формы:
// - Восстановление значения Выбранного ОбъектаБД.
//
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	// Не сохраняются при закрытии Обработки.
	
	Объект.ВыполнятьВТранзакции	= Истина;
	Объект.МонопольныйРежим 	= МонопольныйРежим();
	
	// Восстанавливаем настройки / Устанавливаем начальные значения.
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		Значение = Настройки.Получить("ОбъектБД");
		Объект.ОбъектБД = ?(Значение <> Неопределено, Значение, Неопределено);
	КонецЕсли; 
	
	Значение = Настройки.Получить("ПоказыватьСообщения");
	Объект.ПоказыватьСообщения = ?(Значение <> Неопределено, Значение, Ложь);
	
	Значение = Настройки.Получить("ОдинаковыйТипПриЗамене");
	ЭтаФорма.ОдинаковыйТипПриЗамене = ?(Значение <> Неопределено, Значение, Истина);
	
	Значение = Настройки.Получить("СкрыватьПустыеТабличныеЧасти");
	Объект.СкрыватьПустыеТабличныеЧасти = ?(Значение <> Неопределено, Значение, Истина);
	
	Значение = Настройки.Получить("СкрыватьПустыеТаблицыДвиженийРегистров");
	Объект.СкрыватьПустыеТаблицыДвиженийРегистров = ?(Значение <> Неопределено, Значение, Истина);

	Значение = Настройки.Получить("ГлавныйУзелПредыдущий");
	Объект.ГлавныйУзелПредыдущий = ?(Значение <> Неопределено, Значение, Неопределено);

	Значение = Настройки.Получить("ИспользоватьСтандартнуюФормуВыбора");
	Объект.ИспользоватьСтандартнуюФормуВыбора = ?(Значение <> Неопределено, Значение, Истина);
	
	//+yuraos, 10.09.2015
	//Значение = ХранилищеНастроекДанныхФорм.Загрузить(ИмяФормы, "ОбъектБД.СписокВыбора");
	Значение = Настройки["ОбъектБД.СписокВыбора"]; // Результат тот же.
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		ThisInitiate();
		СписокВыбора = Элементы.ОбъектБД.СписокВыбора;
		СписокВыбора.Очистить();
		сзСписок = This.СписокПустыхСсылокйПолучить(Значение);
		Для Каждого СтрС ИЗ сзСписок Цикл
			ЗаполнитьЗначенияСвойств(СписокВыбора.Добавить(), СтрС);
		КонецЦикла;
	КонецЕсли; 
	//+yuraos, 10.09.2015
	
КонецПроцедуры

// При Открытии формы:
// - Выполнение минимального количества процедур по восстановленному значению Выбранного ОбъектаБД.
// Необходимо для восстановления состояния пред прошлым закрытием формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//+yuraos, 10.09.2015
	спСвязанныеОкнаПроверитьДобавить(Неопределено,Ложь);
	Если ЗначениеЗаполнено(ЭтаФорма.ПараметрыОбъектБД) Тогда
		ЭтоСвязанноеОкно = Неопределено;
		ФормаОбработки = НайтиОбъектВСвязанныхОкнах(ЭтаФорма.ПараметрыОбъектБД, Истина, ЭтоСвязанноеОкно);
		Если ЭтоСвязанноеОкно = Истина И ФормаОбработки <> Неопределено И ЭтаФорма <> ФормаОбработки Тогда
			Объект.ОбъектБД = Неопределено;
		Иначе
			Объект.ОбъектБД = ЭтаФорма.ПараметрыОбъектБД;
		КонецЕсли; 
	КонецЕсли;
	ЭтаФорма.ПараметрыОбъектБД = Неопределено;
	//+yuraos, 10.09.2015
	
	Если ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		Состояние("" + Объект.СинонимОбработки + "
		|Заполнение элементов формы данными объекта: 
		|" + Объект.ОбъектБД);
	КонецЕсли;
	
	ФормаПолучитьЗаголовокРежима();
	
	Если ЭтаФорма.ПроверкаПравПользователя.Отказ Тогда
		ПредупреждениеСообщение(, ЭтаФорма.ПроверкаПравПользователя.ОписаниеОшибки);
		Закрыть();
		Возврат;
	Иначе
		Если НЕ ЭтаФорма.ПроверкаПравПользователя.ОписаниеОшибки = "" Тогда
			ПредупреждениеСообщение(, ЭтаФорма.ПроверкаПравПользователя.ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
	
	СтрокаРавно = "=============================================================";
	
	Если НЕ Объект.ОбъектБД = Неопределено Тогда
		ОбъектБДПриИзменении("ПриОткрытии");
	Иначе
		ФормаОпределитьДоступнность(Неопределено);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	спСвязанныеОкнаПроверитьДобавить(Неопределено,Ложь);	//+yuraos, 10.09.2015
	ОбъектБДПриИзменении(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	ЭтаФорма.СохраняемыеВНастройкахДанныеМодифицированы = Истина;	// Связано с использованием "Структуры подчинения" и открытия связанного документа в связанном окне обработки.
КонецПроцедуры

// Перед закрытием формы:
// - Сохранение значения Выбранного ОбъектаБД.
//
&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Настройки.Вставить("ОбъектБД"								, Объект.ОбъектБД);
	Настройки.Вставить("ПоказыватьСообщения"					, Объект.ПоказыватьСообщения);
	Настройки.Вставить("ОдинаковыйТипПриЗамене"					, ЭтаФорма.ОдинаковыйТипПриЗамене);
	Настройки.Вставить("СкрыватьПустыеТабличныеЧасти"			, Объект.СкрыватьПустыеТабличныеЧасти);
	Настройки.Вставить("СкрыватьПустыеТаблицыДвиженийРегистров"	, Объект.СкрыватьПустыеТаблицыДвиженийРегистров);
	
	Настройки.Вставить("ГлавныйУзелПредыдущий"					, Объект.ГлавныйУзелПредыдущий);
	
	Настройки.Вставить("ИспользоватьСтандартнуюФормуВыбора"		, Объект.ИспользоватьСтандартнуюФормуВыбора);
	
	Настройки.Вставить("ОбъектБД.СписокВыбора"					, Элементы.ОбъектБД.СписокВыбора.ВыгрузитьЗначения()); //+yuraos, 10.09.2015
	
КонецПроцедуры

// Вывести в окно "Состояние" текст.
// - Восстановление значения Выбранного ОбъектаБД.
//
&НаКлиенте
Процедура ФормаВывестиВСостояние(Текст = Неопределено, Процент = Неопределено, Пояснение = Неопределено)
	
	Если Текст = Неопределено Тогда
		Текст = Объект.СинонимОбработки;
	КонецЕсли;
	
	Если Пояснение = Неопределено Тогда
		Пояснение = "Формирование основных таблиц обработки ...";
	КонецЕсли;
	
	Состояние(Текст, Процент, Пояснение);
	
КонецПроцедуры

// Определить доступность элементов формы.
// 
&НаКлиенте
Процедура ФормаОпределитьДоступнность(ТипОбъектаБД)
	
	глКВоТСсылки 		= Объект.ТСсылкиСписок.Количество() > 0;
	глКВоТСправочники 	= Объект.ТСправочникСписок.Количество() > 0;
	КВоТДокументы 		= Объект.ТДокументСписок.Количество();
	КВоТМетаданные 		= Объект.ТМетаданныеРегистрыОбъекта.Количество();
	ПроведениеРазрешено	= Объект.ПроведениеРазрешено;
	
	глТипНеопределено	= НЕ ЗначениеЗаполнено(ТипОбъектаБД);
	глТипСправочник		= ТипОбъектаБД = "Справочник";
	глТипДокумент		= ТипОбъектаБД = "Документ";
	глТипПВХ			= ТипОбъектаБД = "ПланВидовХарактеристик";
	глТипБизнесПроцесс	= ТипОбъектаБД = "БизнесПроцесс";
	глТипЗадача			= ТипОбъектаБД = "Задача";
	глТипПланОбмена		= ТипОбъектаБД = "ПланОбмена";
	
	// Процедура используется часто, поэтому для производительности сравниваем со строкой типа: ТипОбъектаБД = "Документ".
	
	// ОСНОВНОЕ МЕНЮ.
	
	Элементы.кнОМЗаписать.Доступность									= НЕ глТипНеопределено;
	Элементы.кнОМСоздатьКопированием.Доступность						= глТипДокумент;
	
	// Подменю Проведение. Доступность									= глТипДокумент;
	Элементы.кнОМПровести.Доступность									= глТипДокумент И ПроведениеРазрешено;
	Элементы.кнОМПровестиОснованиеИДокумент.Доступность					= глТипДокумент И Объект.ЕстьДокументОснование;
	Элементы.кнОМОтменитьПроведение.Доступность							= глТипДокумент И ПроведениеРазрешено;
	
	//+yuraos, 10.09.2015 
	Элементы.кнОМЗаписатьИзмененныеНаборыДвижений.Доступность			= Ложь; // Изначально таблицы наборов движений недоступны для изменения.
	Элементы.кнОМЗаписатьИзмененныеНаборыДвижений.Видимость				= Объект.ПоказатьДвижения;
	//+yuraos, 10.09.2015 
	
	// Подменю "Удаление". Доступность									= НЕ глТипНеопределено;
	Элементы.кнОМПометитьНаУдаление.Доступность							= НЕ глТипНеопределено;
	Элементы.кнОМУдалить.Доступность									= НЕ глТипНеопределено;
	Элементы.кнОМСнятьПометкуУдаления.Доступность						= НЕ глТипНеопределено;

	Элементы.кнОМИсторияИзмененийОбъекта.Доступность					= НЕ глТипНеопределено;
	Элементы.кнОМСтруктураПодчиненности.Доступность						= глТипДокумент;
	Элементы.кнОМКартаМаршрута.Доступность								= глТипБизнесПроцесс ИЛИ глТипЗадача;
	
	Элементы.кнОМПроверитьРегистрациюВПланахОбмена.Доступность			= НЕ глТипНеопределено И НЕ глТипПланОбмена;
	Элементы.кнОМРегистрацияВПланахОбмена.Доступность					= НЕ глТипНеопределено И НЕ глТипПланОбмена;
	Элементы.кнОМУдалитьРегистрациюВПланахОбмена.Доступность			= НЕ глТипНеопределено И НЕ глТипПланОбмена;
	
	// ОСНОВНЫЕ ПАРАМЕТРЫ ФОРМЫ.
	
	Элементы.ОбменДаннымиЗагрузка.ТолькоПросмотр						= глТипНеопределено;
	Элементы.ПоказатьДвижения.ТолькоПросмотр							= НЕ глТипДокумент;
	Элементы.СкрыватьПустыеТаблицыДвиженийРегистров.ТолькоПросмотр		= НЕ глТипДокумент;
	Элементы.СкрыватьПустыеТабличныеЧасти.ТолькоПросмотр				= глТипНеопределено;
	
	// ВКЛАДКА "Дополнительно".
	
	// Вкладки: "Свойства Объекта", "Ссылки на Объект", "Метаданные Регистров".
	//Элементы.СтраницаСвойстваОбъекта.ТолькоПросмотр					= глТипНеопределено;
	
	//Элементы.СтраницаМетаДанные.ТолькоПросмотр						= глТипНеопределено;
	Элементы.кнТМетаДанныеПолучитьСписокРегистровСвязанныхСОбъектомМД.Доступность	= ЗначениеЗаполнено(ТипОбъектаБД);
	Элементы.кнПолучитьСписокПодписокНаСобытияОбъектаМД.Доступность		= ЗначениеЗаполнено(ТипОбъектаБД);
	Элементы.кнПолучитьСписокФункциональныхОпцийОбъектаМД.Доступность	= ЗначениеЗаполнено(ТипОбъектаБД);
	Элементы.кнПолучитьСписокПлановОбменаОбъектаМД.Доступность			= ЗначениеЗаполнено(ТипОбъектаБД);
	Элементы.кнПолучитьСпискиВводНаОснованииОбъектаМД.Доступность		= ЗначениеЗаполнено(ТипОбъектаБД);
	
	// Вкладка "Ссылки на объект".
	Элементы.СтраницаСсылкиНаОбъект.ТолькоПросмотр						= ТипОбъектаБД = "" ИЛИ глТипПланОбмена;	// Замену Ссылок для ПланаОбмена ИКЛЮЧИТЬ.
	Элементы.ОбъектБДЗаменяющий.ТолькоПросмотр							= глТипНеопределено;
	Элементы.ОдинаковыйТипПриЗамене.ТолькоПросмотр						= глТипНеопределено;
	Элементы.кнТСсылкиНайти.Доступность									= НЕ глТипНеопределено;											// Поиск Ссылок для ПланаОбмена РАЗРЕШИТЬ.
	Элементы.кнТСсылкиОчистить.Доступность								= глКВоТСсылки;													// Очистку Списка Ссылок для ПланаОбмена РАЗРЕШИТЬ.
	Элементы.кнТСсылкиУстановитьФлажки.Доступность						= глКВоТСсылки И ЭтаФорма.ОбъектБДЗаменяющий <> Неопределено;	// Замену Ссылок для ПланаОбмена ИКЛЮЧИТЬ.
	Элементы.кнТСсылкиСнятьФлажки.Доступность							= глКВоТСсылки И ЭтаФорма.ОбъектБДЗаменяющий <> Неопределено;	// Замену Ссылок для ПланаОбмена ИКЛЮЧИТЬ.
	Элементы.кнТСсылкиПроизвестиЗамену.Доступность						= глКВоТСсылки И ЭтаФорма.ОбъектБДЗаменяющий <> Неопределено;	// Замену Ссылок для ПланаОбмена ИКЛЮЧИТЬ.
	
	// Вкладка "Справочник (Дополнительно)".
	//Элементы.СтраницаСправочникДополнительно.ТолькоПросмотр			= НЕ глТипСправочник;
	Элементы.кнТСправочникПолучитьНачальныеПараметры.Доступность		= глТипСправочник;
	Элементы.кнТСправочникОписаниеДействия.Доступность					= глТипСправочник;
	Элементы.кнТСправочникБлокированныеРеквизиты.Доступность			= глТипСправочник И ЗначениеЗаполнено(Объект.БлокируемыеРеквизиты);
	Элементы.кнТСправочникНайти.Доступность								= глТипСправочник И ЗначениеЗаполнено(ЭтаФорма.СправочникДействие) И ЗначениеЗаполнено(ЭтаФорма.СправочникИзменяемыйРеквизит) И ЗначениеЗаполнено(ЭтаФорма.СправочникНовоеЗначениеРеквизита);
	Элементы.кнТСправочникОчистить.Доступность							= глТипСправочник И глКВоТСправочники;
	Элементы.кнТСправочникУстановитьФлажки.Доступность					= глТипСправочник И глКВоТСправочники;
	Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность		= глТипСправочник И глКВоТСправочники;
	Элементы.кнТСправочникСнятьФлажки.Доступность						= глТипСправочник И глКВоТСправочники;
	Элементы.кнТСправочникНайтиВСписке.Доступность						= глТипСправочник И глКВоТСправочники;
	Элементы.кнТСправочникОтменитьПоиск.Доступность						= глТипСправочник И глКВоТСправочники;
	Элементы.кнТСправочникВыполнить.Доступность							= глТипСправочник И глКВоТСправочники И ЗначениеЗаполнено(ЭтаФорма.СправочникДействие) И ЗначениеЗаполнено(ЭтаФорма.СправочникИзменяемыйРеквизит) И ЗначениеЗаполнено(ЭтаФорма.СправочникНовоеЗначениеРеквизита);
	Элементы.СправочникДатаРанее.ТолькоПросмотр							= НЕ ЭтаФорма.СправочникДействие = "НеиспользуемыеЭлементы_ПоискИУдаление";

	// Вкладка "Документ (Дополнительно)".	Доступно ВСЕГДА. Ограничение: Перенумерация.
	//Элементы.СтраницаДокументСоздатьНаОсновании.ТолькоПросмотр		= НЕ глТипДокумент;
	Элементы.ДокументОснование.Доступность								= глТипДокумент;
	Элементы.кнДокументСоздатьДокументПоДокументуОснованию.Доступность	= глТипДокумент;

	Элементы.кнТДокументПолучитьНачальныеПараметрыПеренумерации.Доступность	= глТипДокумент;
	
	Элементы.кнТДокументПеренумерацияНайти.Доступность					= глТипДокумент И ЗначениеЗаполнено(ЭтаФорма.ДокументДействие);
	
	Элементы.кнТДокументПеренумероватьДокументы.Доступность				= КВоТДокументы > 0;
	
	Элементы.кнТДокументПровести.Доступность							= ЭтаФорма.ДокументПроведениеТСписокВидов.Количество() > 0;
	
	Элементы.кнДокументДвиженияПолучитьНепроведенныеСДвижениями.Доступность	= ЭтаФорма.ДокументДвиженияТСписокВидов.Количество() > 0;
	Элементы.кнДокументДвиженияОчиститьДвиженияНепроведенныхСДвижениями.Доступность = ЭтаФорма.ДокументДвиженияНайденыНепроведенныеСДвижениями;
	
	Элементы.кнДокументДвиженияПолучитьПроведенныеБезДвижений.Доступность = ЭтаФорма.ДокументДвиженияТСписокВидов.Количество() > 0;
	Элементы.кнДокументДвиженияПровестиПроведенныеБезДвижений.Доступность = ЭтаФорма.ДокументДвиженияНайденыПроведенныеБезДвижений;
	
	// Вкладка "ПланыОбмена (Дополнительно)".
	Элементы.СтраницаПланОбменаДополнительно.ТолькоПросмотр				= НЕ глТипПланОбмена;
	Элементы.кнПланОбменаПолучитьИнформациюОБДиУзлахРИБ.Доступность		= глТипПланОбмена;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБАТЫВАЕМЫЙ ОБЪЕКТ БД.
// 

// Выбор объекта через GUID/НавигационнуюСсылку.

&НаКлиенте
Процедура GUID(Команда)
	Если Элементы.GUID_НС_ПолучитьСсылку.Видимость Тогда
		Элементы.GUID_НС_ПолучитьСсылку.Видимость = Ложь;
		Элементы.ОбъектБД_GUID_НС_Строка.Видимость = Ложь;
	Иначе
		Элементы.GUID_НС_ПолучитьСсылку.Видимость = Истина;
		Элементы.ОбъектБД_GUID_НС_Строка.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура GUID_НС_ПолучитьСсылку(Команда)
	Перем ОбъектБД;
	
	Если НЕ Строка(Объект.ОбъектGUID) = "00000000-0000-0000-0000-000000000000" Тогда
		Состояние("Подождите немного. Считываются данные...");
		ОбъектБД = GUIDНайтиОбъектНаСервере(Объект.ОбъектGUID);
		Если ЗначениеЗаполнено(ОбъектБД) Тогда
			ОбъектБДОчистка(Неопределено);
			Объект.ОбъектБД = ОбъектБД;
			ВызватьОбъектБДПриИзменении(Объект.ОбъектБД, Элементы.ОбъектБД);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБД_GUID_НС_СтрокаПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД_GUID_НС_Строка) Тогда
		Возврат;
	КонецЕсли;
	
	Если Найти(Объект.ОбъектБД_GUID_НС_Строка, "/") > 0 Тогда
		Попытка
			Объект.ОбъектGUID = Новый УникальныйИдентификатор(ПолучитьGUIDизНавигационнойСсылки(Объект.ОбъектБД_GUID_НС_Строка));
		Исключение
			Объект.ОбъектGUID = Неопределено;
			ПредупреждениеСообщение(, "Не удалось преобразовать """ + Объект.ОбъектБД_GUID_НС_Строка + """
			|в Уникальный идентификатор.");
		КонецПопытки;
	Иначе
		Попытка
			Объект.ОбъектGUID = Новый УникальныйИдентификатор(Объект.ОбъектБД_GUID_НС_Строка);
		Исключение
			Объект.ОбъектGUID = Неопределено;
			ПредупреждениеСообщение(, "Не удалось преобразовать """ + Объект.ОбъектБД_GUID_НС_Строка + """
			|в Уникальный идентификатор.");
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

// e1cib/data/Справочник.A?ref=848afa5e326a5e3711e6793c38b7618a -> 38b7618a-793c-11e6-848a-fa5e326a5e37.
&НаКлиенте
Функция ПолучитьGUIDизНавигационнойСсылки(НавигационнаяСсылка)
	Перем GUID, ПозицияGUID;
	
	GUID = Неопределено;
	
	ПозицияGUID = Найти(НавигационнаяСсылка, "?ref=");
	Если ПозицияGUID > 0 Тогда       
		GUID = Сред(НавигационнаяСсылка, ПозицияGUID + 5, 32);
		GUID = Прав(GUID,8) + "-" + Сред(GUID, 21, 4) + "-" + Сред(GUID,17,4) + "-" + Лев(GUID, 4) + "-" + Сред(GUID, 5, 12);
		GUID = Новый УникальныйИдентификатор(GUID);
	КонецЕсли;
	
	Возврат(GUID);
	
КонецФункции 

&НаКлиенте
Процедура ОбъектБДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	Если Объект.ОбъектБД = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ВызватьВыборТипаОбъектаБД();	//+yuraos, 10.09.2015
		//-yuraos, 10.09.2015 - функционал вынесен в процедуру ВызватьВыборТипаОбъектаБД()
	Иначе	
		СтандартнаяОбработка = Ложь;
		Элемент.ВыбиратьТип = Ложь;
		ПланОбменаОчиститьПараметры();
		
		Если НЕ ЗначениеЗаполнено(Объект.ТипОбъектаБД) Тогда
			ОбъектБДИнициализироватьОсновныеПараметры(Объект.ОбъектБД);
			ОбъектБДСформироватьЗаголовокЭлементаФормы(Объект.ТипОбъектаБД, Элементы.ОбъектБД, Объект.ПолноеСтроковоеИмяТипа, Объект.Предопределенный);
		КонецЕсли;
		
		хПозицияТочки = Найти(Объект.ПолноеСтроковоеИмяТипа, ".");
		хТипМД 	= Лев(Объект.ПолноеСтроковоеИмяТипа, хПозицияТочки-1);
		хВидМД = Сред(Объект.ПолноеСтроковоеИмяТипа, хПозицияТочки+1);
		Если хТипМД = "Справочник" И хВидМД = "ВариантыОтчетов" Тогда	// Специфический справочник.
			
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("РежимВыбора", Истина);
			СтруктураПараметров.Вставить("ТекущаяСтрока", Объект.ОбъектБД); //+yuraos, 10.09.2015
			СтруктураПараметров.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
			ФормаВыбораОбъектаБД = ПолучитьФорму(Объект.Настройки.ИмяФормыВариантыОтчетов, СтруктураПараметров, Элемент, ЭтаФорма);	//+yuraos, 10.09.2015
			
			Если ИспользоватьРежимМодальности() Тогда
				// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
				ВыбранноеЗначение = ФормаВыбораОбъектаБД.ОткрытьМодально();
				ВызватьОбъектБДПриИзменении(ВыбранноеЗначение, Элемент);
			Иначе
				// Стандартно в немодальном режиме (8.3) с обработкой результата.
				// - через механизм обработки выбора значения.
				ФормаВыбораОбъектаБД.Открыть();
			КонецЕсли;
			
		Иначе
			Если Объект.ИспользоватьСтандартнуюФормуВыбора Тогда
				СтандартнаяОбработка = Истина;
			Иначе
				ВыбранныйОбъектМетаДанных = Объект.ПолноеСтроковоеИмяТипа;
				ВызватьВыборОбъектаБД(ВыбранныйОбъектМетаДанных, Элемент);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьВыборОбъектаБД(ВыбранныйОбъектМетаДанных, Элемент) Экспорт
	
	//+yuraos, 10.09.2015
	Если Объект.ОбъектБД <> Неопределено И ВыбранныйОбъектМетаДанных <> ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Объект.ОбъектБД) Тогда
		ОбъектБДОчистка(Элемент, Истина);
	КонецЕсли; 
	//+yuraos, 10.09.2015
	
	Если ЗначениеЗаполнено(ВыбранныйОбъектМетаДанных) Тогда
		
		ИмяФормыВыбораОбъектаДанных = Объект.Настройки.ИмяФормыВыбораОбъектаДанных;
		
		хПозицияТочки = Найти(ВыбранныйОбъектМетаДанных, ".");
		хТипМД 	= Лев(ВыбранныйОбъектМетаДанных, хПозицияТочки-1);
		хВидМД = Сред(ВыбранныйОбъектМетаДанных, хПозицияТочки+1);
		
		Элемент.ОграничениеТипа = Новый ОписаниеТипов(хТипМД + "Ссылка." + хВидМД);
		Элемент.ВыбиратьТип = Ложь;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("РежимВыбора", Истина);
		СтруктураПараметров.Вставить("ТекущаяСтрока", Объект.ОбъектБД);	//+ yuraos, 10.09.2015
		СтруктураПараметров.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
		
		ВыбранныйОбъектМД = Новый Структура("хТипМД, хВидМД", хТипМД, хВидМД);
		СтруктураПараметров.Вставить("ВыбранныйОбъектМД", ВыбранныйОбъектМД);
		СтруктураПараметров.Вставить("ВыбранныйОбъектИБ", Объект.ОбъектБД);
		
		Если хТипМД = "Справочник" И хВидМД = "ВариантыОтчетов" Тогда	// Специфический справочник.
			ФормаВыбораОбъектаБД = ПолучитьФорму(Объект.Настройки.ИмяФормыВариантыОтчетов, СтруктураПараметров, Элемент, ЭтаФорма);	//+yuraos, 10.09.2015
		Иначе
			Попытка
				Если Объект.ИспользоватьСтандартнуюФормуВыбора Тогда
					ФормаВыбораОбъектаБД = ПолучитьФорму(хТипМД + "." + хВидМД + ".ФормаВыбора", СтруктураПараметров, Элемент, ЭтаФорма);	//+yuraos, 10.09.2015
				Иначе
					ФормаВыбораОбъектаБД = ПолучитьФорму(ИмяФормыВыбораОбъектаДанных, СтруктураПараметров, Элемент, ЭтаФорма);	//+yuraos, 10.09.2015
				КонецЕсли;
			Исключение
				ПредупреждениеСообщение(, "Невозможно получить ФормуВыбора для " + ВыбранныйОбъектМетаДанных + "
				|
				|Причина: " + ОписаниеОшибки() + "
				|
				|Открытие ФормыСписка (без выбора) ...");
				ФормаВыбораОбъектаБД = ПолучитьФорму(хТипМД + "." + хВидМД + ".ФормаСписка", СтруктураПараметров);
			КонецПопытки;
		КонецЕсли;
		
		Если ИспользоватьРежимМодальности() Тогда
			// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
			ВыбранноеЗначение = ФормаВыбораОбъектаБД.ОткрытьМодально();
			ВызватьОбъектБДПриИзменении(ВыбранноеЗначение, Элемент);
		Иначе
			// Стандартно в немодальном режиме (8.3) с обработкой результата.
			// - через механизм обработки выбора значения.
			ФормаВыбораОбъектаБД.Открыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьОбъектБДПриИзменении(ВыбранноеЗначение, Элемент) Экспорт
	
	Если ОбъектТипа_ЗначениеСсылочногоТипа(ЭтаФорма.ОбъектТип,ВыбранноеЗначение,Ложь) Тогда
		Объект.ОбъектБД = ВыбранноеЗначение;
		ОбъектБДПриИзменении(Элемент);
	КонецЕсли;
	
КонецПроцедуры

// Открыт Новый ОбъектБД:
// - Очистка содержимого формы
// - Обновление содержимого формы
//
&НаКлиенте
Процедура ОбъектБДПриИзменении(Элемент)
	Перем ОбъектБД;
	
	Если Объект.ОбъектБД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОбъектБД = Объект.ОбъектБД;
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипОбъектаБД) Тогда
		ОбъектБДИнициализироватьОсновныеПараметры(ОбъектБД);
		ОбъектБДСформироватьЗаголовокЭлементаФормы(Объект.ТипОбъектаБД, Элементы.ОбъектБД, Объект.ПолноеСтроковоеИмяТипа, Объект.Предопределенный);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОбъектБД) Тогда
		
		Если Объект.ПоказыватьСообщения Тогда
			Если НЕ Элемент = "ОбъектБДЗаписать" Тогда
				ОчиститьСообщения();
			КонецЕсли;
			Сообщить(СтрокаРавно);
			Сообщить("МФ: Подождите. Производится формирование основных таблиц данных:");
		КонецЕсли;
		
		ОбъектБДСформироватьДанные(ОбъектБД);
		
		Если Объект.ТипОбъектаБД = "Документ" Тогда
			ПоказатьДвиженияПриИзменении("ОбъектБДПриИзменении");
		Иначе	
			Объект.ПоказатьДвижения = Ложь;
		КонецЕсли;
		
		Если Объект.ПоказыватьСообщения Тогда
			Сообщить("МФ: Формирование основных таблиц данных завершено.");
			Сообщить(СтрокаРавно);
		КонецЕсли;
		
		//+yuraos, 10.09.2015
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
			СписокВыбора = Элементы.ОбъектБД.СписокВыбора;
			сзСписок = СписокВыбора.Скопировать(); // Передача на сервер по ссылке фактически не работает !!!
			ОбъектТипа_СписокПустыхСсылокВставить(ЭтаФорма.ОбъектТип, сзСписок, ОбъектБД,);
			СписокВыбора.Очистить();
			Для Каждого СтрС ИЗ сзСписок Цикл
				ЗаполнитьЗначенияСвойств(СписокВыбора.Добавить(), СтрС);
			КонецЦикла; 
		КонецЕсли; 
		//+yuraos, 10.09.2015
		
	КонецЕсли;
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
	ПланОбменаСообщитьОНеУстановленномГлавномУзле();

КонецПроцедуры

// Установить Заголовок для ОбъектБД или ОбъектБДЗаменяющий.
//
&НаКлиенте
Процедура ОбъектБДСформироватьЗаголовокЭлементаФормы(ТипОбъектаБД, Элемент, ПолноеСтроковоеИмяТипа = "", Предопределенный = Ложь)

	Если ТипОбъектаБД = "Справочник" ИЛИ ТипОбъектаБД = "ПланОбмена" ИЛИ ТипОбъектаБД = "ПланВидовХарактеристик" Тогда
		Элемент.Заголовок = "" + ПолноеСтроковоеИмяТипа;
	Иначе
		Элемент.Заголовок = "" + ТипОбъектаБД;
	КонецЕсли;
	
КонецПроцедуры

// МО: Вкладка "Свойства Объекта" и др.
// - Таблица Реквизитов.
// - Таблицы Табличных частей.
// - Таблицы Регистров Движений Документов.
//
&НаСервере
Процедура ОбъектБДСформироватьДанные(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	мдОбъектаБД	= ОбъектБД.Метаданные();
	
	ОбъектБДСформироватьТРеквизиты(ОбъектОбработка, мдОбъектаБД, ОбъектБД);
	
	Если Объект.ТипОбъектаБД = "ПланСчетов" ИЛИ Объект.ТипОбъектаБД = "ПланВидовРасчета" Тогда	// Имена типов - Производительность.
		Если ОбъектБДЕстьСтандартныеТабличныеЧастиОбъекта(ОбъектОбработка, мдОбъектаБД, ОбъектБД) Тогда
			ОбъектБДСформироватьТЧасти(ОбъектОбработка, мдОбъектаБД, ОбъектБД, "СтандартныеТабличныеЧасти");
		КонецЕсли;
	КонецЕсли;
	
	ОбъектБДСформироватьТЧасти(ОбъектОбработка, мдОбъектаБД, ОбъектБД, "ТабличныеЧасти");
	ОбъектБДПоказатьФОпцииРеквизитов(ОбъектБД);	
	
КонецПроцедуры

// МО: Вкладка "Свойства Объекта" и др. Сформировать Таблицу Реквизиты выбранного ОбъектаБД.
// - Таблица Реквизитов.
// - Таблицы Табличных частей.
// - Таблицы Регистров Движений Документов.
//
&НаСервере
Процедура ОбъектБДСформироватьТРеквизиты(ОбъектОбработка, мдОбъектаБД, ОбъектБД)
	
	СтруктураВозврат = ОбъектОбработка.ОбъектБДСформироватьТРеквизиты(ОбъектБД);
	
	ЗначениеВРеквизитФормы(СтруктураВозврат.ТСвойстваОбъекта, "Объект.ТСвойстваОбъекта");
	ЗначениеВРеквизитФормы(СтруктураВозврат.ТРеквизитыОбъекта, "Объект.ТРеквизитыОбъекта");
	
	Объект.СтруктураРеквизиты = СтруктураВозврат.СтруктураРеквизиты;
	Объект.СписокРеквизиты = СтруктураВозврат.СписокРеквизиты;
	Объект.БлокируемыеРеквизиты = СтруктураВозврат.БлокируемыеРеквизиты;
	Объект.НеРедактируемыеРеквизиты = СтруктураВозврат.НеРедактируемыеРеквизиты;
	
	Если Объект.ТипОбъектаБД = "Справочник" Тогда
		
		// Вкладка "Дополнительно"-"Справочники".
		//Если мдОбъектаБД.Иерархический Тогда
		//	Элементы.ДекорИерархияСправочника.Заголовок = "Иерархический";
		//Иначе
		//	Элементы.ДекорИерархияСправочника.Заголовок = "Неиерархический";
		//КонецЕсли;
	
	ИначеЕсли Объект.ТипОбъектаБД = "Документ" Тогда
		
		Объект.ПроведениеРазрешено	= ОбъектОбработка.МетаданныеПроведениеДокументаРазрешено(ОбъектБД);
		Если НЕ Объект.ПроведениеРазрешено Тогда	// Для документа, которому запрещено проведение - красный цвет.
			Элементы.ОбъектБД.ЦветТекстаЗаголовка = Новый Цвет(255, 0, 0);
		КонецЕсли;
		
		Объект.ЕстьДокументОснование= ОбъектОбработка.ЕстьРеквизитОбъекта1С("ДокументОснование", ОбъектБД);
		Объект.ИмяРеквизитаДокументаОснования = "";
		Если Объект.ЕстьДокументОснование Тогда
			 Объект.ИмяРеквизитаДокументаОснования = "ДокументОснование";
		Иначе
			// У документа может и небыть Реквизита "ДокументОснование", но может быть другой реквизит типа документ, который и является Документом-Основанием.
			ИмяРеквизитаДокументОснование = ОбъектОбработка.ОбъектБДПолучитьИмяДокументаОснование(ОбъектБД);
			Если ЗначениеЗаполнено(ИмяРеквизитаДокументОснование) Тогда
				Объект.ЕстьДокументОснование = Истина;
				Объект.ИмяРеквизитаДокументаОснования = ИмяРеквизитаДокументОснование;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование таблицы РеквизитыОбъекта завершено.");
	КонецЕсли;
	
	Если Найти(Строка(ОбъектБД), "<Объект не найден> (") = 0 Тогда
		Объект.IDОбъектаБД	= ОбъектБД.УникальныйИдентификатор();
	Иначе
		Объект.IDОбъектаБД	= ОбъектОбработка.GUIDБитогоОбъектаНаСервере(ОбъектБД);
	КонецЕсли;
	ЭтаФорма.IDОбъектаБДСтрока	= Объект.IDОбъектаБД;
	ЭтаФорма.НавигационнаяСсылкаОбъектаБД = ПолучитьНавигационнуюСсылку(ОбъектБД);
	ЭтаФорма.ДатаИВремяСоздания = ОбъектОбработка.GUIDПолучитьДатуИВремяСозданияОбъектаНаСервере(ОбъектБД);
	ЭтаФорма.СостояниеОбъектаБД	= ОбъектОбработка.ОбъектБДПолучитьСостояниеОбъекта(ОбъектБД);
	
КонецПроцедуры

// Вкладка "Свойства Объекта" и др. Сформировать Основные ТабличныеЧасти выбранного ОбъектаБД.
//
&НаСервере
Процедура ОбъектБДСформироватьТЧасти(ОбъектОбработка, мдОбъектаБД, ОбъектБД, ВидТабличнойЧасти = "ТабличныеЧасти")
	
	ТипОбъектаБД = Объект.ТипОбъектаБД;
	
	Если НЕ ЗначениеЗаполнено(ОбъектБД) 
		ИЛИ (ВидТабличнойЧасти = "ТабличныеЧасти" И мдОбъектаБД[ВидТабличнойЧасти].Количество() = 0) Тогда
		Возврат;
	КонецЕсли;

	МассивУдаляемыхРеквизитовФормы 	= Новый Массив;
	МассивУдаляемыхЭлементовФормы	= Новый Массив;
	
	// Заполнение табличных частей ОбъектаБД.
	// 1. Создание Реквизита Формы типа "ТаблицаЗначений".
	// 2. Создание Реквизита Формы типа "ГруппаФормы" для размещения ТаблицыЗначений.
	// 3. Создание Реквизита Формы типа "ТаблицаФормы" на ГруппеФормы.
	Для Каждого мдТЧ ИЗ мдОбъектаБД[ВидТабличнойЧасти] Цикл
		
		СчитыватьДанные = Ложь;
		Если Объект.ТипОбъектаБД = "Справочник" ИЛИ Объект.ТипОбъектаБД = "ПланВидовХарактеристик" Тогда
			Если (мдТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы И ОбъектБД.ЭтоГруппа)
				ИЛИ (мдТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента И НЕ ОбъектБД.ЭтоГруппа)
				ИЛИ (мдТЧ.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента) Тогда
				СчитыватьДанные = Истина;
			КонецЕсли;
		Иначе
			СчитыватьДанные = Истина;
		КонецЕсли;
		
		Если СчитыватьДанные Тогда
			
			хИмя = ФормаПолучитьИмяРеквизитаТабличнаяЧасть(мдТЧ.Имя);
			РеквизитТаблицаТЧ = ФормаСоздатьРеквизитТипаТаблицаЗначений(хИмя, ОбъектБД[мдТЧ.Имя].Выгрузить());
			Если ДобавленныеРеквизиты.НайтиПоЗначению(хИмя) = Неопределено Тогда
				ДобавленныеРеквизиты.Добавить(хИмя);
			КонецЕсли;

			хИмя = ФормаПолучитьИмяСтраницыТабличнаяЧасть(мдТЧ.Имя);
			ФормаСтраницаТЧ = ФормаСоздатьРеквизитТипаГруппаФормы(хИмя, мдТЧ.Представление(), Элементы.СтраницыТабличныеЧасти);
			Если ДобавленныеЭлементы.НайтиПоЗначению(хИмя) = Неопределено Тогда
				ДобавленныеЭлементы.Добавить(хИмя);
			КонецЕсли;
			
			хИмя = ФормаПолучитьИмяТаблицыТабличнаяЧасть(мдТЧ.Имя);
			ФормаТаблицаТЧ	= ФормаСоздатьРеквизитТипаТаблицаФормы(хИмя, ФормаПолучитьИмяРеквизитаТабличнаяЧасть(мдТЧ.Имя), РеквизитТаблицаТЧ, ФормаСтраницаТЧ);
			Если ТипОбъектаБД = ИмяТипаПланыСчетов() И мдТЧ.Имя = "ВидыСубконто" Тогда
				Элементы.СтраницаТЧВидыСубконто.Заголовок = мдТЧ.Представление() + " (МАХ " + мдОбъектаБД.МаксКоличествоСубконто + ")";
			КонецЕсли;
		
			ФормаСтраницаТЧ.Видимость = (НЕ Объект.СкрыватьПустыеТабличныеЧасти ИЛИ Объект.СкрыватьПустыеТабличныеЧасти И ОбъектБД[мдТЧ.Имя].Количество() > 0);	//+yuraos, 10.09.2015
			
		Иначе
			Если Объект.ПоказыватьСообщения Тогда
				Сообщить("МФ: Табличная часть: " + мдТЧ.Имя + " не формируется, т.к. используется для: " + мдТЧ.Использование + " !");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.СтраницаТабличныеЧасти.Видимость = Истина;
	Если Объект.ПоказатьДвижения Тогда
		Элементы.СтраницаРегистрыСведений.Видимость 	= Истина;
		Элементы.СтраницаРегистрыНакоплений.Видимость 	= Истина;
		Элементы.СтраницаРегистрыБухгалтерии.Видимость 	= Истина;
		Элементы.СтраницаРегистрыРасчета.Видимость 		= Истина;
	КонецЕсли;
	
	// Удаление Реквизитов и Элементов, согласно ранее сформированным Массивам: МассивУдаляемыхРеквизитовФормы и МассивУдаляемыхЭлементовФормы.
	Если МассивУдаляемыхРеквизитовФормы.Количество() > 0 Тогда
		ФормаУдалитьПустыеТаблицы(МассивУдаляемыхРеквизитовФормы, МассивУдаляемыхЭлементовФормы);
	КонецЕсли;
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование таблиц ТабличныеЧасти завершено.");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбъектБДПоказатьФОпцииРеквизитов(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	
	ОбъектОбработка.ПолучитьФункциональныеОпцииРеквизитовОбъекта(ОбъектБД, Объект.ТРеквизитыОбъекта, Неопределено, Истина);
	
	Для Каждого ЭлементТЧасть ИЗ Элементы.СтраницыТабличныеЧасти.ПодчиненныеЭлементы Цикл
		ОбъектОбработка.ПолучитьФункциональныеОпцииРеквизитовОбъекта(ОбъектБД, ЭлементТЧасть, СтрЗаменить(ЭлементТЧасть.Имя, "СтраницаТЧ", ""), Истина);
	КонецЦикла;
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование ф.опций реквизитов завершено.");
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Реквизиты Объекта" и др. Создать Реквизит Формы типа ТаблицаЗначений.
//
&НаСервере
Функция ФормаСоздатьРеквизитТипаТаблицаЗначений(ИмяРеквизита, ТаблицаТЧРЕГ)
	
	// Сдвиг Колонок актуально для Регистров.
	
	Колонка = ТаблицаТЧРЕГ.Колонки.Найти("НомерСтроки");
	Если Колонка <> Неопределено Тогда
		Индекс = ТаблицаТЧРЕГ.Колонки.Индекс(Колонка);
		ТаблицаТЧРЕГ.Колонки.Сдвинуть(Индекс, -Индекс);
	КонецЕсли;
	
	Колонка = ТаблицаТЧРЕГ.Колонки.Найти("Период");
	Если Колонка <> Неопределено Тогда
		Индекс = ТаблицаТЧРЕГ.Колонки.Индекс(Колонка);
		ТаблицаТЧРЕГ.Колонки.Сдвинуть(Индекс, ТаблицаТЧРЕГ.Колонки.Количество()-Индекс-1);
	КонецЕсли;
	
	Колонка = ТаблицаТЧРЕГ.Колонки.Найти("Регистратор");
	Если Колонка <> Неопределено Тогда
		Индекс = ТаблицаТЧРЕГ.Колонки.Индекс(Колонка);
		ТаблицаТЧРЕГ.Колонки.Сдвинуть(Индекс, ТаблицаТЧРЕГ.Колонки.Количество()-Индекс-1);
	КонецЕсли;

	Если НЕ ФормаЕстьРеквизит(ИмяРеквизита) Тогда
		// Добавление Реквизита в Форму
		НовыеРеквизиты = Новый Массив;
		Реквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("ТаблицаЗначений"), , ИмяРеквизита, Ложь);
		НовыеРеквизиты.Добавить(Реквизит);
	
		ИзменитьРеквизиты(НовыеРеквизиты);
	КонецЕсли;
	
	ЭтаФорма.ЗначениеВРеквизитФормы(ТаблицаТЧРЕГ, ИмяРеквизита);
	Возврат ТаблицаТЧРЕГ;
	
КонецФункции

// Вкладка "Свойства Объекта" и др. Создать Элемент формы типа Страница
// - Необходимо для последующего размещения Элемента формы типа ТаблицаФормы.
//
&НаСервере
Функция ФормаСоздатьРеквизитТипаГруппаФормы(ИмяРеквизита, Заголовок, Родитель)
	
	Если НЕ ФормаЕстьЭлемент(ИмяРеквизита) Тогда
		// Добавление Реквизита в Форму
		СтраницаФормы = Элементы.Добавить(ИмяРеквизита, Тип("ГруппаФормы"), Родитель);
		СтраницаФормы.Вид                      = ВидГруппыФормы.Страница;
		СтраницаФормы.Заголовок                = Заголовок;
		СтраницаФормы.РастягиватьПоВертикали   = Истина;
		СтраницаФормы.РастягиватьПоГоризонтали = Истина;
	Иначе	
		СтраницаФормы = Элементы[ИмяРеквизита];
	КонецЕсли;
	
	Возврат СтраницаФормы;
	
КонецФункции

// Вкладка "Свойства Объекта" и др. Создать Элемент формы ТаблицаФормы на Элементе формы СтраницаФормы.
// - ИмяПоляТаблицыФормы - Имя элемента на Форме типа Таблица.
// - ИмяРеквизитаДанныеФормыКоллекция - Имя реквизита формы, который необходимо создать.
// - ТаблицаЗначений - Создаваемая таблица.
//
&НаСервере
Функция ФормаСоздатьРеквизитТипаТаблицаФормы(ИмяПоляТаблицыФормы, ИмяРеквизитаДанныеФормыКоллекция, ТаблицаЗначений, Родитель)
	
	Если НЕ ФормаЕстьЭлемент(ИмяПоляТаблицыФормы) Тогда
		
		ТаблицаФормы = Элементы.Добавить(ИмяПоляТаблицыФормы, Тип("ТаблицаФормы"), Родитель);
		ТаблицаФормы.ПутьКДанным = ИмяРеквизитаДанныеФормыКоллекция;
		ТаблицаФормы.ЧередованиеЦветовСтрок = Истина;
		ТаблицаФормы.ВысотаШапки = 2;
	
		// Процедура "ФормаТаблицаПередНачаломИзменения" должна быть Экпортируемой.
		ТаблицаФормы.УстановитьДействие("ПередНачаломИзменения", 	"ТРеквизитыПередНачаломИзменения");
		ТаблицаФормы.УстановитьДействие("ПриАктивизацииПоля", 		"ТРеквизитыПриАктивизацииПоля");
		
		// Удаляемые реквизиты Формы.
		УдаляемыеРеквизиты = Новый Массив;
		РеквизитыДляУдаления = ПолучитьРеквизиты(ИмяРеквизитаДанныеФормыКоллекция);
		Для Каждого РеквизитУдаления ИЗ РеквизитыДляУдаления Цикл
			УдаляемыеРеквизиты.Добавить(ИмяРеквизитаДанныеФормыКоллекция + "." + РеквизитУдаления.Имя);
			// Удаляем элементы формы.
			Элементы.Удалить(Элементы[ИмяПоляТаблицыФормы + РеквизитУдаления.Имя]);
		КонецЦикла;
		
		// При наличии в ТЧ Колонки с типом "ХранилищеЗначения" на ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты); выходит на ошибку,
		// поэтому: Произвольный тип.
		
		// Добавляемые реквизиты Формы.
		ДобавляемыеРеквизиты = Новый Массив;
		Для Каждого КолонкаТЧРЕГ ИЗ ТаблицаЗначений.Колонки Цикл
			Если КолонкаТЧРЕГ.ТипЗначения.СодержитТип(Тип("ХранилищеЗначения")) Тогда	// Произвольный тип.	// ХранилищеЗначения может быть только единственным типом в данных.
				ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(КолонкаТЧРЕГ.Имя, Новый ОписаниеТипов, ИмяРеквизитаДанныеФормыКоллекция, КолонкаТЧРЕГ.Заголовок));
			Иначе
				ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(КолонкаТЧРЕГ.Имя, КолонкаТЧРЕГ.ТипЗначения, ИмяРеквизитаДанныеФормыКоллекция, КолонкаТЧРЕГ.Заголовок));
			КонецЕсли;
		КонецЦикла;
		ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
		
		//+yuraos, 10.09.2015
		ТаблицаФормы.УстановитьДействие("Выбор", "ТФормаТаблицаВыбор");
		ThisInitiate(); 
		//+yuraos, 10.09.2015
		
		// Добавление элементов в Таблицу Формы.
		Для Каждого КолонкаТЧРЕГ ИЗ ТаблицаЗначений.Колонки Цикл
			
			//+yuraos, 10.09.2015
			ПолеТаблицыФормы = Неопределено;
			ЭтоПростойТип = Null;
			ЭтоБулево = This.ОТДСодержитТип(КолонкаТЧРЕГ.ТипЗначения,Истина,ЭтоПростойТип);
			ЭтоСсылки = This.ОТДСодержитСсылки(КолонкаТЧРЕГ.ТипЗначения);
			//+yuraos, 10.09.2015
			
			Попытка
				ПолеТаблицыФормы = Элементы.Добавить(ТаблицаФормы.Имя + КолонкаТЧРЕГ.Имя, Тип("ПолеФормы"), ТаблицаФормы);
				ПолеТаблицыФормы.Вид 					= ВидПоляФормы.ПолеВвода;
				ПолеТаблицыФормы.Заголовок				= КолонкаТЧРЕГ.Заголовок;
				ПолеТаблицыФормы.РежимРедактирования	= РежимРедактированияКолонки.Вход;
				ПолеТаблицыФормы.ВыделятьОтрицательные	= Истина;
				ПолеТаблицыФормы.ОграничениеТипа		= КолонкаТЧРЕГ.ТипЗначения;
				
				ПолеТаблицыФормы.ПутьКДанным			= ТаблицаФормы.ПутьКДанным + "." + КолонкаТЧРЕГ.Имя;
			Исключение
				// МА! Проблема решена: МенеджерРегистра.СоздатьНаборЗаписей() вместо Запрос по Регистру.
				Сообщить("МФ: Ошибка ПолеТаблицыФормы.ПутьКДанным = " + ТаблицаФормы.ПутьКДанным + "." + КолонкаТЧРЕГ.Имя);
			КонецПопытки;
			
			//+yuraos, 10.09.2015
			Если ПолеТаблицыФормы <> Неопределено Тогда
				Если ЭтоБулево = Истина И ЭтоПростойТип = Истина Тогда
					ПолеТаблицыФормы.Вид = ВидПоляФормы.ПолеФлажка;
				ИначеЕсли ЭтоСсылки = Истина Тогда
					ПолеТаблицыФормы.КнопкаОткрытия = Истина;
					ПолеТаблицыФормы.УстановитьДействие("Открытие","ТФормаКолонкаОткрытие");
				КонецЕсли; 
			КонецЕсли; 
			//+yuraos, 10.09.2015
			
		КонецЦикла;
		
		ТаблицаФормы.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	Иначе
		ТаблицаФормы = Элементы[ИмяПоляТаблицыФормы];
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ТаблицаЗначений, ИмяРеквизитаДанныеФормыКоллекция);
	Возврат ТаблицаФормы;
	
КонецФункции

// Флажок "Показать движения". Видимость Элементов формы типа СтраницаРегистра.
//
&НаКлиенте
Процедура ПоказатьДвиженияПриИзменении(Элемент)
		
	Если Объект.ПоказатьДвижения Тогда
		ФормаВывестиВСостояние(, , "Производится формирование таблиц движений по регистрам ...");
		
		Элементы.СтраницаРегистрыСведений.Видимость 	= Истина;
		Элементы.СтраницаРегистрыНакоплений.Видимость 	= Истина;
		Элементы.СтраницаРегистрыБухгалтерии.Видимость 	= Истина;
		Элементы.СтраницаРегистрыРасчета.Видимость 		= Истина;
		
		ОбъектБДСформироватьТДвижений(Объект.ОбъектБД);
	Иначе
		Элементы.СтраницаРегистрыСведений.Видимость 	= Ложь;
		Элементы.СтраницаРегистрыНакоплений.Видимость 	= Ложь;
		Элементы.СтраницаРегистрыБухгалтерии.Видимость 	= Ложь;
		Элементы.СтраницаРегистрыРасчета.Видимость 		= Ложь;
	КонецЕсли;
	
	//+yuraos, 10.09.2015
	Если ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	Иначе
		ФормаОпределитьДоступнность(Неопределено);
	КонецЕсли;
	//+yuraos, 10.09.2015
	
КонецПроцедуры

// Вкладка "Ссылки на Объект". Флажок "Отключатель триггеров записи".
// (Необходимо записать объект без проверок, например, без проверки на уникальность кода и т.п.)
// Предназначено для изменения становки признака загрузки объектов, что приводит к минимуму проверок объекта при записи, 
// поскольку считается, что объект получен через механизмы обмена и все данные корректны.
// 
&НаКлиенте
Процедура ОбменДаннымиЗагрузкаПриИзменении(Элемент)
	
	Если Объект.ОбменДаннымиЗагрузка Тогда
		ПредупреждениеСообщение(, "ОТКЛЮЧИТЬ ТРИГГЕРЫ ЗАПИСИ (<Объект изменяемый>.ОбменДанными.Загрузка = Истина):
		|
		|Изменение значения вляет только на поведение обработки, но не на сеанс работы в 1С.
		|
		|1С: Если значение данного свойства Истина, 
		|то при выполнении записи или удаления данных будет производиться минимум проверок, 
		|так как при этом делается предположение, что производится запись данных, 
		|полученных через механизмы обмена данными, и эти данные корректны.
		|
		|Триггер (Trigger) - это особый вид хранимых процедур, автоматически выполняемых 
		|при исполнении оператора Update, Insert или Delete над данными таблицы.",, "УСТАНОВЛЕН РЕЖИМ:");
	Иначе	
		ПредупреждениеСообщение(, "ВКЛЮЧИТЬ ТРИГГЕРЫ ЗАПИСИ (<Объект изменяемый>.ОбменДанными.Загрузка = Ложь):
		|
		|Изменение значения вляет только на поведение обработки, но не на сеанс работы в 1С.
		|
		|1С: Если значение данного свойства Истина, 
		|то при выполнении записи или удаления данных будет производиться минимум проверок, 
		|так как при этом делается предположение, что производится запись данных, 
		|полученных через механизмы обмена данными, и эти данные корректны.
		|
		|Триггер (Trigger) - это особый вид хранимых процедур, автоматически выполняемых 
		|при исполнении оператора Update, Insert или Delete над данными таблицы.",, "УСТАНОВЛЕН РЕЖИМ:");
	КонецЕсли;

КонецПроцедуры

// Вкладка "Свойства Объекта" и др. Сформировать Таблицы Движений выбранного ОбъектаБД (Документ).
// - Создание Реквизита Формы типа ТаблицаЗначений.
// - Создание Элемента Формы типа Страница.
// - Создание Элемента Формы типа ТаблицаФормы в Элементе формы типа Страница.
//
&НаСервере
Процедура ОбъектБДСформироватьТДвижений(ОбъектБД)
	
	Если НЕ Объект.ПоказатьДвижения 
		ИЛИ НЕ Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ОбъектБД)) 
		ИЛИ ОбъектБД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	мдОбъектаБД = ОбъектБД.Метаданные();
	
	ОбязательноПоказать = НЕ Объект.СкрыватьПустыеТаблицыДвиженийРегистров И (мдОбъектаБД.Имя = "КорректировкаРегистров" ИЛИ мдОбъектаБД.Имя = "КорректировкаЗаписейРегистров" ИЛИ мдОбъектаБД.Движения.Количество() > 20);
	Если Объект.ПоказыватьСообщения ИЛИ ОбязательноПоказать Тогда
		Если ОбязательноПоказать Тогда
			Сообщить("МФ: Общее количество движений: " + мдОбъектаБД.Движения.Количество() + ".");
			Сообщить("МФ: Для того, чтобы не выводить на экрант таблицы Регистров, в которых документ не осуществил движения 
			|воспользуйтесь настройкой параметра ""Скрывать пустые таблицы движений регистров"".");
		КонецЕсли;
		Если Объект.СкрыватьПустыеТаблицыДвиженийРегистров Тогда
			Сообщить("МФ: " + "Некоторые таблицы (пустые) ""Движения по Регистрам"" не отображаются." , СтатусСообщения.Важное);
		КонецЕсли;
		Сообщить("МФ: " + "Формирование таблиц ""Движения по Регистрам"". " + ТекущаяДата() + " Подождите ... " , СтатусСообщения.Важное);
	КонецЕсли;
	
	// Органичение выводимых на экран Страниц+Таблиц Движений Регистров:
	// 
	// Последовательность действий:
	//
	// 1. СкрыватьПустыеТаблицыДвиженийРегистров = Ложь;
	// 2. ПоказатьДвижения = Истина; 	- Показали ВСЕ движения, в т.ч. и пустые таблицы.
	// 3. СкрыватьПустыеТаблицыДвиженийРегистров = Истина;
	// 4. ПоказатьДвижения = Ложь;		- Скрыли Страницы Движений.
	// 5. ПоказатьДвижения = Истина;	- Показали ТОЛЬКО непустые.
	
	МассивУдаляемыхРеквизитовФормы 	= Новый Массив;
	МассивУдаляемыхЭлементовФормы	= Новый Массив;
	
	//+yuraos, 10.09.2015
	КартинкаНулл = Новый Картинка;
	МассивУдаляемыхКоманд = Новый Массив;
	ИмяКомандыПрефикс1 = "ОбъектБДИзменятьТДвижений_";
	ИмяКомандыПрефикс2 = "ОбъектБДЗаписатьТДвижений_";
	// Удаляем команды старых контекстных меню таблиц движений документа.
	Для Каждого Команда ИЗ Команды Цикл
		Если Лев(Команда.Имя,СтрДлина(ИмяКомандыПрефикс1)) = ИмяКомандыПрефикс1 Тогда
			МассивУдаляемыхКоманд.Добавить(Команда);
		ИначеЕсли Лев(Команда.Имя,СтрДлина(ИмяКомандыПрефикс2)) = ИмяКомандыПрефикс2 Тогда
			МассивУдаляемыхКоманд.Добавить(Команда);
		КонецЕсли; 
	КонецЦикла; 
	Для Каждого Команда ИЗ МассивУдаляемыхКоманд Цикл
		Команды.Удалить(Команда);
	КонецЦикла; 
	МассивУдаляемыхКоманд.Очистить();
	// Удаляем кнопки старых контекстных меню таблиц движений документа.
	Для Каждого Элемент ИЗ Элементы Цикл
		Если ТипЗнч(Элемент) <> Тип("КнопкаФормы") Тогда
			Продолжить;
		КонецЕсли; 
		Если Лев(Элемент.Имя,СтрДлина(ИмяКомандыПрефикс1)) = ИмяКомандыПрефикс1 Тогда
			МассивУдаляемыхЭлементовФормы.Добавить(Элемент);
		ИначеЕсли Лев(Элемент.Имя,СтрДлина(ИмяКомандыПрефикс2)) = ИмяКомандыПрефикс2 Тогда
			МассивУдаляемыхЭлементовФормы.Добавить(Элемент);
		КонецЕсли; 
	КонецЦикла;
	Для Каждого Элемент ИЗ МассивУдаляемыхЭлементовФормы Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	МассивУдаляемыхЭлементовФормы.Очистить();
	//+yuraos, 10.09.2015
	
	мдОбъектаБД = ОбъектБД.Метаданные();
	Для Каждого МетаданныеРегистра ИЗ мдОбъектаБД.Движения Цикл
		Если Метаданные.РегистрыСведений.Индекс(МетаданныеРегистра) > -1 Тогда
			ТипРегистра 		= "РегистрСведений";
			МенеджерРегистра	= РегистрыСведений[МетаданныеРегистра.Имя];
			ЭлементРодитель		= Элементы.СтраницыРС;
		ИначеЕсли Метаданные.РегистрыНакопления.Индекс(МетаданныеРегистра) > -1 Тогда
			ТипРегистра 		= "РегистрНакопления";
			МенеджерРегистра	= РегистрыНакопления[МетаданныеРегистра.Имя];
			ЭлементРодитель 	= Элементы.СтраницыРН;
		ИначеЕсли Метаданные.РегистрыБухгалтерии.Индекс(МетаданныеРегистра) > -1 Тогда
			ТипРегистра 		= "РегистрБухгалтерии";
			МенеджерРегистра	= РегистрыБухгалтерии[МетаданныеРегистра.Имя];
			ЭлементРодитель 	= Элементы.СтраницыРБ;
		ИначеЕсли Метаданные.РегистрыРасчета.Индекс(МетаданныеРегистра) > -1 Тогда
			ТипРегистра 		= "РегистрРасчета";
			МенеджерРегистра	= РегистрыРасчета[МетаданныеРегистра.Имя];
			ЭлементРодитель 	= Элементы.СтраницыРР;
		КонецЕсли;
		
		НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();	// Быстрее Запроса ~ 4 раза (замер производительности) + Исправляет ошибку в ФормаСоздатьРеквизитТипаТаблицаФормы().
		НаборЗаписей.Отбор.Регистратор.Установить(ОбъектБД);
		
		НаборЗаписей.Прочитать();
		
		ТаблицаРегистра = НаборЗаписей.Выгрузить();
		
		// Удалим "лишние" - "неполезные" колонки.
		ТаблицаРегистра.Колонки.Удалить("Регистратор");
		Если ТаблицаРегистра.Колонки.Найти("МоментВремени") <> Неопределено Тогда
			ТаблицаРегистра.Колонки.Удалить("МоментВремени");
		КонецЕсли;
		
		// Заполнение Таблиц Реквизитов - Движения ОбъектаБД.
		// 1. Создание Реквизита Формы типа "ТаблицаЗначений".
		// 2. Создание Реквизита Формы типа "ГруппаФормы" для размещения ТаблицыЗначений.
		// 3. Создание Реквизита Формы типа "ТаблицаФормы" на ГруппеФормы.
		
		хИмя = ФормаПолучитьИмяРеквизитаРегистр(МетаданныеРегистра.Имя);
		РеквизитРегистр = ФормаСоздатьРеквизитТипаТаблицаЗначений(хИмя, ТаблицаРегистра);
		Если ДобавленныеРеквизиты.НайтиПоЗначению(хИмя) = Неопределено Тогда
			ДобавленныеРеквизиты.Добавить(хИмя);
		КонецЕсли;
		
		хИмя = ФормаПолучитьИмяСтраницыРегистр(МетаданныеРегистра.Имя);
		ГруппаФормы = ФормаСоздатьРеквизитТипаГруппаФормы(хИмя, МетаданныеРегистра.Представление(), ЭлементРодитель);
		Если ДобавленныеЭлементы.НайтиПоЗначению(хИмя) = Неопределено Тогда
			ДобавленныеЭлементы.Добавить(хИмя);
		КонецЕсли;
		
		хИмя = ФормаПолучитьИмяТаблицыРегистр(МетаданныеРегистра.Имя);
		ТаблицаФормы = ФормаСоздатьРеквизитТипаТаблицаФормы(хИмя, ФормаПолучитьИмяРеквизитаРегистр(МетаданныеРегистра.Имя), РеквизитРегистр, ГруппаФормы);

		ТаблицаФормы.ИзменятьПорядокСтрок	= Ложь;
		ТаблицаФормы.ИзменятьСоставСтрок	= Ложь;
		ТаблицаФормы.ТолькоПросмотр			= Истина;
		ТаблицаФормы.Доступность			= Истина;
		
		//+yuraos, 10.09.2015
		// Добавим команды и кнопки контекстного меню для ТаблицаФормы.
		// 1. Команда для включения редактирования набора движений документа.
		//ИмяКомандыПрефикс1 = "ОбъектБДИзменятьТДвижений_";
		КомандаИмя = ИмяКомандыПрефикс1 + МетаданныеРегистра.Имя;
		Команда = Команды.Добавить(КомандаИмя);
		ЗаполнитьЗначенияСвойств(Команда,Команды[СтрЗаменить(ИмяКомандыПрефикс1, "_", "")], , "Имя,СочетаниеКлавиш");
		Кнопка = Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), ТаблицаФормы.КонтекстноеМеню);
		Кнопка.ИмяКоманды = Команда.Имя;
		Кнопка.Доступность = ТаблицаФормы.ТолькоПросмотр;
		// 2. Команда для записи редактируемого набора движений документа в базу.
		//ИмяКомандыПрефикс2 = "ОбъектБДЗаписатьТДвижений_";
		КомандаИмя = ИмяКомандыПрефикс2 + МетаданныеРегистра.Имя;
		Команда = Команды.Добавить(КомандаИмя);
		ЗаполнитьЗначенияСвойств(Команда,Команды[СтрЗаменить(ИмяКомандыПрефикс2, "_", "")], , "Имя,СочетаниеКлавиш");
		Кнопка = Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), ТаблицаФормы.КонтекстноеМеню);
		Кнопка.ИмяКоманды = Команда.Имя;
		Кнопка.Доступность = НЕ ТаблицаФормы.ТолькоПросмотр;
		// 3. Видимость и оформление родительской ГруппаФормы.
		ГруппаФормы.Видимость = (НЕ Объект.СкрыватьПустыеТаблицыДвиженийРегистров ИЛИ Объект.СкрыватьПустыеТаблицыДвиженийРегистров И ТаблицаРегистра.Количество() > 0); 
		ГруппаФормы.Картинка = КартинкаНулл;
		//+yuraos, 10.09.2015
		
	КонецЦикла;
	
	// Удаление Реквизитов и Элементов, согласно ранее сформированным Массивам: МассивУдаляемыхРеквизитовФормы и МассивУдаляемыхЭлементовФормы.
	Если МассивУдаляемыхРеквизитовФормы.Количество() > 0 Тогда
		ФормаУдалитьПустыеТаблицы(МассивУдаляемыхРеквизитовФормы, МассивУдаляемыхЭлементовФормы);
	КонецЕсли;
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование таблиц Движения по Регистрам завершено.");
	КонецЕсли;
		
КонецПроцедуры

Процедура ФормаУдалитьПустыеТаблицы(МассивУдаляемыхРеквизитовФормы, МассивУдаляемыхЭлементовФормы)
	Перем Элемент, Удаляемый;
	
	// Удаление Реквизитов и Элементов, согласно ранее сформированным Массивам: МассивУдаляемыхРеквизитовФормы и МассивУдаляемыхЭлементовФормы.
	Если МассивУдаляемыхРеквизитовФормы.Количество() > 0 Тогда
		ИзменитьРеквизиты(, МассивУдаляемыхРеквизитовФормы);
		Для Каждого Элемент ИЗ МассивУдаляемыхРеквизитовФормы Цикл
			Удаляемый = ДобавленныеРеквизиты.НайтиПоЗначению(Элемент);
			Если НЕ Удаляемый = Неопределено Тогда
				ДобавленныеРеквизиты.Удалить(Удаляемый);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Элемент ИЗ МассивУдаляемыхЭлементовФормы Цикл
			Удаляемый = ДобавленныеЭлементы.НайтиПоЗначению(Элемент);
			Если НЕ Удаляемый = Неопределено Тогда
				ФормаУдалитьЭлементНаСервере(Элемент);
				ДобавленныеЭлементы.Удалить(Удаляемый);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Открыть ОбъектБД. Нестандартная обработка открятия.
//
&НаКлиенте
Процедура ОбъектБДОткрытие(Элемент, СтандартнаяОбработка)
	Перем РежимОткрытияФормы;
	
	Если Объект.ОбъектБД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	РежимОткрытияФормы = ОткрытьФормуОбъектаМодальноИлиНемодальноВРежимеБлокировкиВладельца(Объект.ОбъектБД, СтандартнаяОбработка);
	Если РежимОткрытияФормы = "Модально" Тогда
		// Флажок Форма....Модифицированность не всегда адекватен производимым действиям - поэтому:
		ОбъектБДПриИзменении("ОбъектБДОткрытие");
		ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ОбъектБД, ВидИзмененияДанных.Изменение);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ОткрытьФормуОбъектаМодальноИлиНемодальноВРежимеБлокировкиВладельца(Знач Ключ, СтандартнаяОбработка)
	Перем РежимОткрытияФормы;
	
	Попытка
		СтандартнаяОбработка = Ложь;
		ПолноеСтроковоеИмяТипа = ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Ключ);
		СтруктураПараметров = Новый Структура("Ключ", Ключ);
		
		ФормаОбъектаБД = ПолучитьФорму(ПолноеСтроковоеИмяТипа + ".ФормаОбъекта", СтруктураПараметров, ЭтаФорма);
		ФормаОбъектаБД.ОткрытьМодально();
		РежимОткрытияФормы = "Модально";
	Исключение
		Попытка
			СтандартнаяОбработка = Ложь;
			ПолноеСтроковоеИмяТипа = ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Ключ);
			СтруктураПараметров = Новый Структура("Ключ", Ключ);
			
			ФормаОбъектаБД = ПолучитьФорму(ПолноеСтроковоеИмяТипа + ".ФормаОбъекта", СтруктураПараметров, ЭтаФорма);
			ФормаОбъектаБД.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			ФормаОбъектаБД.Открыть();
			
			Если Ключ = Объект.ОбъектБД Тогда
				Сообщить("После внесения изменений в стандартной ФормеОбъекта необходимо выполнить ""Обновить"" в форме обработки для отображения внесенных изменений.");
			КонецЕсли;
			
			РежимОткрытияФормы = "БлокировкаВладельца";
		Исключение
			СтандартнаяОбработка = Истина;
			
			Если Ключ = Объект.ОбъектБД Тогда
				Сообщить("После внесения изменений в стандартной ФормеОбъекта необходимо выполнить ""Обновить"" в форме обработки для отображения внесенных изменений.");
			КонецЕсли;
			
			РежимОткрытияФормы = "Стандартно";
		КонецПопытки;
	КонецПопытки;
	
	Возврат РежимОткрытияФормы;
	
КонецФункции

// Кнопка "Очистить". Очистка формы.
//
&НаКлиенте
Процедура ОбъектБДОчистка(Элемент, СтандартнаяОбработка = Истина) //*yuraos, 10.09.2015	//Процедура ОбъектБДОчистка(Элемент) //-yuraos, 10.09.2015
	СтандартнаяОбработка = Ложь; //+yuraos, 10.09.2015
	
	ОчиститьСообщения();
	
	Объект.ОбъектБД 						= Неопределено;
	Элементы.ОбъектБД.ОграничениеТипа		= Новый ОписаниеТипов("Неопределено");
	Объект.ТипОбъектаБД						= Неопределено;
	Объект.ИмяОбъектаБД						= "";
	Объект.ПолноеСтроковоеИмяТипа 			= "";
	
	Элементы.ВсегоСсылок.Заголовок 			= "∑:";
	
	Объект.ПроведениеРазрешено				= Ложь;
	Объект.ИмяРеквизитаДокументаОснования 	= "";
	Объект.ЕстьДокументОснование			= Ложь;
	
	Элементы.ОбъектБД.Заголовок				= "ОбъектБД";
	Элементы.ОбъектБД.ЦветТекстаЗаголовка	= Новый Цвет(65, 48, 3);	// Цвет текста формы.

	Объект.ПоказатьДвижения 				= Ложь;
	
	Объект.ВыполнятьВТранзакции				= Истина;
	
	ФормаОчистить();	// После Объекта.
	
	// Вкладка "Дополнительно" - "Ссылки на Объект".
	ТСсылкиОчиститьПараметры();

	// Вкладка "Дополнительно" - "Справочник (Дополнительно)".
	ТСправочникОчиститьПараметры();
	
	// Вкладка "Дополнительно" - "Документ (Дополнительно)".
	ТДокументПеренумерацияОчиститьПараметры();
	
	// Вкладка "Дополнительно" - "Документ (Статистика)".
	ДокументСтатистикаОчиститьПараметры();
	
	// Вкладка "Дополнительно" - "Документ (Движения)".
	ДокументДвиженияОчиститьПараметры();
	
	// Вкладка "Дополнительно" - "Документ (Удаление)".
	ДокументУдалениеОчиститьПараметры();
	
	// План Обмена. Реанимация подчиненного узла. Очистка регистрации измененний.
	ПланОбменаОчиститьПараметры();
	
	// Восстановление "Битых" ссылок.
	GUIDОчиститьПараметры();
	
	// Строка информации.
	ЭтаФорма.СостояниеОбъектаБД	= "";
	Объект.IDОбъектаБД			= Неопределено;
	ЭтаФорма.IDОбъектаБДСтрока	= "";
	ЭтаФорма.НавигационнаяСсылкаОбъектаБД = Неопределено;
	ЭтаФорма.ДатаИВремяСоздания = Неопределено;
	
	ФормаОпределитьДоступнность(Неопределено);

КонецПроцедуры

// Кнопка "Очистить". Удаление Реквизитов, Элементов, Очистка таблиц.
//
&НаКлиенте
Процедура ФормаОчистить()
	
	//Элементы.СтраницаТабличныеЧасти.Видимость 		= Ложь;
	//Элементы.СтраницаРегистрыСведений.Видимость 		= Ложь;
	//Элементы.СтраницаРегистрыНакоплений.Видимость 	= Ложь;
	//Элементы.СтраницаРегистрыБухгалтерии.Видимость 	= Ложь;
	//Элементы.СтраницаРегистрыРасчета.Видимость 		= Ложь;
	
	// ===========================================================
	// Удаление созданных программно Элементов и Реквизитов Формы.
	Если ДобавленныеЭлементы.Количество() > 0 Тогда
		ФормаУдалитьСозданныеПрограммноЭлементыФормыИРеквизитыНаСервере();
	КонецЕсли;
	// ===========================================================
	
	// Очистка таблицы Свойства Объекта.
	Объект.ТСвойстваОбъекта.Очистить();
	// Очистка таблицы Реквизитов Объекта.
	Объект.ТРеквизитыОбъекта.Очистить();
	
	// Прочие Страницы.
	Объект.ТМетаданныеПодпискиНаСобытия.Очистить();
	Объект.ТМетаданныеФункциональныеОпции.Очистить();
	Объект.ТМетаданныеПланыОбмена.Очистить();
	Если Объект.ТМетаданныеРегистрыОбъекта.Количество() > 0 Тогда
		Элементы.ДекорКоличествоРегистровСвязанныхСОбъектом.Заголовок = "Количество уникальных регистров: ";
		Объект.ТМетаданныеРегистрыОбъекта.Очистить();
	КонецЕсли;
	Объект.ТМетаданныеВводитсяНаОсновании.Очистить();
	Объект.ТМетаданныеЯвляетсяОснованиемДля.Очистить();
	
	Если Объект.ТСсылкиСписок.Количество() > 0 Тогда
		Элементы.ДекорВсегоЗаменяемыхСсылок.Заголовок = "Всего: ";
		Объект.ТСсылкиСписок.Очистить();
	КонецЕсли;
	
	Если Объект.ТСправочникСписок.Количество() > 0 ИЛИ ЗначениеЗаполнено(ЭтаФорма.СправочникЭлементГруппа) ИЛИ ЗначениеЗаполнено(ЭтаФорма.СправочникДействие) Тогда
		Элементы.ДекорИерархияСправочника.Заголовок = "";
		Объект.ТСправочникСписок.Очистить();
	КонецЕсли;
	
	Объект.ТДокументСписок.Очистить();
	
	ЭтаФорма.ДокументПроведениеТСписокВидов.Очистить();
	
	ЭтаФорма.ДокументТСтатистика.Очистить();
	
	ЭтаФорма.ДокументДвиженияТСписокВидов.Очистить();
	ЭтаФорма.ДокументТДвижения.Очистить();
	
	ЭтаФорма.ДокументОснование = Неопределено;
	ЭтаФорма.ДокументОснованиеТабличныеЧасти.Очистить();
	
	ФайлXMLОчистка(Неопределено, Истина);
	
КонецПроцедуры

// Удалить Элементы И Реквизиты Формы.
//
&НаСервере
Процедура ФормаУдалитьСозданныеПрограммноЭлементыФормыИРеквизитыНаСервере()
	
	// ===========================================================
	// Удаление созданных программно Элементов Формы.
	ФормаУдалитьЭлементЫНаСервере(ДобавленныеЭлементы);
	
	ДобавленныеЭлементы.Очистить();
	// ===========================================================
	
	// ===========================================================
	// Удаление созданных программно Реквизитов Формы.
	ФормаУдалитьМассивРеквизитовНаСервере(ДобавленныеРеквизиты);
	
	ДобавленныеРеквизиты.Очистить();
	// ===========================================================
	
КонецПроцедуры

// Удалить Элементы Формы.
//
&НаСервере
Процедура ФормаУдалитьЭлементЫНаСервере(УдаляемыеЭлементы)
	
	Для Каждого Элемент ИЗ УдаляемыеЭлементы Цикл
		ФормаУдалитьЭлементНаСервере(Элемент.Значение);
	КонецЦикла;
	
КонецПроцедуры

// Удалить Элемент Формы.
//
&НаСервере
Процедура ФормаУдалитьЭлементНаСервере(Имя)
	
	ЭлементФормы = Элементы.Найти(Имя);
	Если ЭлементФормы <> Неопределено Тогда
		Элементы.Удалить(ЭлементФормы);
	КонецЕсли;
	
КонецПроцедуры

// Удалить Реквизит Формы.
//
&НаСервере
Процедура ФормаУдалитьМассивРеквизитовНаСервере(МассивРеквизитов)
	
	Если МассивРеквизитов.Количество() > 0 Тогда
		УдаляемыеРеквизиты = Новый Массив;
		Для Каждого Реквизит ИЗ МассивРеквизитов Цикл
			УдаляемыеРеквизиты.Добавить(Реквизит.Значение);
		КонецЦикла;
		ИзменитьРеквизиты(, УдаляемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ТАБЛИЦА СВОЙСТВ ОБЪЕКТА.
//

&НаКлиенте
Процедура ФормаТСвойстваОбъектаЗначениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	хЗаголовок = Элемент.Родитель.ТекущиеДанные.Свойство;
	Если ТипЗнч(Элемент.Родитель.ТекущиеДанные.Значение) = Тип("Строка") Тогда
		МногоСтрочныйТекст = СтрЗаменить(Элемент.ТекстРедактирования, ",", Символы.ПС);
	Иначе
		МногоСтрочныйТекст = СтрЗаменить(Элемент.ТекстРедактирования, ";", Символы.ПС);
	КонецЕсли;
	
	ПросмотретьМногострочныйТекст_Стандартно(МногоСтрочныйТекст, хЗаголовок);
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВКЛАДКА "МЕТАДАННЫЕ".
// 

// В целях оптимизации - непосредственно на Вкладке "Метаданные".
//
&НаКлиенте
Процедура кнПолучитьСписокРегистровСвязанныхСОбъектомМД(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Не выбран основной объект.");
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите. Производится формирование списка регистров,
	|в которых может присутствовать объект.");
	
	кнПолучитьСписокРегистровСвязанныхСОбъектомМДНаСервере(Объект.ОбъектБД);	// ~ 1 сек.
		
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// МО: Вкладка "Метаданные" - "Регистры".
// - Имя Регистра.
// - Измерение/Ресурс/Реквизит Регистра
// - Признак Ведущщее (только для Регистра Сведений).
//
&НаСервере
Процедура кнПолучитьСписокРегистровСвязанныхСОбъектомМДНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	
	хРезультат	= ОбъектОбработка.ПолучитьСписокРегистровСвязанныхСОбъектомМД(ОбъектБД);
	тзРегистры	= хРезультат.Таблица;
	хЗаголовок	= хРезультат.Заголовок;
	
	ЗначениеВРеквизитФормы(тзРегистры, "Объект.ТМетаданныеРегистрыОбъекта");
	
	Элементы.ДекорКоличествоРегистровСвязанныхСОбъектом.Заголовок = хЗаголовок;
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование таблицы Регистров завершено.");
	КонецЕсли;
	
КонецПроцедуры

// В целях оптимизации - непосредственно на Вкладке "Метаданные".
//
&НаКлиенте
Процедура кнПолучитьСписокПодписокНаСобытияОбъектаМД(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Не выбран основной объект.");
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите. Производится формирование списка подписок на события
	|объекта метаданных.");
	
	кнПолучитьСписокПодписокНаСобытияОбъектаМДНаСервере(Объект.ОбъектБД);	// ~ 1 сек.
		
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// МО: Вкладка "Метаданные" - "Подписки".
// - Имя Подписки.
// - Обработчик.
// - Событие.
//
&НаСервере
Процедура кнПолучитьСписокПодписокНаСобытияОбъектаМДНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	
	хРезультат		= ОбъектОбработка.ПолучитьСписокПодписокНаСобытияОбъектаМД(ОбъектБД);
	тзПодписки		= хРезультат.Таблица;
	хЗаголовок		= хРезультат.Заголовок;
	
	ЗначениеВРеквизитФормы(тзПодписки, "Объект.ТМетаданныеПодпискиНаСобытия");
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование таблицы Подписок на события завершено.");
	КонецЕсли;
	
КонецПроцедуры

// В целях оптимизации - непосредственно на Вкладке "Метаданные".
//
&НаКлиенте
Процедура кнПолучитьСписокФункциональныхОпцийОбъектаМД(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Не выбран основной объект.");
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите. Производится формирование списка функциональных опций
	|объекта метаданных.");
	
	кнПолучитьСписокФункциональныхОпцийОбъектаМДНаСервере(Объект.ОбъектБД);	// ~ 1 сек.
		
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// МО: Вкладка "Метаданные" - "Подписки".
// - Имя Подписки.
// - Обработчик.
// - Событие.
//
&НаСервере
Процедура кнПолучитьСписокФункциональныхОпцийОбъектаМДНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	
	хРезультат		= ОбъектОбработка.ПолучитьСписокФункциональныхОпцийОбъектаМД(ОбъектБД);
	тзФОпции		= хРезультат.Таблица;
	
	ЗначениеВРеквизитФормы(тзФОпции, "Объект.ТМетаданныеФункциональныеОпции");
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование таблицы Функциональные опции завершено.");
	КонецЕсли;
	
КонецПроцедуры

// В целях оптимизации - непосредственно на Вкладке "Метаданные".
//
&НаКлиенте
Процедура кнПолучитьСписокПлановОбменаОбъектаМД(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Не выбран основной объект.");
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите. Производится формирование списка планов обмена
	|объекта метаданных.");
	
	кнПолучитьСписокПлановОбменаОбъектаМДНаСервере(Объект.ОбъектБД);	// ~ 1 сек.
		
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// МО: Вкладка "Метаданные" - "Планы обмена".
// - Имя Подписки.
// - Обработчик.
// - Событие.
//
&НаСервере
Процедура кнПолучитьСписокПлановОбменаОбъектаМДНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	
	хРезультат		= ОбъектОбработка.ПолучитьСписокПлановОбменаОбъектаМД(ОбъектБД);
	тзПОбмена		= хРезультат.Таблица;
	хЗаголовок		= хРезультат.Заголовок;
	
	ЗначениеВРеквизитФормы(тзПОбмена, "Объект.ТМетаданныеПланыОбмена");
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование таблицы Ппланы обмена завершено.");
	КонецЕсли;
	
КонецПроцедуры

// В целях оптимизации - непосредственно на Вкладке "Метаданные".
//
&НаКлиенте
Процедура кнПолучитьСпискиВводНаОснованииОбъектаМД(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Не выбран основной объект.");
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите. Производится формирование списков ""ввод на основании""
	|объекта метаданных.");
	
	кнПолучитьСпискиВводНаОснованииОбъектаМДНаСервере(Объект.ОбъектБД);	// ~ 1 сек.
		
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// МО: Вкладка "Метаданные" - "Ввод на основании".
// - Объект.
//
&НаСервере
Процедура кнПолучитьСпискиВводНаОснованииОбъектаМДНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	
	хРезультат		= ОбъектОбработка.ПолучитьСписокВводНаОснованииОъектаМД(ОбъектБД);
	Таблица_1		= хРезультат.Таблица_1;
	Таблица_2		= хРезультат.Таблица_2;
	
	ЗначениеВРеквизитФормы(Таблица_1, "Объект.ТМетаданныеВводитсяНаОсновании");
	ЗначениеВРеквизитФормы(Таблица_2, "Объект.ТМетаданныеЯвляетсяОснованиемДля");
	
	Если Объект.ПоказыватьСообщения Тогда
		Сообщить("МФ: Формирование таблиц Ввод на основании завершено.");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РЕДАКТИРОВАНИЕ ОБЪЕКТА.
//

////////////////////////////////////////////////////////////////////////////////
// РЕДАКТИРОВАНИЕ РЕКВИЗИТА ОБЪЕКТА ИЛИ РЕКВИЗИТА ТАБЛИЧНОЙ ЧАСТИ.
// 

// Вкладка "Реквизиты Объекта". Событие Таблицы "ТабличнаяЧасть"... ПриАктивизацииПоля(Колонки).
// Проверить возможность редактирования значения Реквизита.
// - Если Значение имеет тип "ХранилищеЗначения" - отказаться от редактирования.
// - Если Значение имеет тип "Булево" и Имя Реквизита = "ЭтоГруппа" для Справочника - отказаться от редактирования.
// - Если Значение имеет тип Имя Реквизита = "НомерОтправленного" или "НомерПринятого" для ПланаОбмена - отказаться от редактирования.
//
&НаКлиенте
Процедура ТРеквизитыПриАктивизацииПоля(Элемент)

	Если (Элемент.ТекущиеДанные = Неопределено ИЛИ Элемент.ТекущийЭлемент = Неопределено) Тогда
		Возврат;
	КонецЕсли;

	Если Элемент.Имя = "ТРеквизиты" Тогда
		ИзменяемоеЗначение = Элемент.ТекущиеДанные.Значение;
		ТипИзменяемогоЗначения = ТипЗнч(ИзменяемоеЗначение);
		ОписаниеТипаИзменяемогоЗначения = Элемент.ТекущиеДанные.ОписаниеТипов;
		Если НЕ ОписаниеТипаИзменяемогоЗначения.СодержитТип(Тип("ХранилищеЗначения")) Тогда	// ХранилищеЗначения может быть только единственным типом в данных.
			Если ТипИзменяемогоЗначения = Тип("Строка") Тогда
				ДлинаСтроки = Элемент.ТекущиеДанные.ОписаниеТипов.КвалификаторыСтроки.Длина;
			КонецЕсли;
		Иначе
			ТипИзменяемогоЗначения = Тип("ХранилищеЗначения");
		КонецЕсли;
	Иначе	
		ИмяРеквизитаИзменяемогоЗначения = СтрЗаменить(Элемент.ТекущийЭлемент.Имя,Элемент.Имя,"");
		ИзменяемоеЗначение = Элемент.ТекущиеДанные[ИмяРеквизитаИзменяемогоЗначения];
		ТипИзменяемогоЗначения = ТипЗнч(ИзменяемоеЗначение);
		ОписаниеТипаИзменяемогоЗначения = Элемент.ТекущийЭлемент.ОграничениеТипа;
		Если НЕ ОписаниеТипаИзменяемогоЗначения.СодержитТип(Тип("ХранилищеЗначения")) Тогда	// ХранилищеЗначения может быть только единственным типом в данных.
			Если ТипИзменяемогоЗначения = Тип("Строка") Тогда
				ДлинаСтроки = ОписаниеТипаИзменяемогоЗначения.КвалификаторыСтроки.Длина;
			КонецЕсли;
		Иначе
			ТипИзменяемогоЗначения = Тип("ХранилищеЗначения");
		КонецЕсли;
	КонецЕсли;
	
	Отказ = Ложь;
	
	Если ТипИзменяемогоЗначения = Тип("ХранилищеЗначения") Тогда	// ХранилищеЗначения может быть только единственным типом в данных.
		Если НЕ Элемент.Имя = "ТРеквизиты" Тогда
			Элемент.ТекущийЭлемент.ТолькоПросмотр = Истина;
			ТРеквизитыПередНачаломИзменения(Элемент, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Реквизиты Объекта". Событие Таблицы "ТабличнаяЧасть"... ПередНачаломРедактирования.
// Проверить возможность редактирования значения Реквизита.
// - Если Значение имеет тип "ХранилищеЗначения" - отказаться от редактирования.
// - Если Значение имеет тип "Булево" и Имя Реквизита = "ЭтоГруппа" для Справочника - отказаться от редактирования.
// - Если Значение имеет тип Имя Реквизита = "НомерОтправленного" или "НомерПринятого" для ПланаОбмена - отказаться от редактирования.
//
&НаКлиенте
Процедура ТРеквизитыПередНачаломИзменения(Элемент, Отказ) Экспорт
	
	Если Элемент.Имя = "ТРеквизиты" Тогда
		ИмяТаблицы	= "Таблица ""Реквизиты""";
		ИмяРеквизитаИзменяемогоЗначения = Элемент.ТекущиеДанные.Имя;
		ПредставлениеРеквизитаИзменяемогоЗначения = Элемент.ТекущиеДанные.Представление;
		ИзменяемоеЗначение = Элемент.ТекущиеДанные.Значение;
		ТипИзменяемогоЗначения = ТипЗнч(ИзменяемоеЗначение);
		ОписаниеТипаИзменяемогоЗначения = Элемент.ТекущиеДанные.ОписаниеТипов;
		ПроверятьРеквизитСНеопределеннымТипом = Истина;
		ТекущаяКолонка = Элемент.ТекущийЭлемент.Имя;
		Если НЕ ОписаниеТипаИзменяемогоЗначения.СодержитТип(Тип("ХранилищеЗначения")) Тогда	// ХранилищеЗначения может быть только единственным типом в данных.
			Если ТипИзменяемогоЗначения = Тип("Строка") Тогда
				ДлинаСтроки = Элемент.ТекущиеДанные.ОписаниеТипов.КвалификаторыСтроки.Длина;
			КонецЕсли;
		Иначе
			ТипИзменяемогоЗначения = Тип("ХранилищеЗначения");
		КонецЕсли;
	Иначе	
		ИмяТаблицы	= "Таблица ТЧ: """ + СтрЗаменить(Элемент.Имя, ФормаПолучитьИмяТаблицыТабличнаяЧасть(""),"") + "";
		ИмяРеквизитаИзменяемогоЗначения = СтрЗаменить(Элемент.ТекущийЭлемент.Имя, Элемент.Имя, "");
		ПредставлениеРеквизитаИзменяемогоЗначения = Элемент.ТекущийЭлемент.Заголовок;
		ИзменяемоеЗначение = Элемент.ТекущиеДанные[ИмяРеквизитаИзменяемогоЗначения];
		ТипИзменяемогоЗначения = ТипЗнч(ИзменяемоеЗначение);
		ОписаниеТипаИзменяемогоЗначения = Элемент.ТекущийЭлемент.ОграничениеТипа;
		ПроверятьРеквизитСНеопределеннымТипом = Ложь;
		ТекущаяКолонка = Элемент.ТекущийЭлемент.Имя;
		Если НЕ ОписаниеТипаИзменяемогоЗначения.СодержитТип(Тип("ХранилищеЗначения")) Тогда	// ХранилищеЗначения может быть только единственным типом в данных.
			Если ТипИзменяемогоЗначения = Тип("Строка") Тогда
				ДлинаСтроки = ОписаниеТипаИзменяемогоЗначения.КвалификаторыСтроки.Длина;
			КонецЕсли;
		Иначе
			ТипИзменяемогоЗначения = Тип("ХранилищеЗначения");
		КонецЕсли;
	КонецЕсли;
	
	РезультатПроверки = ОбъектБДПроверитьВозможностьРедактированияЗначенияРеквизитаТаблицы(Объект.ОбъектБД, ИмяТаблицы, ИмяРеквизитаИзменяемогоЗначения, ТипИзменяемогоЗначения, ИзменяемоеЗначение, ПроверятьРеквизитСНеопределеннымТипом);
	
	Если (РезультатПроверки.Отказ) Тогда
		ПредупреждениеСообщение(, РезультатПроверки.ОписаниеОшибки);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если (Элемент.Имя = "ТРеквизиты" И ТекущаяКолонка = "ТРеквизитыЗначение" И Элемент.ТекущиеДанные.Блокированный) Тогда
		ПредупреждениеСообщение(, "Изменение блокированного реквизита объекта.
		|
		|Согласно проектной логике конфигурации """ + Объект.КонфигурацияИВерсия + """:
		|Реквизит не рекомендуется/нельзя изменять, если Объект участвует в документах.
		|
		|""Блокированные"" реквизиты определяются индивидуально для вида Объекта.", 20);
	ИначеЕсли (Элемент.Имя = "ТРеквизиты" И ТекущаяКолонка = "ТРеквизитыЗначение" И ИмяРеквизитаИзменяемогоЗначения = "ПометкаУдаления") Тогда
		ПредупреждениеСообщение(, "Изменение значения реквизита ""ПометкаУдаления""
		|
		|Рекомедуется производить действием в подменю ""Удаление"".
		|(Стандартное УстановитьПометкуУдаления(Истина/Ложь))", 20);
	ИначеЕсли (Элемент.Имя = "ТРеквизиты" И ТекущаяКолонка = "ТРеквизитыЗначение" И ТипЗнч(ИзменяемоеЗначение) = Тип("УникальныйИдентификатор")) Тогда
		ПредупреждениеСообщение(, "Изменение значения реквизита типа ""Уникальный идентификатор""
		|
		|Будьте внимательны при редактировании реквизита.
		|Реквизит имеет определенный формат:
		|XXXXXXXX-XXXX-XXXX-XXXX-XXXXXXXXXXXX", 5);
	КонецЕсли;
	
	Если НЕ (Элемент.Имя = "ТРеквизиты" И ТекущаяКолонка = "ТРеквизитыПроверяемый") И ТипИзменяемогоЗначения = Тип("Строка") Тогда
		РедактироватьМногострочныйТекстВФорме(Элемент, ИмяРеквизитаИзменяемогоЗначения, ИзменяемоеЗначение, ПредставлениеРеквизитаИзменяемогоЗначения, ДлинаСтроки, Отказ);
		Возврат;
	ИначеЕсли ТипИзменяемогоЗначения = Тип("УникальныйИдентификатор") Тогда
		РедактироватьУникальныйИдентификатор(Элемент, ИмяРеквизитаИзменяемогоЗначения, ИзменяемоеЗначение, ПредставлениеРеквизитаИзменяемогоЗначения, ДлинаСтроки, Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// МО: Проверить возможность редактирования.
//
&НаСервере
Функция ОбъектБДПроверитьВозможностьРедактированияЗначенияРеквизитаТаблицы(ОбъектБД, ИмяТаблицы, ИмяРеквизитаИзменяемогоЗначения, ТипИзменяемогоЗначения, ИзменяемоеЗначение, ПроверятьРеквизитСНеопределеннымТипом)
	
	ОбъектОбработка = ОбъектОбработка();
	РезультатПроверки = ОбъектОбработка.ОбъектБДПроверитьВозможностьРедактированияЗаписиЗначения(ОбъектБД, ИмяТаблицы, ИмяРеквизитаИзменяемогоЗначения, ТипИзменяемогоЗначения, ИзменяемоеЗначение, ПроверятьРеквизитСНеопределеннымТипом);
	Возврат РезультатПроверки;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОСМОТР/РЕДАКТИРОВАНИЕ УНИКАЛЬНОГО ИДЕНТИФИКАТОРА.
// 

&НаКлиенте
Процедура РедактироватьУникальныйИдентификатор(Элемент, ИмяРеквизитаИзменяемогоЗначения, ИзменяемоеЗначение, ПредставлениеРеквизитаИзменяемогоЗначения, ДлинаСтроки, Отказ)
	Перем ПараметрыВводаСтроки;
	
	ПараметрыВводаСтроки = Новый Структура;
	ПараметрыВводаСтроки.Вставить("Заголовок"			, "Уникальный идентификатор");
	ПараметрыВводаСтроки.Вставить("ФормаИлиОбработчик"	, ЭтаФорма);
	ПараметрыВводаСтроки.Вставить("ВладелецРеквизита"	, Элемент.ТекущиеДанные);
	Если Элемент.Имя = "ТРеквизиты" Тогда
		ПараметрыВводаСтроки.Вставить("ИмяРеквизита"	, "Значение");
	Иначе	
		ПараметрыВводаСтроки.Вставить("ИмяРеквизита"	, ИмяРеквизитаИзменяемогоЗначения);
	КонецЕсли;
	ПараметрыВводаСтроки.Вставить("ИсходноеЗначение"	, ИзменяемоеЗначение);
	ПараметрыВводаСтроки.Вставить("ДлинаСтроки"			, СтрДлина(ИзменяемоеЗначение));
	ПараметрыВводаСтроки.Вставить("Многострочность"		, Ложь);
	
	РедактироватьМногострочныйТекст_Стандартно(ПараметрыВводаСтроки);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОСМОТР/РЕДАКТИРОВАНИЕ МНОГОСТРОЧНОГО ТЕКСТА.
// 

&НаКлиенте
Процедура ПросмотретьМногострочныйТекст_Стандартно(МногоСтрочныйТекст, Заголовок)
	Перем ПараметрыВводаСтроки;
	
	ПараметрыВводаСтроки = Новый Структура;
	ПараметрыВводаСтроки.Вставить("Заголовок"			, Заголовок);
	ПараметрыВводаСтроки.Вставить("ФормаИлиОбработчик"	, ЭтаФорма);
	ПараметрыВводаСтроки.Вставить("ВладелецРеквизита"	, Неопределено);
	ПараметрыВводаСтроки.Вставить("ИмяРеквизита"		, Неопределено);
	ПараметрыВводаСтроки.Вставить("ИсходноеЗначение"	, МногоСтрочныйТекст);
	ПараметрыВводаСтроки.Вставить("ДлинаСтроки"			, 0);
	ПараметрыВводаСтроки.Вставить("Многострочность"		, Истина);
	
	РедактироватьМногострочныйТекст_Стандартно(ПараметрыВводаСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьМногострочныйТекстВФорме(Элемент, ИмяРеквизитаИзменяемогоЗначения, ИзменяемоеЗначение, Заголовок = "", ДлинаСтроки = 0, Отказ = Ложь)
	Перем ИмяФормыМногоСтрочногоТекста, ПараметрыМСТ, ПараметрыВводаСтроки, Оповещение;
	
	Если ДлинаСтроки = 0 ИЛИ ДлинаСтроки >= 50 Тогда
			
		ПараметрыВводаСтроки = Новый Структура;
		ПараметрыВводаСтроки.Вставить("Заголовок"			, Заголовок);
		ПараметрыВводаСтроки.Вставить("ФормаИлиОбработчик"	, ЭтаФорма);
		ПараметрыВводаСтроки.Вставить("ВладелецРеквизита"	, Элемент.ТекущиеДанные);
		Если Элемент.Имя = "ТРеквизиты" Тогда
			ПараметрыВводаСтроки.Вставить("ИмяРеквизита"	, "Значение");
		Иначе	
			ПараметрыВводаСтроки.Вставить("ИмяРеквизита"	, ИмяРеквизитаИзменяемогоЗначения);
		КонецЕсли;
		ПараметрыВводаСтроки.Вставить("ИсходноеЗначение"	, ИзменяемоеЗначение);
		ПараметрыВводаСтроки.Вставить("ДлинаСтроки"			, ДлинаСтроки);
		
		Если ((Найти(ВРег(ИмяРеквизитаИзменяемогоЗначения), "АЛГОРИТМ") > 0) 
			ИЛИ (Найти(ВРег(ИмяРеквизитаИзменяемогоЗначения), "АДРЕС") > 0)
			ИЛИ (Найти(ВРег(ИмяРеквизитаИзменяемогоЗначения), "ЗАПРОС") > 0)
			ИЛИ (Найти(ВРег(ИмяРеквизитаИзменяемогоЗначения), "КОММЕНТАРИЙ") > 0)) Тогда
			
			ПараметрыВводаСтроки.Вставить("Многострочность"	, Истина);
			
			// Управляемая Форма (Форма обработки).
			
			ИмяФормыМногоСтрочногоТекста = Объект.Настройки.ИмяФормыМногоСтрочногоТекста;
				
			ПараметрыМСТ = Новый Структура("МногоСтрочныйТекст", ИзменяемоеЗначение);
			
			ФормаМногоСтрочногоТекста = ПолучитьФорму(ИмяФормыМногоСтрочногоТекста, ПараметрыМСТ, ЭтаФорма);    
			ФормаМногоСтрочногоТекста.Заголовок							= Заголовок;
			ФормаМногоСтрочногоТекста.ЗакрыватьПриЗакрытииВладельца 	= Истина;
			ФормаМногоСтрочногоТекста.МногоСтрочныйТекст				= ИзменяемоеЗначение;
			
			Если ИспользоватьРежимМодальности() Тогда
				// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
				РезультатРедактирования = ФормаМногоСтрочногоТекста.ОткрытьМодально();
				РедактироватьМногострочныйТекстЗавершение(РезультатРедактирования, ПараметрыВводаСтроки);
				Отказ = Истина;
				Возврат;
			Иначе
				// Стандартно в немодальном режиме (8.3) с обработкой результата.
				//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
				Оповещение = Вычислить("Новый ОписаниеОповещения(""РедактироватьМногострочныйТекстЗавершение"", ЭтаФормаЭтотОбъект(), ПараметрыВводаСтроки)");
				Выполнить("ФормаМногоСтрочногоТекста.ОписаниеОповещенияОЗакрытии = Оповещение");
				ФормаМногоСтрочногоТекста.Открыть();
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
		Иначе
			ПараметрыВводаСтроки.Вставить("Многострочность"	, Истина);
		КонецЕсли;
		
		РедактироватьМногострочныйТекст_Стандартно(ПараметрыВводаСтроки);
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьМногострочныйТекст_Стандартно(ПараметрыВводаСтроки)
	Перем РезультатРедактирования;
	
	Если ИспользоватьРежимМодальности() Тогда
		РезультатРедактирования = ПараметрыВводаСтроки.ИсходноеЗначение;
		РедактироватьМногострочныйТекст_ВвестиСтроку(ПараметрыВводаСтроки.ИсходноеЗначение, РезультатРедактирования, Ложь, ПараметрыВводаСтроки.Заголовок, ПараметрыВводаСтроки.ДлинаСтроки, ПараметрыВводаСтроки.Многострочность);
		РедактироватьМногострочныйТекстЗавершение(РезультатРедактирования, ПараметрыВводаСтроки);
	Иначе
		РедактироватьМногострочныйТекст_ПоказатьВводСтроки(ПараметрыВводаСтроки);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму редактирования произвольного многострочного текста модально
//
// Параметры:
// МногострочныйТекст      - Строка - произвольный текст, который необходимо отредактировать
// РезультатРедактирования - Строка - переменная, в которую будет помещен результат редактирования
// Модифицированность       - Строка - флаг модифицированности формы
// Заголовок               - Строка - текст, который необходимо отобразить в заголовке формы
//
&НаКлиенте
Процедура РедактироватьМногострочныйТекст_ВвестиСтроку(Знач МногострочныйТекст, РезультатРедактирования, Модифицированность = Ложь, Заголовок = Неопределено, ДлинаСтроки = 0, Многострочность = Истина)
	
	Если Заголовок = Неопределено Тогда
		ТекстВведен = ВвестиСтроку(МногострочныйТекст, , ДлинаСтроки, Многострочность);
	Иначе
		ТекстВведен = ВвестиСтроку(МногострочныйТекст, Заголовок, ДлинаСтроки, Многострочность);
	КонецЕсли;
	                              
	Если НЕ ТекстВведен Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РезультатРедактирования = МногострочныйТекст Тогда
		РезультатРедактирования = МногострочныйТекст;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму редактирования произвольного многострочного текста немодально.
&НаКлиенте
Процедура РедактироватьМногострочныйТекст_ПоказатьВводСтроки(ПараметрыВводаСтроки)
	Перем Оповещение, Заголовок, ТекстРедактирования, ДлинаСтроки, Многострочность;
	
	Если ПустаяСтрока(Заголовок) Тогда
		Заголовок = НСтр("ru = '.'");
	КонецЕсли;
	
	Заголовок 			= ПараметрыВводаСтроки.Заголовок;
	ТекстРедактирования = ПараметрыВводаСтроки.ИсходноеЗначение;
	ДлинаСтроки			= ПараметрыВводаСтроки.ДлинаСтроки;
	Многострочность 	= ПараметрыВводаСтроки.Многострочность;
	
	//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
	Оповещение = Вычислить("Новый ОписаниеОповещения(""РедактироватьМногострочныйТекстЗавершение"", ЭтаФормаЭтотОбъект(), ПараметрыВводаСтроки)");
	Выполнить("ПоказатьВводСтроки(Оповещение, ТекстРедактирования, Заголовок, ДлинаСтроки, Многострочность)");
	
КонецПроцедуры

// Обработчик результата работы процедуры РедактироватьМногострочныйТекст.
&НаКлиенте
Процедура РедактироватьМногострочныйТекстЗавершение(Текст, ПараметрыВводаСтроки) Экспорт
	Перем Форма, Обработчик, ИсходноеЗначение, GUID;
	
	Форма = ПараметрыВводаСтроки.ФормаИлиОбработчик;
	ИсходноеЗначение = ПараметрыВводаСтроки.ИсходноеЗначение;
	
	Если Текст <> Неопределено И НЕ ПараметрыВводаСтроки.ВладелецРеквизита = Неопределено Тогда
		
		Если ТипЗнч(ПараметрыВводаСтроки.ВладелецРеквизита) = Тип("ДанныеФормыЭлементДерева")
			ИЛИ ТипЗнч(ПараметрыВводаСтроки.ВладелецРеквизита) = Тип("ДанныеФормыЭлементКоллекции") Тогда
			Если ТипЗнч(ПараметрыВводаСтроки.ВладелецРеквизита[ПараметрыВводаСтроки.ИмяРеквизита]) = Тип("УникальныйИдентификатор") Тогда
				GUID = Новый УникальныйИдентификатор(Текст);
				ЗаполнитьЗначенияСвойств(ПараметрыВводаСтроки.ВладелецРеквизита, Новый Структура(ПараметрыВводаСтроки.ИмяРеквизита, GUID));
			Иначе
				ЗаполнитьЗначенияСвойств(ПараметрыВводаСтроки.ВладелецРеквизита, Новый Структура(ПараметрыВводаСтроки.ИмяРеквизита, Текст));
			КонецЕсли;
		Иначе
			Если ТипЗнч(ПараметрыВводаСтроки.ВладелецРеквизита[ПараметрыВводаСтроки.ИмяРеквизита]) = Тип("УникальныйИдентификатор") Тогда
				GUID = Новый УникальныйИдентификатор(Текст);
				ПараметрыВводаСтроки.ВладелецРеквизита[ПараметрыВводаСтроки.ИмяРеквизита] = GUID;
			Иначе
				ПараметрыВводаСтроки.ВладелецРеквизита[ПараметрыВводаСтроки.ИмяРеквизита] = Текст;
			КонецЕсли;
		КонецЕсли;
		
		Если Форма <> Неопределено Тогда
			Если НЕ Текст = ИсходноеЗначение Тогда
				Форма.Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОСНОВНЫЕ ДЕЙСТВИЯ ФОРМЫ.
// 

////////////////////////////////////////////////////////////////////////////////
// КНОПКИ ОСНОВНОГО МЕНЮ. ЗАПИСЬ ОБЪЕКТА.
// 

// Кнопка "Записать".
//
&НаКлиенте
Процедура ОМЗаписать(Команда, РежимЗаписи = Неопределено)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		Возврат;
	КонецЕсли;
	
	ТипОбъектаБД = Объект.ТипОбъектаБД;
	
	Если РежимЗаписи = Неопределено Тогда
		
		ТекстВопроса = "ЗАПИСАТЬ " + ВРег(ТипОбъектаБД) + ":
		|" + Объект.ОбъектБД + " ?";
		
		ЗаголовокВопроса = "ЗАПИСЬ ОБЪЕКТА";
		Если ИспользоватьРежимМодальности() Тогда
			// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
			ОМЗаписатьЗавершение(Ответ, РежимЗаписи);
		Иначе
			// Стандартно в немодальном режиме (8.3) с обработкой результата.
			//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
			Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМЗаписатьЗавершение"", ЭтаФормаЭтотОбъект(), РежимЗаписи)");
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		КонецЕсли;
		
	Иначе
		ОМЗаписатьЗавершение(КодВозвратаДиалога.Да, РежимЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМЗаписатьЗавершение(Ответ, РежимЗаписи) Экспорт
	Перем ЗначениеОтвета, РезультатСоздания;
	Перем ТипОбъектаБД;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ТипОбъектаБД = Объект.ТипОбъектаБД;
	
	Если Объект.ПоказыватьСообщения Тогда
		ОчиститьСообщения();
		Сообщить(СтрокаРавно);
		Если РежимЗаписи = Неопределено Тогда
			Сообщить("МФ: " + "ЗАПИСЬ """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """.", СтатусСообщения.ОченьВажное);
		ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			Сообщить("МФ: " + "ПРОВЕДЕНИЕ ДОКУМЕНТА: """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """.", СтатусСообщения.ОченьВажное);
		ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			Сообщить("МФ: " + "ОТМЕНА ПРОВЕДЕНИЯ ДОКУМЕНТА: """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """.", СтатусСообщения.ОченьВажное);
		КонецЕсли;
	КонецЕсли;
	
	Отказ = Ложь;
	
	ОбъектБДЗаписатьНаСервере(Объект.ОбъектБД, Отказ, РежимЗаписи);
	
	// При Проведении и ОтменыПроведения документа высвечивается: "Формирование таблиц движения ...".
	ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ОбъектБД, ВидИзмененияДанных.Изменение);	// Обновление отображения в реквизите ОбъектБД!
	ЭтаФорма.Модифицированность = Ложь;
	
	Если Отказ Тогда
		
		Если Объект.ПоказыватьСообщения Тогда
			Если РежимЗаписи = Неопределено Тогда
				Сообщить("МФ: " + "ОБЪЕКТ: """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """ НЕ ЗАПИСАН.", СтатусСообщения.ОченьВажное);
			ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				Сообщить("МФ: " + "ДОКУМЕНТ: """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """ НЕ ПРОВЕДЕН.", СтатусСообщения.ОченьВажное);
			ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
				Сообщить("МФ: " + "ДОКУМЕНТ: """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """ ПРОВЕДЕНИЕ НЕ ОТМЕНЕНО.", СтатусСообщения.ОченьВажное);
			КонецЕсли;
			Сообщить(СтрокаРавно);
		КонецЕсли;
		
	Иначе	
		
		Если Объект.ПоказыватьСообщения Тогда
			Сообщить(СтрокаРавно);
			Если РежимЗаписи = Неопределено Тогда
				Сообщить("МФ: " + "ОБЪЕКТ: """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """ ЗАПИСАН.", СтатусСообщения.ОченьВажное);
			ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				Сообщить("МФ: " + "ДОКУМЕНТ: """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """ ПРОВЕДЕН.", СтатусСообщения.ОченьВажное);
			ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
				Сообщить("МФ: " + "ДОКУМЕНТ: """ + ТипОбъектаБД + "." + Объект.ОбъектБД + """ ПРОВЕДЕНИЕ ОТМЕНЕНО.", СтатусСообщения.ОченьВажное);
			КонецЕсли;
			
			Если НЕ Объект.ПоказатьДвижения ИЛИ НЕ РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
				Сообщить(СтрокаРавно);
			КонецЕсли;
		КонецЕсли;
		
		Если РежимЗаписи = Неопределено Тогда
			Если ТипОбъектаБД = ИмяТипаДокументы() И ОбъектБДПолучитьПризнакПроведенОбъекта(Объект.ОбъектБД) Тогда
				ПредупреждениеСообщение(, "ПРОВЕДЕННЫЙ РАНЕЕ ДОКУМЕНТ: 
				|""" + ВРег(Объект.ОбъектБД) + """
				|
				|- ЗАПИСАН, НО МОЖЕТ БЫТЬ НЕ ПРОВЕДЕН ПОВТОРНО.
				|=> (следовательно)
				|- ДВИЖЕНИЯ ПО РЕГИСТРАМ МОГУТ ОСТАТЬСЯ ПРЕЖНИМИ.
				|
				|РЕКОМЕНДУЕТСЯ ВЫПОЛНИТЬ ПРОВЕДЕНИЕ ДОКУМЕНТА.", , Объект.КонфигурацияИВерсия);
			КонецЕсли;
		КонецЕсли;
		
		Если (ТипОбъектаБД = ИмяТипаБизнесПроцессы() ИЛИ ТипОбъектаБД = ИмяТипаЗадачи()) Тогда
			ЭтаФорма.СостояниеОбъектаБД	= ОбъектБДПолучитьСостояниеОбъекта(Объект.ОбъектБД);
		КонецЕсли;
		
		ОбъектБДПриИзменении("ОбъектБДЗаписать");
		
	КонецЕсли;

КонецПроцедуры

// МО: Записать Выбранный ОбъектБД.
//
&НаСервере
Процедура ОбъектБДЗаписатьНаСервере(ОбъектБД, Отказ, РежимЗаписи = Неопределено)
	
	ОбъектОбработка = ОбъектОбработка();
	ОбъектОбработка.ОбъектБДЗаписатьНаСервере(ОбъектБД, ЭтаФорма, Отказ, РежимЗаписи);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// КНОПКИ ОСНОВНОГО МЕНЮ. ПРОВЕДЕНИЕ ВыбранногоОбъекта (Тип: Документ).
// 

// Кнопка "СоздатьКопированием" ДОПОЛНИТЕЛЬНОГО МЕНЮ. ТОЛЬКО ДОКУМЕНТ.
//
&НаКлиенте
Процедура ОМСоздатьКопированием(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		Возврат;
	КонецЕсли;
			
	Если ОбъектБДПолучитьПометкуУдаленияОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, """" + Объект.ОбъектБД + """ помечен на удаление.
		|Снимите пометку удаления и повторите попытку создания копированием.
		|Операция не выполнена.");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Вы подтверждаете создание копированием нового объекта типа
	|
	|" + ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Объект.ОбъектБД) + " ?
	|
	|ОСОБЕННОСТИ УСТАНОВКИ ЗНАЧЕНИЙ РЕКВИЗИТОВ ДОКУМЕНТА:
	|
	|1. Номер документа устанавливается в соответствии со штатным префиксом в данной БД.
	|2. Дата документа устанавливается в зависимости от Периодичности нумерации Документа и Даты исходного документа.";
	
	ЗаголовокВопроса = "СОЗДАНИЕ НОВОГО ОБЪЕКТА КОПИРОВАНИЕМ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ОМСоздатьКопированиемЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМСоздатьКопированиемЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМСоздатьКопированиемЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета, РезультатСоздания;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	РезультатСоздания = ОМСоздатьКопированиемНаСервере(Объект.ОбъектБД);
	Если НЕ РезультатСоздания.Отказ Тогда
		ОбъектБДПриИзменении("ОМСоздатьКопированием");
		ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ОбъектБД, ВидИзмененияДанных.Изменение);
	КонецЕсли;
	
	ПредупреждениеСообщение(, РезультатСоздания.ОписаниеОшибки, , Объект.КонфигурацияИВерсия);
	
КонецПроцедуры

// МО: Кнопка "СоздатьКопрированием". Только Документ.
//
&НаСервере
Функция ОМСоздатьКопированиемНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.НовыйОбъектБДСоздатьКопированиемНаСервере(ОбъектБД);

КонецФункции

// Кнопка "Провести".
//
&НаКлиенте
Процедура ОМПровести(Команда, ЗадатьВопрос = Истина, ОтветПоОснованию = Неопределено)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если ОбъектБДПолучитьПометкуУдаленияОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Удаленный документ: """ + Объект.ОбъектБД + """ не может быть проведен.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если НЕ Объект.ПроведениеРазрешено Тогда
		КоличествоДвижений = ПолучитьКоличествоДвиженийДокументаНаСервере(Объект.ОбъектБД);
		ПредупреждениеСообщение(, "Тип документа: """ + Объект.ПолноеСтроковоеИмяТипа + """ не может быть проведен,
		|т.к. Проведение ЗАПРЕЩЕНО.
		|
		|Тем не менее документ может иметь движения.
		|Коллекция возможных Движений: " + КоличествоДвижений + ".
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если ЗадатьВопрос Тогда
		ТекстВопроса = "Подтвердите проведение документа:
		|
		|" + Объект.ОбъектБД + " .";
		
		ЗаголовокВопроса = "ПРОВЕДЕНИЕ ДОКУМЕНТА";
		Если ИспользоватьРежимМодальности() Тогда
			// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
			Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
			ОМПровестиЗавершение(Ответ, Неопределено);
		Иначе
			// Стандартно в немодальном режиме (8.3) с обработкой результата.
			//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
			Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМПровестиЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		КонецЕсли;
	Иначе
		ОМПровестиЗавершение(ОтветПоОснованию, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМПровестиЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ОМЗаписать(Неопределено, РежимЗаписиДокумента.Проведение);
	
	ЭтаФорма.СостояниеОбъектаБД = ОбъектБДПолучитьСостояниеОбъекта(Объект.ОбъектБД);
	
КонецПроцедуры

// МО: Получить возможное количество движений.
//
&НаСервере
Функция ПолучитьКоличествоДвиженийДокументаНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ПолучитьКоличествоДвиженийДокументаНаСервере(ОбъектБД);
	
КонецФункции

// Кнопка "ПровестиОснованиеИДокумент".
//
&НаКлиенте
Процедура ОМПровестиОснованиеИДокумент(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если ОбъектБДПолучитьПометкуУдаленияОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Удаленный документ: """ + Объект.ОбъектБД + """ не может быть проведен.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если НЕ Объект.ПроведениеРазрешено Тогда
		КоличествоДвижений = ПолучитьКоличествоДвиженийДокументаНаСервере(Объект.ОбъектБД);
		ПредупреждениеСообщение(, "Тип документа: """ + Объект.ПолноеСтроковоеИмяТипа + """ не может быть проведен,
		|т.к. Проведение ЗАПРЕЩЕНО.
		|
		|Тем не менее документ может иметь движения.
		|Коллекция возможных Движений: " + КоличествоДвижений + ".
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ИмяРеквизитаДокументаОснования) Тогда
		ПредупреждениеСообщение(, "У объекта типа """ + Объект.ПолноеСтроковоеИмяТипа + """ отсутствует реквизит Документ-Основание.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	ДокументОснованиеЗначениеИзТРеквизиты = ПолучитьЗначениеДокументаОснованияИзТРеквизиты(Объект.ИмяРеквизитаДокументаОснования);
	
	РезультатПолучения = ПолучитьДокументОснованиеТекущегоОбъектаНаСервере(Объект.ОбъектБД, Объект.ИмяРеквизитаДокументаОснования, ДокументОснованиеЗначениеИзТРеквизиты);
	ДокОснование = РезультатПолучения.ДокОснование;
	ОписаниеОшибки = РезультатПолучения.ОписаниеОшибки;
	
	Если ДокОснование = Неопределено Тогда	// Неопределено - при ошибке получения значения.
		ПредупреждениеСообщение(, "Не удалось получить Документ-Основание для: 
		|""" + Объект.ОбъектБД + """. 
		|
		|" + ОписаниеОшибки + "
		|
		|Документ-Основание не может быть проведен.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если ОбъектБДПолучитьПометкуУдаленияОбъекта(ДокОснование) Тогда
		ПредупреждениеСообщение(, "Удаленный Документ-Основание: """ + ДокОснование + """ не может быть проведен.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	ДобавочныйТекст = "";
	Если ДатаДокументаОснованияБольшеДатыТекущегоДокументаНаСервере(ДокОснование, Объект.ОбъектБД) Тогда
		ДобавочныйТекст = "
		|ВНИМАНИЕ! Дата Документа-Основания > Даты Текущего Документа !
		|";
	КонецЕсли;
	
	Если НЕ ОбъектБДПолучитьПризнакПроведенОбъекта(ДокОснование) Тогда
		ДобавочныйТекст = "
		|ВНИМАНИЕ! Документ-Основание не является проведенным!
		|";
	КонецЕсли;
	
	ТекстВопроса = "Текущий Документ: """ + Объект.ОбъектБД + """
	|" + ДобавочныйТекст + "
	|Документа-Основания: """ + ДокОснование + "
	|
	|ПОДТВЕРДИТЕ ПРОВЕДЕНИЕ";
	
	ЗаголовокВопроса = "ПРОВЕДЕНИЕ ДОКУМЕНТОВ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 100, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ОМПровестиОснованиеИДокументЗавершение(Ответ, ДокОснование);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение= Вычислить("Новый ОписаниеОповещения(""ОМПровестиОснованиеИДокументЗавершение"", ЭтаФормаЭтотОбъект(), ДокОснование)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 100, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМПровестиОснованиеИДокументЗавершение(Ответ, ДокОснование) Экспорт
	Перем ЗначениеОтвета, Проведено;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		ПредупреждениеСообщение(, "Вы отказались от проведения Документа-Основания: 
		|
		|""" + ДокОснование + """
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Проведено = ПровестиДокументНаСервере(ДокОснование);
	Если Проведено Тогда
		ОМПровести(Неопределено, Ложь, КодВозвратаДиалога.Да);
	Иначе
		ПредупреждениеСообщение(, "Документ-Основание: 
		|""" + ДокОснование + """ провести не удалось.
		|
		|Текущий Документ: 
		|""" + Объект.ОбъектБД + """ так же не будет проведен.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// МО: Получить Значение НаСервере.
//
&НаСервере
Функция ОбъектБДПолучитьИмяДокументаОснование(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьИмяДокументаОснование(ОбъектБД);
	
КонецФункции

// Получить Значение отображаемое в Таблице "Реквизиты".
//
&НаКлиенте
Функция ПолучитьЗначениеДокументаОснованияИзТРеквизиты(ИмяДокументаОснование)
	
	тзТмп = Объект.ТРеквизитыОбъекта;
	ДокументОснованиеЗначениеИзТЗ = "";
	
	СтруктураПоиска = Новый Структура("Имя", ИмяДокументаОснование);
	
	Массив = тзТМП.НайтиСтроки(СтруктураПоиска);	// Должен быть только 1(один) элемент.
	Если Массив.Количество() > 0 Тогда
		ДокументОснованиеЗначениеИзТРеквизиты = Строка(Массив[0].Значение);
	КонецЕсли;
	
	Возврат ДокументОснованиеЗначениеИзТРеквизиты;
	
КонецФункции

// Получить Значение НаСервере.
//
&НаСервере
Функция ПолучитьДокументОснованиеТекущегоОбъектаНаСервере(ОбъектБД, ИмяДокументаОснование, ДокументОснованиеЗначениеИзТРеквизиты)
	
	РезультатПолучения = Новый Структура;
	РезультатПолучения.Вставить("ДокОснование", Неопределено);
	РезультатПолучения.Вставить("ОписаниеОшибки", "");
	
	Попытка
		РезультатПолучения.ДокОснование		= ОбъектБД[ИмяДокументаОснование];	// Получаем Ссылку на Документ-Основание. Передать в Клиент ДокументОбъект невозвожно.
		РезультатПолучения.ОписаниеОшибки	= "Документ-Основание " + РезультатПолучения.ДокОснование + " найден.";
		
		// Проверяем.
		// Потому что:
		// В Таблице "Реквизиты" удаленный документ визуально может отображаеться нормально!
		
		// Фактически у удаленного документа НУЛЕВАЯ дата.
		ДатаДокОсн	 = РезультатПолучения.ДокОснование.Дата;				// Документ может быть удален непосредственно, а в Таблице "Реквизиты" он виден, но после выхода из 1С и повторного входа - все как полагается <Объект не найден и т.д.>
		Если ДатаДокОсн = Дата("00010101000000") Тогда						// Документа все-таки нет.
			Если НЕ Строка(РезультатПолучения.ДокОснование) = Строка(ДокументОснованиеЗначениеИзТРеквизиты) Тогда
				РезультатПолучения.ОписаниеОшибки = "НА СТОРОНЕ СЕРВЕРА: (Объект не найден. Возможно удален.)
				|" + РезультатПолучения.ДокОснование + " 
				|
				|НА СТОРОНЕ КЛИЕНТА: (Значение из Таблицы ""Реквизиты""): 
				|" + ДокументОснованиеЗначениеИзТРеквизиты + "
				|
				|ПОЛУЧАЕМ АНЕКДОТ ПРО ПРОГРАММИСТОВ И АВТОМОБИЛЬ, КОТОРЫЙ НЕ ЗАВОДИТСЯ:
				|
				|""ДАВАЙТЕ ВЫЙДЕМ - И ЗАЙДЕМ - ВДРУГ ПОЕДЕТ?!"" (аналог: MS Windows - ""ПЕРЕЗАГРУЗИМСЯ ?!"")
				|
				|В нашем случае Выйти из 1С - и снова зайти ... (тогда в поле Значения для ДокументаОснования возможно будет <Объект не найден> ...)
				|или
				|Реквизит типа ""Документ-Основание"" был изменен с незаполненного на определенный документ, а запись изменений не была произведена.
				|Запишите текущий документ и повторно попытайтесь провести связку документов: Основание+Текущий.";
			Иначе
				РезультатПолучения.ОписаниеОшибки = "НА СТОРОНЕ СЕРВЕРА: (Объект не найден. Возможно удален.)
				|" + РезультатПолучения.ДокОснование + " 
				|
				|НА СТОРОНЕ КЛИЕНТА: (Значение из Таблицы ""Реквизиты""): 
				|" + ДокументОснованиеЗначениеИзТРеквизиты;
			КонецЕсли;
			РезультатПолучения.ДокОснование = Неопределено;
		КонецЕсли;
		
	Исключение
		РезультатПолучения.ДокОснование = Неопределено;
		РезультатПолучения.ОписаниеОшибки = ОписаниеОшибки();
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
	КонецПопытки;
	
	Возврат РезультатПолучения;
	
КонецФункции

// Ставнить даты Документа-Основания и ОбъектаБД.
//
&НаСервере
Функция ДатаДокументаОснованияБольшеДатыТекущегоДокументаНаСервере(Объект_1, Объект_2)
	Возврат Объект_1.Дата > Объект_2.Дата;
КонецФункции

// Выполнить проведение Документа-Основания.
//
&НаСервере
Функция ПровестиДокументНаСервере(ДокументСсылка)
	
	Проведен = Ложь;
	Попытка
		ОбъектИЗМ = ДокументСсылка.ПолучитьОбъект();
		// ОтключениеКонтроляЗаписи: ОбменДанными.Загрузка = Истина; Устанавливать нельзя - Ошибка при Проведении/ОтменеПроведения документа.
		ОбъектИЗМ.Записать(РежимЗаписиДокумента.Проведение);
		Проведен = Истина;
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
		Проведен = Ложь;
	КонецПопытки;
	
	Возврат Проведен;

КонецФункции

// Кнопка "Отмена проведения".
//
&НаКлиенте
Процедура ОМОтменитьПроведение(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если НЕ Объект.ПроведениеРазрешено Тогда
		КоличествоДвижений = ПолучитьКоличествоДвиженийДокументаНаСервере(Объект.ОбъектБД);
		ПредупреждениеСообщение(, "Тип документа: """ + Объект.ПолноеСтроковоеИмяТипа + """ не может быть проведен,
		|т.к. Проведение ЗАПРЕЩЕНО, следовательно отменить проведение невозможно.
		|
		|Тем не менее документ может иметь движения.
		|Коллекция возможных Движений: " + КоличествоДвижений + ".
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если ОбъектБДПолучитьПометкуУдаленияОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Удаленный документ: """ + Объект.ОбъектБД + """ не является проведенным, 
		|Повторная отмена проведения невозможна.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбъектБДПолучитьПризнакПроведенОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Документ: """ + Объект.ОбъектБД + """ не является проведенным, 
		|Повторная отмена проведения невозможна.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "ОТМЕНИТЬ ПРОВЕДЕНИЕ ДОКУМЕНТА:
	|" + Объект.ОбъектБД + " ?";
	
	ЗаголовокВопроса = "ОТМЕНА ПРОВЕДЕНИЯ ДОКУМЕНТА";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ОМОтменитьПроведениеЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМОтменитьПроведениеЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМОтменитьПроведениеЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета, РезультатСоздания;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ОМЗаписать(Неопределено, РежимЗаписиДокумента.ОтменаПроведения);
	
	ЭтаФорма.СостояниеОбъектаБД = ОбъектБДПолучитьСостояниеОбъекта(Объект.ОбъектБД);

КонецПроцедуры

&НаСервере
Функция ОбъектБДПолучитьПризнакПроведенОбъекта(ОбъектБД)
	Возврат ОбъектБД.Проведен;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// КНОПКИ ОСНОВНОГО МЕНЮ. УДАЛЕНИЕ ВыбранногоОбъекта.
// 

// Кнопка "Отмена пометки удаления".
//
&НаКлиенте
Процедура ОМСнятьПометкуУдаления(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если ОбъектБДПолучитьПризнакПредопределенныйОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, """Снять пометку удаления"" для Предопределенного Объекта неприменима.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбъектБДПолучитьПометкуУдаленияОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, """" + Объект.ОбъектБД +  """ не помечен на удаление.
		|Снять пометку на удаление невозможно.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "СНЯТЬ ПОМЕТКУ УДАЛЕНИЯ С ОБЪЕКТА:
	|" + Объект.ОбъектБД + " ?";
	
	ЗаголовокВопроса = "ОТМЕНА ПОМЕТКИ НА УДАЛЕНИЕ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ОМСнятьПометкуУдаленияЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение= Вычислить("Новый ОписаниеОповещения(""ОМСнятьПометкуУдаленияЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМСнятьПометкуУдаленияЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета, РезультатСоздания;
	Перем Отказ;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;

	Отказ = Ложь;
	ОбъектБДУдалениеНаСервере(Объект.ОбъектБД, Отказ, "СнятьПометкуУдаления");
	УстановитьЗначениеРеквизитаПометкаУдаленияНаФорме(Объект.ОбъектБД);
	
	ЭтаФорма.СостояниеОбъектаБД = ОбъектБДПолучитьСостояниеОбъекта(Объект.ОбъектБД);

КонецПроцедуры

// Кнопка "Пометить на удаление".
//
&НаКлиенте
Процедура ОМПометитьНаУдаление(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если ОбъектБДПолучитьПризнакПредопределенныйОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Предопределенный Объект удалить невозможно.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если ОбъектБДПолучитьПометкуУдаленияОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, """" + Объект.ОбъектБД + """ помечен на удаление.
		|Повторно пометить на удаление невозможно.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	ТипОбъектаБД = Объект.ТипОбъектаБД;
	
	СообщениеДляГруппы = Неопределено;
	Если ТипОбъектаБД = ИмяТипаСправочники() ИЛИ Объект.ТипОбъектаБД = ИмяТипаПланыВидовХарактеристик() Тогда
		СообщениеДляГруппы = ?(ОбъектБДПолучитьПризнакЭтоГруппа(Объект.ОбъектБД), "
		|ВАЖНО: Это Группа Справочника/ПВХ. 
		|Будут помечены на удаление подчиненные Группы и Элементы.
		|", СообщениеДляГруппы);
	КонецЕсли;
	
	ТекстВопроса = "ПОМЕТИТЬ НА УДАЛЕНИЕ ОБЪЕКТ:
	|" + Объект.ОбъектБД + "
	|" + СообщениеДляГруппы + "
	|Фактическое удаление необходимо провести стандартным способом:
	|1С:Предприятие -> Операции -> Удаление помеченных объектов.
	|
	|""СЕМЬ РАЗ ОТМЕРЬ - ОДИН РАЗ ОТРЕЖЬ."" (народная мудрость)";
	
	ЗаголовокВопроса = "ПОМЕТКА НА УДАЛЕНИЕ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ОМПометитьНаУдалениеЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМПометитьНаУдалениеЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМПометитьНаУдалениеЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета, РезультатСоздания;
	Перем Отказ;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;

	Отказ = Ложь;
	ОбъектБДУдалениеНаСервере(Объект.ОбъектБД, Отказ, "ПометкаНаУдаление");
	УстановитьЗначениеРеквизитаПометкаУдаленияНаФорме(Объект.ОбъектБД);
	ПоказатьДвиженияПриИзменении("ОМПометитьНаУдаление");
	
	ЭтаФорма.СостояниеОбъектаБД = ОбъектБДПолучитьСостояниеОбъекта(Объект.ОбъектБД);

КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеРеквизитаПометкаУдаленияНаФорме(ОбъектБД)
	Перем МассивСтрок, СтрокаКоллекции;
	
	МассивСтрок = Объект.ТРеквизитыОбъекта.НайтиСтроки(Новый Структура("Имя", "ПометкаУдаления"));
	Если МассивСтрок.Количество() > 0 Тогда
		СтрокаКоллекции = МассивСтрок[0];
		СтрокаКоллекции.Значение = ОбъектБДПолучитьПометкуУдаленияОбъекта(ОбъектБД);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбъектБДПолучитьПометкуУдаленияОбъекта(ОбъектБД)
	Возврат ОбъектБД.ПометкаУдаления;
КонецФункции

&НаСервере
Функция ОбъектБДПолучитьПризнакЭтоГруппа(ОбъектБД)
	Возврат ОбъектБД.ЭтоГруппа;
КонецФункции

// Кнопка "Удалить". НЕПОСРЕДСТВЕННОЕ УДАЛЕНИЕ БЕЗ ВОЗМОЖНОСТИ ВОССТАНОВЛЕНИЯ.
//
&НаКлиенте
Процедура ОМУдалить(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если ОбъектБДПолучитьПризнакПредопределенныйОбъекта(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Предопределенный Объект удалить невозможно.
		|
		|Действие не выполнено.");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "УДАЛИТЬ НЕПОСРЕДСТВЕННО ОБЪЕКТ:
	|" + Объект.ОбъектБД + "
	|
	|ЭТО МОЖЕТ НАРУШИТЬ ССЫЛОЧНУЮ ЦЕЛОСТНОСТЬ ДАННЫХ.
	|
	|""СЕМЬ РАЗ ОТМЕРЬ - ОДИН РАЗ ОТРЕЖЬ."" (народная мудрость)";
	
	ЗаголовокВопроса = "НЕПОСРЕДСТВЕННОЕ УДАЛЕНИЕ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ОМУдалитьЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМУдалитьЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМУдалитьЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета, РезультатСоздания;
	Перем Отказ;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ОбъектБДУдалениеНаСервере(Объект.ОбъектБД, Отказ, "Удалить");
	
	Если НЕ Отказ Тогда
		ОбъектБДОчистка(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

// МО: Выполнить Пометка на удаления / Снять пометку удаления / Удалить (НЕПОСРЕДСТВЕННО).
//
&НаСервере
Процедура ОбъектБДУдалениеНаСервере(ОбъектБД, Отказ, Действие = "ПометкаНаУдаление")
	
	ОбъектОбработка = ОбъектОбработка();
	ОбъектОбработка.ОбъектБДУдалениеНаСервере(ОбъектБД, Отказ, Действие);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВКЛАДКА "ДОПОЛНИТЕЛЬНО".
// "ССЫЛКИ НА ОБЪЕКТ", "СПРАВОЧНИК (ДОПОЛНИТЕЛЬНО)", "ДОКУМЕНТ (ДОПОЛНИТЕЛЬНО)"
// 

////////////////////////////////////////////////////////////////////////////////
// ВКЛАДКА "ССЫЛКИ НА ОБЪЕКТ". ПОИСК И ЗАМЕНА ССЫЛОК НА ОБЪЕКТ.
// 

// Вкладка "Ссылки на Объект". Поиск ссылок на выбранный ОбъектБД.
//
&НаКлиенте
Процедура ТСсылкиНайтиСсылкиНаОбъект(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Объект не выбран.");
		Возврат
	КонецЕсли;
	
	Состояние("Подождите. Производится поиск ссылок на ОбъектБД: "+Объект.ОбъектБД);
	
	ТСсылкиНайтиСсылкиНаОбъектНаСервере(Объект.ОбъектБД);
	Элементы.ДекорВсегоЗаменяемыхСсылок.Заголовок = "Всего: " + Объект.ТСсылкиСписок.Количество();
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
	Состояние("Поиск ссылок на ОбъектБД: " + Объект.ОбъектБД + " завершен.");
	
КонецПроцедуры

// МО: Вкладка "Ссылки на Объект". Поиск ссылок на выбранный ОбъектБД.
//
&НаСервере
Процедура ТСсылкиНайтиСсылкиНаОбъектНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	тзНайденныеСсылки = ОбъектОбработка.ТСсылкиНайтиСсылкиНаОбъектНаСервере(ОбъектБД);
	ЗначениеВРеквизитФормы(тзНайденныеСсылки, "Объект.ТСсылкиСписок");
 
КонецПроцедуры

// Вкладка "Ссылки на Объект". Очистка списка найденных ссылок на объект.
//
&НаКлиенте
Процедура ТСсылкиОчистить(Команда)

	Элементы.ДекорВсегоЗаменяемыхСсылок.Заголовок = "Всего: ";
	Объект.ТСсылкиСписок.Очистить();
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);

КонецПроцедуры

// Вкладка "Ссылки на Объект". Установить Флажки в списке Ссылок на Объект.
//
&НаКлиенте
Процедура ТСсылкиУстановитьФлажки(Команда)
	ФормаУстановитьСнятьФлажокВТаблицеНаСервере(Истина, "ТСсылкиСписок");
КонецПроцедуры

// Вкладка "Ссылки на Объект". Снять Флажки в списке Ссылок на Объект.
//
&НаКлиенте
Процедура ТСсылкиСнятьФлажки(Команда)
	ФормаУстановитьСнятьФлажокВТаблицеНаСервере(Ложь, "ТСсылкиСписок");
КонецПроцедуры

// Вкладка "Ссылки на Объект". Событие Формы НачалоВыбора для ОбъектЗамещающий.
//
&НаКлиенте
Процедура ТСсылкиОбъектБДЗаменяющийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ЭтаФорма.ОдинаковыйТипПриЗамене Тогда
		Если Объект.ПоказыватьСообщения Тогда
			Сообщить(СтрокаРавно);
			Сообщить("Для назначения Одинакового типа """ + ТипЗнч(Объект.ОбъектБД) + """ для Исходного и Замещающего воспользуйтесь настройкой Параметра ""Одинаковый тип Объектов при замене значений"".");
		КонецЕсли;
	КонецЕсли;
	ТСсылкиОбъектБДЗаменяющийОграничитьТип();
	
	ЭтаФорма.ТекущийЭлемент = Элементы.ОбъектБДЗаменяющий;
	
КонецПроцедуры

// Вкладка "Ссылки на Объект". Ограничение Типа ОбъектЗаменяющий согласно настройке ОдинаковыйТипПриЗамене (Тип объекта заменяющего = Тип объекта БД).
// см. Форма.Настройка.
//
&НаКлиенте
Процедура ТСсылкиОбъектБДЗаменяющийОграничитьТип()
	
	Если Объект.ОбъектБД = Неопределено Тогда
		//Элементы.ОбъектБДЗаменяющий.Заголовок = "Заменяющий объект";
		Элементы.ОбъектБДЗаменяющий.ОграничениеТипа = Новый ОписаниеТипов;
		Элементы.ОбъектБДЗаменяющий.ВыбиратьТип = Истина;
		Возврат;
	КонецЕсли;
	
	ПолноеСтроковоеИмяТипа = ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Объект.ОбъектБД);
	
	Если ЭтаФорма.ОдинаковыйТипПриЗамене Тогда
		ПолноеСтроковоеИмяТипа = СтрЗаменить(ПолноеСтроковоеИмяТипа, "." ,"Ссылка.");	// подобно "СправочникСсылка.Номенклатура"
		Элементы.ОбъектБДЗаменяющий.ОграничениеТипа = Новый ОписаниеТипов(ПолноеСтроковоеИмяТипа);
		Элементы.ОбъектБДЗаменяющий.ВыбиратьТип = Ложь;
	Иначе
		Элементы.ОбъектБДЗаменяющий.ОграничениеТипа = Новый ОписаниеТипов;
		Элементы.ОбъектБДЗаменяющий.ВыбиратьТип = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Ссылки на Объект". Выбран Новый ЗАМЕЩАЮЩИЙ ОбъектБД во вкладке "Ссылки на Объект":
//
&НаКлиенте
Процедура ТСсылкиОбъектБДЗаменяющийПриИзменении(Элемент)
	
	ЭтаФорма.ТекущийЭлемент = Элементы.ОбъектБДЗаменяющий;
	
	Если ЭтаФорма.ОбъектБДЗаменяющий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.ОбъектБДЗаменяющий = Объект.ОбъектБД Тогда
		ПредупреждениеСообщение(, "В качестве Заменяющего объекта выбран Исходный объект:
		|" + Объект.ОбъектБД + "
		|Необходимо выбрать другой Заменяющий объект.");
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий) = Тип("Число") 
		ИЛИ ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий) = Тип("Строка")
		ИЛИ ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий) = Тип("Дата")
		ИЛИ ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий) = Тип("Булево") Тогда
		
		ПолноеСтроковоеИмяТипа	= Строка(ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий));
		ТипОбъектаБДЗаменяющего	= ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий);
		Предопределенный		= Ложь;
		
	Иначе
	
		ПолноеСтроковоеИмяТипа	= ОбъектБДПолучитьПолноеСтроковоеИмяТипа(ЭтаФорма.ОбъектБДЗаменяющий);
		ТипОбъектаБДЗаменяющего	= ОбъектБДПолучитьТипОбъекта(ЭтаФорма.ОбъектБДЗаменяющий);
		Предопределенный		= ОбъектБДПолучитьПризнакПредопределенныйОбъекта(ЭтаФорма.ОбъектБДЗаменяющий);
	
	КонецЕсли;
	
	ОбъектБДСформироватьЗаголовокЭлементаФормы(ТипОбъектаБДЗаменяющего, Элементы.ОбъектБДЗаменяющий, ПолноеСтроковоеИмяТипа, Предопределенный);
	Элементы.ОбъектБДЗаменяющий.Заголовок = "Заменяющий объект [" + Элементы.ОбъектБДЗаменяющий.Заголовок + "]";
	
	ТипИсходный 	= ТипЗнч(Объект.ОбъектБД);
	ТипЗамещающий	= ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий);
	
	Если НЕ ТипЗамещающий = ТипИсходный Тогда
		ПредупреждениеСообщение(, "ТИПЫ ЗНАЧЕНИЙ ИСХОДНОГО И ЗАМЕНЯЮЩЕГО ОБЪЕКТОВ НЕ СОВПАДАЮТ:
		|
		|ТИП ИСХОДНОГО: """ + ВРег(ТипИсходный) + """ ТИП ЗАМЕНЯЮЩЕГО: """ + ВРег(ТипЗамещающий) + """
		|
		|Будьте внимательны при выборе объектов, в которых будет производиться замена.
		|
		|Если в заменяемом Объекте в составном поле отсутствуют одновременно типы исходного и заменяющего объектов,
		|то возникает конфликтная ситуация ...
		|
		|для установки одинакового типа используйте Параметр ""Одинаковый тип Объектов при замене значений"".");
	КонецЕсли;
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
	Если НЕ ЭтаФорма.ОдинаковыйТипПриЗамене Тогда	// Для возможности выбирать простой тип после ссылочного.
		ТСсылкиОбъектБДЗаменяющийОграничитьТип();
	КонецЕсли;
	
	ЭтаФорма.ТекущийЭлемент = Элементы.ОбъектБДЗаменяющий;
	
КонецПроцедуры

// Вкладка "Ссылки на Объект". Очистка содержимого элемента формы ЗАМЕЩАЮЩИЙ ОбъектБД:
//
&НаКлиенте
Процедура ТСсылкиОбъектБДЗаменяющийОчистка(Элемент)
	
	ЭтаФорма.ОбъектБДЗаменяющий = Неопределено;
	Элементы.ОбъектБДЗаменяющий.Заголовок = "Заменяющий объект";
	ТСсылкиОбъектБДЗаменяющийОграничитьТип();
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// Вкладка "Ссылки на Объект". Произвести открытие ОбъектБДЗаменяющий.
//
&НаКлиенте
Процедура ТСсылкиОбъектБДЗаменяющийОткрытие(Элемент, СтандартнаяОбработка)
		
	Если ЭтаФорма.ОбъектБДЗаменяющий = Неопределено Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

// Вкладка "Ссылки на Объект". Произвести Замену ссылок Выбранного ОбъектаБД на Заменяющий ОбъектБД.
//
&НаКлиенте
Процедура ТСсылкиПроизвестиЗамену(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если Объект.ТСсылкиСписок.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Список объектов, в которых предполагается произвести замену значения:
		|" + Объект.ОбъектБД + " пуст.
		|Произведите поиск объектов, Выберите объекты, в которых необходимо произвести замену и повторите операцию.
		|Замена значения не произведена.");
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.ОбъектБДЗаменяющий = Неопределено Тогда
		ПредупреждениеСообщение(, "Заменяющий объект не определен.
		|Выберите Заменяющий объект.
		|Замена значения не произведена.");
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.ОбъектБДЗаменяющий = Объект.ОбъектБД Тогда
		ПредупреждениеСообщение(, "В качестве Заменяющего объекта выбран Исходный объект:
		|" + Объект.ОбъектБД + "
		|Необходимо выбрать другой Заменяющий объект.
		|Замена значения не произведена.");
		Возврат;
	КонецЕсли;
	
	ОтборВыбранныхСсылок = Новый Структура("Включено", Истина);
	
	Если Объект.ТСсылкиСписок.НайтиСтроки(ОтборВыбранныхСсылок).Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Не выбраны Объекты, в которых необходимо произвести Замену Значения:
		|""" + Объект.ОбъектБД + """ на """ + ЭтаФорма.ОбъектБДЗаменяющий + """
		|Необходимо выбрать Объект(ы) в списке Ссылок.
		|Замена значения не произведена.");
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий) = Тип("Число") 
		ИЛИ ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий) = Тип("Строка")
		ИЛИ ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий) = Тип("Дата")
		ИЛИ ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий) = Тип("Булево") Тогда
		
		ПолноеСтроковоеИмяТипа	= Строка(ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий));
		ТипОбъектаБДЗаменяющего	= ТипЗнч(ЭтаФорма.ОбъектБДЗаменяющий);
		Предопределенный		= Ложь;
		
	Иначе
	
		ПолноеСтроковоеИмяТипа	= ОбъектБДПолучитьПолноеСтроковоеИмяТипа(ЭтаФорма.ОбъектБДЗаменяющий);
		ТипОбъектаБДЗаменяющего	= ОбъектБДПолучитьТипОбъекта(ЭтаФорма.ОбъектБДЗаменяющий);
		Предопределенный		= ОбъектБДПолучитьПризнакПредопределенныйОбъекта(ЭтаФорма.ОбъектБДЗаменяющий);
	
	КонецЕсли;
	
	ТекстВопроса = "Вы подтверждаете замену выбранного значения 
	|" + ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Объект.ОбъектБД) + ": " + Объект.ОбъектБД + "
	|на 
	|" + ПолноеСтроковоеИмяТипа + ": " + ЭтаФорма.ОбъектБДЗаменяющий + " ?";
	
	ЗаголовокВопроса = "ЗАМЕНА ССЫЛОК НА ОБЪЕКТ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ТСсылкиПроизвестиЗаменуЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ТСсылкиПроизвестиЗаменуЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТСсылкиПроизвестиЗаменуЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите, пожалуйста. Выполняется обработка выбранных объектов...");
		
	ОчиститьСообщения();
	ТСсылкиПроизвестиЗаменуСсылокНаСервере(ЭтаФорма.ОбъектБДЗаменяющий);
		
	ПредупреждениеСообщение(, "Обработка завершена!");
	
КонецПроцедуры

// МО: Вкладка "Ссылки на Объект". Произвести Замену ссылок Выбранного ОбъектаБД на Заменяющий ОбъектБД.
//
&НаСервере
Процедура ТСсылкиПроизвестиЗаменуСсылокНаСервере(ОбъектБДЗаменяющий)
	
	ОбъектОбработка = ОбъектОбработка();
	ОбъектОбработка.ТСсылкиПроизвестиЗаменуСсылокНаСервере(ОбъектБДЗаменяющий);
	
КонецПроцедуры

// Вкладка "Ссылки на объект". Открыть текущий элемент списка.
//
&НаКлиенте
Процедура ТСсылкиОткрытие(Элемент, СтандартнаяОбработка)
			
	Если Элемент.ТекущийЭлемент.Имя = "Данные" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуОбъектаМодальноИлиНемодальноВРежимеБлокировкиВладельца(Элемент.ТекущиеДанные.Данные, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТСсылкиОчиститьПараметры()
	
	Если ЗначениеЗаполнено(ЭтаФорма.ОбъектБДЗаменяющий) Тогда
		Элементы.ОбъектБДЗаменяющий.Заголовок = "Заменяющий объект";
	КонецЕсли;
	ЭтаФорма.ОбъектБДЗаменяющий	= Неопределено;
	ЭтаФорма.ОдинаковыйТипПриЗамене = Истина;
	ТСсылкиОбъектБДЗаменяющийОграничитьТип();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВКЛАДКА "ДОПОЛНИТЕЛЬНО". "СПРАВОЧНИК (ДОПОЛНИТЕЛЬНО)".
// 
 
&НаКлиенте
Процедура ТСправочникПолучитьНачальныеПараметры(Команда)
	
	Объект.ТСправочникСписок.Очистить();
	ЭтаФорма.СправочникЭлементГруппа = Объект.ОбъектБД;
	ТСправочникСформироватьСписокДействийСоСправочником(Объект.ОбъектБД);
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)" Список возможных действий (Наименования обработок).
//
&НаСервере
Процедура ТСправочникСформироватьСписокДействийСоСправочником(ОбъектБД)
	
	Если НЕ Объект.ТипОбъектаБД = ИмяТипаСправочники() Тогда
		ЭтаФорма.СправочникСписокДействия.Очистить();
		Возврат;
	КонецЕсли;
	
	мдОбъектаБД = ОбъектБД.Метаданные();
	
	Если мдОбъектаБД.Иерархический Тогда
		Элементы.ДекорИерархияСправочника.Заголовок = "Иерархический";
	Иначе
		Элементы.ДекорИерархияСправочника.Заголовок = "Неиерархический";
	КонецЕсли;
	
	ЭтаФорма.СправочникСписокДействия.Очистить();
	
	ЭтаФорма.СправочникСписокДействия.Добавить("ЧистыйСтроковыйРеквизит_БезЛишнихСимволов"		, "Удалить лишние символы в реквизите типа Строка");
	ЭтаФорма.СправочникСписокДействия.Добавить("УдалитьНедопустимыеСимволыXML"					, "Удалить недопустимые символы XML в реквизите типа Строка");
	ЭтаФорма.СправочникСписокДействия.Добавить("ИсключитьНеЧитаемыеСимволыИзСтроки"				, "Удалить нечитаемые символы в реквизите типа Строка");
	ЭтаФорма.СправочникСписокДействия.Добавить("Реквизит_Значение"								, "Заполнить Реквизит Значением");
	
	ЭтаФорма.СправочникЕстьРеквизитОписание = ЕстьРеквизитОбъекта1С("Описание", ОбъектБД);
	Элементы.ТСправочникОписание.Видимость = ЭтаФорма.СправочникЕстьРеквизитОписание;
	
	ЭтаФорма.СправочникЕстьРеквизитНаименованиеПолное = ЕстьРеквизитОбъекта1С("НаименованиеПолное", ОбъектБД);
	
	Если ЭтаФорма.СправочникЕстьРеквизитНаименованиеПолное И ЭтаФорма.СправочникЕстьРеквизитОписание Тогда
		ЭтаФорма.СправочникСписокДействия.Добавить("НаименованиеПолное_НаименованиеИОписание"	, "Заполнить НаименованиеПолное по Наименование+Описание");
	КонецЕсли;
	
	ЭтаФорма.СправочникЕстьРеквизитАртикул = ЕстьРеквизитОбъекта1С("Артикул", ОбъектБД);
	Элементы.ТСправочникАртикул.Видимость = ЭтаФорма.СправочникЕстьРеквизитАртикул;
	
	Если ЕстьРеквизитОбъекта1С("Наименование", Объект.ОбъектБД) И ЭтаФорма.СправочникЕстьРеквизитАртикул Тогда
		ЭтаФорма.СправочникСписокДействия.Добавить("Наименование_АртикулВНаименование"			, "Вставить Артикул в начало Наименования");
		ЭтаФорма.СправочникСписокДействия.Добавить("Наименование_НаименованиеБезАртикула"		, "Удалить Артикул из Наименования");
	КонецЕсли;
	
	Если ((Найти(ВРег(Объект.ПолноеСтроковоеИмяТипа), "КОНТРАГЕНТ") > 0) ИЛИ (Найти(ВРег(Объект.ПолноеСтроковоеИмяТипа), "ОРГАНИЗАЦИ") > 0)) Тогда
		ЭтаФорма.СправочникСписокДействия.Добавить("Наименования_ПоОКОПФ"						, "Заполнить Наименования по ОКОПФ");
		Элементы.ТСправочникНаименованиеПолное.Видимость = ЭтаФорма.СправочникЕстьРеквизитНаименованиеПолное;
		ЭтаФорма.СправочникЕстьРеквизитНаименованиеСокращенное = ЕстьРеквизитОбъекта1С("НаименованиеСокращенное", ОбъектБД);
		Элементы.ТСправочникНаименованиеСокращенное.Видимость = ЭтаФорма.СправочникЕстьРеквизитНаименованиеСокращенное;
	КонецЕсли;
	
	ЭтаФорма.СправочникСписокДействия.Добавить("НеиспользуемыеЭлементы_ПоискИУдаление"			, "Найти и пометить на удаление неиспользуемые элементы");
	
	Если НЕ мдОбъектаБД.Иерархический Тогда
		Элементы.ТСправочникРодитель.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)" Кнопка FAQ.
//
&НаКлиенте
Процедура ТСправочникОписаниеДействия(Команда)
	
	СтруктураВозврат = ТСправочникПолучитьПояснениеДействия();
	
	ТСправочникПояснениеДействия = СтруктураВозврат.ТСправочникПояснениеДействия;
	ТСправочникПараметрыДействия = СтруктураВозврат.ТСправочникПараметрыДействия;
	
	Если ЗначениеЗаполнено(ТСправочникПояснениеДействия) Тогда
		ПредупреждениеСообщение(, ТСправочникПояснениеДействия);
	Иначе
		ПредупреждениеСообщение(, "Выберите действие и повторите просмотр подсказки.");
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)" Описание возможных действий.
//
&НаКлиенте
Функция ТСправочникПолучитьПояснениеДействия()
	
	СтруктураВозврат = Новый Структура;
	
	ТСправочникПояснениеДействия = "";
	ТСправочникПараметрыДействия = Новый Структура;
	
	Если ЭтаФорма.СправочникДействие = "ЧистыйСтроковыйРеквизит_БезЛишнихСимволов" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Изменяются элементы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|Из Наименований удаляются: Двойные Пробелы, символы: Табуляции, НеразрывныйПробел и т.д.
		|А также пробелы, находящиеся ""не на своем месте"" или которыми можно пренебречь.
		|
		|Например:
		|
		|"" = "" (пробелы в начале и в конце) => преобразуется в ""="" (без пробелов).
		|"" ,"" (пробел в начале) = > преобразуется в "","" (без пробелов).
		|""( "" (пробел в конце) = > преобразуется в ""("" (без пробелов).
		|
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Удаление ""лишних"" символов");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите удаление лишних символов в выбранных элементах");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Происходит удаление лишних символов ...");
		
	ИначеЕсли ЭтаФорма.СправочникДействие = "УдалитьНедопустимыеСимволыXML" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Изменяются элементы, содержащие недопустимые символы XML.
		|Изменение происходит при нажатии кнопки ""Найти и исправить"".
		|
		|Первым исправляемым реквизитом выбрать ""Наименование"".
		|
		|В реквизите типа ""строка"" недопустимые символы XML заменяются на пробел.
		|Удаление нечитаемых символов не производится.
		|
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Удаление недопустимых символов XML");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите удаление недопустимых символов XML в выбранных элементах");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Происходит удаление недопустимых символов XML ...");
		
		
	ИначеЕсли ЭтаФорма.СправочникДействие = "ИсключитьНеЧитаемыеСимволыИзСтроки" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Изменяются элементы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|В наименовани остаются:
		|Латиница = ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz;
		|Кирилица = АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюя;
		|БеларусА = АаБбВвГгДдЕеЁёЖжЗзІіЙйКкЛлМмНнОоПпРрСсТтУуЎўФфХхЦцЧчШшЫыЬьЭэЮюЯя;
		|УкраинаА = АаБбВвГгҐґДдЕеЄєЖжЗзИиІіЇїЙйКкЛлМмНнОоПпРрСсТтУуФфХхЦцЧчШшЩщЬьЮюЯя;
		|Греческие = ΑΒΓΔΕΖΗΘΙΚΛΜΝΞΟΠΡΣΤΥΦΧΨΩαβγδεζηθικλμνξοπρστυφχψω;
		|Цифры = 0123456789;
		|СпециальныеСимволы = ~`!@#$%^&*(){}[]_-=+\|/*:;.<>?,№«» ;
		|ДвойнаяКавычка = """";
		|ОдинарнаяКавычка = ';
		|АпострофОбратный = ́;			// КодСимвола 769. Обратный для символа на букве Ё.
		|АвторскоеПраво = ©;			// КодСимвола 169. Copyright - латинская буква C в окружности - авторское право.
		|Зарезервировано = ®; 			// КодСимвола 174. Registered - латинская буква R в окружности - товарный знак.
		|ТоварныйЗнак = ™;				// Верхний  индекс ТМ.
		|ШирокоеТире = —;				// КодСимвола 8212.
		|ДенежныеСимволы = ^¤^ք^¢^€^£^¥;// Денежная единица, Рубль, Цент, Евро, Фунт стерлингов, Иена или юань. Код символа ք = 1412.
		|ДробныеСимволы = ½¼¾;			// Дроби: 1/2, 1/4, 3/4.
		|СимволыСтепени = ¹²³;			// Степени: 1, 2, 3
		|ПрочиеСимволы = °±×÷Øƒµ+Символ(167);	// Градус, Плюс/Минус, Знак умножения, Знак деления, Диаметр, Знак функции, Микро, Параграф.
		|
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Исключение нечитаемых символов");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите исключение нечитаемых символов в выбранных элементах");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Происходит исключение нечитаемых символов ...");
		
	ИначеЕсли ЭтаФорма.СправочникДействие = "Реквизит_Значение" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Изменяются элементы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|В выбранный реквизит заносится значение из реквизита ""Новое Значение"".
		|
		|Например:
		|
		|БЫЛО: Номенклатура реквизит ""Весовой"" = Ложь.
		|Новое значение: Истина.
		|СТАЛО: Номенклатура реквизит ""Весовой"" = Истина.
		|
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Заполнение реквизита значением");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите заполнение реквизита значением в выбранных элементах");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Происходит заполнение реквизита значением ...");
		
	ИначеЕсли ЭтаФорма.СправочникДействие = "НаименованиеПолное_НаименованиеИОписание" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Изменяются элементы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|Пример:
		|
		|Исходные значение реквизитов:
		|1. Наименование: ""Бублик""
		|2. Описание; ""с дыркой""
		|
		|Результат: НаименованиеПолное = ""Бублик с дыркой""
		|
		|ПРИМЕЧАНИЕ: НаименованиеПолное используется в печатных формах.
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Заполнение Наименования Полного по Наименованию и Описанию");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите заполнение в выбранных элементах");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Происходит заполнение Наименования Полного по Наименованию и Описанию. ");
		
	ИначеЕсли ЭтаФорма.СправочникДействие = "Наименование_АртикулВНаименование" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Изменяются элементы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|Реквизит Артикул обязательно должен быть заполнен, если нет, то элемент не обрабатывается.
		|Из наименования извлекается 1-ое слово. 
		|Если 1-ым словом является Артикул, то элемент не обрабатывается.
		|Все другие вхождения подстроки Артикула типа ""1234567890"" удаляются, например в конце строки.
		|
		|Пример:
		|
		|Исходное значение реквизита Артикул: 1234567890, а Наименования: Тара 1234567890
		|
		|Результат: Наименование: 1234567890 Тара
		|
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Добавление Артикула в Наименования");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите добавление артикула в выбранных элементах");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Происходит добавление артикула 1-м словом в Наименованиях. ");
		
	ИначеЕсли ЭтаФорма.СправочникДействие = "Наименование_НаименованиеБезАртикула" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Изменяются элементы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|Реквизит Артикул обязательно должен быть заполнен, если нет, то элемент не обрабатывается.
		|Все вхождения подстроки Артикула типа ""1234567890"" удаляются.
		|
		|Пример:
		|
		|Исходное значение реквизита Артикул: ""1234567890"", а Наименования: ""1234567890 Тара 1234567890""
		|
		|Результат: Наименование: ""Тара""
		|
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Удаление Артикула из Наименования");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите удаление артикула в выбранных элементах ");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Происходит удаление артикула из Наименования. ");
		
	ИначеЕсли ЭтаФорма.СправочникДействие = "Наименования_ПоОКОПФ" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Изменяются элементы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|Ключевое значение: Наименование.
		|Из Наименования извлекается 1-ое слово.
		|Если 1-ым словом является Аббревиатура (согласно ОКОПФ), то элемент обрабатывается.
		|Все другие вхождения подстроки Аббревиатуры типа "" ООО "" (пробелы слева и справа) удаляются.
		|
		|Пример:
		|
		|Исходное значение реквизита Наименования: ООО БеломорКанал
		|
		|Результат:
		|
		|1. Реквизит Наименование:______________БеломорКанал ООО
		|2. Реквизит НаименованиеСокращенное:___ООО БеломорКанал
		|3. Реквизит НаименованиеПолное:________Общество с ограниченной ответственностью БеломорКанал
		|
		|ВАЖНО: В реквизите Наименование должно быть ПОЛНОЕ ПРАВИЛЬНОЕ наименование с АББРЕВИАТУРОЙ из ОКОПФ в начале.
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Обработка Наименования справочника Контрагенты/Организации");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите обработку в выбранных элементах");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Обработка Наименования Контрагенты/Организации. ");
		
	ИначеЕсли ЭтаФорма.СправочникДействие = "НеиспользуемыеЭлементы_ПоискИУдаление" Тогда
		
		ТСправочникПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Принцип действия:
		|
		|Помечаются на удаление элементы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|ВАЖНО! Помеченные на удаление элементы раньше (до выполняемой данного действия) 
		|исключаются из поиска и последующей пометки на удаление.
        |
		|ВАЖНО! Предопределенные элементы исключаются из поиска и последующей пометки на удаление
		|(удалить предопределенные элементы в режиме 1С:Предприятие невозможно).
        |
		|ВАЖНО! В поле «Объект/Группа» рекомендуется выбирать Группу/Элемент-Родитель для иерархического справочника. 
		|Обработка всего справочника может быть очень длительной, исключение неиерархический справочник 
		|(предположение: неиерархические справочники - небольшие по количеству элементов)
		|
		|Фактическое удаление производится штатными средствами 1С:Предприятие:
		|- Обычное приложение: Главное меню - Операции - Удаление помеченных объектов.
        |- Управляемое приложение: Администрирование - Удаление помеченных объектов.
        |- Фактическое удаление рекомендуется производить в монопольном режиме.
        |
		|Ключевое значение: Ссылка.
		|Элемент не встречается нигде, кроме текущего справочника.
		|
		|==============================================================================================================
		|";
		
		ТСправочникПараметрыДействия.Вставить("Действие"		, ЭтаФорма.СправочникДействие);
		ТСправочникПараметрыДействия.Вставить("Заголовок"		, "Найти и пометить на удаление неиспользуемые элементы");
		ТСправочникПараметрыДействия.Вставить("ТекстВопроса"	, ТСправочникПояснениеДействия + "Подтвердите обработку в выбранных элементах");
		ТСправочникПараметрыДействия.Вставить("ТекстСостояние"	, "Подождите немного. Обработка элементов. Пометка на удаление. ");
		
	КонецЕсли;
	
	СтруктураВозврат.Вставить("ТСправочникПояснениеДействия", ТСправочникПояснениеДействия);
	СтруктураВозврат.Вставить("ТСправочникПараметрыДействия", ТСправочникПараметрыДействия);
	
	Возврат СтруктураВозврат;
	
КонецФункции

// Вкладка "Справочник (Дополнительно)". Показать список Блокированных реквизитов.
//
&НаКлиенте
Процедура ТСправочникБлокированныеРеквизиты(Команда)
	Перем ЭлементСписка, МногоСтрочныйТекст;
	
	МногоСтрочныйТекст = "";
	Для Каждого ЭлементСписка ИЗ Объект.БлокируемыеРеквизиты Цикл
		МногоСтрочныйТекст = МногоСтрочныйТекст + ЭлементСписка.Значение + Символы.ПС;
	КонецЦикла;
	
	ПросмотретьМногострочныйТекст_Стандартно(МногоСтрочныйТекст, "Блокируемые реквизиты объекта");

КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Выбрать "Действие" из СпискаЗначений СправочникСписокДействия.
//
&НаКлиенте
Процедура ТСправочникДействиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Перем ВыбранныйЭлемент, Оповещение, Параметры;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Элемент", Элемент);
	Параметры.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		ВыбранныйЭлемент = ВыбратьИзСписка(ЭтаФорма.СправочникСписокДействия, Элемент, ДанныеВыбора);
		ТСправочникДействиеНачалоВыбораЗавершение(ВыбранныйЭлемент, Параметры);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ТСправочникДействиеНачалоВыбораЗавершение"", ЭтаФормаЭтотОбъект(), Параметры)");
		Выполнить("ПоказатьВыборИзСписка(Оповещение, ЭтаФорма.СправочникСписокДействия, Элемент)");
	КонецЕсли;;
		
КонецПроцедуры

&НаКлиенте
Процедура ТСправочникДействиеНачалоВыбораЗавершение(ВыбранныйЭлемент, Параметры) Экспорт
	
	Элемент = Параметры.Элемент;
	СтандартнаяОбработка = Параметры.СтандартнаяОбработка;
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		ТСправочникДействиеПриИзменении(Неопределено);
		Возврат;
	КонецЕсли;
	
	Объект.ТСправочникСписок.Очистить();
	
	ТСправочникДействиеОчистка(Элемент, СтандартнаяОбработка, Истина);
	
	ЭтаФорма.СправочникДействие = ВыбранныйЭлемент.Значение;
	ТСправочникДействиеПриИзменении(Неопределено);
	
	Если ЭтаФорма.СправочникДействие = "ЧистыйСтроковыйРеквизит_БезЛишнихСимволов" Тогда
		Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
		Элементы.СправочникИзменяемыйРеквизит.ТолькоПросмотр 		= Ложь;
		Элементы.СправочникНовоеЗначениеРеквизита.ТолькоПросмотр 	= Истина;
		ЭтаФорма.СправочникНовоеЗначениеРеквизита 					= "без лишних символов";
	ИначеЕсли ЭтаФорма.СправочникДействие = "УдалитьНедопустимыеСимволыXML" Тогда
		Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
		Элементы.СправочникИзменяемыйРеквизит.ТолькоПросмотр 		= Ложь;
		Элементы.СправочникНовоеЗначениеРеквизита.ТолькоПросмотр 	= Истина;
		ЭтаФорма.СправочникНовоеЗначениеРеквизита 					= "Удалить недопустимые символы XML";
	ИначеЕсли ЭтаФорма.СправочникДействие = "ИсключитьНеЧитаемыеСимволыИзСтроки" Тогда
		Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
		Элементы.СправочникИзменяемыйРеквизит.ТолькоПросмотр 		= Ложь;
		Элементы.СправочникНовоеЗначениеРеквизита.ТолькоПросмотр 	= Истина;
		ЭтаФорма.СправочникНовоеЗначениеРеквизита 					= "Исключить нечитаемые символы";
	ИначеЕсли ЭтаФорма.СправочникДействие = "Реквизит_Значение" Тогда
		Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Истина;
		Элементы.СправочникИзменяемыйРеквизит.ТолькоПросмотр 		= Ложь;
		Элементы.СправочникНовоеЗначениеРеквизита.ТолькоПросмотр 	= Ложь;
	ИначеЕсли ЭтаФорма.СправочникДействие = "НеиспользуемыеЭлементы_ПоискИУдаление" Тогда
		Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
		Элементы.СправочникИзменяемыйРеквизит.ТолькоПросмотр 		= Истина;
		Этаформа.СправочникИзменяемыйРеквизит 						= "Ссылка";
		Элементы.СправочникНовоеЗначениеРеквизита.ТолькоПросмотр 	= Истина;
		ЭтаФорма.СправочникНовоеЗначениеРеквизита 					= "Удалить, нельзя помиловать";
		Элементы.СправочникДатаРанее.ТолькоПросмотр					= Ложь;
		ЭтаФорма.СправочникДатаРанее								= НачалоГода(ТекущаяДата());
	Иначе
		Если ЕстьРеквизитОбъекта1С("Наименование", Объект.ОбъектБД) Тогда
			Элементы.СправочникИзменяемыйРеквизит.ТолькоПросмотр 		= Истина;
			Элементы.СправочникНовоеЗначениеРеквизита.ТолькоПросмотр	= Истина;
			Если ЭтаФорма.СправочникДействие = "НаименованиеПолное_НаименованиеИОписание" Тогда
				Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Истина;
				Этаформа.СправочникИзменяемыйРеквизит 		= "НаименованиеПолное";
				ЭтаФорма.СправочникНовоеЗначениеРеквизита 	= "= Наименование+Описание";
			ИначеЕсли ЭтаФорма.СправочникДействие = "Наименование_АртикулВНаименование" Тогда
				Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
				Этаформа.СправочникИзменяемыйРеквизит 		= "Наименование";
				ЭтаФорма.СправочникНовоеЗначениеРеквизита 	= "= Артикул + Наименование";
			ИначеЕсли ЭтаФорма.СправочникДействие = "Наименование_НаименованиеБезАртикула" Тогда
				Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
				Этаформа.СправочникИзменяемыйРеквизит 		= "Наименование";
				ЭтаФорма.СправочникНовоеЗначениеРеквизита 	= "= Наименование без Артикула";
			ИначеЕсли ЭтаФорма.СправочникДействие = "Наименования_ПоОКОПФ" Тогда
				Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
				Этаформа.СправочникИзменяемыйРеквизит 		= "Наименование";
				ЭтаФорма.СправочникНовоеЗначениеРеквизита 	= "Согласно ОКОПФ (а также Полное и Сокращенное)";
			КонецЕсли;
		Иначе
			// Наименование отсутствует.
			ПредупреждениеСообщение(, "Реквизит ""Наименование"" отсутствует у отбъекта типа " + Объект.ПолноеСтроковоеИмяТипа);
			ЭтаФорма.СправочникДействие = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

&НаКлиенте
Процедура ТСправочникДействиеПриИзменении(Элемент)
	Если ЭтаФорма.СправочникДействие = "УдалитьНедопустимыеСимволыXML" Тогда
		Элементы.кнТСправочникНайти.Заголовок = "Найти и исправить";
		Элементы.ТСправочникВключено.ТолькоПросмотр = Истина;
	Иначе
		Элементы.кнТСправочникНайти.Заголовок = "Найти";
		Элементы.ТСправочникВключено.ТолькоПросмотр = Ложь;
	КонецЕсли;
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Очистить реквизит "Действие".
//
&НаКлиенте
Процедура ТСправочникДействиеОчистка(Элемент, СтандартнаяОбработка, Очистить = Истина)
	
	Объект.ТСправочникСписок.Очистить();
	
	ЭтаФорма.СправочникДействие = Неопределено;
	ТСправочникИзменяемыйРеквизитОчистка(Элемент, СтандартнаяОбработка, Очистить);
	ТСправочникНовоеЗначениеРеквизитаОчистка(Элемент, СтандартнаяОбработка);
		
	Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
	Элементы.СправочникИзменяемыйРеквизит.ТолькоПросмотр = Истина;
	Элементы.СправочникНовоеЗначениеРеквизита.ТолькоПросмотр = Истина;
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Очистить Элемент "СправочникЭлементГруппа".
//
&НаКлиенте
Процедура ТСправочникЭлементГруппаПриИзменении(Элемент)
	
	Объект.ТСправочникСписок.Очистить();
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Очистить Элемент "ОбрабатываемыйЭлементГруппа" и "ТСправочникСписок".
//
&НаКлиенте
Процедура ТСправочникЭлементГруппаОчистка(Элемент, СтандартнаяОбработка)
	
	ЭтаФорма.СправочникЭлементГруппа = Неопределено;
	ТСправочникОчистить(Элемент);
	ТСправочникЭлементГруппаИзменениеОчисткаПредупреждение();
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Очистить Элемент "ОбрабатываемыйЭлементГруппа" и "ТСправочникСписок".
//
&НаКлиенте
Процедура ТСправочникЭлементГруппаИзменениеОчисткаПредупреждение()
	
	ПредупреждениеСообщение(, "Объект/Группа не выбран(а). Будет обрабатываться весь справочник.
	|
	|Если Справочник содержит большое количество элементов то,
	|это может вызывать проблему нехватки памяти при обработке.
	|
	|Рекомендуется выбирать Группу/Элемент-Родитель для иерархического справочника.");
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Выбор "ИзменяемыйРеквизит".
//
&НаКлиенте
Процедура ТСправочникИзменяемыйРеквизитНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Перем ВыбранныйЭлемент, Оповещение;
	
	СтандартнаяОбработка = Ложь;
	
	Элемент.ЦветТекста 	= Объект.ЦветТекстаПоля;
	Элемент.ЦветФона 	= Объект.ЦветФонаПоля;
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		ВыбранныйЭлемент = ВыбратьИзСписка(Объект.СписокРеквизиты, Элемент, ДанныеВыбора);
		ТСправочникИзменяемыйРеквизитНачалоВыбораЗавершение(ВыбранныйЭлемент, Элемент);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ТСправочникИзменяемыйРеквизитНачалоВыбораЗавершение"", ЭтаФормаЭтотОбъект(), Элемент)");
		Выполнить("ПоказатьВыборИзСписка(Оповещение, Объект.СписокРеквизиты, Элемент)");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТСправочникИзменяемыйРеквизитНачалоВыбораЗавершение(ВыбранныйЭлемент, Элемент) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		ФормаПредупреждениеБлокированныйРеквизит(Элемент, ЭтаФорма.СправочникИзменяемыйРеквизит);
		Возврат;
	КонецЕсли;
	
	Если ВРег(ВыбранныйЭлемент) = "ЭТОГРУППА" Тогда
		ПредупреждениеСообщение(, "Стандартный реквизит ""ЭтоГруппа"" (признак группы) Справочника/ПВХ недоступно для корректировки/записи.
		|
		|Выберите другой реквизит.");
		Возврат;
	КонецЕсли;
	
	Если Объект.СтруктураРеквизиты[ВыбранныйЭлемент.Значение].СодержитТип(Тип("ХранилищеЗначения")) Тогда
		ПредупреждениеСообщение(, "Поле Объекта: """ + ВыбранныйЭлемент + """ типа ""ХранилищеЗначения"" недоступно для корректировки/записи.
		|
		|Выберите другой реквизит.");
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.СправочникДействие = "Реквизит_Значение" Тогда
		// Значения разных типов.
		ЭтаФорма.СправочникНовоеЗначениеРеквизита = Неопределено;
	Иначе
		Если НЕ Объект.СтруктураРеквизиты[ВыбранныйЭлемент.Значение].СодержитТип(Тип("Строка")) Тогда
			ПредупреждениеСообщение(, "Выбран реквизит не строкового типа: """ + ВыбранныйЭлемент.Значение + """
			|
			|Выберите другой реквизит.");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЭтаФорма.СправочникИзменяемыйРеквизит = ВыбранныйЭлемент.Значение;
	
	Если ЭтаФорма.СправочникДействие = "Реквизит_Значение" Тогда
		Объект.ТСправочникСписок.Очистить();
		Элементы.СправочникНовоеЗначениеРеквизита.ОграничениеТипа 	= Объект.СтруктураРеквизиты[ВыбранныйЭлемент.Значение];
	КонецЕсли;
	
	ФормаПредупреждениеБлокированныйРеквизит(Элемент, ЭтаФорма.СправочникИзменяемыйРеквизит);
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры
	
// Предупреждение: "Изменение блокированного реквизита объекта".
//
&НаКлиенте
Процедура ФормаПредупреждениеБлокированныйРеквизит(Элемент, БлокированныйРеквизит)
	
	Если Объект.БлокируемыеРеквизиты.Количество() > 0 Тогда
		Если НЕ Объект.БлокируемыеРеквизиты.НайтиПоЗначению(БлокированныйРеквизит) = Неопределено Тогда
			Элемент.ЦветТекста 	= Объект.ЦветТекстаБлокируемогоРеквизита;
			Элемент.ЦветФона 	= Объект.ЦветФонаБлокируемогоРеквизита;
			
			ТекстСообщения = "Изменение блокированного реквизита объекта.
			|
			|Согласно проектной логике Конфигурации """ + Объект.КонфигурацияИВерсия + """:
			|Реквизит не рекомендуется/нельзя изменять, если Объект участвует в документах.
			|
			|""Блокированные"" реквизиты определяются индивидуально для вида Объекта.";
			ПредупреждениеСообщение(, ТекстСообщения);
		Иначе
			Элемент.ЦветТекста 	= Объект.ЦветТекстаПоля;
			Элемент.ЦветФона 	= Объект.ЦветФонаПоля;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КлиентСообщениеПользователю(Текст, Поле = "")
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = Текст;
	Сообщение.Поле = Поле;
	Сообщение.Сообщить();
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Очистка "ИзменяемыйРеквизит", "ЗначениеРеквизита".
//
&НаКлиенте
Процедура ТСправочникИзменяемыйРеквизитОчистка(Элемент, СтандартнаяОбработка, Очистить = Ложь)
	
	ЭтаФорма.СправочникИзменяемыйРеквизит 				= Неопределено;
	Элементы.СправочникИзменяемыйРеквизит.ЦветТекста 	= Объект.ЦветТекстаПоля;
	Элементы.СправочникИзменяемыйРеквизит.ЦветФона 		= Объект.ЦветФонаПоля;
	
	Если ЭтаФорма.СправочникДействие = "Реквизит_Значение" ИЛИ Очистить Тогда
		ТСправочникНовоеЗначениеРеквизитаОчистка(Элемент, СтандартнаяОбработка);
		Элементы.СправочникНовоеЗначениеРеквизита.ОграничениеТипа = Новый ОписаниеТипов;
	КонецЕсли;
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);

КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Новое значение реквизита изменено.
//
&НаКлиенте
Процедура ТСправочникНовоеЗначениеРеквизитаПриИзменении(Элемент)
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Новое значение реквизита очищено.
//
&НаКлиенте
Процедура ТСправочникНовоеЗначениеРеквизитаОчистка(Элемент, СтандартнаяОбработка)
	
	ЭтаФорма.СправочникНовоеЗначениеРеквизита = Неопределено;
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Сформировать список для последующей обработки.
//
&НаКлиенте
Процедура ТСправочникНайтиЭлементы(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	ЗаголовокВопроса = ВРег(ЭтаФорма.СправочникСписокДействия.НайтиПоЗначению(ЭтаФорма.СправочникДействие));
	
	СтруктураВозврат = ТСправочникПолучитьПояснениеДействия();
	ТСправочникПояснениеДействия = СтруктураВозврат.ТСправочникПояснениеДействия;
	ТСправочникПараметрыДействия = СтруктураВозврат.ТСправочникПараметрыДействия;
	
	ТекстДополнительный = "";
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.СправочникЭлементГруппа) Тогда
		ТекстДополнительный = "Весь справочник.";
	Иначе
		ТекстДополнительный = "Элемент/Группа: " + ЭтаФорма.СправочникЭлементГруппа;
	КонецЕсли;
	
	ТекстВопроса = ТСправочникПояснениеДействия + "
	|Произвести выборку данных по " + ТекстДополнительный + " ?
	|";
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 100, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ТСправочникНайтиЭлементыЗавершение(Ответ, ТекстДополнительный);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ТСправочникНайтиЭлементыЗавершение"", ЭтаФормаЭтотОбъект(), ТекстДополнительный)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 100, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТСправочникНайтиЭлементыЗавершение(Ответ, ТекстДополнительный) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите. Производится выборка данных.
	|" + ТекстДополнительный);
	
	Объект.ТСправочникСписок.Очистить();
	Если ЭтаФорма.СправочникДействие = "НеиспользуемыеЭлементы_ПоискИУдаление" Тогда
		ТСправочникНайтиНеиспользуемыеЭлементыНаСервере(Объект.ОбъектБД);
	ИначеЕсли ЭтаФорма.СправочникДействие = "УдалитьНедопустимыеСимволыXML" Тогда
		ТСправочникНайтиЭлементыНаСервере(Объект.ОбъектБД);
		Если Объект.ТСправочникСписок.Количество() > 0 Тогда
			ПредупреждениеСообщение(, "Удаление недопустимых символов XML произведено.");
		Иначе
			ПредупреждениеСообщение(, "Отсутствуют элементы, в которых необходимо удаленить недопустимые символоы XML.");
		КонецЕсли;
		ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
		Возврат;
	Иначе
		ТСправочникНайтиЭлементыНаСервере(Объект.ОбъектБД);
	КонецЕсли;
	
	Если Объект.ТСправочникСписок.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Для действия """ + ВРег(ЭтаФорма.СправочникСписокДействия.НайтиПоЗначению(ЭтаФорма.СправочникДействие)) + """
		|
		|ДАННЫЕ НЕ НАЙДЕНЫ.");
	КонецЕсли;
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Сформировать список.
//
&НаСервере
Процедура ТСправочникНайтиНеиспользуемыеЭлементыНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	мдОбъектаБД = ОбъектБД.Метаданные();
	Объект.ТСправочникСписок.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Справочник.Ссылка
	|ИЗ
	|	" + Объект.ПолноеСтроковоеИмяТипа + " КАК Справочник
	|ГДЕ
	|	(НЕ Справочник.ПометкаУдаления)
	|	И (НЕ Справочник.Предопределенный)";
	
	Если ЗначениеЗаполнено(ЭтаФорма.СправочникЭлементГруппа) Тогда
		
		Если мдОбъектаБД.Иерархический Тогда
			Если мдОбъектаБД.ВидИерархии = МетаДанные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
				Запрос.Текст = Запрос.Текст + "
				|	И (НЕ Справочник.ЭтоГруппа)
				|	И (Справочник.Ссылка В Иерархии (&ЭлементГруппа))";
				Запрос.УстановитьПараметр("ЭлементГруппа", ЭтаФорма.СправочникЭлементГруппа);
			ИначеЕсли мдОбъектаБД.Иерархический И мдОбъектаБД.ВидИерархии = МетаДанные.СвойстваОбъектов.ВидИерархии.ИерархияЭлементов Тогда
				Запрос.Текст = Запрос.Текст + "
				|	И (Справочник.Ссылка В Иерархии (&ЭлементГруппа))";
				Запрос.УстановитьПараметр("ЭлементГруппа", ЭтаФорма.СправочникЭлементГруппа);
			КонецЕсли;
		Иначе
			Запрос.Текст = Запрос.Текст + "
			|	И (Справочник.Ссылка = &ЭлементГруппа)";
			Запрос.УстановитьПараметр("ЭлементГруппа", ЭтаФорма.СправочникЭлементГруппа);
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		МассивСсылок = Новый Массив;
		МассивСсылок = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		ТаблицаСсылокНайденныхЭлементов = НайтиПоСсылкам(МассивСсылок);
		ТаблицаСсылокНайденныхЭлементов.Свернуть("Ссылка");
		
		Запрос.Текст = Запрос.Текст + "
		|	И (НЕ Справочник.Ссылка В (&СписокИспользуемых))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		Запрос.УстановитьПараметр("СписокИспользуемых", ТаблицаСсылокНайденныхЭлементов.ВыгрузитьКолонку("Ссылка"));
		
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			
			ит = 0;
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				Если ЗначениеЗаполнено(ЭтаФорма.СправочникДатаРанее) И ОбъектОбработка.GUIDПолучитьДатуИВремяСозданияОбъектаНаСервере(Выборка.Ссылка) > КонецДня(ЭтаФорма.СправочникДатаРанее) Тогда
					Продолжить;
				КонецЕсли;
			
				Родитель = Неопределено;
				Если мдОбъектаБД.Иерархический Тогда
					Родитель = Выборка.Ссылка.Родитель;
				КонецЕсли;
				
				Описание = Неопределено;
				Если ЭтаФорма.СправочникЕстьРеквизитОписание Тогда
					Описание = Выборка.Ссылка.Описание;
				КонецЕсли;
				
				Артикул = Неопределено;
				Если ЭтаФорма.СправочникЕстьРеквизитАртикул Тогда
					Артикул = Выборка.Ссылка.Артикул;
				КонецЕсли;
				
				ит = ит + 1;
				ТСправочникСписокСтрока = Объект.ТСправочникСписок.Добавить();
				ТСправочникДобавитьСтрокуНаСервере(ТСправочникСписокСтрока, ит, Выборка.Ссылка, Родитель, Описание, Артикул);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Сформировать список.
//
&НаСервере
Процедура ТСправочникНайтиЭлементыНаСервере(ОбъектБД)
	Перем мдОбъектаБД, ОбъектОбработка;
	Перем ЭлементИБ, ОбъектИБ, Артикул, Родитель, Описание, Изменен;
	
	мдОбъектаБД = ОбъектБД.Метаданные();
	ОбъектОбработка = ОбъектОбработка();
	Объект.ТСправочникСписок.Очистить();
	
	ит = 0;
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.СправочникЭлементГруппа) Тогда
		Выборка = Справочники[мдОбъектаБД.Имя].Выбрать();	// ВЕСЬ Справочник.
	Иначе
		
		Описание = Неопределено;
		Если ЭтаФорма.СправочникЕстьРеквизитОписание Тогда
			Описание = Объект.ОбъектБД.Описание;
		КонецЕсли;
		
		Артикул = Неопределено;
		Если ЭтаФорма.СправочникЕстьРеквизитАртикул Тогда
			Артикул = Объект.ОбъектБД.Артикул;
		КонецЕсли;
		
		Если мдОбъектаБД.Иерархический Тогда
			
			Если (мдОбъектаБД.ВидИерархии = МетаДанные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов И ЭтаФорма.СправочникЭлементГруппа.ЭтоГруппа) Тогда
				Выборка = Справочники[мдОбъектаБД.Имя].ВыбратьИерархически(ЭтаФорма.СправочникЭлементГруппа);
			ИначеЕсли мдОбъектаБД.ВидИерархии = МетаДанные.СвойстваОбъектов.ВидИерархии.ИерархияЭлементов Тогда
				ит = 1;
				ТСправочникСписокСтрока = Объект.ТСправочникСписок.Добавить();
				ТСправочникДобавитьСтрокуНаСервере(ТСправочникСписокСтрока, 1, ЭтаФорма.СправочникЭлементГруппа, ЭтаФорма.СправочникЭлементГруппа.Родитель, Описание, Артикул);
				Выборка = Справочники[мдОбъектаБД.Имя].ВыбратьИерархически(ЭтаФорма.СправочникЭлементГруппа);
			Иначе	// Одиночный элемент (не группа) при ИерархияГруппИЭлементов.
				ит = 1;
				ТСправочникСписокСтрока = Объект.ТСправочникСписок.Добавить();
				ТСправочникДобавитьСтрокуНаСервере(ТСправочникСписокСтрока, 1, ЭтаФорма.СправочникЭлементГруппа, ЭтаФорма.СправочникЭлементГруппа.Родитель, Описание, Артикул);
				Возврат;
			КонецЕсли;
			
		Иначе		// Неиерархический справочник.
			
			ит = 1;
			ТСправочникСписокСтрока = Объект.ТСправочникСписок.Добавить();
			ТСправочникДобавитьСтрокуНаСервере(ТСправочникСписокСтрока, 1, ЭтаФорма.СправочникЭлементГруппа, Неопределено, Описание, Артикул);
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Пока Выборка.Следующий()  Цикл
		
		Изменен = Ложь;
		ЭлементИБ = Выборка.Ссылка;
		
		Если ЭлементИБ.ЭтоГруппа И НЕ ВРег(ЭлементИБ[ЭтаФорма.СправочникИзменяемыйРеквизит]) = "НАИМЕНОВАНИЕ" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтаФорма.СправочникДействие = "УдалитьНедопустимыеСимволыXML" Тогда
			Если НайтиНедопустимыеСимволыXML(ЭлементИБ[ЭтаФорма.СправочникИзменяемыйРеквизит]) > 0 Тогда
				ОбъектИБ = ЭлементИБ.ПолучитьОбъект();
				ОбъектИБ[ЭтаФорма.СправочникИзменяемыйРеквизит] = ОбъектОбработка.ЗаменитьНедопустимыеСимволыXML(ОбъектИБ[ЭтаФорма.СправочникИзменяемыйРеквизит]);
				ОбъектИБ.Записать();
				Изменен = Истина;
				ЭлементИБ = ОбъектИБ.Ссылка;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Родитель = Неопределено;
		Если мдОбъектаБД.Иерархический Тогда
			Родитель = ЭлементИБ.Родитель;
		КонецЕсли;
		
		Описание = Неопределено;
		Если ЭтаФорма.СправочникЕстьРеквизитОписание Тогда
			Описание = ЭлементИБ.Описание;
		КонецЕсли;
		
		Артикул = Неопределено;
		Если ЭтаФорма.СправочникЕстьРеквизитАртикул Тогда
			Артикул = ЭлементИБ.Артикул;
		КонецЕсли;
		
		ит = ит + 1;
		ТСправочникСписокСтрока = Объект.ТСправочникСписок.Добавить();
		ТСправочникДобавитьСтрокуНаСервере(ТСправочникСписокСтрока, ит, ЭлементИБ, Родитель, Описание, Артикул, Изменен);
		
	КонецЦикла;
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Добавить строку в ТСправочникСписок.
//
&НаСервере
Процедура ТСправочникДобавитьСтрокуНаСервере(ТСправочникСписокСтрока, ит, Ссылка, Родитель, Описание, Артикул, Изменен = Ложь)
	
	ТСправочникСписокСтрока.НПП = ит;
	ТСправочникСписокСтрока.Элемент = Строка(Ссылка);
	ТСправочникСписокСтрока.Ссылка = Ссылка;
	Если ЭтаФорма.СправочникДействие = "Наименования_ПоОКОПФ" Тогда
		ТСправочникСписокСтрока.РеквизитСтароеЗначение = ОбъектБДПолучитьЗначениеРеквизитаОбъекта("Наименование", Ссылка);
	Иначе
		ТСправочникСписокСтрока.РеквизитСтароеЗначение = ОбъектБДПолучитьЗначениеРеквизитаОбъекта(ЭтаФорма.СправочникИзменяемыйРеквизит, Ссылка);
	КонецЕсли;
	ТСправочникСписокСтрока.Родитель= Строка(Родитель);
	ТСправочникСписокСтрока.Описание= Описание;
	ТСправочникСписокСтрока.Артикул = Артикул;
	ТСправочникСписокСтрока.Изменен = Изменен;
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Установить флажки у всех элементов.
//
&НаКлиенте
Процедура ТСправочникУстановитьФлажки(Команда)
		
	Состояние("Подождите. Производится пометка данных ...");
	ФормаУстановитьСнятьФлажокВТаблицеНаСервере(Истина, "ТСправочникСписок")
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Установить флажки у элементов с ПУСТЫМ обрабатываемым реквизитом.
//
&НаКлиенте
Процедура ТСправочникУстановитьФлажкиНеЗаполненных(Команда)
	
	Состояние("Подождите. Производится пометка данных ...");
	
	ОграничениеТипаСтрока = Строка(Объект.СтруктураРеквизиты[ЭтаФорма.СправочникИзменяемыйРеквизит]);
	
	Для Каждого СтрокаСписка ИЗ Объект.ТСправочникСписок Цикл
		Попытка
			Если ОграничениеТипаСтрока = "Строка" Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаСписка.РеквизитСтароеЗначение) Тогда	// Пустое значение.
					СтрокаСписка["Включено"] = Истина;
				КонецЕсли;
			ИначеЕсли ОграничениеТипаСтрока = "Число" Тогда
				Если СтрокаСписка.РеквизитСтароеЗначение = 0 Тогда						// = 0.
					СтрокаСписка["Включено"] = Истина;
				КонецЕсли;
			ИначеЕсли ОграничениеТипаСтрока = "Булево" Тогда
				Если НЕ СтрокаСписка.РеквизитСтароеЗначение Тогда						// = Ложь.
					СтрокаСписка["Включено"] = Истина;
				КонецЕсли;
			ИначеЕсли ОграничениеТипаСтрока = "Дата" Тогда
				Если СтрокаСписка.РеквизитСтароеЗначение = Дата("00010101000000") Тогда	// = Дата неопределена.
					СтрокаСписка["Включено"] = Истина;
				КонецЕсли;
			Иначе
				Если НЕ ЗначениеЗаполнено(СтрокаСписка.РеквизитСтароеЗначение) Тогда	// Пустое значение.
					СтрокаСписка["Включено"] = Истина;
				КонецЕсли;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Снять флажки у всех элементов.
//
&НаКлиенте
Процедура ТСправочникСнятьФлажки(Команда)
	
	Состояние("Подождите. Производится снятие пометки данных ...");
	ФормаУстановитьСнятьФлажокВТаблицеНаСервере(Ложь, "ТСправочникСписок")
	
КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Очистка списка.
//
&НаКлиенте
Процедура ТСправочникОчистить(Команда)
	
	Объект.ТСправочникСписок.Очистить();
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);

КонецПроцедуры

// Вкладка "Справочник (Дополнительно)". Задать вопрос о Выполнении обработки элементов справочника.
//
&НаКлиенте
Процедура ТСправочникВыполнить(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	ЗаголовокВопроса = ВРег(ЭтаФорма.СправочникСписокДействия.НайтиПоЗначению(ЭтаФорма.СправочникДействие));
	
	СтруктураВозврат = ТСправочникПолучитьПояснениеДействия();
	
	ТСправочникПояснениеДействия = СтруктураВозврат.ТСправочникПояснениеДействия;
	ТСправочникПараметрыДействия = СтруктураВозврат.ТСправочникПараметрыДействия;
	
	ТекстВопроса = ТСправочникПараметрыДействия.ТекстВопроса;
	
	Параметр = Новый Структура;
	Параметр.Вставить("ЗаголовокВопроса", ЗаголовокВопроса);
	Параметр.Вставить("Действие"		, ТСправочникПараметрыДействия.Действие);
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 100, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ТСправочникВыполнитьЗавершение(Ответ, Параметр);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ТСправочникВыполнитьЗавершение"", ЭтаФормаЭтотОбъект(), Параметр)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 100, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТСправочникВыполнитьЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;

	Состояние("Подождите. Производится обработка данных ...
	|" + Параметр.ЗаголовокВопроса);
	
	ВремяНачала = ВремяВМиллисекундах();
	РезультатВыполнения = ТСправочникВыполнитьОбработкуСправочникаНаСервере(Параметр.Действие);
	ВремяОкончания = ВремяВМиллисекундах();
	ЭтаФорма.Справочник_Время = "" + (ВремяОкончания - ВремяНачала)/1000 + " с";
	
	Если РезультатВыполнения.Отказ Тогда
		
		Предупреждение = "Обработка завершена аварийно. Изменено: " + РезультатВыполнения.Изменено + " из " + РезультатВыполнения.Всего + " элементов.
		|
		|Элемент, на котором произошел отказ: 
		|" + РезультатВыполнения.ЭлементОтказ + ".";
		Если Объект.ВыполнятьВТранзакции Тогда
			Предупреждение = Предупреждение + "
			|
			|Транзакция отменена.
			|Все произведенные изменения в информационной базе не сохранены.";
		Иначе	
			Предупреждение = Предупреждение + "
			|
			|Произведенные изменения этого элемента в информационной базе не сохранены.";
		КонецЕсли;
		ПредупреждениеСообщение(, Предупреждение);
	
	Иначе
		
		Предупреждение = "Обработка завершена. Изменено: " + РезультатВыполнения.Изменено + " из " + РезультатВыполнения.Всего + " элементов.";
		Если РезультатВыполнения.Изменено < РезультатВыполнения.Всего Тогда
			Предупреждение = Предупреждение + "
			|
			|Обработаны не все выбранные элементы.
			|
			|Возможные причины такого результата:
			|
			|1. Возможно не выбраны элементы таблицы для обработки. Графа ""+/-"" (Включено).
			|Никакие изменения элементов не произведены.
			|
			|2. Значение ""ДоИзменения"" равно Значению ""ПослеИзменения"". 
			|Запись таких изменений не производится.
			|
			|3. Ключевой(ые) реквизит(ы) не соответствуют условиям выполненной обработки. 
			|Такие элементы не обрабатываются и, следовательно, не записываются.";
		КонецЕсли;
		ПредупреждениеСообщение(, Предупреждение);
		
	КонецЕсли;
	
	ОбъектБДПриИзменении("ОбработкаСправочника");
	
	ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ОбъектБД, ВидИзмененияДанных.Изменение);
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// МО: Вкладка "Справочник (Дополнительно)". Переход в МодульОбъекта.
//
&НаСервере
Функция ТСправочникВыполнитьОбработкуСправочникаНаСервере(Действие)
	
	ОбъектОбработка = ОбъектОбработка();
	
	ТСправочникСписок = РеквизитФормыВЗначение("Объект.ТСправочникСписок");
	
	хРезультат = Новый Структура;
	хРезультат = ОбъектОбработка.ТСправочникВыполнитьОбработкуСправочникаНаСервере(ТСправочникСписок, ЭтаФорма.СправочникИзменяемыйРеквизит, ЭтаФорма.СправочникНовоеЗначениеРеквизита, Действие);
	ЗначениеВРеквизитФормы(хРезультат.ТСправочникСписок, "Объект.ТСправочникСписок");
	
	хРезультат.Удалить("ТСправочникСписок");
	
	Возврат хРезультат;

КонецФункции

// Вкладка "Справочник (Дополнительно)" Открыть элемент из списка (ТСправочникСписок).
//
&НаКлиенте
Процедура ТСправочникЭлементОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуОбъектаМодальноИлиНемодальноВРежимеБлокировкиВладельца(Элемент.Родитель.ТекущиеДанные.Ссылка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТСправочникОчиститьПараметры()
	
	Объект.БлокируемыеРеквизиты = Неопределено;
	Элементы.СправочникИзменяемыйРеквизит.ЦветТекста 	= Объект.ЦветТекстаПоля;
	Элементы.СправочникИзменяемыйРеквизит.ЦветФона 		= Объект.ЦветФонаПоля;
	
	Элементы.кнТСправочникУстановитьФлажкиНеЗаполненных.Доступность = Ложь;
	
	ЭтаФорма.СправочникДействие					= Неопределено;
	ЭтаФорма.СправочникЭлементГруппа			= Неопределено;
	ЭтаФорма.СправочникИзменяемыйРеквизит 		= Неопределено;
	ЭтаФорма.СправочникНовоеЗначениеРеквизита 	= Неопределено;
	
	ЭтаФорма.СправочникДатаРанее = Неопределено;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВКЛАДКА "ДОПОЛНИТЕЛЬНО". "ДОКУМЕНТ (ДОПОЛНИТЕЛЬНО)".
//

// Вкладка "Справочник (Дополнительно)" Список возможных действий (Наименования обработок).
//
&НаСервере
Процедура ТДокументСформироватьСписокДействийСДокументом(ОбъектБД)
	
	Если НЕ Объект.ТипОбъектаБД = ИмяТипаДокументы() Тогда
		ЭтаФорма.ДокументСписокДействия.Очистить();
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.ДокументСписокДействия.Очистить();
	ЭтаФорма.ДокументСписокДействия.Добавить("Документы_Перенумерация"	, "Перенумерация документов");
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)" Кнопка FAQ.
//
&НаКлиенте
Процедура ТДокументПеренумерацияFAQ(Команда)
	
	ЭтаФорма.ДокументДействие = "Документы_Перенумерация";

	СтруктураВозврат = ТДокументПолучитьПояснениеДействияПеренумерация();
	ТДокументПояснениеДействия = СтруктураВозврат.ТДокументПояснениеДействия;
	ТДокументПараметрыДействия = СтруктураВозврат.ТДокументПараметрыДействия;
	
	Если ЗначениеЗаполнено(ТДокументПояснениеДействия) Тогда
		ПредупреждениеСообщение(, ТДокументПояснениеДействия, , ВРег(ЭтаФорма.ДокументСписокДействия.НайтиПоЗначению(ЭтаФорма.ДокументДействие)));
	Иначе
		ПредупреждениеСообщение(, "Получите ""начальные параметры"" и повторите просмотр подсказки.");
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)" Описание возможных действий.
//
&НаКлиенте
Функция ТДокументПолучитьПояснениеДействияПеренумерация()
	
	СтруктураВозврат = Новый Структура;
	
	ТДокументПояснениеДействия = "";
	ТДокументПараметрыДействия = Новый Структура;
	
	Если ЭтаФорма.ДокументДействие = "Документы_Перенумерация" Тогда
		
		ТДокументПояснениеДействия = "Перед применением на РАБОЧЕЙ БД произвести обработку ТЕСТОВОЙ БД.
		|==============================================================================================================
		|Перенумерация возможна при выбранном основном объекте-документе.
		|По основному объекту производится определение параметров перенумерации.
		|
		|Принцип действия:
		|
		|Перенумерация документов возможна за произвольный период.
		|Рекомендуется производить перенумерацию в пределах периодичности документов определенного вида.
		|
		|Перенумеровываются документы, которые помечены флажком ""+/-"" - ""Включено"".
		|
		|Фильтрация документов:
		|
		|1. По Организации + Префикс - ПрефиксИсключение;
		|2. По Префикс - ПрефиксИсключение (если реквизит ""Организация"" отсутствует в документе).
		|
		|Пример ""ПрефиксИсключение"": АА;ББ;ВВ (разделены "";"" - точка с запятой без пробелов), 
		|где АА, ББ, ВВ - Префиксы (или их части) документов, которые необходимо исключить из дальнейшей обработки.
		|
		|ВАЖНО! В ПрефиксеИсключение недопустимы пробелы, если они не являются частью префикса. 
		|(Пробелы считаются значащими символами)
		|
		|==============================================================================================================
		|";
		
		ТДокументПараметрыДействия.Вставить("Действие"		, ЭтаФорма.ДокументДействие);
		ТДокументПараметрыДействия.Вставить("Заголовок"		, "Перенумерация документов");
		ТДокументПараметрыДействия.Вставить("ТекстВопроса"	, ТДокументПояснениеДействия + "Подтвердите перенумерацию документов");
		ТДокументПараметрыДействия.Вставить("ТекстСостояние", "Подождите немного. Происходит перенумерация документов.");
		
	КонецЕсли;
	
	СтруктураВозврат.Вставить("ТДокументПояснениеДействия", ТДокументПояснениеДействия);
	СтруктураВозврат.Вставить("ТДокументПараметрыДействия", ТДокументПараметрыДействия);
	
	Возврат СтруктураВозврат;
	
КонецФункции

// Вкладка "Документ (Дополнительно)". Кнопка ПолучитьНачальныеПараметрыПеренумерации.
//
&НаКлиенте
Процедура ТДокументПолучитьНачальныеПараметрыПеренумерации(Команда)
	
	ТДокументСформироватьСписокДействийСДокументом(Объект.ОбъектБД);

	ЭтаФорма.ДокументДействие = "Документы_Перенумерация";	// Пока единственное действие.
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ПредупреждениеСообщение(, "Не выбран Основной объект-Документ.
		|
		|Перенумерация производится только при выбранном типе документов.
		|");
		Возврат;
	Иначе
		ТДокументПолучитьНачальныеПараметрыПеренумерацииНаСервере(Объект.ОбъектБД);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.ДокументПрефиксОрганизация) Тогда
		// Нет реквизита "Организация".
		ПредупреждениеСообщение(, "" + Объект.ПолноеСтроковоеИмяТипа + "
		|Реквизит ""Организация"" отсутствует.
		|Рекомендуется в качестве фильтра использовать ""ПрефиксИсключение"".");
	КонецЕсли;
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Установка начальных Параметров обработки документов.
//
&НаСервере
Процедура ТДокументПолучитьНачальныеПараметрыПеренумерацииНаСервере(ОбъектБД)
	
	Если НЕ ЗначениеЗаполнено(ОбъектБД) Тогда
		Возврат;
	КонецЕсли;
	
	мдОбъектаБД = ОбъектБД.Метаданные();
	
	УстановитьЗначениеПараметраНаВкладке("ДокументПрефиксОрганизация", "Организация", "Организации", Объект.ЕстьСправочникОрганизации);

	Если ТипЗнч(ОбъектБД.Номер) = Тип("Строка") Тогда
		ЭтаФорма.ДокументПрефиксТекущий = УдалитьЦифрыВСтроке(ОбъектБД.Номер);
		ЭтаФорма.ДокументПрефиксНовый = "";
		ЭтаФорма.ДокументНомерНач = 0;
	КонецЕсли;
	
	ЭтаФорма.ДокументПериодичность = мдОбъектаБД.ПериодичностьНомера;
	
	Если мдОбъектаБД.ПериодичностьНомера = Метаданные.СвойстваОбъектов.ПериодичностьНомераДокумента.Год Тогда
		ЭтаФорма.ДокументПериод.ДатаНачала = НачалоГода(ОбъектБД.Дата);
		ЭтаФорма.ДокументПериод.ДатаОкончания = ?(КонецГода(ОбъектБД.Дата) < КонецДня(ТекущаяДата()), КонецГода(ОбъектБД.Дата), КонецДня(ТекущаяДата()));
	ИначеЕсли мдОбъектаБД.ПериодичностьНомера = Метаданные.СвойстваОбъектов.ПериодичностьНомераДокумента.Квартал Тогда
		ЭтаФорма.ДокументПериод.ДатаНачала = НачалоКвартала(ОбъектБД.Дата);
		ЭтаФорма.ДокументПериод.ДатаОкончания = ?(КонецКвартала(ОбъектБД.Дата) < КонецДня(ТекущаяДата()), КонецКвартала(ОбъектБД.Дата), КонецДня(ТекущаяДата()));
	ИначеЕсли мдОбъектаБД.ПериодичностьНомера = Метаданные.СвойстваОбъектов.ПериодичностьНомераДокумента.Месяц Тогда
		ЭтаФорма.ДокументПериод.ДатаНачала = НачалоМесяца(ОбъектБД.Дата);
		ЭтаФорма.ДокументПериод.ДатаОкончания = ?(КонецМесяца(ОбъектБД.Дата) < КонецДня(ТекущаяДата()), КонецМесяца(ОбъектБД.Дата), КонецДня(ТекущаяДата()));
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Реквизит Префикс новый.
//
&НаКлиенте
Процедура ТДокументПрефиксНовыйПриИзменении(Элемент)
	
	Если ВРег(СокрЛП(ЭтаФорма.ДокументПрефиксНовый)) = ВРег(СокрЛП(ЭтаФорма.ДокументПрефиксТекущий)) Тогда
		ПредупреждениеСообщение(, "Новый Префикс = Старому. 
		|Рекомендуется Префикс, не используемый при штатной нумерации документов.");
	КонецЕсли;
	
	ЭтаФорма.ДокументНомерНач = ТДокументПрефиксНомерНовыйНаСервере(Объект.ОбъектБД) + 1;
	
КонецПроцедуры

// МО: Вкладка "Документ дополнительно". Получить начальное значение Номера.
//
&НаСервере
Функция ТДокументПрефиксНомерНовыйНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ТДокументПрефиксНомерНовыйНаСервере(ОбъектБД, ЭтаФорма.ДокументПрефиксНовый, ЭтаФорма.ДокументПериод.ДатаНачала, ЭтаФорма.ДокументПериод.ДатаОкончания);
	
КонецФункции

// Вкладка "Документ (Дополнительно)".  Поле Номер начальный. Проверить значение.
//
&НаКлиенте
Процедура ТДокументНомерНачПриИзменении(Элемент)
	
	НомерНач1 = ТДокументПрефиксНомерНовыйНаСервере(Объект.ОбъектБД)+1;
	НомерСтрокой1 = СокрЛП(СтрЗаменить(Формат(НомерНач1,"ЧЦ=10; ЧВН="), Символы.НПП, ""));
	НомерСтрокой1 = ""+ЭтаФорма.ДокументПрефиксНовый + Сред(НомерСтрокой1,СтрДлина(ЭтаФорма.ДокументПрефиксНовый));
	
	НомерСтрокой = СокрЛП(СтрЗаменить(Формат(ЭтаФорма.ДокументНомерНач,"ЧЦ=10; ЧВН="), Символы.НПП, ""));
	НомерСтрокой = ""+ЭтаФорма.ДокументПрефиксНовый + Сред(НомерСтрокой,СтрДлина(ЭтаФорма.ДокументПрефиксНовый));
	
	Если ЭтаФорма.ДокументНомерНач < НомерНач1 Тогда
		ПредупреждениеСообщение(, "Указанный начальный № " + НомерСтрокой + " некорректен, 
		|т.к. существует документ с таким же или большим Номером.");
		ЭтаФорма.ДокументНомерНач = НомерНач1;
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Найти Документы.
//
&НаКлиенте
Процедура ТДокументНайтиДокументы(Команда)
	
	ЭтаФорма.ДокументПрефиксНовый = Неопределено;
	ЭтаФорма.ДокументНомерНач = 0;
	
	ФормаВывестиВСостояние(, , "Подождите. Производится отбор документов для последующей перенумерации ...");
	
	ТДокументНайтиДокументыНаСервере(Объект.ОбъектБД);
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
	Состояние("Поиск документов завершен.");
	
КонецПроцедуры

// МО: Вкладка "Документ (Дополнительно)". Найти Документы.
//
&НаСервере
Процедура ТДокументНайтиДокументыНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	тзДокСписок = ОбъектОбработка.ТДокументНайтиДокументыНаСервере(ОбъектБД, ЭтаФорма.ДокументПрефиксОрганизация, ЭтаФорма.ДокументПрефиксТекущий, ЭтаФорма.ДокументПрефиксИсключение, ЭтаФорма.ДокументПериод.ДатаНачала, ЭтаФорма.ДокументПериод.ДатаОкончания);
	ЗначениеВРеквизитФормы(тзДокСписок, "Объект.ТДокументСписок");
	
КонецПроцедуры

// Вкладка "Документ дополнительно". Очистить таблицу найденных документов.
//
&НаКлиенте
Процедура ТДокументПеренумерацияОчистить(Команда)

	Объект.ТДокументСписок.Очистить();
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);

КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Перенумеровать документы.
//
&НаКлиенте
Процедура ТДокументПеренумероватьДокументы(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если Объект.ТДокументСписок.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Список документов пуст.");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.ДокументПрефиксНовый) Тогда
		ПредупреждениеСообщение(, "Новый Префикс не определен.");
		Возврат;
	Иначе	
		Если ВРег(СокрЛП(ЭтаФорма.ДокументПрефиксНовый)) = ВРег(СокрЛП(ЭтаФорма.ДокументПрефиксТекущий)) Тогда
			ПредупреждениеСообщение(, "Новый Префикс = Старому. 
			|Рекомендуется Префикс, не используемый при штатной нумерации документов.");
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтаФорма.ДокументНомерНач = 0 Тогда
		ПредупреждениеСообщение(, "Начальный Номер не определен.");
		Возврат;
	Иначе
		
		ДокументСНомеромНач = ТДокументПроверитьСуществованиеДокументаСНомеромНач(Объект.ОбъектБД, ЭтаФорма.ДокументНомерНач, ЭтаФорма.ДокументПрефиксНовый, ЭтаФорма.ДокументПериод.ДатаОкончания);
		
		Если НЕ ДокументСНомеромНач = Неопределено Тогда
			НомерСтрокой = СокрЛП(СтрЗаменить(Формат(ЭтаФорма.ДокументНомерНач,"ЧЦ=10; ЧВН="), Символы.НПП, ""));
			НомерСтрокой = ""+ЭтаФорма.ДокументПрефиксНовый+Сред(НомерСтрокой,СтрДлина(ЭтаФорма.ДокументПрефиксНовый));
			ПредупреждениеСообщение(, "Документ с № " + НомерСтрокой + " существует: " + ДокументСНомеромНач + "
			|Обработка прервана.");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекстВопроса = "Подтвердите/Отмените перенумерацию документов
	|";
	
	ЗаголовокВопроса = "ПЕРЕНУМЕРАЦИЯ ДОКУМЕНТОВ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ТДокументПеренумероватьДокументыЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ТДокументПеренумероватьДокументыЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТДокументПеренумероватьДокументыЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ФормаВывестиВСостояние(, , "Подождите. Производится перенумерация документов ...");
	
	ИзменениеОбъектаБД = ТДокументПеренумероватьДокументыНаСервере(Объект.ОбъектБД, ЭтаФорма.ДокументПериод.ДатаНачала, ЭтаФорма.ДокументПериод.ДатаОкончания, ЭтаФорма.ДокументПрефиксНовый, ЭтаФорма.ДокументНомерНач);
	
	ПредупреждениеСообщение(, "Обработка завершена!");
	
	Если ИзменениеОбъектаБД Тогда
		Д1 = ЭтаФорма.ДокументПериод.ДатаНачала;
		Д2 = ЭтаФорма.ДокументПериод.ДатаОкончания;
		ОбъектБДПриИзменении("ТДокументПроизвестиЗамену");
		ЭтаФорма.ДокументПериод.ДатаНачала = Д1;
		ЭтаФорма.ДокументПериод.ДатаОкончания = Д2;
		ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ОбъектБД, ВидИзмененияДанных.Изменение);
	КонецЕсли;
	
	ЭтаФорма.ТекущийЭлемент = Элементы.ТДокументСписок;
	
КонецПроцедуры

// МО: Вкладка "Документ (Дополнительно)". Проверка существования документа с НомерНач.
//
&НаСервере
Функция ТДокументПроверитьСуществованиеДокументаСНомеромНач(ОбъектБД, НомерНач, ПрефиксНовый, ДатаКон)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ТДокументПроверитьСуществованиеДокументаСНомеромНач(ОбъектБД, НомерНач, ПрефиксНовый, ДатаКон);
	
КонецФункции

// МО: Вкладка "Документ (Дополнительно)". Перенумеровать документы.
//
&НаСервере
Функция ТДокументПеренумероватьДокументыНаСервере(ОбъектБД, ДатаНач, ДатаКон, ПрефиксНовый, НомерНач)
	
	ОбъектОбработка = ОбъектОбработка();
	
	хРезультат = ОбъектОбработка.ТДокументПеренумероватьДокументыНаСервере(ОбъектБД, ДатаНач, ДатаКон, ПрефиксНовый, НомерНач);
	ИзменениеОбъектаБД = хРезультат.ИзменениеОбъектаБД;
	
	ЗначениеВРеквизитФормы(хРезультат.Таблица, "Объект.ТДокументСписок");
	
	Возврат ИзменениеОбъектаБД;
	
КонецФункции

// Вкладка "Документ (Дополнительно)". Получить ДокументСсылка по данным ячейки таблицы.
//
&НаСервере
Функция ТДокументПолучитьДокументПоДаннымТаблицы(ИскомоеЗначение)
	
	ТабТмп = РеквизитФормыВЗначение("Объект.ТДокументСписок");
	Возврат ТабТмп.Найти(ИскомоеЗначение, "ПредставлениеТек").ДокументТек;
	
КонецФункции

// Вкладка "Документ (Дополнительно)". Установить Флажки в списке Ссылок на Документ.
//
&НаКлиенте
Процедура ТДокументПеренумерацияУстановитьФлажки(Команда)
	ФормаУстановитьСнятьФлажокВТаблицеНаСервере(Истина, "ТДокументСписок")
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Снять Флажки в списке Ссылок на Документ.
//
&НаКлиенте
Процедура ТДокументПеренумерацияСнятьФлажки(Команда)
	ФормаУстановитьСнятьФлажокВТаблицеНаСервере(Ложь, "ТДокументСписок")
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Открыть текущий документ.
//
&НаКлиенте
Процедура ТДокументОткрытие(Элемент, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент.Имя = "ТДокументПредставление" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуОбъектаМодальноИлиНемодальноВРежимеБлокировкиВладельца(Элемент.ТекущиеДанные.ДокументТек, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТДокументПеренумерацияОчиститьПараметры()
	
	ЭтаФорма.ДокументПрефиксОрганизация 	= Неопределено;
	ЭтаФорма.ДокументПериод.ДатаНачала 		= Дата("00010101");
	ЭтаФорма.ДокументПериод.ДатаОкончания 	= Дата("00010101");
	ЭтаФорма.ДокументПрефиксТекущий 		= "";
	ЭтаФорма.ДокументПрефиксИсключение		= "";
	ЭтаФорма.ДокументПрефиксНовый 			= "";
	ЭтаФорма.ДокументНомерНач 				= 0;
	ЭтаФорма.ДокументПериодичность			= Неопределено;
	
	ЭтаФорма.ДокументПроведениеПериод.ДатаНачала	= Дата("00010101");
	ЭтаФорма.ДокументПроведениеПериод.ДатаОкончания	= Дата("00010101");
	ЭтаФорма.ДокументПроведениеОрганизация 	= Неопределено;
	ЭтаФорма.ДокументПроводить				= Неопределено;
	ЭтаФорма.ДокументПроведениеОжидатьСнятиеБлокировки		= Ложь;
	ЭтаФорма.ДокументПроведениеВремяОжиданияСнятияБлокировки= 0;
	Элементы.ДокументПроведениеВремяОжиданияСнятияБлокировки.ТолькоПросмотр = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВКЛАДКА "ДОПОЛНИТЕЛЬНО". "ДОКУМЕНТ (ДОПОЛНИТЕЛЬНО) ПРОВЕДЕНИЕ".
//

// Вкладка "Документ (Дополнительно)". Кнопка ПолучитьНачальныеПараметрыПроведения.
//
&НаКлиенте
Процедура ТДокументПолучитьНачальныеПараметрыПроведения(Команда)
	
	Если НЕ Объект.МонопольныйРежим Тогда
		ЭтаФорма.ДокументПроведениеОжидатьСнятиеБлокировки = Истина;
		ДокументПроведениеОжидатьСнятиеБлокировкиПриИзменении(Команда);
	КонецЕсли;
	
	ЭтаФорма.ДокументПроведениеПериод.ДатаНачала 	= НачалоМесяца(ТекущаяДата());
	ЭтаФорма.ДокументПроведениеПериод.ДатаОкончания = КонецДня(ТекущаяДата());
	Этаформа.ДокументПроводить	= "Проведенные";
	
	УстановитьЗначениеПараметраНаВкладке("ДокументПроведениеОрганизация", "Организация", "Организации", Объект.ЕстьСправочникОрганизации);
	
	ТДокументПолучитьНачальныеПараметрыПроведенияНаСервере(ЭтаФорма.ДокументПроведениеОрганизация);
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);

КонецПроцедуры

&НаКлиенте
Процедура ТДокументПроведениеFAQ(Команда)
	
	ПредупреждениеСообщение(, "Проведение документов рекомендуется выполнять в монопольном режиме.
	|
	|Для установки монопольного режима воспользуйтесь формой ""Настройка"" обработки:
	|гиперссылка ""Многопользовательский режим"" или кнопка основного меню ""Настройка"".
	|
	|При немонопольном режиме проведения рекомендуется включить вывод сообщений.
	|Кнопка основного меню ""Настройка"", иначе вывод сообщений только о непроведенных.
	|
	|Перед проведением документов необходимо также проверить даты запрета изменений
	|Сервис - Даты запрета изменений данных и при необходимости скорректировать.");
	
КонецПроцедуры

// МО: Вкладка "Документ (Дополнительно)". Кнопка ПолучитьНачальныеПараметрыПроведения.
//
&НаСервере
Процедура ТДокументПолучитьНачальныеПараметрыПроведенияНаСервере(ДокументПроведениеОрганизация)
	
	ОбъектОбработка = ОбъектОбработка();
	ТаблицаСписокВидовДокументов = РеквизитФормыВЗначение("ДокументПроведениеТСписокВидов");
	ТаблицаСписокВидовДокументов = ОбъектОбработка.ТДокументПроведениеЗаполнитьСписокВидовДокументов(ТаблицаСписокВидовДокументов, ДокументПроведениеОрганизация);
	ЗначениеВРеквизитФормы(ТаблицаСписокВидовДокументов, "ДокументПроведениеТСписокВидов");
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Действия при блокированном документе..
//
&НаКлиенте
Процедура ДокументПроведениеОжидатьСнятиеБлокировкиПриИзменении(Элемент)
	
	Если ЭтаФорма.ДокументПроведениеОжидатьСнятиеБлокировки Тогда
		Элементы.ДокументПроведениеВремяОжиданияСнятияБлокировки.ТолькоПросмотр = Ложь;
		ЭтаФорма.ДокументПроведениеВремяОжиданияСнятияБлокировки = 1;
	Иначе
		Элементы.ДокументПроведениеВремяОжиданияСнятияБлокировки.ТолькоПросмотр = Истина;
		ЭтаФорма.ДокументПроведениеВремяОжиданияСнятияБлокировки = 0;
	КонецЕсли;
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Фильтрация списка видов документов.
//
&НаКлиенте
Процедура ДокументПроведениеОрганизацияПриИзменении(Элемент)
	ТДокументПолучитьНачальныеПараметрыПроведенияНаСервере(ЭтаФорма.ДокументПроведениеОрганизация);
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Кнопка Установить флажки в списке видов документов.
//
&НаКлиенте
Процедура ТДокументПроведениеУстановитьФлажки(Команда)
	ТДокументПроведениеУстановитьСнятьФлажки(Истина);
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Кнопка Снять флажки в списке видов документов.
//
&НаКлиенте
Процедура ТДокументПроведениеСнятьФлажки(Команда)
	ТДокументПроведениеУстановитьСнятьФлажки(Ложь);
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Кнопка Установить/Снять флажки в списке видов документов.
//
&НаКлиенте
Процедура ТДокументПроведениеУстановитьСнятьФлажки(Флажок)
	
	Для Каждого ВидДокумента ИЗ ЭтаФорма.ДокументПроведениеТСписокВидов Цикл
		Если ВидДокумента.Значение = "ЧекККМ" ИЛИ ВидДокумента.Значение = "ЧекККМВозврат" Тогда
			ВидДокумента.Пометка = Ложь;
		Иначе
			ВидДокумента.Пометка = Флажок;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Кнопка Инвентировать флажки в списке видов документов.
//
&НаКлиенте
Процедура ТДокументПроведениеИнвертироватьФлажки(Команда)
	
	Для Каждого ВидДокумента ИЗ ЭтаФорма.ДокументПроведениеТСписокВидов Цикл
		Если ВидДокумента.Значение = "ЧекККМ" ИЛИ ВидДокумента.Значение = "ЧекККМВозврат" Тогда
			ВидДокумента.Пометка = Ложь;
		Иначе
			ВидДокумента.Пометка = НЕ ВидДокумента.Пометка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Провести документы по отмеченным в списке видов документов.
//
&НаКлиенте
Процедура ТДокументПровести(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если ЭтаФорма.ДокументПроведениеПериод.ДатаНачала = Дата("00010101000000") Тогда
		ПредупреждениеСообщение(, "Незаполнена начальная дата.
		|Проведение документов не выполнено.");
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.ДокументПроведениеПериод.ДатаОкончания = Дата("00010101000000") Тогда
		ПредупреждениеСообщение(, "Незаполнена конечная дата.
		|Проведение документов не выполнено.");
		Возврат;
	КонецЕсли;
	
	ДатаНачала = НачалоДня(ЭтаФорма.ДокументПроведениеПериод.ДатаНачала);
	ДатаОкончания = ?(ЭтаФорма.ДокументПроведениеПериод.ДатаОкончания = Дата("00010101000000"), КонецДня(ТекущаяДата()), КонецДня(ЭтаФорма.ДокументПроведениеПериод.ДатаОкончания));
	
	ТекстВопроса = "Параметры проведения:
	|
	|Дата Первого документа: " + ДатаНачала + "
	|Дата Последнего документа: " + ДатаОкончания + "
	|
	|Дополнительный фильтр: " + ?(ЗначениеЗаполнено(ЭтаФорма.ДокументПроведениеОрганизация), """Организация""" + " " + ЭтаФорма.ДокументПроведениеОрганизация, "Отсутствует") + "
	|" + ?(ЗначениеЗаполнено(ЭтаФорма.ДокументПроведениеОрганизация), "(только документы, имеющие реквизит ""Организация"")", "") + "
	|
	|Подтвердите проведение документов.";
	
	Параметр = Новый Структура;
	Параметр.Вставить("ДатаНачала"		, ДатаНачала);
	Параметр.Вставить("ДатаОкончания"	, ДатаОкончания);
	
	ЗаголовокВопроса = "ПРОВЕДЕНИЕ ДОКУМЕНТОВ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ТДокументПровестиЗавершение(Ответ, Параметр);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ТДокументПровестиЗавершение"", ЭтаФормаЭтотОбъект(), Параметр)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТДокументПровестиЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ФормаВывестиВСостояние(, , "Подождите. Производится перепроведение документов ...");
	
	КоличествоПроведенных = 0;
	ЕстьБлокированные = Ложь;
	
	ТДокументПровестиНаСервере(КоличествоПроведенных, ЕстьБлокированные, Параметр.ДатаНачала, Параметр.ДатаОкончания);
	
	ПредупреждениеСообщение(, "Количество проведенных документов: " + КоличествоПроведенных + "
	|" + ?(ЕстьБлокированные, "Есть блокированные документы.
	|Дополнительно:
	|В главном меню в подменю ""Сервис"" можно открыть список активных пользователей.", ""));
	
КонецПроцедуры
	
// МО: Вкладка "Документ (Дополнительно)". Провести документы по отмеченным в списке видов документов.
//
&НаСервере
Процедура ТДокументПровестиНаСервере(КоличествоПроведенных, ЕстьБлокированные, ДатаНачала, ДатаОкончания)
	
	ОбъектОбработка = ОбъектОбработка();
	ТаблицаСписокВидов = РеквизитФормыВЗначение("ДокументПроведениеТСписокВидов");
	ОбъектОбработка.ТДокументПровестиДокументыИзСпискаВыбранныхВидов(ДатаНачала, ДатаОкончания, ЭтаФорма.ДокументПроведениеОрганизация, ТаблицаСписокВидов, ЭтаФорма.ДокументПроводить, КоличествоПроведенных, ЭтаФорма.ДокументПроведениеОжидатьСнятиеБлокировки, ЭтаФорма.ДокументПроведениеВремяОжиданияСнятияБлокировки, ЕстьБлокированные);
	ЗначениеВРеквизитФормы(ТаблицаСписокВидов, "ДокументПроведениеТСписокВидов");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВКЛАДКА "ДОПОЛНИТЕЛЬНО". "ПЛАН ОБМЕНА (ДОПОЛНИТЕЛЬНО)".
//

&НаКлиенте
Процедура ПланОбменаПолучитьИнформациюОБДиУзлахРИБ(Команда)
	
	ОчиститьСообщения();
	ПланОбменаПолучитьИнформациюОБДиУзлахРИБНаСервере();
	
КонецПроцедуры

// МО:
//
&НаСервере
Процедура ПланОбменаПолучитьИнформациюОБДиУзлахРИБНаСервере()
	
	ОбъектОбработка = ОбъектОбработка();
	
	ЭтаФорма.БДвРИБ = ОбъектОбработка.ОбъектБДПолучитьПоложениеБДвРИБ(Объект.ОбъектБД);
	
	ЭтаФорма.ПланОбменаКомментарий = "=================================================================================================================";
	Если Объект.ЭтоГлавныйУзел Тогда
		ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
		|Центральная БД: Отключение/Установка Главного Узла в Центральной БД невозможно, т.к. БД сама является Главным Узлом.";
	Иначе
		ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
		|Периферийная БД: Для Отключения/Установки Главного Узла необходим монопольный доступ (Конфигуратор, сеансы - закрыты).";
	КонецЕсли;
	
	ЭтаФорма.ГлавныйУзел = ОбъектОбработка.ОбъектБДПолучитьГлавныйУзелВРИБ();
	ЭтаФорма.ТекущийУзел = Объект.ОбъектБД;
	Элементы.ГлавныйУзелПредыдущий.Доступность = Истина;
	
	ЭтаФорма.ПланОбменаНомерПринятого = Объект.ОбъектБД.НомерПринятого;
	ЭтаФорма.ПланОбменаНомерОтправленного = Объект.ОбъектБД.НомерОтправленного;
	
	ЕстьИзменения = ПланОбменаВывестиКомментарийОНаличииНеподтвержденныхИзменений(Объект.ОбъектБД);
	
	ПланОбменаПросмотрНеподтвержденныхИзмененийДоступность();
	ПланОбменаУдалитьРегистрациюИзмененийДоступность(ЕстьИзменения);
	ПланОбменаОтключитьГлавныйУзелДоступность(Объект.ГлавныйУзелПредыдущий);
	ПланОбменаУстановитьГлавныйУзелДоступность(Объект.ГлавныйУзелПредыдущий);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОбменаСообщитьОНеУстановленномГлавномУзле()
	
	Если НЕ ЭтаФорма.ПредупреждениеСделано И ЗначениеЗаполнено(Объект.ГлавныйУзелПредыдущий) Тогда
		ЭтаФорма.ПредупреждениеСделано = Истина;
		ПредупреждениеСообщение(, "ВНИМАНИЕ! 
		|Периферийная информационная база. ГлавныйУзел ОТКЛЮЧЕН.
		|
		|НЕОБХОДИМО Установить ГлавныйУзел.
		|
		|Страница ""Дополнительно"" - Вкладка ""План Обмена"".");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОбменаСправка1СНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПредупреждениеСообщение(, "Справка 1С: ""О запрете загрузки данных"" 
	|
	|Запрет загрузки может быть установлен для данных, которые должны редактироваться только в одном узле БД, 
	|а также по правилам, которые следуют из требований обмена данными, а не из типовых бизнес-процессов организации.
	|
	|ВАЖНО: Если дата запрета загрузки данных установлена, то при попытке загрузить данные в БД до даты запрета 
	|все запрещенные для загрузки данные будут пропущены, сведения о них будут записаны в журнал регистрации, 
	|а разрешенные - загружены.
	|
	|(см. также ""Опции"" - ""Даты запрета ..."")");
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОбменаСпециальноеПояснениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПредупреждениеСообщение(, "Если в Периферийной БД (!!!) Главный Узел неопределен (реквизит ""Главный узел (ТЕКУЩИЙ)"" пуст)
	|и использовалась другая возможность для отключения Главного Узла в этой Периферийной БД, то:
	|
	|Для восстановления Главного Узла в этой Периферийной БД необходимо выбрать ""Главный Узел (ПРЕДЫДУЩИЙ)"" вручную.
	|(в перийной БД у Главного Узла значения: НомерПринятого # 0, НомерОтправленного # 0)
	|
	|Если использовалась другая возможность для отключения Главного Узла в этой Периферийной БД, то
	|до выбора Главного Узла значения ЦентральнаяБД/ПериферийнаяБД, ГлавныйУзел/ПолчиненныйУзел в Периферийной БД 
	|могут определяться неверно.");
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОбменаГлавныйУзелПредыдущийПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ГлавныйУзелПредыдущий) Тогда
		ЭтаФорма.ГлавныйУзел = Неопределено;
		ОМОбновить(Элемент);
		ПланОбменаПолучитьИнформациюОБДиУзлахРИБНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПланОбменаПросмотрНеподтвержденныхИзмененийДоступность()
	
	Элементы.кнПланОбменаПросмотрНеподтвержденныхИзменений.Доступность			= Истина;
	Элементы.ПланОбменаОграничениеКоличестваПросматриваемыхИзменений.Доступность= Истина;
	ЭтаФорма.ПланОбменаОграничениеКоличестваПросматриваемыхИзменений			= 10;
	
КонецПроцедуры

&НаСервере
Процедура ПланОбменаУдалитьРегистрациюИзмененийДоступность(ЕстьИзменения)
	Элементы.кнПланОбменаУдалитьРегистрациюИзменений.Доступность = ЕстьИзменения;
КонецПроцедуры

&НаСервере
Процедура ПланОбменаОтключитьГлавныйУзелДоступность(Узел)
	Элементы.кнПланОбменаОтключитьГлавныйУзел.Доступность = НЕ Объект.ЭтоГлавныйУзел И НЕ ЗначениеЗаполнено(Узел);
КонецПроцедуры

&НаСервере
Процедура ПланОбменаУстановитьГлавныйУзелДоступность(Узел)
	Перем УсловиеДоступность;
	
	УсловиеДоступность = ( (НЕ Объект.ЭтоГлавныйУзел ИЛИ ЭтаФорма.ГлавныйУзел = Неопределено) И ЗначениеЗаполнено(Узел));
	Элементы.кнПланОбменаУстановитьГлавныйУзел.Доступность			= УсловиеДоступность;
	ЭтаФорма.ПланОбменаКомментарийУстановкаГлавногоУзла				= "- НЕОБХОДИМО Установить ГлавныйУзел.";
	Элементы.ПланОбменаКомментарийУстановкаГлавногоУзла.Видимость	= УсловиеДоступность;
	Элементы.ПланОбменаКомментарийУстановкаГлавногоУзла.Доступность	= УсловиеДоступность;
	
КонецПроцедуры

&НаСервере
Функция ПланОбменаВывестиКомментарийОНаличииНеподтвержденныхИзменений(ОбъектБД)
	
	Если ПланОбменаЕстьИзмененияНаСервере(ОбъектБД) Тогда
		ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
		|=================================================================================================================
		|""" + ОбъектБД + """ есть неподтвержденные изменения. № Принятого/Отправленного: " + ОбъектБД.НомерПринятого + "/" + ОбъектБД.НомерОтправленного + " (см. ""Свойства Объекта"").";
		Возврат Истина;
	Иначе
		ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
		|=================================================================================================================
		|""" + ОбъектБД + """ нет неподтвержденных изменений. № Принятого/Отправленного: " + ОбъектБД.НомерПринятого + "/" + ОбъектБД.НомерОтправленного + " (см. ""Свойства Объекта"").";
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПланОбменаЕстьИзмененияНаСервере(ОбъектБД)
	
	мдОбъектаБД = ОбъектБД.Метаданные();
	ИмяПланаОбмена = мдОбъектаБД.Имя;
	СтруктураВыборки = Новый Структура("Наименование", ОбъектБД.Наименование);
	
	Выборка = ПланыОбмена[ИмяПланаОбмена].Выбрать(СтруктураВыборки);
	Пока Выборка.Следующий() Цикл
		Попытка
			Изменения = ПланыОбмена.ВыбратьИзменения(Выборка.Ссылка, ОбъектБД.НомерОтправленного+1);
			Пока Изменения.Следующий() Цикл
				Возврат Истина;
			КонецЦикла;
		Исключение
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ПланОбменаПросмотрНеподтвержденныхИзменений(Команда)
	ОчиститьСообщения();
	ПланОбменаПросмотрНеподтвержденныхИзмененийНаСервере(ЭтаФорма.ТекущийУзел);
КонецПроцедуры

&НаСервере
Процедура ПланОбменаПросмотрНеподтвержденныхИзмененийНаСервере(ОбъектБД)
	
	мдОбъектаБД = ОбъектБД.Метаданные();
	ИмяПланаОбмена = мдОбъектаБД.Имя;
	СтруктураВыборки = Новый Структура("Наименование", ОбъектБД.Наименование);
	
	НомерСледующегоСообщения = ОбъектБД.НомерОтправленного + 1;
	Сообщить("План обмена: """ + ОбъектБД.Наименование + """. №= " + НомерСледующегоСообщения + " (следующее сообщение,  в которое будут помещены выбранные изменения).");	// Номер сообщения обмена данными, в который будут помещены выбранные изменения.
	
	Выборка = ПланыОбмена[ИмяПланаОбмена].Выбрать(СтруктураВыборки);
	Пока Выборка.Следующий() Цикл
		Попытка
			Изменения = ПланыОбмена.ВыбратьИзменения(Выборка.Ссылка, НомерСледующегоСообщения);	// Номер сообщения обмена данными, в который будут помещены выбранные изменения.
			ит = 0;
			Пока Изменения.Следующий() Цикл
				ит = ит + 1;
				Сообщить(Изменения.Получить());
				Если ЭтаФорма.ПланОбменаОграничениеКоличестваПросматриваемыхИзменений > 0 И ит >= ЭтаФорма.ПланОбменаОграничениеКоличестваПросматриваемыхИзменений	Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОбменаУдалитьРегистрациюИзменений(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	ТекстВопроса = "Подтвердите УДАЛЕНИЕ РЕГИСТРАЦИИ ИЗМЕНЕНИЙ
	|
	|Плана обмена """ + ЭтаФорма.ТекущийУзел + """ ?
	|
	|см. Справка 1С: ""О запрете загрузки данных""";
	
	ЗаголовокВопроса = "УДАЛЕНИЕ РЕГИСТРАЦИИ ИЗМЕНЕНИЙ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ПланОбменаУдалитьРегистрациюИзмененийЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ПланОбменаУдалитьРегистрациюИзмененийЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОбменаУдалитьРегистрациюИзмененийЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ПланОбменаУдалитьРегистрациюИзмененийНаСервере(ЭтаФорма.ТекущийУзел);
	
КонецПроцедуры

&НаСервере
Функция ПланОбменаУдалитьРегистрациюИзмененийНаСервере(Узел)
	
	Попытка
		ПланыОбмена.УдалитьРегистрациюИзменений(Узел, Неопределено);
		Предупреждение = "+ Удаление регистрации изменений Плана обмена """ + Узел + """ произведено.";
		ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
		|=================================================================================================================
		|" + Предупреждение;
		ЕстьИзменения = ПланОбменаВывестиКомментарийОНаличииНеподтвержденныхИзменений(Узел);
		Элементы.кнПланОбменаУдалитьРегистрациюИзменений.Доступность = ЕстьИзменения;
		Элементы.ПланОбменаКомментарийУдалениеРегистрацииИзменений.Доступность = ЕстьИзменения;
		Возврат Предупреждение;
	Исключение
		Предупреждение = "- Удаление регистрации изменений Плана обмена """ + Узел + """ произвести не удалось.";
		ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
		|=================================================================================================================
		|" + Предупреждение;
		Возврат Предупреждение;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ПланОбменаОтключитьГлавныйУзел(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	ТекстВопроса = "Подтвердите ОТКЛЮЧЕНИЕ ГЛАВНОГО УЗЛА
	|
	|""" + ЭтаФорма.ГлавныйУзел + """ ?
	|
	|""СЕМЬ РАЗ ОТМЕРЬ - ОДИН РАЗ ОТРЕЖЬ."" (народная мудрость)
	|";
	
	ЗаголовокВопроса = "ОТКЛЮЧЕНИЕ ГЛАВНОГО УЗЛА";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ПланОбменаОтключитьГлавныйУзелЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ПланОбменаОтключитьГлавныйУзелЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОбменаОтключитьГлавныйУзелЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	Перем Предупреждение;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	Предупреждение = ПланОбменаОтключитьГлавныйУзелНаСервере(ЭтаФорма.ГлавныйУзел);
	
	Если ЗначениеЗаполнено(Предупреждение) Тогда
		ПредупреждениеСообщение(, Предупреждение);
	КонецЕсли;
	
	ЭтаФорма.ОбновитьОтображениеДанных();
	
КонецПроцедуры

// МО;
//
&НаСервере
Функция ПланОбменаОтключитьГлавныйУзелНаСервере(Узел)
	
	ОбъектОбработка = ОбъектОбработка();
		
	Предупреждение = "";
	Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда		// В Центральной БД - ГлавныйУзел() всега Неопределено.
		Если ОбъектОбработка.ОбъектБДПроверитьЭтоГлавныйУзелПланаОбмена(Узел) Тогда
			Предупреждение = "Центральная БД: """ + Узел + """ - это Главный Узел.
			|
			|Отключение Главного Узла на Центральной БД невозможно, т.к. БД по-умолчанию является Главным Узлом.
			|";
			Возврат Предупреждение;
		КонецЕсли;
	Иначе
		Если НЕ ОбъектОбработка.ОбъектБДПроверитьЭтоГлавныйУзелПланаОбмена(Узел) Тогда
			Предупреждение = "Периферийная БД: """ + Узел + """ - это  Подчиненный Узел.
			|
			|Выбранный узел не является Главным Узлом.
			|";
			Возврат Предупреждение;
		Иначе
			Попытка
				ПланыОбмена.УстановитьГлавныйУзел(Неопределено);				
				Объект.ГлавныйУзелПредыдущий = Узел;
				Узел = Неопределено;
				ПланОбменаОтключитьГлавныйУзелДоступность(Объект.ГлавныйУзелПредыдущий);
				ПланОбменаУстановитьГлавныйУзелДоступность(Объект.ГлавныйУзелПредыдущий);
				Элементы.ГлавныйУзелПредыдущий.Доступность = Истина;
    			Предупреждение = "+ Главный Узел """ + Объект.ГлавныйУзелПредыдущий + """ отключен.";
				ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
				|=================================================================================================================
				|" + Предупреждение;
				Возврат Предупреждение;
			Исключение
				Предупреждение = "- Отключить Главный Узел """ + ЭтаФорма.ГлавныйУзел + """ не удалось.
				|Возможно не установлен Монопольный режим доступа к базе.";
				ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
				|=================================================================================================================
				|" + Предупреждение;
				Возврат Предупреждение;
			КонецПопытки;
 		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПланОбменаУстановитьГлавныйУзел(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если НЕ ЗначениеЗаполнено(Объект.ГлавныйУзелПредыдущий) Тогда
		ПредупреждениеСообщение(, "Предыдущий ""ГлавныйУзел"" неопределен.
		|
		|Произведите выбор и повторите попытку установки ГлавногоУзла.");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Подтвердите УСТАНОВКУ ГЛВАНЫМ УЗЛОМ
	|
	|""" + Объект.ГлавныйУзелПредыдущий + """.";
	
	ЗаголовокВопроса = "УСТАНОВКА ГЛАВНОГО УЗЛА";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ПланОбменаУстановитьГлавныйУзелЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ПланОбменаУстановитьГлавныйУзелЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОбменаУстановитьГлавныйУзелЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	Перем Предупреждение;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	Предупреждение = ПланОбменаУстановитьГлавныйУзелНаСервере(Объект.ГлавныйУзелПредыдущий);
	
	ПредупреждениеСообщение(, Предупреждение);
	
КонецПроцедуры

&НаСервере
Функция ПланОбменаУстановитьГлавныйУзелНаСервере(Узел)
	
	Попытка
	    ПланыОбмена.УстановитьГлавныйУзел(Узел);
		ЭтаФорма.ГлавныйУзел = Узел;
		Узел = Неопределено;
		ПланОбменаОтключитьГлавныйУзелДоступность(Неопределено);
		ПланОбменаУстановитьГлавныйУзелДоступность(Неопределено);
		Элементы.ГлавныйУзелПредыдущий.Доступность = Ложь;
    	Предупреждение = "+ Главный узел установлен: """ + ЭтаФорма.ГлавныйУзел + """.";
		ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
		|=================================================================================================================
		|" + Предупреждение;
		Объект.ЭтоГлавныйУзел = ЭтоГлавныйУзел();
	Исключение
		Объект.ЭтоГлавныйУзел = ЭтоГлавныйУзел();
		Предупреждение = "- Установить """ + Узел + """ Главным Узлом не удалось.
		|Варианты ошибки:
		|- Возможно не установлен Монопольный режим доступа к базе.
		|- Узел """ + Узел + """ не может быть установлен Главным Узлом.";
		ЭтаФорма.ПланОбменаКомментарий = ЭтаФорма.ПланОбменаКомментарий + "
		|=================================================================================================================
		|" + Предупреждение;
	КонецПопытки;
	
	Возврат Предупреждение;
	
КонецФункции

// Очистить страницу ПланОбмена (Дополнительно).
//
&НаКлиенте
Процедура ПланОбменаОчиститьПараметры()
	
	ЭтаФорма.ГлавныйУзел			= Неопределено;
	ЭтаФорма.ТекущийУзел			= Неопределено;
	
	ЭтаФорма.ПланОбменаКомментарий 	= "";
	
	Элементы.кнПланОбменаПросмотрНеподтвержденныхИзменений.Доступность			= Ложь;
	Элементы.ПланОбменаОграничениеКоличестваПросматриваемыхИзменений.Доступность= Ложь;
	Элементы.кнПланОбменаУдалитьРегистрациюИзменений.Доступность	= Ложь;
	Элементы.кнПланОбменаОтключитьГлавныйУзел.Доступность			= Ложь;
	Элементы.кнПланОбменаУстановитьГлавныйУзел.Доступность			= Ложь;
	
	Элементы.ГлавныйУзелПредыдущий.Доступность						= Ложь;
	Элементы.ПланОбменаКомментарийУдалениеРегистрацииИзменений.Доступность = Ложь;
	Элементы.ПланОбменаКомментарийОтключениеГлавногоУзла.Доступность= Ложь;
	Элементы.ПланОбменаКомментарийУстановкаГлавногоУзла.Доступность	= Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПОЛУЧИТЬ ИМЯ СОЗДАВАЕМЫХ/СОЗДАННЫХ ЭЛЕМЕНТОВ ФОРМЫ.
//

// Возвращает Имя Реквизита Формы, связанного с ТЧ.
// Вызов определения из МодуляОбъекта долгий - поэтому здесь.
//
&НаКлиентеНаСервереБезКонтекста //*yuraos, 10.09.2015 - для оптимизации вызова на клиенте
Функция ФормаПолучитьИмяРеквизитаТабличнаяЧасть(ТЧИмя)
	Возврат "ТЧасть" + ТЧИмя;
КонецФункции

// Возвращает Имя Реквизита Формы, связанного с Регистром.
// Вызов определения из МодуляОбъекта долгий - поэтому здесь.
//
&НаКлиентеНаСервереБезКонтекста //*yuraos, 10.09.2015 - для оптимизации вызова на клиенте 
Функция ФормаПолучитьИмяРеквизитаРегистр(ТЧИмя)
	Возврат "ТДвижений" + ТЧИмя;
КонецФункции

// Возвращает имя Страницы Формы, связанной с ТЧ.
//
&НаКлиентеНаСервереБезКонтекста //*yuraos, 10.09.2015 - для оптимизации вызова на клиенте 
Функция ФормаПолучитьИмяСтраницыТабличнаяЧасть(ТЧИмя)
	Возврат "СтраницаТЧ" + ТЧИмя;
КонецФункции

// Возвращает имя Страницы Формы, связанной с Регистром.
//
&НаКлиентеНаСервереБезКонтекста //*yuraos, 10.09.2015 - для оптимизации вызова на клиенте 
Функция ФормаПолучитьИмяСтраницыРегистр(ТЧИмя)
	Возврат "СтраницаРЕГ" + ТЧИмя;
КонецФункции

// Возвращает имя Таблицы Формы, связанной с ТЧ.
//
&НаКлиентеНаСервереБезКонтекста //*yuraos, 10.09.2015 - для оптимизации вызова на клиенте 
Функция ФормаПолучитьИмяТаблицыТабличнаяЧасть(ТЧИмя)
	Возврат "ТФормыТЧасти" + ТЧИмя;
КонецФункции

// Возвращает имя Таблицы Формы, связанной с Регистром.
//
&НаКлиентеНаСервереБезКонтекста //*yuraos, 10.09.2015 - для оптимизации вызова на клиенте 
Функция ФормаПолучитьИмяТаблицыРегистр(ТЧИмя)
	Возврат "ТФормыДвижений" + ТЧИмя;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ТИП ОБЪЕКТА.
//

// Органичени типа Реквизита Объекта, т.к. в основном это СправочникСсылка, ПеречислениеСсылка, ДокументСсылка и т.д.
//
&НаСервере
Процедура ОграничитьТипРеквизита(Реквизит, Тип)
	
	Если ТипЗнч(Тип) = Тип("Строка") Тогда
		Массив = Новый Массив;
		Массив.Добавить(Тип(Тип));
	Иначе
		Массив = Тип;
	КонецЕсли;
	
	Попытка
		ЭтаФорма[Реквизит].ТипЗначения = Новый ОписаниеТипов(Массив);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки;
	
КонецПроцедуры

// Органичени типа Реквизита Объекта, т.к. в основном это СправочникСсылка, ПеречислениеСсылка, ДокументСсылка и т.д.
//
&НаСервере
Процедура ОграничитьТипЭлемента(Элемент, Тип)
	
	Если ТипЗнч(Тип) = Тип("Строка") Тогда
		Массив = Новый Массив;
		Массив.Добавить(Тип(Тип));
	Иначе
		Массив = Тип;
	КонецЕсли;
	
	Попытка
		Элементы[Элемент].ДоступныеТипы = Новый ОписаниеТипов(Массив);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки;
	
	Элементы[Элемент].ОграничениеТипа = Новый ОписаниеТипов(Массив);
	
	Попытка
		Элементы[Элемент].РежимВыбораИзСписка = Ложь;
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки;
	
КонецПроцедуры

// МО: Возвращает для Описание ОбъектаБД.
// Вызов определения из МодуляОбъекта долгий - поэтому здесь.
//
&НаСервере
Функция ОбъектБДПолучитьОписаниеОбъекта(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьОписаниеОбъекта(ОбъектБД);
	
КонецФункции

// МО: Возвращает для Справочника значение Свойства Предопределенный.
//
&НаСервере
Функция ОбъектБДПолучитьПризнакПроведениеРазрешено(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьПризнакПроведениеРазрешено(ОбъектБД);
	
КонецФункции

// МО: Возвращает для Справочника значение Свойства Предопределенный.
//
&НаСервере
Функция ОбъектБДПолучитьПризнакПредопределенныйОбъекта(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьПризнакПредопределенныйОбъекта(ОбъектБД);
	
КонецФункции

// МО: Возвращает для Выбранного ОбъектаБД значение Свойства Предопределенный, Удаленный, Проведенный.
// Вызов определения из МодуляОбъекта долгий - поэтому здесь.
//
&НаСервере
Функция ОбъектБДПолучитьСостояниеОбъекта(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьСостояниеОбъекта(ОбъектБД);
	
КонецФункции

// МО: 
//
&НаСервере
Процедура ОбъектБДИнициализироватьОсновныеПараметры(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	
	Объект.ТипОбъектаБД 			= ОбъектОбработка.ОбъектБДПолучитьТипОбъекта(ОбъектБД);
	Объект.ПолноеСтроковоеИмяТипа	= ОбъектОбработка.ОбъектБДПолучитьПолноеСтроковоеИмяТипа(ОбъектБД);
	Объект.ИмяОбъектаБД				= ОбъектОбработка.ОбъектБДПолучитьИмяОбъекта(ОбъектБД);
	Объект.Предопределенный 		= ОбъектОбработка.ОбъектБДПолучитьПризнакПредопределенныйОбъекта(ОбъектБД);	// Проверка по типам объектов в МО.
	
КонецПроцедуры

// МО: Возвращает для Значение строку типа "Справочник".
//
&НаСервере
Функция ОбъектБДПолучитьТипОбъекта(Значение)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьТипОбъекта(Значение);
	
КонецФункции

// МО: Возвращает для Значение строку типа "Справочник.Номенклатура".
// Используется при формировании Таблицы Реквизитов.
//
&НаСервере
Функция ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Значение)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Значение);
	
КонецФункции

// МО: Возвращает для Значение строку типа "Номенклатура".
// Используется при формировании Таблицы Реквизитов.
//
&НаСервере
Функция ОбъектБДПолучитьИмяОбъекта(Значение)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьИмяОбъекта(Значение);
	
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "Справочники"
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаСправочники()
	Возврат "Справочник";				// Для скорости.
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСправочник(ТипОбъектаБД)
	Возврат ТипОбъектаБД = ИмяТипаСправочники();
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "Документы"
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаДокументы()
	Возврат "Документ";					// Для скорости.
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоДокумент(ТипОбъектаБД)
	Возврат ТипОбъектаБД = ИмяТипаДокументы();
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "Планы видов характеристик"
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаПланыВидовХарактеристик()
	Возврат "ПланВидовХарактеристик";	// Для скорости.
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПланВидовХарактеристик(ТипОбъектаБД)
	Возврат ТипОбъектаБД = ИмяТипаПланыВидовХарактеристик();
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "ПланыОбмена"
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаПланыОбмена()
	Возврат "ПланОбмена";				// Для скорости.
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПланОбмена(ТипОбъектаБД)
	Возврат ТипОбъектаБД = ИмяТипаПланыОбмена();
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "Бизнес процессы"
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаБизнесПроцессы() Экспорт
	Возврат "БизнесПроцесс";			// Для скорости.
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоБизнесПроцесс(ТипОбъектаБД)
	Возврат ТипОбъектаБД = ИмяТипаБизнесПроцессы();
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "Задачи"
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаЗадачи() Экспорт
	Возврат "Задача";					// Для скорости.
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЗадача(ТипОбъектаБД)
	Возврат ТипОбъектаБД = ИмяТипаЗадачи();
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "Планы видов расчета"
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаПланыВидовРасчета() Экспорт
	Возврат "ПланВидовРасчета";			// Для скорости.
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПланВидовРасчета(ТипОбъектаБД)
	Возврат ТипОбъектаБД = ИмяТипаПланыВидовРасчета();
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "Планы счетов"
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаПланыСчетов() Экспорт
	Возврат "ПланСчетов";				// Для скорости.
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПланСчетов(ТипОбъектаБД)
	Возврат ТипОбъектаБД = ИмяТипаПланыСчетов();
КонецФункции

// Функция-свойство: возвращает значение для идентификации общего типа "Справочники"/"Документы"/ и т.д.
//
// Тип: Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ИмяТипаОбъектов(ТипОбъектаБД)
	Перем ИмяТипаОбъектов;
	
	Если ЭтоСправочник(ТипОбъектаБД) Тогда
		Возврат "Справочники";
	ИначеЕсли ЭтоДокумент (ТипОбъектаБД) Тогда
		Возврат "Документы";
	ИначеЕсли ЭтоПланВидовХарактеристик(ТипОбъектаБД) Тогда
		Возврат "ПланыВидовХарактеристик";
	ИначеЕсли ЭтоПланОбмена(ТипОбъектаБД) Тогда
		Возврат "ПланыОбмена";
	ИначеЕсли ЭтоБизнесПроцесс (ТипОбъектаБД) Тогда
		Возврат "БизнесПроцессы";
	ИначеЕсли ЭтоЗадача (ТипОбъектаБД) Тогда
		Возврат "Задачи";
	ИначеЕсли ЭтоПланВидовРасчета (ТипОбъектаБД) Тогда
		Возврат "ПланыВидовРасчета";
	ИначеЕсли ЭтоПланСчетов (ТипОбъектаБД) Тогда
		Возврат "ПланыСчетов";
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция вОписаниеТипа(ЗначениеТипа)

	МассивТипов = Новый Массив;
	Если ТипЗнч(ЗначениеТипа) = Тип("Строка") Тогда
		МассивТипов.Добавить(Тип(ЗначениеТипа));
	Иначе
		МассивТипов.Добавить(ЗначениеТипа);
	КонецЕсли; 
	ОписаниеТипов	= Новый ОписаниеТипов(МассивТипов);

	Возврат ОписаниеТипов;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ.
//

// МО: Проверить на существование Реквизита в Объекте.
//
&НаСервере
Функция ОбъектБДЕстьСтандартныеТабличныеЧастиОбъекта(ОбъектОбработка, мдОбъектаБД, ОбъектБД)
	
	// 2012.10.12. Метод мдОбъектаБД.СтандартныеТабличныеЧасти.Количество() у ПланаСчетов отсутствует.
	
	Попытка
		СтТЧ = мдОбъектаБД.СтандартныеТабличныеЧасти[0];
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Ложь;
	
КонецФункции 

// МО Возвращает Объект типа СправочникОбъект/ДокументОбъект.
//
&НаСервере
Функция ОбъектБДПолучитьОбъектИзСсылки(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьОбъектИзСсылки(ОбъектБД);
	
КонецФункции

// МО: Проверить на существование Реквизита в Объекте.
//
&НаСервере
Функция ЕстьРеквизитОбъекта1С(ИмяРеквизита, Объект)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ЕстьРеквизитОбъекта1С(ИмяРеквизита, Объект);
	
КонецФункции 

// Получить Значение Реквизита в Объекта.
//
&НаСервере
Функция ОбъектБДПолучитьЗначениеРеквизитаОбъекта(ИмяРеквизита, Объект)
	Возврат Объект[ИмяРеквизита];
КонецФункции 

// Получить Тип Реквизита в Объекта.
//
&НаСервере
Функция ОбъектБДПолучитьТипРеквизитаОбъекта(ИмяРеквизита, Объект)
	Возврат ТипЗнч(Объект[ИмяРеквизита]);
КонецФункции 

// МО: Получить Реквизит ОбъектаБД по Имени.
//
&НаСервере
Функция ОбъектБДПолучитьМДРеквизита(ИмяРеквизита, Объект)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ОбъектБДПолучитьМДРеквизита(ИмяРеквизита, Объект);
	
КонецФункции 

// Проверить на существование Реквизита в Форме.
//
&НаСервере
Функция ФормаЕстьРеквизит(ИмяРеквизита)
	
	Попытка
		ЕстьРеквизит = Этаформа[ИмяРеквизита];
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

// Проверить на существование Элемента на Форме.
//
&НаСервере
Функция ФормаЕстьЭлемент(ИмяРеквизита)
	Возврат НЕ Элементы.Найти(ИмяРеквизита) = Неопределено;
КонецФункции

// Органичени типа Реквизита Объекта, т.к. в основном это СправочникСсылка, ПеречислениеСсылка, ДокументСсылка и т.д.
//
&НаСервере
Процедура ФормаОграничитьТипЭлемента(Элемент, Тип)
	
	Если ТипЗнч(Тип) = Тип("Строка") Тогда
		Массив = Новый Массив;
		Массив.Добавить(Тип(Тип));
	Иначе
		Массив = Тип;
	КонецЕсли;
	
	Попытка
		Элементы[Элемент].ДоступныеТипы = Новый ОписаниеТипов(Массив);
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки;
	
	Элементы[Элемент].ОграничениеТипа = Новый ОписаниеТипов(Массив);
	
	Попытка
		Элементы[Элемент].РежимВыбораИзСписка = Ложь;
	Исключение
	    //Сообщить(НСтр("ru = '"+ОписаниеОшибки()+"'"), СтатусСообщения.Внимание);
	КонецПопытки;
	
КонецПроцедуры

// Сервисное сообщение.
//
&НаСервере
Процедура СообщитьОбОшибкеПриЗаписи(Информация)
	
	Причина = ?(Информация.Причина = Неопределено, Информация, Информация.Причина);
	Сообщить(Причина.Описание, СтатусСообщения.Важное);
	
КонецПроцедуры

// Удаляет цифры из строки, оставляя символы.
//
&НаСервере
Функция УдалитьЦифрыВСтроке(НомерСЦифрами)
	
	Массив = Новый Массив;
	Массив.Добавить("0");
	Массив.Добавить("1");
	Массив.Добавить("2");
	Массив.Добавить("3");
	Массив.Добавить("4");
	Массив.Добавить("5");
	Массив.Добавить("6");
	Массив.Добавить("7");
	Массив.Добавить("8");
	Массив.Добавить("9");
	
	НомерБезЦифр = НомерСЦифрами;
	Для нМ = 0 По 9 Цикл
		Пока Найти(НомерБезЦифр, Массив[нМ]) > 0 Цикл
			НомерБезЦифр = СтрЗаменить(НомерБезЦифр, Массив[нМ], "");
		КонецЦикла;
	КонецЦикла;
	НомерБезЦифр = СокрЛП(НомерБезЦифр);
	
	Возврат НомерБезЦифр;
КонецФункции

// Вкладка "Ссылки на Объект". Установить/Снять Флажки в списке Ссылок на Объект.
//
&НаСервере
Процедура  ФормаУстановитьСнятьФлажокВТаблицеНаСервере(Действие, Таблица)
	
	Если Таблица = "ТСсылкиСписок" Тогда
		Для Каждого СтрокаСписка ИЗ Объект.ТСсылкиСписок Цикл
			СтрокаСписка["Включено"] = Действие;
		КонецЦикла;
	ИначеЕсли Таблица = "ТСправочникСписок" Тогда
		Для Каждого СтрокаСписка ИЗ Объект.ТСправочникСписок Цикл
			СтрокаСписка["Включено"] = Действие;
		КонецЦикла;
	ИначеЕсли Таблица = "ТДокументСписок" Тогда
		Для Каждого СтрокаСписка ИЗ Объект.ТДокументСписок Цикл
			СтрокаСписка["Включено"] = Действие;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Обработка оповещения ЭтойФормы от Формы "Настройка".
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Строка(Источник) = "Форма" И НЕ ТипЗнч(Источник) = Тип("УправляемаяФорма") Тогда	// в Управляемом приложении отсутствует Тип("Форма").
		Возврат;
	КонецЕсли;
	
	Попытка
		
		Если Источник.ВладелецФормы = ЭтаФорма Тогда
			Если ИмяСобытия = "ПоказыватьСообщения" Тогда								// Из формы "Настройка".
				Объект.ПоказыватьСообщения 						= Параметр;
			ИначеЕсли ИмяСобытия = "СкрыватьПустыеТаблицыДвиженийРегистров" Тогда		// Из формы "Настройка".
				Объект.СкрыватьПустыеТаблицыДвиженийРегистров	= Параметр;
			ИначеЕсли ИмяСобытия = "СкрыватьПустыеТабличныеЧасти" Тогда					// Из формы "Настройка".
				Объект.СкрыватьПустыеТабличныеЧасти				= Параметр;
			ИначеЕсли ИмяСобытия = "ОбменДаннымиЗагрузка" Тогда							// Из формы "Настройка".
				Объект.ОбменДаннымиЗагрузка 					= Параметр;
			ИначеЕсли ИмяСобытия = "МонопольныйРежим" Тогда								// Из формы "Настройка".
				Объект.МонопольныйРежим							= Параметр;
				ФормаПолучитьЗаголовокРежима();
			ИначеЕсли ИмяСобытия = "ИспользоватьСтандартнуюФормуВыбора" Тогда			// Из формы "Настройка".
				Объект.ИспользоватьСтандартнуюФормуВыбора		= Параметр;
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		// Не всегда Источником является Форма, следовательно Владельца формы нет.
	КонецПопытки;
	
КонецПроцедуры

// Получить заголовок типа Монопольный/Многопользовательский доступ.
//
&НаКлиенте
Процедура ФормаПолучитьЗаголовокРежима()
	
	Если Объект.МонопольныйРежим Тогда
		Элементы.Режим.Заголовок = "Монопольно";
	Иначе
		Элементы.Режим.Заголовок = "Немонопольно";
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает время в миллисекундах или секундах.
//
// Пример использования:
//
// Начало = ВремяВМиллисекундах();
// КоличествоСтрок = 0;
// Для Каждого Элемент ИЗ Массив Цикл
// 	  КоличествоСтрок = КоличествоСтрок  + 1;
//    ...
// КонецЦикла;
// Конец  = ВремяВМиллисекундах();
// ВремяВыполнения = (Конец - Начало) / 1000;
//
// РезультСтрока = НСтр("ru = 'Результат процесса (количество строк = %КоличествоСтрок%, время выполнения = %ВремяВыполнения% с)'");
// РезультСтрока = СтрЗаменить(РезультСтрока, "%КоличествоСтрок%", Строка(КоличествоСтрок));
// РезультСтрока = СтрЗаменить(РезультСтрока, "%ВремяВыполнения%", Строка(ВремяВыполнения));
//
&НаКлиентеНаСервереБезКонтекста
Функция ВремяВМиллисекундах()
	Перем Script, Время;
	
	Попытка
		Выполнить("Время = ТекущаяУниверсальнаяДатаВМиллисекундах()");	// Версия Платформы 8.2.17.153 и выше.
	Исключение
		Попытка
			Script = Новый COMОбъект("MSScriptControl.ScriptControl");
			Script.Language = "javascript";
			Script.Timeout	= -1;
			Время = Script.Eval("var d = new Date(); d.getTime()");
		Исключение
			Время = ТекущаяДата();
		КонецПопытки;	
	КонецПопытки;
	
	Возврат Время;
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// КНОПКИ ОСНОВНОГО МЕНЮ. ОПЦИИ.
// 

// Кнопка "Обновить" Повторное считывание данных выбранного объекта.
//
&НаКлиенте
Процедура ОМОбновить(Команда)
	
	ОчиститьСообщения();

	Если ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		ОбъектБДПриИзменении("ОМОбновить");
	Иначе
		ФормаОпределитьДоступнность(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДомашняяСтраницаОбработки(Команда)
	ЗапуститьПриложение(Объект.ДомашняяСтраницаОбработки);
КонецПроцедуры

&НаКлиенте
Процедура ОМПолнотекстовыйПоискДанных(Команда)
	
	Попытка
		// Форма типовой конфигурации.
		ОткрытьФорму("Обработка.ПолнотекстовыйПоискВДанных.Форма", , ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		Попытка
			ОткрытьФорму("Обработка.ПолнотекстовыйПоискВДанных.Форма.УпрощеннаяФорма", , ЭтаФорма, ЭтаФорма.КлючУникальности);
		Исключение
			ИмяФормыПолнотекстовыйПоискДанных = Объект.Настройки.ИмяФормыПолнотекстовыйПоискДанных;
			Если ИмяФормыПолнотекстовыйПоискДанных = Неопределено Тогда
				ПредупреждениеСообщение(, "Форма ""Полнотекстовый поиск данных"" не найдена.
				|Невозможно осуществить поиск.");
				Возврат;
			КонецЕсли;
			Попытка
				ОткрытьФорму(ИмяФормыПолнотекстовыйПоискДанных, Новый Структура("Настройки", Объект.Настройки), ЭтаФорма, ЭтаФорма.КлючУникальности);
			Исключение
				Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
			КонецПопытки;
		КонецПопытки;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМУправлениеПолнотекстовымПоиском(Команда)
	
	ИмяФормыУправлениеПолнотекстовымПоиском = Объект.Настройки.ИмяФормыУправлениеПолнотекстовымПоиском;
	Если ИмяФормыУправлениеПолнотекстовымПоиском = Неопределено Тогда
		ПредупреждениеСообщение(, "Форма ""Управление полнотекстовым поиском"" не найдена.
		|Действия прекращены.");
		Возврат;
	КонецЕсли;
	Попытка
		ОткрытьФорму(ИмяФормыУправлениеПолнотекстовымПоиском, , ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМКонстанты(Команда)
	
	ИмяФормыКонстанты = Объект.Настройки.ИмяФормыКонстанты;
	Если ИмяФормыКонстанты = Неопределено Тогда
		ПредупреждениеСообщение(, "Форма ""Константы"" не найдена.
		|Просмотр и редактирование констант невозможен.");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект.Настройки, Объект);
	
	Попытка
		Состояние("Формирование таблицы констант 
		|конфигурации " + Объект.КонфигурацияИВерсия);
		ОткрытьФорму(ИмяФормыКонстанты, Новый Структура("Настройки", Объект.Настройки), ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		Состояние();
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ОМСоздатьЭлементыМенюСервисНаСервере(ИмяПанели, ИмяПанелиИсключения, ГруппаПанели)
	Перем Обработка, ТаблицаПанели, НоваяСтрока;
	Перем Подменю;
	
	ТаблицаПанели = Новый ТаблицаЗначений;
	ТаблицаПанели.Колонки.Добавить("ИмяОбработки");
	ТаблицаПанели.Колонки.Добавить("СинонимОбработки");
	ТаблицаПанели.Колонки.Добавить("КомментарийОбработки");
	ТаблицаПанели.Колонки.Добавить("Префикс");
	
	Для Каждого Обработка ИЗ Метаданные.Обработки Цикл
		Если ЗначениеЗаполнено(ИмяПанелиИсключения) И Найти(Обработка.Имя, ИмяПанелиИсключения) > 0 Тогда
			Продолжить;
		КонецЕсли;
		Если Найти(Обработка.Имя, ИмяПанели) > 0 Тогда
			НоваяСтрока = ТаблицаПанели.Добавить();
			НоваяСтрока.ИмяОбработки = Обработка.Имя;
			НоваяСтрока.СинонимОбработки = Обработка.Синоним;
			НоваяСтрока.КомментарийОбработки = Обработка.Комментарий;
			НоваяСтрока.Префикс = СтрЗаменить(Обработка.Имя, ИмяПанели, "");
			НоваяСтрока.Префикс = ?(Найти(НоваяСтрока.Префикс, "БСП") > 0, "БСП", НоваяСтрока.Префикс);
			НоваяСтрока.Префикс = ?(Найти(НоваяСтрока.Префикс, "1СДокументооборот") > 0, "1СД", НоваяСтрока.Префикс);
			НоваяСтрока.Префикс = ?(Найти(НоваяСтрока.Префикс, "Справочник") > 0, "СПР", НоваяСтрока.Префикс);
			НоваяСтрока.Префикс = ?(СтрДлина(НоваяСтрока.Префикс) > 3, "КФГ", НоваяСтрока.Префикс);
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаПанели.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Обработка ИЗ ТаблицаПанели Цикл
		Подменю = Элементы.Вставить(Обработка.ИмяОбработки, Тип("ГруппаФормы"), ГруппаПанели);
		Подменю.Заголовок = ?(ЗначениеЗаполнено(НоваяСтрока.КомментарийОбработки), НоваяСтрока.КомментарийОбработки, Обработка.СинонимОбработки);
		Подменю.Вид = ВидГруппыФормы.Подменю;
		Подменю.Картинка = БиблиотекаКартинок.УстановитьФлажки;
		Подменю.Отображение = ОтображениеКнопки.КартинкаИТекст;
		ОМНастройкиКонфигурацииНаСервере(Подменю, Обработка.ИмяОбработки, Обработка.Префикс);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОМНастройкиКонфигурацииНаСервере(Подменю, ИмяОбработки, Префикс)
	Перем мдПанельАдминистрирования, СинонимОбработки, КомментарийОбработки;
	
	мдПанельАдминистрирования = Метаданные.Обработки[ИмяОбработки];
	СинонимОбработки = мдПанельАдминистрирования.Синоним;
	КомментарийОбработки = мдПанельАдминистрирования.Комментарий;
	
	Для Каждого Форма ИЗ мдПанельАдминистрирования.Формы Цикл
		Если ВРег(Форма.Имя) = "ФОРМА" И Найти(ИмяОбработки, "ПанельАдминистрирования") > 0 Тогда
			Продолжить;
		КонецЕсли;
		Команда = ЭтаФорма.Команды.Добавить(мдПанельАдминистрирования.Имя + "_" + Форма.Имя);
		Команда.Действие  = "ОМПодключаемый_ВыполнитьКоманду";
		Если мдПанельАдминистрирования.Формы.Количество() = 1 Тогда
			Команда.Заголовок = Префикс + ":" + ?(ЗначениеЗаполнено(КомментарийОбработки), КомментарийОбработки, СинонимОбработки);
		Иначе
			Команда.Заголовок = Префикс + ":" + ?(ЗначениеЗаполнено(Форма.Комментарий), Форма.Комментарий, Форма.Синоним);
		КонецЕсли;
		Команда.Картинка = БиблиотекаКартинок.ЗаписатьИЗакрыть;
		Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
		
		Элемент = ЭтаФорма.Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), Подменю);
		Элемент.ИмяКоманды = Команда.Имя;
		Элемент.Заголовок = Команда.Заголовок;
		Элемент.ТолькоВоВсехДействиях = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМПодключаемый_ВыполнитьКоманду(Команда)
	Перем МассивОФ, ИмяОбработки, ФормаОбработки;
	
	Попытка
		МассивОФ = РазложитьСтрокуВМассивПодстрок(Команда.Имя, "_");
		Если МассивОФ.Количество() = 2 Тогда
			ИмяОбработки = МассивОФ[0];
			ФормаОбработки = МассивОФ[1];
		ИначеЕсли МассивОФ.Количество() = 3 Тогда
			ИмяОбработки = МассивОФ[0];
			ФормаОбработки = МассивОФ[1] + "_" + МассивОФ[2];
		КонецЕсли;
		ОткрытьФорму("Обработка." + ИмяОбработки + ".Форма." + ФормаОбработки);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

// Кнопка "Структура подчиненности".
//
&НаКлиенте
Процедура ОМСтруктураПодчиненности(Команда)
	
	ТипОбъектаБД = Объект.ТипОбъектаБД;
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) 
		ИЛИ НЕ ТипОбъектаБД = ИмяТипаДокументы() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФормыСтруктурыПодчиненности = Объект.Настройки.ИмяФормыСтруктурыПодчиненности;
	
	Если ИмяФормыСтруктурыПодчиненности = Неопределено Тогда
		ПредупреждениеСообщение(, "Форма Структура Подчиненности не найдена.
		|Просмотр информации о Подчиненности документов невозможен.");
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектОтбора", Объект.ОбъектБД);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Ложь);
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);	//+yuraos, 10.09.2015
	ПараметрыФормы.Вставить("РежимИспользованияМодальностиБулево", Объект.РежимИспользованияМодальностиБулево);
	Попытка
		ОткрытьФорму(ИмяФормыСтруктурыПодчиненности, ПараметрыФормы, ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
    КонецПопытки;
	
КонецПроцедуры

// Открыть Форму для просмотра Дат Запрета редактирования даных.
//
&НаКлиенте
Процедура ОМДатыЗапрета(Команда)

	ИмяРегистраДатыЗапрета = Объект.ИмяРегистраДатыЗапрета;
	
	Если НЕ ЗначениеЗаполнено(ИмяРегистраДатыЗапрета) Тогда
		ПредупреждениеСообщение(, "Не обнаружен Регистр ""Даты запрета изменения данных"".
		|Просмотр данных невозможен.");
		Возврат;
	КонецЕсли;
	
	Попытка
		ОткрытьФорму("РегистрСведений." + ИмяРегистраДатыЗапрета + ".ФормаСписка");
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
	КонецПопытки;
		
КонецПроцедуры

// Открыть Форму "Карта маршрута".
//
&НаКлиенте
Процедура ОМКартаМаршрута(Команда)
	
	ТипОбъектаБД = Объект.ТипОбъектаБД;
	
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) 
		ИЛИ НЕ ТипОбъектаБД = ИмяТипаБизнесПроцессы() Тогда
		Если НЕ ТипОбъектаБД = ИмяТипаЗадачи() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ИмяФормыКартаМаршрута = Объект.Настройки.ИмяФормыКартаМаршрута;
	
	Если ИмяФормыКартаМаршрута = Неопределено Тогда
		ПредупреждениеСообщение(, "Форма ""КартаМаршрута"" не найдена.
		|Просмотр карты бизнес-процесса невозможен.");
		Возврат;
	КонецЕсли;
	
	Попытка
		ПараметрыФормы = Новый Структура;
		Если ТипОбъектаБД = ИмяТипаБизнесПроцессы() Тогда
			ПараметрыФормы.Вставить("БизнесПроцесс", Объект.ОбъектБД);
		ИначеЕсли ТипОбъектаБД = ИмяТипаЗадачи() Тогда
			БПроцесс = ОМКартаМаршрутаПолучитьБизнесПроцессЗадачиНаСервере(Объект.ОбъектБД);
			ПараметрыФормы.Вставить("БизнесПроцесс", БПроцесс);
			Если НЕ ЗначениеЗаполнено(БПроцесс) Тогда
				ПредупреждениеСообщение(, "Задача: """ + Объект.ОбъектБД + """ 
				|
				|Бизнес-процесс неопределен.");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		ПараметрыФормы.Вставить("РежимИспользованияМодальностиБулево", Объект.РежимИспользованияМодальностиБулево);
	
		ОткрытьФорму(ИмяФормыКартаМаршрута, ПараметрыФормы, ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
    КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ОМКартаМаршрутаПолучитьБизнесПроцессЗадачиНаСервере(ОбъектБД)
	Возврат ОбъектБД.БизнесПроцесс;
КонецФункции

// Кнопка "История изменений объекта". Дополнительная форма.
//
&НаКлиенте
Процедура ОМИсторияИзмененийОбъекта(Команда)
	
	ИмяФормыЖурналаРегистрации = Объект.Настройки.ИмяФормыЖурналаРегистрации;
	
	Если ИмяФормыЖурналаРегистрации = Неопределено Тогда
		ПредупреждениеСообщение(, "Форма ""Журнала регистрации"" не найдена.
		|Просмотр списка регистрации изменений данной БД, невозможен.");
		Возврат;
	КонецЕсли;
	
	Попытка
		Состояние("Подождите ...
		|Производится открытие Журнала регистрации.");
	
		ПараметрыФормы = Новый Структура;
		Если ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
			ПараметрыФормы.Вставить("ОбъектБД", Объект.ОбъектБД);
		КонецЕсли;
		ПараметрыФормы.Вставить("РежимИспользованияМодальностиБулево", Объект.РежимИспользованияМодальностиБулево);
	
		ОткрытьФорму(ИмяФормыЖурналаРегистрации, ПараметрыФормы, ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМПроверитьРегистрациюВПланахОбмена(Команда)
	Перем СписокУзловОбмена, ЗначенияВыбраны;
	Перем Оповещение;
	
	ОчиститьСообщения();
	
	СписокУзловОбмена = ПолучитьСписокУзловОбменаДляОбъекта(Объект.ОбъектБД);
	
	Если СписокУзловОбмена.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Объект в обмене не участвует.");
		Возврат;
	КонецЕсли;
	
	// Установить пометку у всех строк.
    СписокУзловОбмена.ЗаполнитьПометки(Истина);
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		ЗначенияВыбраны = СписокУзловОбмена.ОтметитьЭлементы("Выберите узел(ы) обмена для проверки регистрации объекта");
		ОМПроверитьРегистрациюВПланахОбменаЗавершение(СписокУзловОбмена, ЗначенияВыбраны);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше .
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМПроверитьРегистрациюВПланахОбменаЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		СписокУзловОбмена.ПоказатьОтметкуЭлементов(Оповещение, "Выберите узел(ы) обмена для проверки регистрации объекта");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОМПроверитьРегистрациюВПланахОбменаЗавершение(СписокУзловОбмена, Параметр) Экспорт
	Перем ПроверкаРегистрации;
	
	Если СписокУзловОбмена = Неопределено ИЛИ (ТипЗнч(Параметр) = Тип("Булево") И НЕ Параметр) Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Проверка регистрации изменений объекта: 
	|" + Объект.ОбъектБД);
	
	ПроверкаРегистрации = ЗарегистрироватьИзменения_УдалитьРегистрациюИзменений_ИзменениеЗарегистрировано_ОбъектаВУзлахОбмена(Объект.ОбъектБД, СписокУзловОбмена, "ИзменениеЗарегистрировано");
	
	ПредупреждениеСообщение(, "Проверка регистрации изменений объекта в узлах обмена произведена. 
	|");
	
КонецПроцедуры

&НаКлиенте
Процедура ОМРегистрацияВПланахОбмена(Команда)
	Перем СписокУзловОбмена, ЗначенияВыбраны;
	Перем Оповещение;
	
	ОчиститьСообщения();

	СписокУзловОбмена = ПолучитьСписокУзловОбменаДляОбъекта(Объект.ОбъектБД);
	
	Если СписокУзловОбмена.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Объект в обмене не участвует.");
		Возврат;
	КонецЕсли;
	
	// Установить пометку у всех строк.
    СписокУзловОбмена.ЗаполнитьПометки(Истина); 
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		ЗначенияВыбраны = СписокУзловОбмена.ОтметитьЭлементы("Выберите узел(ы) обмена для регистрации объекта");
		ОМРегистрацияВПланахОбменаЗавершение(СписокУзловОбмена, ЗначенияВыбраны);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМРегистрацияВПланахОбменаЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		СписокУзловОбмена.ПоказатьОтметкуЭлементов(Оповещение, "Выберите узел(ы) обмена для регистрации объекта");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОМРегистрацияВПланахОбменаЗавершение(СписокУзловОбмена, Параметр) Экспорт
	Перем РегистрацияПроизведена;
	
	Если СписокУзловОбмена = Неопределено ИЛИ (ТипЗнч(Параметр) = Тип("Булево") И НЕ Параметр) Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Регистрация изменений объекта: 
	|" + Объект.ОбъектБД);
	
	РегистрацияПроизведена = ЗарегистрироватьИзменения_УдалитьРегистрациюИзменений_ИзменениеЗарегистрировано_ОбъектаВУзлахОбмена(Объект.ОбъектБД, СписокУзловОбмена, "ЗарегистрироватьИзменения");
	
	Если НЕ РегистрацияПроизведена Тогда
		ПредупреждениеСообщение(, "Регистрация изменений объекта в узлах обмена не произведена. 
		|см. сообщения.");
	Иначе
		ПредупреждениеСообщение(, "Регистрация изменений объекта, а также ""связанных"" объектов, в узлах обмена завершена.
		|");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМУдалитьРегистрациюВПланахОбмена(Команда)
	Перем СписокУзловОбмена, ЗначенияВыбраны;
	Перем Оповещение;
	
	ОчиститьСообщения();
	
	СписокУзловОбмена = ПолучитьСписокУзловОбменаДляОбъекта(Объект.ОбъектБД);
	
	Если СписокУзловОбмена.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Объект в обмене не участвует.");
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		ЗначенияВыбраны = СписокУзловОбмена.ОтметитьЭлементы("Выберите узел(ы) обмена для удаления регистрации объекта");
		ОМУдалитьРегистрациюВПланахОбменаЗавершение(СписокУзловОбмена, ЗначенияВыбраны);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМУдалитьРегистрациюВПланахОбменаЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		СписокУзловОбмена.ПоказатьОтметкуЭлементов(Оповещение, "Выберите узел(ы) обмена для удаления регистрации объекта");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОМУдалитьРегистрациюВПланахОбменаЗавершение(СписокУзловОбмена, Параметр) Экспорт
	Перем ОтменаРегистрацииПроизведена;
	
	Если СписокУзловОбмена = Неопределено ИЛИ (ТипЗнч(Параметр) = Тип("Булево") И НЕ Параметр) Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Удаление регистрации изменений объекта: 
	|" + Объект.ОбъектБД);
	
	ОтменаРегистрацииПроизведена = ЗарегистрироватьИзменения_УдалитьРегистрациюИзменений_ИзменениеЗарегистрировано_ОбъектаВУзлахОбмена(Объект.ОбъектБД, СписокУзловОбмена, "УдалитьРегистрациюИзменений");
	
	Если НЕ ОтменаРегистрацииПроизведена Тогда
		ПредупреждениеСообщение(, "Удаление регистрации изменений объекта в узлах обмена не произведено.
		|см. сообщения.");
	Иначе
		ПредупреждениеСообщение(, "Удаление регистрации изменений объекта в узлах обмена завершено.
		|");
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокУзловОбменаДляОбъекта(ОбъектБД)
	Перем ОбъектОбработка;
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ПолучитьСписокУзловОбменаДляОбъекта(ОбъектБД);
		
КонецФункции

&НаСервере
Функция ЗарегистрироватьИзменения_УдалитьРегистрациюИзменений_ИзменениеЗарегистрировано_ОбъектаВУзлахОбмена(ОбъектБД, СписокУзловОбмена, Действие)
	Перем ОбъектОбработка;
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.ЗарегистрироватьИзменения_УдалитьРегистрациюИзменений_ИзменениеЗарегистрировано_ОбъектаВУзлахОбмена(ОбъектБД, СписокУзловОбмена, Действие);
		
КонецФункции

&НаКлиенте
Процедура ОМРегистрацияИзмененийОбъектовВПланахОбмена(Команда)
	Перем ФормаРегистрацииИзмененийОбъектовВПланахОбмена;
	
	ФормаРегистрацииИзмененийОбъектовВПланахОбмена = ПолучитьФорму(Объект.Настройки.ИмяФормыРегистрацияИзменений, Новый Структура("Настройки", Объект.Настройки), ЭтаФорма);
	ФормаРегистрацииИзмененийОбъектовВПланахОбмена.Открыть();
	
КонецПроцедуры

// Кнопка "Журнал регистрации". Дополнительная форма.
//
&НаКлиенте
Процедура ОМЖурналРегистрации(Команда)
	
	Попытка
		// Форма типовой конфигурации.
		ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", , ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		ИмяФормыЖурналаРегистрации = Объект.Настройки.ИмяФормыЖурналаРегистрации;
		Если ИмяФормыЖурналаРегистрации = Неопределено Тогда
			ПредупреждениеСообщение(, "Форма ""Журнала регистрации"" не найдена.
			|Просмотр списка регистрации изменений данной БД, невозможен.");
			Возврат;
		КонецЕсли;
		Попытка
			ОткрытьФорму(ИмяФормыЖурналаРегистрации, Новый Структура("РежимИспользованияМодальностиБулево", Объект.РежимИспользованияМодальностиБулево), ЭтаФорма, ЭтаФорма.КлючУникальности);
		Исключение
			Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
		КонецПопытки;
	КонецПопытки;
	
КонецПроцедуры

// Кнопка "Активные пользователи". Дополнительная форма.
//
&НаКлиенте
Процедура ОМАктивныеПользователиИБ(Команда)	// + см. форму "Журнал регистрации".
	
	Попытка
		// Форма типовой конфигурации.
		ОткрытьФорму("Обработка.АктивныеПользователи.Форма", , ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		ИмяФормыАктивныеПользователи = Объект.Настройки.ИмяФормыАктивныеПользователи;
		Если ИмяФормыАктивныеПользователи = Неопределено Тогда
			ПредупреждениеСообщение(, "Форма ""Активные пользователи"" не найдена.
			|Просмотр списка пользователей, работающих с данной БД, невозможен.");
			Возврат;
		КонецЕсли;
		Попытка
			ОткрытьФорму(ИмяФормыАктивныеПользователи, , ЭтаФорма, ЭтаФорма.КлючУникальности);
		Исключение
			Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
		КонецПопытки;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМБлокировкаРаботыПользователейИБ(Команда)
	
	Попытка
		// Форма типовой конфигурации.
		ОткрытьФорму("Обработка.БлокировкаРаботыПользователей.Форма", , ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		Попытка
			// Форма типовой конфигурации.
			ОткрытьФорму("Обработка.БлокировкаСоединенийСИнформационнойБазой.Форма", , ЭтаФорма, ЭтаФорма.КлючУникальности);
		Исключение
			ПредупреждениеСообщение(, "В конфигурации отсутствует обработка ""БлокировкаРаботыПользователей"" (""БлокировкаСоединенийСИнформационнойБазой"").");
		КонецПопытки;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМВыгрузитьЗагрузитьПользователейИБ(Команда)
	
	ИмяФормыВыгрузкаЗагрузкаПользователей = Объект.Настройки.ИмяФормыВыгрузкаЗагрузкаПользователей;
	
	Если ИмяФормыВыгрузкаЗагрузкаПользователей = Неопределено Тогда
		ПредупреждениеСообщение(, "Форма ""Выгрузка/Загрузка пользователей"" не найдена.
		|Выгрузка/Загрузка пользователей, работающих с данной БД, невозможен.");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект.Настройки, Объект);
	
	Попытка
		ОткрытьФорму(ИмяФормыВыгрузкаЗагрузкаПользователей, Объект.Настройки, ЭтаФорма, ЭтаФорма.КлючУникальности);
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОМЗагрузитьПользователейИБизWindows(Команда)
	Перем DomainComputerUser, Computer, СписокПользователейWindows, Оповещение;
	
	Состояние("Определение системного окружения ...");
	
	DomainComputerUser = DomainComputerUser();
	Если ЗначениеЗаполнено(DomainComputerUser.DomainLDAP) Тогда
		ПредупреждениеСообщение(, "Произведена в регистрация в домене " + DomainComputerUser.DomainDNS + ".
		|Выполните загрузку пользователей ИБ из Active Directory.");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(DomainComputerUser.Computer) Тогда
		Если ВРег(DomainComputerUser.Domain) = ВРег(DomainComputerUser.Computer) Тогда
			Computer = ИмяКомпьютера();
		Иначе
			Computer = DomainComputerUser.Computer;
		КонецЕсли;
	Иначе
		Computer = ИмяКомпьютера();
	КонецЕсли;
	
	Состояние("Считывание пользователей " + Computer + " из Windows ...");
	
	СписокПользователейWindows = Новый СписокЗначений;
	СписокПользователейWindows = ОМПолучитьПользователейWindowsНаСервере(Computer);
	
	Если СписокПользователейWindows.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Не удалось получить пользователей Windows.");
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		СписокПользователейWindows.ОтметитьЭлементы("Выбор пользователей Windows");
		ОМЗагрузитьПользователейИБизWindowsИлиADЗавершениеНаСервере(СписокПользователейWindows, Computer);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМЗагрузитьПользователейИБизWindowsИлиADЗавершениеНаСервере"", ЭтаФормаЭтотОбъект(), Computer)");
		СписокПользователейWindows.ПоказатьОтметкуЭлементов(Оповещение, "Выбор пользователей Windows");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОМЗагрузитьПользователейИБизWindowsИлиADЗавершениеНаСервере(СписокПользователейWindowsAD, ComputerAD) Экспорт
	Перем ДобавленоПользователей;
	
	ДобавленоПользователей = 0;
	
	Если НЕ СписокПользователейWindowsAD = Неопределено Тогда
		ДобавленоПользователей = ОбъектОбработка().ЗагрузитьПользователеWindowsADвИБ(СписокПользователейWindowsAD, ComputerAD);
	КонецЕсли;
	
	Если ДобавленоПользователей = 0 Тогда
		Сообщить("Новые пользователи не добавлены.");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция DomainComputerUser()
	Возврат ОбъектОбработка().DomainComputerUser();
КонецФункции

&НаСервере
Функция ОМПолучитьПользователейWindowsНаСервере(Computer = ".")
	Возврат ОбъектОбработка().ПолучитьПользователейWindows(Computer);
КонецФункции

&НаКлиенте
Процедура ОМЗагрузитьПользователейИБизActiveDirectory(Команда)
	Перем DomainComputerUser, DomainLDAP, DomainDNS, СписокПользователейAD, Оповещение;
	
	Состояние("Определение системного окружения ...");
	
	DomainComputerUser = DomainComputerUser();
	DomainLDAP = DomainComputerUser.DomainLDAP;
	Если НЕ ЗначениеЗаполнено(DomainLDAP) Тогда
		ПредупреждениеСообщение(, "Домен не определен.
		|Выполните загрузку пользователей ИБ из Windows.");
		Возврат;
	КонецЕсли;
	DomainDNS = DomainComputerUser.DomainDNS;
	
	Состояние("Считывание пользователей " + DomainDNS + " из Active Directory ...");
	
	СписокПользователейAD = Новый СписокЗначений;
	СписокПользователейAD = ОМПолучитьПользователейActiveDirectoryНаСервере(DomainLDAP);
	
	Если СписокПользователейAD.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Не удалось получить пользователей Active Directory.");
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		СписокПользователейAD.ОтметитьЭлементы("Выбор пользователей домена "+DomainDNS);
		ОМЗагрузитьПользователейИБизWindowsИлиADЗавершениеНаСервере(СписокПользователейAD, DomainDNS);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОМЗагрузитьПользователейИБизWindowsИлиADЗавершениеНаСервере"", ЭтаФормаЭтотОбъект(), DomainDNS)");
		СписокПользователейAD.ПоказатьОтметкуЭлементов(Оповещение, "Выбор пользователей домена "+ DomainDNS);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ОМПолучитьПользователейActiveDirectoryНаСервере(DomainLDAP)
	Возврат ОбъектОбработка().ПолучитьПользователейActiveDirectory(DomainLDAP);
КонецФункции

// Кнопка "Настройка" Параметров обработки.
//
&НаКлиенте
Процедура ОМНастройка(Команда)
	
	ИмяФормыНастройкиОбработки = Объект.Настройки.ИмяФормыНастройкиОбработки;
	
	Если ИмяФормыНастройкиОбработки = Неопределено Тогда
		ПредупреждениеСообщение(, "Форма Настройки Обработки не найдена.");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект.Настройки, Объект);
	
	Попытка
		ФормаНастройки = ПолучитьФорму(ИмяФормыНастройкиОбработки, Объект.Настройки, ЭтаФорма);
		ФормаНастройки.ЗакрыватьПриЗакрытииВладельца = Истина;
		Если ФормаНастройки.Открыта() Тогда
			ФормаНастройки.Закрыть();
		КонецЕсли;
		ФормаНастройки.Открыть();	// НЕМОДАЛЬНО !!! ОПОВЕЩЕНИЯ.
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
	КонецПопытки;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБЩЕГО НАЗНАЧЕНИЯ.
//

// 25.03.2016. Получить значение реквизита произвольной коллекции.
&НаСервере
Функция ЗначениеИзКоллекции1С(Знач Коллекция, Знач Реквизит, ОписаниеОшибки = "")
	Перем Значение;
	
	Попытка
		Значение = Коллекция[Реквизит];
		Возврат Значение;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НСтр1С(Знач Строка)
	Возврат НСтр("ru = '" + Строка + "'");
КонецФункции

// Функция "расщепляет" строку на подстроки, используя заданный
//      разделитель. Разделитель может иметь любую длину.
//      Если в качестве разделителя задан пробел, рядом стоящие пробелы
//      считаются одним разделителем, а ведущие и хвостовые пробелы параметра Стр
//      игнорируются.
//      Например,
//      РазложитьСтрокуВМассивПодстрок(",один,,,два", ",") возвратит массив значений из пяти элементов,
//      три из которых - пустые строки, а
//      РазложитьСтрокуВМассивПодстрок(" один   два", " ") возвратит массив значений из двух элементов
//
//  Параметры:
//      Стр -           строка, которую необходимо разложить на подстроки.
//                      Параметр передается по значению.
//      Разделитель -   строка-разделитель, по умолчанию - запятая.
//
//  Возвращаемое значение:
//      массив значений, элементы которого - подстроки
//
&НаКлиентеНаСервереБезКонтекста
Функция РазложитьСтрокуВМассивПодстрок(Знач Стр, Разделитель = ",")
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока 1 = 1 Цикл
			Поз = Найти(Стр, Разделитель);
			Если Поз = 0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр, Поз - 1));
			Стр = СокрЛ(Сред(Стр, Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока 1 = 1 Цикл
			Поз = Найти(Стр, Разделитель);
			Если Поз = 0 Тогда
				Если (СокрЛП(Стр) <> "") Тогда
					МассивСтрок.Добавить(Стр);
				КонецЕсли;
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз - 1));
			Стр = Сред(Стр, Поз + ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции 

// Подставляет параметры в строку. Максимально возможное число параметров - 9.
// Параметры в строке задаются как %<номер параметра>. Нумерация параметров
// начинается с единицы.
//
// Параметры
//  СтрокаПодстановки  – Строка – шаблон строки с параметрами (вхождениями вида "%ИмяПараметра").
// Параметр<n>         - Строка - параметр
// Возвращаемое значение:
//   Строка   – текстовая строка с подставленными параметрами
//
// Пример:
// Строка = ПодставитьПараметрыВСтроку(НСтр("ru='%1 пошел в %2'"), "Вася", "Зоопарк");
//
&НаСервере
Функция ПодставитьПараметрыВСтроку(Знач СтрокаПодстановки,
	Знач Параметр1,	Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено)
	
	Если СтрокаПодстановки = Неопределено ИЛИ СтрДлина(СтрокаПодстановки) = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = "";
	НачПозиция = 1;
	Позиция = 1;
	Пока Позиция <= СтрДлина(СтрокаПодстановки) Цикл
		СимволСтроки = Сред(СтрокаПодстановки, Позиция, 1);
		Если СимволСтроки <> "%" Тогда
			Позиция = Позиция + 1;
			Продолжить;
		КонецЕсли;
		Результат = Результат + Сред(СтрокаПодстановки, НачПозиция, Позиция - НачПозиция);
		Позиция = Позиция + 1;
		СимволСтроки = Сред(СтрокаПодстановки, Позиция, 1);
		
		Если СимволСтроки = "%" Тогда
			Позиция = Позиция + 1;
			НачПозиция = Позиция;
			Продолжить;
		КонецЕсли;
		
		Попытка
			НомерПараметра = Число(СимволСтроки);
		Исключение
			ВызватьИсключение НСтр("ru='Входная строка СтрокаПодстановки имеет неверный формат: %'" + СимволСтроки);
		КонецПопытки;
		
		Если СимволСтроки = "1" Тогда
			ЗначениеПараметра = Параметр1;
		ИначеЕсли СимволСтроки = "2" Тогда
			ЗначениеПараметра = Параметр2;
		ИначеЕсли СимволСтроки = "3" Тогда
			ЗначениеПараметра = Параметр3;
		ИначеЕсли СимволСтроки = "4" Тогда
			ЗначениеПараметра = Параметр4;
		ИначеЕсли СимволСтроки = "5" Тогда
			ЗначениеПараметра = Параметр5;
		ИначеЕсли СимволСтроки = "6" Тогда
			ЗначениеПараметра = Параметр6;
		ИначеЕсли СимволСтроки = "7" Тогда
			ЗначениеПараметра = Параметр7;
		ИначеЕсли СимволСтроки = "8" Тогда
			ЗначениеПараметра = Параметр8;
		ИначеЕсли СимволСтроки = "9" Тогда
			ЗначениеПараметра = Параметр9;
		Иначе
			ВызватьИсключение НСтр("ru='Входная строка СтрокаПодстановки имеет неверный формат: %'" + ЗначениеПараметра);
		КонецЕсли;
		Если ЗначениеПараметра = Неопределено Тогда
			ЗначениеПараметра = "";
		Иначе
			ЗначениеПараметра = Строка(ЗначениеПараметра);
		КонецЕсли;
		Результат = Результат + ЗначениеПараметра;
		Позиция = Позиция + 1;
		НачПозиция = Позиция;
	
	КонецЦикла;
	
	Если (НачПозиция <= СтрДлина(СтрокаПодстановки)) Тогда
		Результат = Результат + Сред(СтрокаПодстановки, НачПозиция, СтрДлина(СтрокаПодстановки) - НачПозиция + 1);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сравнить две строки версий.
//
// Параметры
//  СтрокаВерсии1  – Строка – номер версии в формате РР.{П|ПП}.ЗЗ.СС
//  СтрокаВерсии2  – Строка – второй сравниваемый номер версии
//
// Возвращаемое значение:
//   Число   – больше 0, если СтрокаВерсии1 > СтрокаВерсии2; 0, если версии равны.
//
&НаСервере
Функция СравнитьВерсии(Знач СтрокаВерсии1, Знач СтрокаВерсии2)
	
	Строка1 = ?(ПустаяСтрока(СтрокаВерсии1), "0.0.0.0", СтрокаВерсии1);
	Строка2 = ?(ПустаяСтрока(СтрокаВерсии2), "0.0.0.0", СтрокаВерсии2);
	Версия1 = РазложитьСтрокуВМассивПодстрок(Строка1, ".");
	Если Версия1.Количество() <> 4 Тогда
		ВызватьИсключение ПодставитьПараметрыВСтроку(
		                    НСтр("ru = 'Неправильный формат строки версии: %1'"), СтрокаВерсии1);
	КонецЕсли;
	Версия2 = РазложитьСтрокуВМассивПодстрок(Строка2, ".");
	Если Версия2.Количество() <> 4 Тогда
		ВызватьИсключение ПодставитьПараметрыВСтроку(
	                         НСтр("ru = 'Неправильный формат строки версии: %1'"), СтрокаВерсии2);
	КонецЕсли;
	
	Результат = 0;
	Для Разряд = 0 По 3 Цикл
		Результат = Число(Версия1[Разряд]) - Число(Версия2[Разряд]);
		Если Результат <> 0 Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

// Проверяет содержит ли строка только цифры.
//
// Параметры:
//  СтрокаПроверки - строка для проверки.
//  УчитыватьЛидирующиеНули - Булево - нужно ли учитывать лидирующие нули.
//  УчитыватьПробелы - Булево - нужно ли учитывать пробелы.
//
// Возвращаемое значение:
//  Истина       - строка содержит только цифры;
//  Ложь         - строка содержит не только цифры.
//
&НаСервере
Функция ТолькоЦифрыВСтроке(Знач СтрокаПроверки, Знач УчитыватьЛидирующиеНули = Истина, Знач УчитыватьПробелы = Истина)
	
	Если ТипЗнч(СтрокаПроверки) <> Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаПроверки) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ УчитыватьПробелы Тогда
		СтрокаПроверки = СтрЗаменить(СтрокаПроверки, " ", "");
	КонецЕсли;
	
	Если НЕ УчитыватьЛидирующиеНули Тогда
		НомерПервойЦифры = 0;
		Для а = 1 По СтрДлина(СтрокаПроверки) Цикл
			НомерПервойЦифры = НомерПервойЦифры + 1;
			КодСимвола = КодСимвола(Сред(СтрокаПроверки, а, 1));
			Если КодСимвола <> 48 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		СтрокаПроверки = Сред(СтрокаПроверки, НомерПервойЦифры);
	КонецЕсли;
	
	Для а = 1 По СтрДлина(СтрокаПроверки) Цикл
		КодСимвола = КодСимвола(Сред(СтрокаПроверки, а, 1));
		Если НЕ (КодСимвола >= 48 И КодСимвола <= 57) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции // ТолькоЦифрыВСтроке()

// Удаляет двойные кавычки с начала и конца строки, если они есть.
//
// Параметры:
//  Строка       - входная строка;
//
// Возвращаемое значение:
//  Строка - строка без двойных кавычек.
// 
&НаСервере
Функция СократитьДвойныеКавычки(Знач Строка)
	
	Результат = Строка;
	Пока Найти(Результат, """") = 1 Цикл
		Результат = Сред(Результат, 2); 
	КонецЦикла; 
	Пока Найти(Результат, """") = СтрДлина(Результат) И СтрДлина(Результат) > 0 Цикл
		Результат = Сред(Результат, 1, СтрДлина(Результат) - 1); 
	КонецЦикла; 
	Возврат Результат;
	
КонецФункции 
          
// Процедура удаляет из строки указанное количество символов справа
//
&НаСервере
Процедура УдалитьПоследнийСимволВСтроке(Текст, ЧислоСимволов)
	Текст = Лев(Текст, СтрДлина(Текст) - ЧислоСимволов);
КонецПроцедуры 

// Находит символ в строке с конца
//
&НаСервере
Функция НайтиСимволСКонца(Знач СтрокаВся, Знач ОдинСимвол)
	
	НачальнаяПозиция = 1; 
	ДлинаСтроки = СтрДлина(СтрокаВся);
	
	Для ТекущаяПозиция = 1 По СтрДлина(СтрокаВся) Цикл
		РеальнаяПозиция = ДлинаСтроки - ТекущаяПозиция + 1;
		ТекущийСимвол = Сред(СтрокаВся, РеальнаяПозиция, 1);
		Если ТекущийСимвол = ОдинСимвол Тогда
			Возврат РеальнаяПозиция;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

// Функция проверяет, является ли переданная в неё строка уникальным идентификатором
//
&НаСервере
Функция ЭтоУникальныйИдентификатор(ИдентификаторСтрока)
	
	УИСтрока = ИдентификаторСтрока;
	Шаблон = "XXXXXXXX-XXXX-XXXX-XXXX-XXXXXXXXXXXX";
	
	Если СтрДлина(Шаблон) <> СтрДлина(УИСтрока) Тогда
		Возврат Ложь;
	КонецЕсли;
	Для Сч = 1 По СтрДлина(УИСтрока) Цикл
		Если КодСимвола(Шаблон, сч) = 88 И 
			((КодСимвола(УИСтрока, сч) < 48 ИЛИ КодСимвола(УИСтрока, сч) > 57) И (КодСимвола(УИСтрока, сч) < 97 или КодСимвола(УИСтрока, сч) > 102)) Тогда
			Возврат ложь; 
		 ИначеЕсли КодСимвола(Шаблон, сч) = 45 И КодСимвола(УИСтрока, сч) <> 45 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;

КонецФункции

// Формирует строку повторяющихся символов заданной длины
//
&НаСервере
Функция СформироватьСтрокуСимволов(Символ, КоличествоСимволов)
	Перем Индекс, Результат;
	
	// возвращаемое значение функции
	Результат = "";
	Для Индекс = 1 ПО КоличествоСимволов Цикл
		Результат = Результат + Символ;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Дополняет переданную в качестве первого параметра строку символами слева\справа до заданной длины и возвращает ее
// Незначащие символы слева и справа удаляются
// По умолчанию функция добавляет строку нулями слева
//
// Параметры:
//  Строка      - Строка - исходная строка, которую необходимо дополнить символами до заданной длины
//  ДлинаСтроки - Число - требуемая конечная длина строки
//  Символ      - Строка - (необязательный) значение символа, которым необходимо дополнить строку
//  Режим       - Строка - (необязательный) [Слева|Справа] режим добавления символов к исходной строке: слева или справа
// 
// Пример 1:
// Строка = "1234"; ДлинаСтроки = 10; Символ = "0"; Режим = "Слева"
// Возврат: "0000001234"
//
// Пример 2:
// Строка = " 1234  "; ДлинаСтроки = 10; Символ = "#"; Режим = "Справа"
// Возврат: "1234######"
//
// Возвращаемое значение:
//  Строка - строка, дополненная символами слева или справа
//
&НаСервере
Функция ДополнитьСтроку(Знач Строка, Знач ДлинаСтроки, Знач Символ = "0", Знач Режим = "Слева")
	
	Если ПустаяСтрока(Символ) Тогда
		Символ = "0";
	КонецЕсли;
	
	// длина символа не должна превышать единицы
	Символ = Лев(Символ, 1);
	// удаляем крайние пробелы слева и справа строки
	Строка = СокрЛП(Строка);
	
	КоличествоСимволовНадоДобавить = ДлинаСтроки - СтрДлина(Строка);
	Если КоличествоСимволовНадоДобавить > 0 Тогда
		СтрокаДляДобавления = СформироватьСтрокуСимволов(Символ, КоличествоСимволовНадоДобавить);
		Если ВРег(Режим) = "СЛЕВА" Тогда
			Строка = СтрокаДляДобавления + Строка;
		ИначеЕсли ВРег(Режим) = "СПРАВА" Тогда
			Строка = Строка + СтрокаДляДобавления;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Строка;
	
КонецФункции

// Удаляет повторяющиеся символы слева/справа в переданной строке
//
// Параметры:
//  Строка      - Строка - исходная строка, из которой необходимо удалить повторяющиеся символы
//  Символ      - Строка - значение символа, который необходимо удалить
//  Режим       - Строка - (необязательный) [Слева|Справа] режим добавления символов к исходной строке: слева или справа
//
&НаСервере
Функция УдалитьПовторяющиесяСимволы(Знач Строка, Знач Символ, Знач Режим = "Слева")
	
	Если ВРег(Режим) = "СЛЕВА" Тогда
		Пока Лев(Строка, 1)= Символ Цикл
			Строка = Сред(Строка, 2);
		КонецЦикла;
	ИначеЕсли ВРег(Режим) = "СПРАВА" Тогда
		Пока Прав(Строка, 1)= Символ Цикл
			Строка = Лев(Строка, СтрДлина(Строка) - 1);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Строка;
КонецФункции

// Выполняет в строке ГДЕ замену символов ЧТО на соответствующие по номерам символы из строки НаЧто
//
// Параметры:
//  Что		- Строка - строка символов, каждый из которых будет заменен
//  Где		- Строка - исходная строка, в которой будет выполняться замена
//  НаЧто	- Строка - строка символов, на каждый из которых нужно заменить исходные символы
// 
//  Возвращаемое значение:
//  Строка - строка с замененными символами
//
&НаСервере
Функция ЗаменитьОдниСимволыДругими(Что, Где, НаЧто)
	
	Рез = Где;
	Для Сч = 1 По СтрДлина(Что) Цикл
		Рез = СтрЗаменить(Рез, Сред(Что, Сч, 1), Сред(НаЧто, Сч, 1));
	КонецЦикла;
	Возврат Рез;
	
КонецФункции

&НаСервере
Процедура ПеренестиИзТаблицыИсточникаВТаблицуПриемник(Источник, Приемник)
	
	Для Каждого СтрокаИсточника ИЗ Источник Цикл
		ЗаполнитьЗначенияСвойств(Приемник.Добавить(), СтрокаИсточника);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// МОДАЛЬНОЕ/НЕМОДАЛЬНОЕ ПРЕДУПРЕЖДЕНИЕ.
//

&НаКлиенте
Процедура ПредупреждениеСообщение(Оповещение, ТекстПредупрежденияСообщения, Таймаут = 0, Заголовок = "")
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Предупреждение(ТекстПредупрежденияСообщения, Таймаут, Заголовок);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		Выполнить("ПоказатьПредупреждение(Оповещение, ТекстПредупрежденияСообщения, Таймаут, Заголовок)");
	КонецЕсли;;
		
КонецПроцедуры

&НаКлиенте
Функция ИспользоватьРежимМодальности()
	Возврат Объект.РежимИспользованияМодальностиБулево;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// НЕМОДАЛЬНЫЙ ВОПРОС.
//

&НаКлиенте
Функция ПолучитьКодВозвратаДиалога(Ответ)
	Перем ЗначениеОтвета;
	
	Если ТипЗнч(Ответ) = Тип("Структура") Тогда
		ЗначениеОтвета = Ответ.Значение;
	Иначе
		ЗначениеОтвета = Ответ;
	КонецЕсли;
	
	Возврат ЗначениеОтвета;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ВКЛАДКА "ДОПОЛНИТЕЛЬНО". ВОССТАНОВИТЬ БИТЫЙ ОБЪЕКТ.
//

&НаКлиенте
Процедура GUIDНайтиБитыеОбъекты(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	ТекстВопроса = "Поиск объектов типа:
	|<Объект не найден> (84:bf5600145e3710ab11dda4c605dbe824)
	|
	|Процесс поиска может быть длительным.
	|
	|Продолжить поиск ""Битых"" объектов ?";
	
	ЗаголовокВопроса = "ПОИСК ""БИТЫХ"" ССЫЛОК";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		GUIDНайтиБитыеОбъектыЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""GUIDНайтиБитыеОбъектыЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура GUIDНайтиБитыеОбъектыЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите. Производится поиск ""Битых"" объектов.");
	
	GUIDНайтиБитыеОбъектыНаСервере();
	
	Если Объект.БитыеСсылки.Количество() = 0 Тогда
		ПредупреждениеСообщение(, """Бытые"" объекты не найдены.");
	Иначе
		ПредупреждениеСообщение(, "Найдено " + Объект.БитыеСсылки.Количество() + " ""Битых"" объектов");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура GUIDНайтиБитыеОбъектыНаСервере()
	
	тзБитыеСсылки = ОбъектОбработка().GUIDНайтиБитыеОбъектыНаСервере();
	ЗначениеВРеквизитФормы(тзБитыеСсылки, "Объект.БитыеСсылки");
	
КонецПроцедуры

&НаКлиенте
Процедура GUIDБитыйОбъектПерейтиКВосстановлению(Команда)
	
	Если Объект.БитыеСсылки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.БитыеСсылки.ТекущаяСтрока = Неопределено Тогда
		Элементы.БитыеСсылки.ТекущаяСтрока = 0;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.БитыеСсылки.ТекущиеДанные;
	
	ЭтаФорма.БитыйОбъектТекст = ТекущиеДанные.ОбъектНеНайденСсылка;
	ЭтаФорма.БитыйОбъектGUIDСтрока = Неопределено;
	ЭтаФорма.БитыйОбъектТип = Неопределено;
	ЭтаФорма.БитыйОбъектСсылка = Неопределено;
	
КонецПроцедуры

// МО: Дата и Время создания Объекта.
//
&НаСервере
Функция GUIDПолучитьДатуИВремяСозданияОбъектаНаСервере(ОбъектБД)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.GUIDПолучитьДатуИВремяСозданияОбъектаНаСервере(ОбъектБД);
	
КонецФункции

&НаКлиенте
Процедура GUIDБитыйОбъектТекстПриИзменении(Элемент)
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура GUIDБитыйОбъектПолучитьГУИД(Команда)
	
	ЭтаФорма.БитыйОбъектGUID = GUIDБитогоОбъектаНаСервере(ЭтаФорма.БитыйОбъектТекст);
	ЭтаФорма.БитыйОбъектGUIDСтрока = Строка(ЭтаФорма.БитыйОбъектGUID);
	ЭтаФорма.БитыйОбъектДатаИВремя = GUIDПолучитьДатуИВремяСозданияОбъектаНаСервере(ЭтаФорма.БитыйОбъектGUID);
	
	Если ЗначениеЗаполнено(Объект.ИмяОбъектаБД) И Объект.БитыеСсылки.Количество() = 0 Тогда
		ОбъектБДСсылкаСтрокой = Объект.ТипОбъектаБД + "Ссылка." + Объект.ИмяОбъектаБД;
		ЭтаФорма.БитыйОбъектТип = вОписаниеТипа(ОбъектБДСсылкаСтрокой);
	КонецЕсли;
	
КонецПроцедуры

// МО: GUID "Битого" Объекта.
//
&НаСервере
Функция GUIDБитогоОбъектаНаСервере(Знач БитыйGUID)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.GUIDБитогоОбъектаНаСервере(БитыйGUID);
	
КонецФункции

// Получить ссылку "Битого" Объекта по GUID.
//
&НаКлиенте
Процедура GUIDБитыйОбъектВосстановить(Команда)
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.БитыйОбъектТип) Тогда
		ПредупреждениеСообщение(, "Не выбран тип восстанавливаемого Объекта.");
		Возврат;
	КонецЕсли;
	
	ОбъектБитыйВосстановленный = GUIDБитыйОбъектВосстановитьНаСервере(ЭтаФорма.БитыйОбъектТип.Типы()[0], ЭтаФорма.БитыйОбъектGUID, ЭтаФорма.БитыйОбъектДатаИВремя, ЭтаФорма.БитыйОбъектТекст);
	
	ЭтаФорма.ОтобразитьИзменениеДанных(ЭтаФорма.БитыйОбъектСсылка,ВидИзмененияДанных.Изменение);

КонецПроцедуры

// МО: Получить ссылку "Битого" Объекта по GUID.
//
&НаСервере
Функция GUIDБитыйОбъектВосстановитьНаСервере(БитыйОбъектТип, БитыйОбъектGUID, БитыйОбъектДатаИВремя, БитыйОбъектТекст)
	
	ОбъектОбработка = ОбъектОбработка();
	ЭтаФорма.БитыйОбъектСсылка = ОбъектОбработка.GUIDВосстановитьОбъектПоТипуИГУИД(БитыйОбъектТип, БитыйОбъектGUID, БитыйОбъектДатаИВремя, БитыйОбъектТекст);
	Возврат ЭтаФорма.БитыйОбъектСсылка;
	
КонецФункции

&НаКлиенте
Процедура GUIDБитыйОбъектПерейтиКРедактированию(Команда)
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.БитыйОбъектСсылка) Тогда
		ПредупреждениеСообщение(, "Ссылка на Объект не определена.");
		Возврат;
	КонецЕсли;
	
	Объект.ОбъектБД = ЭтаФорма.БитыйОбъектСсылка;
	
	ОбъектБДПриИзменении(Команда);
	
	ЭтаФорма.ТекущийЭлемент = Элементы.СтраницаСвойстваОбъекта;
	
КонецПроцедуры

&НаКлиенте
Процедура GUIDНайтиОбъектСсылка(Команда)
	
	Попытка
		GUID = Новый УникальныйИдентификатор(ЭтаФорма.ОбъектGUIDТекст);
	Исключение
		GUID = Неопределено;
	КонецПопытки;
	
	Если GUID = Неопределено Тогда
		ПредупреждениеСообщение(, "Не удалось преобразовать " + ЭтаФорма.ОбъектGUIDТекст + "
		|в Уникальный идентификатор.");
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.ОбъектGUIDСсылка = GUIDНайтиОбъектНаСервере(GUID);
	
КонецПроцедуры

&НаСервере
Функция GUIDНайтиОбъектНаСервере(GUID)
	
	ОбъектОбработка = ОбъектОбработка();
	Возврат ОбъектОбработка.GUIDПолучитьСсылкуНаОбъектНаСервере(GUID);
	
КонецФункции

&НаКлиенте
Процедура GUIDОчиститьПараметры()
	
	ЭтаФорма.БитыйОбъектGUID		= Неопределено;
	ЭтаФорма.БитыйОбъектGUIDСтрока 	= "";
	ЭтаФорма.БитыйОбъектДатаИВремя 	= Неопределено;
	ЭтаФорма.БитыйОбъектСсылка		= Неопределено;
	ЭтаФорма.БитыйОбъектТекст		= "";
	ЭтаФорма.БитыйОбъектТип			= Неопределено;
	ЭтаФорма.ОбъектGUIDТекст		= "";
	ЭтаФорма.ОбъектGUIDСсылка		= Неопределено;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ДОКУМЕНТЫ: ДОПОЛНИТЕЛЬНЫЕ ОБРАБОТКИ.
//

////////////////////////////////////////////////////////////////////////////////
// ДОКУМЕНТЫ: Статистика документов.
//
	
&НаКлиенте
Процедура ДокументСтатистикаОчиститьПараметры()
	
	ЭтаФорма.ДокументСтатистикаПериод.ДатаНачала = Дата("00010101");
	ЭтаФорма.ДокументСтатистикаПериод.ДатаОкончания = Дата("00010101");
	ЭтаФорма.ДокументСтатистикаОрганизация = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНачальныеПараметрыДокументСтатистика(Команда)
	
	УстановитьЗначениеПараметраНаВкладке("ДокументСтатистикаОрганизация", "Организация", "Организации", Объект.ЕстьСправочникОрганизации);
	
	ЭтаФорма.ДокументСтатистикаПериод.ДатаНачала = НачалоГода(ТекущаяДата());
	ЭтаФорма.ДокументСтатистикаПериод.ДатаОкончания = ТекущаяДата();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтатистикуДокументов(Команда)
	
	ФормаВывестиВСостояние(, , "Подождите. Производится формирование статистики по документам ...");
	ПолучитьСтатистикуДокументовНаСервере();
	
КонецПроцедуры

// МО:
//
&НаСервере
Процедура ПолучитьСтатистикуДокументовНаСервере()
	
	ОбъектОбработка = ОбъектОбработка();
	ТаблицаСтатистика = РеквизитФормыВЗначение("ДокументТСтатистика");
	ТаблицаСтатистика = ОбъектОбработка.ПолучитьСтатистикуДокументовНаСервере(ТаблицаСтатистика, ЭтаФорма.ДокументСтатистикаПериод.ДатаНачала, ЭтаФорма.ДокументСтатистикаПериод.ДатаОкончания, ЭтаФорма.ДокументСтатистикаОрганизация);
	ЗначениеВРеквизитФормы(ТаблицаСтатистика, "ДокументТСтатистика");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ДОКУМЕНТЫ: Непроведенные документы с движениями.
// Используется список видов документов из проведения документов.
//

&НаКлиенте
Процедура ПолучитьНачальныеПараметрыДляОбработкиДвиженийДокументов(Команда)
	
	ДокументДвиженияОчиститьПараметры();
	
	Состояние("Подождите...
	|Производится формирование списка типов документов ...");
	
	ЭтаФорма.ДокументТДвижения.Очистить();
	
	УстановитьЗначениеПараметраНаВкладке("ДокументДвиженияОрганизация", "Организация", "Организации", Объект.ЕстьСправочникОрганизации);
	
	ЭтаФорма.ДокументДвиженияПериод.ДатаНачала = НачалоГода(ТекущаяДата());
	ЭтаФорма.ДокументДвиженияПериод.ДатаОкончания = ТекущаяДата();
	ЭтаФорма.ДокументДвиженияУчитыватьАктивностьЗаписейРегистров = Истина;
	
	ПолучитьНачальныеПараметрыДляОбработкиДвиженийДокументовНаСервере();
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);

КонецПроцедуры

// МО:
//
&НаСервере
Процедура ПолучитьНачальныеПараметрыДляОбработкиДвиженийДокументовНаСервере()
	
	ОбъектОбработка = ОбъектОбработка();
	ТаблицаСписокВидовДокументов = РеквизитФормыВЗначение("ДокументДвиженияТСписокВидов");
	ТаблицаСписокВидовДокументов = ОбъектОбработка.ДокументДвиженияЗаполнитьСписокВидовДокументов(ТаблицаСписокВидовДокументов, ЭтаФорма.ДокументДвиженияОрганизация);
	ЗначениеВРеквизитФормы(ТаблицаСписокВидовДокументов, "ДокументДвиженияТСписокВидов");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНепроведенныеДокументыСДвижениями(Команда)
	
	ДокументДвиженияОчиститьПараметрыПередПоискомДокументов();
	
	ОчиститьСообщения();

	Состояние("Подождите... 
	|Производится поиск непроведенных документов c движениями.");
	
	Элементы.ТДокументДвижения.Заголовок = "Документы [непроведенные с движениями]";
	
	ПолучитьНепроведенныеДокументыСДвижениямиНаСервере();
	
	ПредупреждениеСообщение(, "" + ?(ЭтаФорма.ДокументТДвижения.Количество() = 0, "Отсутствуют непроведенные документы с движениями.", "Количество непроведенных документов с движениями: " + ЭтаФорма.ДокументТДвижения.Количество()));
	
	ЭтаФорма.ДокументДвиженияНайденыНепроведенныеСДвижениями = ЭтаФорма.ДокументТДвижения.Количество() > 0;
	Элементы.кнДокументДвиженияОчиститьДвиженияНепроведенныхСДвижениями.Доступность = ЭтаФорма.ДокументДвиженияНайденыНепроведенныеСДвижениями;
	
КонецПроцедуры

// МО:
//
&НаСервере
Процедура ПолучитьНепроведенныеДокументыСДвижениямиНаСервере()
	
	ОбъектОбработка = ОбъектОбработка();
	ТаблицаДокументыДвижения = РеквизитФормыВЗначение("ДокументТДвижения");
	ТаблицаДокументыДвижения = ОбъектОбработка.ПолучитьНепроведенныеДокументыСДвижениямиНаСервере(ЭтаФорма.ДокументДвиженияТСписокВидов, ТаблицаДокументыДвижения, ЭтаФорма.ДокументДвиженияУчитыватьАктивностьЗаписейРегистров, ЭтаФорма.ДокументДвиженияПериод.ДатаНачала, ЭтаФорма.ДокументДвиженияПериод.ДатаОкончания, ЭтаФорма.ДокументДвиженияОрганизация);
	ЗначениеВРеквизитФормы(ТаблицаДокументыДвижения, "ДокументТДвижения");
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДвиженияНепроведенныхСДвижениями(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	
	Если ЭтаФорма.ДокументТДвижения.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Список непроведенных документов, имеющих движения пуст.
		|Операция прервана.");
		Возврат;
	КонецЕсли;
	
	Если НЕ Элементы.ТДокументДвижения.Заголовок = "Документы [непроведенные с движениями]" Тогда
		ПредупреждениеСообщение(, "Поиск непроведенных документов, имеющих движения не производился.
		|Операция прервана.");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Существуют непроведенные документы, имеющие движения.
	|Для корректности данных в Информационной Базе 
	|необходимо очистить движения таких документов.
	|
	|Очищаются движения документов, проведение которых возможно.
	|
	|Удалить движения непроведенных документов ?";
	
	ЗаголовокВопроса = "УДАЛЕНИЕ ДВИЖЕНИЙ НЕПРОВЕДЕННЫХ ДОКУМЕНТОВ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		УдалитьДвиженияНепроведенныхСДвижениямиЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""УдалитьДвиженияНепроведенныхСДвижениямиЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДвиженияНепроведенныхСДвижениямиЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Состояние("Подождите...
	|Производится удаление движений непроведенных документов.");
	
	КоличествоОчищенных = УдалитьДвиженияНепроведенныхСДвижениямиНаСервере();
	
	ПредупреждениеСообщение(, "Удалены движения документов: " + КоличествоОчищенных + " из " + ЭтаФорма.ДокументТДвижения.Количество() + "
	|
	|Рекомендуется произвести повторную проверку ""Найти непроведенные с движениями"" 
	|для проверки произведенных действий.");
	
КонецПроцедуры

&НаСервере
Функция УдалитьДвиженияНепроведенныхСДвижениямиНаСервере()
	
	ОбъектОбработка = ОбъектОбработка();
	ТаблицаДокументыДвижения = РеквизитФормыВЗначение("ДокументТДвижения");
	КоличествоОчищенных = ОбъектОбработка.УдалитьДвиженияНепроведенныхСДвижениямиНаСервере(ТаблицаДокументыДвижения);
	Возврат КоличествоОчищенных;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьПроведенныеДокументыБезДвижений(Команда)
	
	ДокументДвиженияОчиститьПараметрыПередПоискомДокументов();
	
	ОчиститьСообщения();
	
	Состояние("Подождите...
	|Производится поиск проведенных документов без движений.");
	
	Элементы.ТДокументДвижения.Заголовок = "Документы [проведенные без движений]";
	
	ПолучитьПроведенныеДокументыБезДвиженийНаСервере();
	
	ПредупреждениеСообщение(, "" + ?(ЭтаФорма.ДокументТДвижения.Количество() = 0, "Отсутствуют проведенные документы без движений.", "Количество проведенных документов без движений: " + ЭтаФорма.ДокументТДвижения.Количество()));
	
	ЭтаФорма.ДокументДвиженияНайденыПроведенныеБезДвижений = ЭтаФорма.ДокументТДвижения.Количество() > 0;
	Элементы.кнДокументДвиженияПровестиПроведенныеБезДвижений.Доступность = ЭтаФорма.ДокументДвиженияНайденыПроведенныеБезДвижений;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПроведенныеДокументыБезДвиженийНаСервере()
	
	ОбъектОбработка = ОбъектОбработка();
	ТаблицаДокументыДвижения = РеквизитФормыВЗначение("ДокументТДвижения");
	ТаблицаДокументыДвижения = ОбъектОбработка.ПолучитьПроведенныеДокументыБезДвиженийНаСервере(ЭтаФорма.ДокументДвиженияТСписокВидов, ТаблицаДокументыДвижения, ЭтаФорма.ДокументДвиженияУчитыватьАктивностьЗаписейРегистров, ЭтаФорма.ДокументДвиженияПериод.ДатаНачала, ЭтаФорма.ДокументДвиженияПериод.ДатаОкончания, ЭтаФорма.ДокументДвиженияОрганизация);
	ЗначениеВРеквизитФормы(ТаблицаДокументыДвижения, "ДокументТДвижения");
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиПроведенныеБезДвижений(Команда)
	Перем ТекстВопроса, Ответ, Оповещение, Отказ;
	
	Если ЭтаФорма.ДокументТДвижения.Количество() = 0 Тогда
		ПредупреждениеСообщение(, "Список проведенных документов, не имеющих движения пуст.
		|Операция прервана.");
		Возврат;
	КонецЕсли;
	
	Если НЕ Элементы.ТДокументДвижения.Заголовок = "Документы [проведенные без движений]" Тогда
		ПредупреждениеСообщение(, "Поиск проведенных документов, не имеющих движения не производился.
		|Операция прервана.");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Существуют проведенные документы, не имеющие движения.
	|Для корректности данных в Информационной Базе
	|необходимо перепровести данные документы.
	|
	|Проводятся документов, проведение которых возможно.
	|
	|Провести документы без движений ?";
	
	Отказ = Неопределено;
	
	ЗаголовокВопроса = "ПРОВЕДЕННЫЕ ДОКУМЕНТЫ БЕЗ ДВИЖЕНИЙ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		Отказ = Ложь;
		ПровестиПроведенныеБезДвиженийЗавершение(Ответ, Отказ);
	Иначе
		Если Отказ = Неопределено Тогда
			Отказ = Ложь;
			// Стандартно в немодальном режиме (8.3) с обработкой результата.
			//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
			Оповещение = Вычислить("Новый ОписаниеОповещения(""ПровестиПроведенныеБезДвиженийЗавершение"", ЭтаФормаЭтотОбъект(), Отказ)");
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиПроведенныеБезДвиженийЗавершение(Ответ, Отказ) Экспорт
	Перем ЗначениеОтвета, ДопСообщение;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Состояние("Подождите...
	|Производится проведение документов без движений.");
	
	КоличествоПроведенных = ПровестиПроведенныеБезДвиженийНаСервере(Отказ);
	
	ДопСообщение = "";
	Если Отказ И Объект.ВыполнятьВТранзакции Тогда
		ДопСообщение = "ВНИМАНИЕ! Транзакция отменена. Состояние документов - прежнее.";
	КонецЕсли;
	
	ПредупреждениеСообщение(, "Проведено документов: " + КоличествоПроведенных + " из " + ЭтаФорма.ДокументТДвижения.Количество() + "
	|" + ДопСообщение + "
	|Рекомендуется произвести повторную проверку ""Найти проведенные без движений"" 
	|для проверки произведенных действий.");
	
КонецПроцедуры

&НаСервере
Функция ПровестиПроведенныеБезДвиженийНаСервере(Отказ)
	
	ОбъектОбработка = ОбъектОбработка();
	ТаблицаДокументыДвижения = РеквизитФормыВЗначение("ДокументТДвижения");
	КоличествоПроведенных = ОбъектОбработка.ПровестиПроведенныеБезДвиженийНаСервере(ТаблицаДокументыДвижения, Отказ);
	Возврат КоличествоПроведенных;
	
КонецФункции

&НаКлиенте
Процедура ДокументДвиженияУстановитьФлажки(Команда)
	ДокументДвиженияУстановитьСнятьФлажки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДокументДвиженияСнятьФлажки(Команда)
	ДокументДвиженияУстановитьСнятьФлажки(Ложь);
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Кнопка Инвентировать флажки в списке видов документов.
//
&НаКлиенте
Процедура ДокументДвиженияУстановитьФлажкиПроводимых(Команда)
	
	Для Каждого ВидДокумента ИЗ ЭтаФорма.ДокументДвиженияТСписокВидов Цикл
		Если ВидДокумента.ПроведениеРазрешено Тогда		// ..., например, КОРРЕКТИРОВКА РЕГИСТРОВ не проводится, но имеет движения! Нецелесобразно.
			Если ВидДокумента.Имя = "ЧекККМ" ИЛИ ВидДокумента.Значение = "ЧекККМВозврат" Тогда		// Чеки ККМ: По-умолчанию не рассматриваем.
				ВидДокумента.Пометка = Ложь;
			Иначе	
				ВидДокумента.Пометка = Истина;
			КонецЕсли;
		Иначе	
			ВидДокумента.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Вкладка "Документ (Дополнительно)". Кнопка Установить/Снять флажки в списке видов документов.
//
&НаКлиенте
Процедура ДокументДвиженияУстановитьСнятьФлажки(Флажок)
	
	Для Каждого ВидДокумента ИЗ ЭтаФорма.ДокументДвиженияТСписокВидов Цикл
		ВидДокумента.Пометка = Флажок;
		Если ВидДокумента.Имя = "ЧекККМ" ИЛИ ВидДокумента.Значение = "ЧекККМВозврат" Тогда		// Чеки ККМ: По-умолчанию не рассматриваем.
			ВидДокумента.Пометка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Не используется.
&НаСервере
Функция ПолучитьПризнакПроведениеРазрешеноДляДокументаНаСервере(ИмяДокумента)
	Возврат Метаданные.НайтиПоПолномуИмени("Документ." + ИмяДокумента).Проведение = МетаДанные.СвойстваОбъектов.Проведение.Разрешить;
КонецФункции

&НаКлиенте
Процедура ДокументДвиженияОчиститьПараметрыПередПоискомДокументов()
	
	ЭтаФорма.ДокументДвиженияНайденыПроведенныеБезДвижений = Ложь;
	ЭтаФорма.ДокументДвиженияНайденыНепроведенныеСДвижениями = Ложь;
	Элементы.кнДокументДвиженияПровестиПроведенныеБезДвижений.Доступность = Ложь;
	Элементы.кнДокументДвиженияОчиститьДвиженияНепроведенныхСДвижениями.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументДвиженияОчиститьПараметры()
	
	Элементы.ТДокументДвижения.Заголовок = "Список документов";
	ЭтаФорма.ДокументДвиженияОрганизация = Неопределено;
	ЭтаФорма.ДокументДвиженияПериод.ДатаНачала = Дата("00010101");
	ЭтаФорма.ДокументДвиженияПериод.ДатаОкончания = Дата("00010101");
	ЭтаФорма.ДокументДвиженияУчитыватьАктивностьЗаписейРегистров = Истина;
	ЭтаФорма.ДокументДвиженияНайденыПроведенныеБезДвижений = Ложь;
	ЭтаФорма.ДокументДвиженияНайденыНепроведенныеСДвижениями = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ДОКУМЕНТЫ: Удаление документов.
// Используется список видов документов.
//

&НаКлиенте
Процедура ПолучитьНачальныеПараметрыДляУдаленияДокументов(Команда)
	
	ДокументУдалениеОчиститьПараметры();
	
	Состояние("Подождите...
	|Производится формирование списка типов документов ...");
	
	ЭтаФорма.ДокументТУдаление.Очистить();
	
	УстановитьЗначениеПараметраНаВкладке("ДокументУдалениеОрганизации"	, "Организация"		, "Организации"		, Объект.ЕстьСправочникОрганизации);
	УстановитьЗначениеПараметраНаВкладке("ДокументУдалениеСклады"		, "Склад"			, "Склады"			, Объект.ЕстьСправочникСклады);
	УстановитьЗначениеПараметраНаВкладке("ДокументУдалениеМагазины"		, "Магазин"			, "Магазины"		, Объект.ЕстьСправочникМагазины);
	УстановитьЗначениеПараметраНаВкладке("ДокументУдалениеКассы"		, "Касса"			, "Кассы"			, Объект.ЕстьСправочникКассы);
	УстановитьЗначениеПараметраНаВкладке("ДокументУдалениеКассыККМ"		, "КассаККМ"		, "КассыККМ"		, Объект.ЕстьСправочникКассыККМ);
	УстановитьЗначениеПараметраНаВкладке("ДокументУдалениеПодразделения", "Подразделение"	, "Подразделения"	, Объект.ЕстьСправочникПодразделения);
	УстановитьЗначениеПараметраНаВкладке("ДокументУдалениеПодразделенияОрганизаций", "ПодразделениеОрганизации", "ПодразделенияОрганизаций", Объект.ЕстьСправочникПодразделенияОрганизаций);

	ЭтаФорма.ДокументУдалениеПериод.ДатаНачала = НачалоГода(ТекущаяДата());
	ЭтаФорма.ДокументУдалениеПериод.ДатаОкончания = ТекущаяДата();
	ЭтаФорма.ДокументУдалениеВремяОжидания = 1;
	
	УстановитьЗначениеПараметраГруппаНоменклатура("ДокументУдалениеГруппаНоменклатуры");
	Элементы.ДокументУдалениеПоискПоНоменклатуре.ТолькоПросмотр = НЕ Объект.ЕстьСправочникНоменклатура;
	Элементы.ДокументУдалениеГруппаНоменклатуры.ТолькоПросмотр  = Истина;
	ЭтаФорма.ДокументУдалениеПометкаУдаление = "ПометкаНаУдаление";
	
	ПолучитьНачальныеПараметрыДляУдалениеДокументовНаСервере();
	
	ФормаОпределитьДоступнность(Объект.ТипОбъектаБД);
	
КонецПроцедуры

// МО:
//
&НаСервере
Процедура ПолучитьНачальныеПараметрыДляУдалениеДокументовНаСервере()
	
	ТаблицаСписокВидовДокументов = РеквизитФормыВЗначение("ДокументУдалениеТСписокВидов");
	ТаблицаСписокВидовДокументов = ОбъектОбработка().ДокументУдалениеЗаполнитьСписокВидовДокументов(ТаблицаСписокВидовДокументов, ЭтаФорма.ДокументУдалениеОрганизации);
	ЗначениеВРеквизитФормы(ТаблицаСписокВидовДокументов, "ДокументУдалениеТСписокВидов");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеПараметраГруппаНоменклатура(ИмяРеквизитаФормы)
	
	Если Объект.ЕстьСправочникНоменклатура Тогда
		Элементы[ИмяРеквизитаФормы].ТолькоПросмотр = Ложь;
		ОграничитьТипЭлемента(ИмяРеквизитаФормы, "СправочникСсылка.Номенклатура");
	Иначе
		Элементы[ИмяРеквизитаФормы].ТолькоПросмотр = Истина;
		ЭтаФорма[ИмяРеквизитаФормы] = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУдалениеПоискПоНоменклатуреПриИзменении(Элемент)
	
	Элементы.ДокументУдалениеГруппаНоменклатуры.ТолькоПросмотр  = НЕ Этаформа.ДокументУдалениеПоискПоНоменклатуре;
	Элементы.ДокументУдалениеУдалитьНоменклатуру.ТолькоПросмотр	= НЕ Этаформа.ДокументУдалениеПоискПоНоменклатуре;
	Если НЕ Этаформа.ДокументУдалениеПоискПоНоменклатуре Тогда
		ЭтаФорма.ДокументУдалениеГруппаНоменклатуры = Неопределено;
		ЭтаФорма.ДокументУдалениеУдалитьНоменклатуру= Ложь;
	Иначе
		ПредупреждениеСообщение(, "Отбор по группе номенклатуры существенно замедляет 
		|формирование списка документов для последующего удаления.");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУдалениеПолучитьСписокУдаляемых(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	Перем КоличествоДокументов;
	
	Если ЭтаФорма.ДокументУдалениеПоискПоНоменклатуре И НЕ ЗначениеЗаполнено(ЭтаФорма.ДокументУдалениеГруппаНоменклатуры) Тогда
		ПредупреждениеСообщение(, "Не заполнен реквизит ""Группа номенклатуры"".");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "
	|Параметры основного отбора:
	|- Период (обязательный).
	|- Организация (обязательный, если существует в конфигурации).
	|- Склад (опция, если существует в конфигурации).
	|- Магазин (опция, если существует в конфигурации).
	|- Касса (опция, если существует в конфигурации).
	|- КассаККМ (опция, если существует в конфигурации).
	|- Подчиненные (для документов, которые могут выступать основанием для других документов).
	|
	|Дополнительно:
	|- Номенклатура (опция, если существует в конфигурации).
	|  При отборе по номенклатуре:
	|  * Игнорируется содержание таблицы типов документов.
	|  * Учитываются параметры: Период, Организация, Склад, Магазин, Касса, КассаККМ, Подразделение, ПодразделениеОрганизации.
	|Отбор по номенклатуре рекомендуется произвести после удаления по основному отбору.
	|Отбор по номенклатуре - длительная операция.
	|
	|Исключаются документы, помеченные на удаление.
	|
	|Произвести отбор документов?
	|";
	
	ЗаголовокВопроса = "ОТБОР ДОКУМЕНТОВ ДЛЯ УДАЛЕНИЯ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ДокументУдалениеПолучитьСписокУдаляемыхЗавершение(Ответ, Неопределено);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ДокументУдалениеПолучитьСписокУдаляемыхЗавершение"", ЭтаФормаЭтотОбъект(), Неопределено)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУдалениеПолучитьСписокУдаляемыхЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ВремяНачала = ВремяВМиллисекундах();
	
	ОчиститьСообщения();
	
	Состояние("Подождите. Производится формирование списка документов
	|для последующего удаления.");
	
	ДокументУдалениеПолучитьСписокУдаляемыхДокументовНаСервере();
	
	КоличествоДокументовИмеющихРеквизитОрганизация 				= ЭтаФорма.ДокументТУдаление.НайтиСтроки(Новый Структура("ЕстьРеквизитОрганизация"				, Истина)).Количество();
	КоличествоДокументовИмеющихРеквизитСклад					= ЭтаФорма.ДокументТУдаление.НайтиСтроки(Новый Структура("ЕстьРеквизитСклад"					, Истина)).Количество();
	КоличествоДокументовИмеющихРеквизитМагазин 					= ЭтаФорма.ДокументТУдаление.НайтиСтроки(Новый Структура("ЕстьРеквизитМагазин"					, Истина)).Количество();
	КоличествоДокументовИмеющихРеквизитКасса 					= ЭтаФорма.ДокументТУдаление.НайтиСтроки(Новый Структура("ЕстьРеквизитКасса"					, Истина)).Количество();
	КоличествоДокументовИмеющихРеквизитКассаККМ 				= ЭтаФорма.ДокументТУдаление.НайтиСтроки(Новый Структура("ЕстьРеквизитКассаККМ"					, Истина)).Количество();
	КоличествоДокументовИмеющихРеквизитПодразделение 			= ЭтаФорма.ДокументТУдаление.НайтиСтроки(Новый Структура("ЕстьРеквизитПодразделение"			, Истина)).Количество();
	КоличествоДокументовИмеющихРеквизитПодразделениеОрганизации = ЭтаФорма.ДокументТУдаление.НайтиСтроки(Новый Структура("ЕстьРеквизитПодразделениеОрганизации"	, Истина)).Количество();
	
	ВремяОкончания = ВремяВМиллисекундах();
	ЗатраченоВремени = "" + (ВремяОкончания - ВремяНачала)/1000 + " с";
	
	ПредупреждениеСообщение(, "Отобрано " + ЭтаФорма.ДокументТУдаление.Количество() + " документов,
	|из них:
	|" + КоличествоДокументовИмеющихРеквизитОрганизация + ", содержащих реквизит ""Организация"".
	|" + КоличествоДокументовИмеющихРеквизитСклад + ", содержащих реквизит ""Склад"".
	|" + КоличествоДокументовИмеющихРеквизитМагазин + ", содержащих реквизит ""Магазин"".
	|" + КоличествоДокументовИмеющихРеквизитКасса + ", содержащих реквизит ""Касса"".
	|" + КоличествоДокументовИмеющихРеквизитКассаККМ + ", содержащих реквизит ""КассаККМ"".
	|" + КоличествоДокументовИмеющихРеквизитПодразделение + ", содержащих реквизит ""Подразделение"".
	|" + КоличествоДокументовИмеющихРеквизитПодразделениеОрганизации + ", содержащих реквизит ""ПодразделениеОрганизации"".
	| 
	|Затрачено: " + ЗатраченоВремени);
	
КонецПроцедуры

&НаСервере
Процедура ДокументУдалениеПолучитьСписокУдаляемыхДокументовНаСервере()
	
	ТаблицаДокументыУдаление = РеквизитФормыВЗначение("ДокументТУдаление");
	ТаблицаДокументыУдаление = ОбъектОбработка().ДокументУдалениеПолучитьСписокУдаляемыхДокументовНаСервере(ЭтаФорма.ДокументУдалениеТСписокВидов, ТаблицаДокументыУдаление, ЭтаФорма.ДокументУдалениеПериод.ДатаНачала, ЭтаФорма.ДокументУдалениеПериод.ДатаОкончания, ЭтаФорма.ДокументУдалениеОрганизации, ЭтаФорма.ДокументУдалениеСклады, ЭтаФорма.ДокументУдалениеМагазины, ЭтаФорма.ДокументУдалениеКассы, ЭтаФорма.ДокументУдалениеКассыККМ, ЭтаФорма.ДокументУдалениеПодразделения, ЭтаФорма.ДокументУдалениеПодразделенияОрганизаций, ЭтаФорма.ДокументУдалениеПоискПоСвязанным, ЭтаФорма.ДокументУдалениеПоискПоНоменклатуре, ЭтаФорма.ДокументУдалениеГруппаНоменклатуры);
	ЗначениеВРеквизитФормы(ТаблицаДокументыУдаление, "ДокументТУдаление");
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУдалениеПроизвестиУдалениеДокументов(Команда)
	Перем ТекстВопроса, Ответ, Оповещение;
	Перем КоличествоУдаленныхДокументов;
	Перем Текст2, Текст3;
	
	Если ЭтаФорма.ДокументУдалениеПометкаУдаление = "ПометкаНаУдаление" Тогда
		ТекстВопроса = "
		|Пометка на удаление документов, отмеченных флажком ""П"" (Пометка) в таблице документов.
		|
		|Фактическое удаление производится штатной процедурой ""Удаление помеченных объектов"".
		|
		|Произвести удаление документов?
		|";
		Текст2 = "Пометка на удаление";
		Текст3 = "Помечено на удаление";
	Иначе
		ТекстВопроса = "
		|Удаление документов, отмеченных флажком ""П"" (Пометка) в таблице документов.
		|(без возможности восстановления)
		|
		|Произвести удаление документов?
		|";
		Текст2 = "Удаление";
		Текст3 = "Удалено";
	КонецЕсли;
	
	Параметр = Новый Структура;
	Параметр.Вставить("Текст2", Текст2);
	Параметр.Вставить("Текст3", Текст3);
	
	ЗаголовокВопроса = "УДАЛЕНИЕ ДОКУМЕНТОВ";
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
		ДокументУдалениеПроизвестиУдалениеДокументовЗавершение(Ответ, Параметр);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ДокументУдалениеПроизвестиУдалениеДокументовЗавершение"", ЭтаФормаЭтотОбъект(), Параметр)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, ЗаголовокВопроса, КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУдалениеПроизвестиУдалениеДокументовЗавершение(Ответ, Параметр) Экспорт
	Перем ЗначениеОтвета;
	
	ЗначениеОтвета = ПолучитьКодВозвратаДиалога(Ответ);
	Если ЗначениеОтвета = Неопределено ИЛИ ЗначениеОтвета = КодВозвратаДиалога.НЕТ ИЛИ ЗначениеОтвета = КодВозвратаДиалога.Таймаут Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Состояние("Подождите. Производится " + Параметр.Текст2 + " документов.");
	
	КоличествоУдаленныхДокументов = 0;
	ДокументУдалениеПроизвестиУдалениеДокументовНаСервере(КоличествоУдаленныхДокументов);
	
	Если КоличествоУдаленныхДокументов > 0 Тогда
		ПредупреждениеСообщение(, "" + Параметр.Текст3 + ": " + КоличествоУдаленныхДокументов + " документов.");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДокументУдалениеПроизвестиУдалениеДокументовНаСервере(КоличествоУдаленныхДокументов)
	
	ТаблицаДокументыУдаление = РеквизитФормыВЗначение("ДокументТУдаление");
	ТаблицаДокументыУдаление = ОбъектОбработка().ДокументУдалениеПроизвестиУдалениеДокументовНаСервере(ЭтаФорма.ДокументУдалениеПометкаУдаление, ТаблицаДокументыУдаление, ЭтаФорма.ДокументУдалениеВремяОжидания, КоличествоУдаленныхДокументов, ЭтаФорма.ДокументУдалениеУдалитьНоменклатуру, ЭтаФорма.ДокументУдалениеГруппаНоменклатуры);
	ЗначениеВРеквизитФормы(ТаблицаДокументыУдаление, "ДокументТУдаление");
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУдалениеПолеЯвляетсяОснованиемДляОткрытие(Элемент, СтандартнаяОбработка)
	Перем МногоСтрочныйТекст;
	
	Если ТипЗнч(Элемент.ТекстРедактирования) = Тип("Строка") Тогда
		МногоСтрочныйТекст = СтрЗаменить(Элемент.ТекстРедактирования, ",", Символы.ПС);
	Иначе
		МногоСтрочныйТекст = СтрЗаменить(Элемент.ТекстРедактирования, ";", Символы.ПС);
	КонецЕсли;
	ПросмотретьМногострочныйТекст_Стандартно(МногоСтрочныйТекст, Элемент.Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументУдалениеОчиститьПараметры()
	
	ЭтаФорма.ДокументУдалениеПериод.ДатаНачала = Дата("00010101");
	ЭтаФорма.ДокументУдалениеПериод.ДатаОкончания = Дата("00010101");
	
	ЭтаФорма.ДокументУдалениеОрганизации			= Новый СписокЗначений;
	ЭтаФорма.ДокументУдалениеСклады 				= Новый СписокЗначений;
	ЭтаФорма.ДокументУдалениеМагазины 				= Новый СписокЗначений;
	ЭтаФорма.ДокументУдалениеКассы 					= Новый СписокЗначений;
	ЭтаФорма.ДокументУдалениеКассыККМ				= Новый СписокЗначений;
	ЭтаФорма.ДокументУдалениеПодразделения			= Новый СписокЗначений;
	ЭтаФорма.ДокументУдалениеПодразделенияОрганизаций= Новый СписокЗначений;
	
	ЭтаФорма.ДокументУдалениеПометкаУдаление 		= Неопределено;
	
	Этаформа.ДокументУдалениеПоискПоНоменклатуре 	= Ложь;
	ЭтаФорма.ДокументУдалениеГруппаНоменклатуры 	= Неопределено;
	
	Элементы.ДокументУдалениеПоискПоНоменклатуре.ТолькоПросмотр = Истина;
	Элементы.ДокументУдалениеГруппаНоменклатуры.ТолькоПросмотр  = Истина;
	
	ЭтаФорма.ДокументУдалениеВремяОжидания = 0;
	
	ЭтаФорма.ДокументУдалениеТСписокВидов.Очистить();
	ЭтаФорма.ДокументТУдаление.Очистить();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СОЗДАТЬ ДОКУМЕНТ НА ОСНОВАНИИ ДРУГОГО ДОКУМЕНТА.
//

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	Перем ЕстьДанные;
	
	ЕстьДанные = ДокументОснованиеПриИзмененииНаСервере(ЭтаФорма.ДокументОснование);
	Если ЭтаФорма.ДокументОснованиеТабличныеЧасти.Количество()> 0 И НЕ ЕстьДанные Тогда
		ПредупреждениеСообщение(, "Отсутствуют данные табличных частей для нового документа.");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДокументОснованиеПриИзмененииНаСервере(ДокументОснование)
	Перем мдНД, мдДО, ТЧастьНД, НоваяСтрокаТЗ, ЕстьДанные;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		ЭтаФорма.ДокументОснованиеТабличныеЧасти.Очистить();
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьДанные = Ложь;
	мдНД = Объект.ОбъектБД.Метаданные();
	мдДО = ЭтаФорма.ДокументОснование.Метаданные();
	ЭтаФорма.ДокументОснованиеТабличныеЧасти.Очистить();
	Для Каждого ТЧастьНД ИЗ мдНД.ТабличныеЧасти Цикл
		НоваяСтрокаТЗ = ЭтаФорма.ДокументОснованиеТабличныеЧасти.Добавить();
		НоваяСтрокаТЗ.нПП = ЭтаФорма.ДокументОснованиеТабличныеЧасти.Количество();
		НоваяСтрокаТЗ.Приемник = ТЧастьНД.Имя;
		Если НЕ мдДО.ТабличныеЧасти.Найти(ТЧастьНД.Имя) = Неопределено Тогда
			НоваяСтрокаТЗ.Основание = ТЧастьНД.Имя;
			НоваяСтрокаТЗ.Копировать = ЭтаФорма.ДокументОснование[ТЧастьНД.Имя].Количество() > 0;
			ЕстьДанные = ?(НоваяСтрокаТЗ.Копировать, НоваяСтрокаТЗ.Копировать, ЕстьДанные);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьДанные;

КонецФункции

&НаКлиенте
Процедура ДокументОснованиеТабличныеЧастиКопироватьПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Элемент.Родитель.ТекущиеДанные.Основание) Тогда
		ПредупреждениеСообщение(, "Не сопоставлена табличная часть документа-основания.");
		Элемент.Родитель.ТекущиеДанные.Копировать = Ложь;
	Иначе
		Если ЭтаФорма.ДокументОснование[Элемент.Родитель.ТекущиеДанные.Основание].Количество() = 0 Тогда
			ПредупреждениеСообщение(, "Табличная часть документа-основания не содержит данных.");
			Элемент.Родитель.ТекущиеДанные.Копировать = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументСоздатьДокументПоДокументуОснованию(Команда)
	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.ДокументОснование) Тогда
		ПредупреждениеСообщение(, "Документ-основание не выбран.");
		Возврат;
	КонецЕсли;
	
	Объект.ОбъектБД = ДокументСоздатьДокументПоДокументуОснованиюНаСервере(ЭтаФорма.ДокументОснование);
	
	ОбъектБДПриИзменении(Команда);
	
	ЭтаФорма.ТекущийЭлемент = Элементы.СтраницаСвойстваОбъекта;
	
КонецПроцедуры

&НаСервере
Функция ДокументСоздатьДокументПоДокументуОснованиюНаСервере(ДокументОснование)
	Перем НовыйДокумент, ТЧастьНД, ТЧастьЗаполнена;
	
	НовыйДокумент = Документы[Объект.ИмяОбъектаБД].СоздатьДокумент();
	НовыйДокумент.Заполнить(ДокументОснование);
	НовыйДокумент.Дата = ТекущаяДата();
	НовыйДокумент.Записать();
	
	ТЧастьЗаполнена = Ложь;
	Для Каждого ТЧастьНД ИЗ ЭтаФорма.ДокументОснованиеТабличныеЧасти Цикл
		Если ТЧастьНД.Копировать Тогда
			Попытка
				НовыйДокумент[ТЧастьНД.Приемник].Загрузить(ДокументОснование[ТЧастьНД.Основание].Выгрузить());
				ТЧастьЗаполнена = Истина;
			Исключение
				Сообщить("Ошибка загрузки в т.часть " + ТЧастьНД.Имя + Символы.ПС + ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	Если ТЧастьЗаполнена Тогда
		НовыйДокумент.Записать();
	КонецЕсли;
	
	Возврат НовыйДокумент.Ссылка;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПАРСЕР ЗАПРОСА.
//

&НаКлиенте
Процедура ОМПарсерЗапроса(Команда)
	//
	//ИмяФормыПарсераЗапроса = Объект.Настройки.ИмяФормыПарсераЗапроса;
	//
	//Если ИмяФормыПарсераЗапроса = Неопределено Тогда
	//	ПредупреждениеСообщение(, "Форма ""ИмяФормыПарсераЗапроса"" не найдена.");
	//	Возврат;
	//КонецЕсли;
	//
	//СтруктураПараметров = Новый Структура;
	//СтруктураПараметров.Вставить("Настройки", Объект.Настройки);
	//				
	//ФормаПарсерЗапроса = ПолучитьФорму(ИмяФормыПарсераЗапроса, СтруктураПараметров);
	//ФормаПарсерЗапроса.ВладелецФормы = ЭтаФорма;
	//ФормаПарсерЗапроса.ЗакрыватьПриЗакрытииВладельца = Истина;
	//	
	//ФормаПарсерЗапроса.Открыть();
	//
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// YURAOS.
//

//+yuraos, 10.09.2015

// функции внеконтекстных серверных вызововов методов обработки
// http://infostart.ru/public/241811/
&НаСервереБезКонтекста 
Функция ОбъектТипа_ЗначениеСсылочногоТипа(ОбъектТип,ЗначениеСсылка,ПоОбъектуБД)
	
	// Создаем "облегченный" (без данных) объект обработки.
	This = Новый(ЗначениеИзСтрокиВнутр(ОбъектТип));
	This.УправляемаяФорма = Истина;
	// Вызываем нужный "статический" (не зависящий от данных объекта) метод.
	Если ПоОбъектуБД = Истина Тогда
		Возврат This.Метаданные().Реквизиты.ОбъектБД.Тип.СодержитТип(ТипЗнч(ЗначениеСсылка)); 
	Иначе
		Возврат This.ЗначениеСсылочногоТипа(ЗначениеСсылка);
	КонецЕсли; 
	
КонецФункции

&НаСервереБезКонтекста 
Функция ОбъектТипа_ФормаПолучитьИмяРеквизитаТабличнаяЧасть(ОбъектТип,ТЧИмя)
	
	// Создаем "облегченный" (без данных) объект обработки.
	This = Новый(ЗначениеИзСтрокиВнутр(ОбъектТип));
	This.УправляемаяФорма = Истина;
	// Вызываем нужный "статический" (не зависящий от данных объекта) метод.
	Возврат This.ФормаПолучитьИмяРеквизитаТабличнаяЧасть(ТЧИмя);
	
КонецФункции

&НаСервереБезКонтекста 
Функция ОбъектТипа_ФормаПолучитьИмяРеквизитаРегистр(ОбъектТип,ТЧИмя)
	
	// Создаем "облегченный" (без данных) объект обработки.
	This = Новый(ЗначениеИзСтрокиВнутр(ОбъектТип));
	This.УправляемаяФорма = Истина;
	// Вызываем нужный "статический" (не зависящий от данных объекта) метод.
	Возврат This.ФормаПолучитьИмяРеквизитаРегистр(ТЧИмя);
	
КонецФункции

&НаСервереБезКонтекста 
Процедура ОбъектТипа_СписокПустыхСсылокВставить(ОбъектТип,сзСписок,ЗначениеСсылка,МаксДлина=10)
	
	// Создаем "облегченный" (без данных) объект обработки.
	This = Новый(ЗначениеИзСтрокиВнутр(ОбъектТип));
	This.УправляемаяФорма = Истина;
	// Вызываем нужный "статический" (не зависящий от данных объекта) метод.
	This.СписокПустыхСсылокВставить(сзСписок,ЗначениеСсылка,МаксДлина);
	
КонецПроцедуры

&НаСервереБезКонтекста 
Процедура ОбъектТипа_СписокПустыхСсылокУстановитьКартинки(ОбъектТип,сзСписок)
	
	// Создаем "облегченный" (без данных) объект обработки.
	This = Новый(ЗначениеИзСтрокиВнутр(ОбъектТип));
	This.УправляемаяФорма = Истина;
	// Вызываем нужный "статический" (не зависящий от данных объекта) метод.
	This.СписокПустыхСсылокУстановитьКартинки(сзСписок);
	
КонецПроцедуры

&НаКлиенте
Процедура ВызватьВыборТипаОбъектаБД()
	Перем Элемент;
	
	Элемент = Элементы.ОбъектБД;
		
	СтруктураПараметров = Новый Структура;
	ПолноеСтроковоеИмяТипа = ОбъектБДПолучитьПолноеСтроковоеИмяТипа(Объект.ОбъектБД);
	СтруктураПараметров.Вставить("ТипОбъектаСтрокой", ПолноеСтроковоеИмяТипа);
	
	ФильтрПоСсылочнымМетаданным = Новый СписокЗначений;
	ФильтрПоСсылочнымМетаданным.Добавить("Справочники");
	ФильтрПоСсылочнымМетаданным.Добавить("Документы");
	ФильтрПоСсылочнымМетаданным.Добавить("ПланыОбмена");
	ФильтрПоСсылочнымМетаданным.Добавить("ПланыВидовХарактеристик");
	ФильтрПоСсылочнымМетаданным.Добавить("БизнесПроцессы");
	ФильтрПоСсылочнымМетаданным.Добавить("Задачи");
	ФильтрПоСсылочнымМетаданным.Добавить("ПланыСчетов");
	ФильтрПоСсылочнымМетаданным.Добавить("ПланыВидовРасчета");
	СтруктураПараметров.Вставить("КоллекцииВыбираемыхОбъектовМетаданных", ФильтрПоСсылочнымМетаданным);
	
	ФормаВыбораОбъектаМетаДанных = ПолучитьФорму(Объект.Настройки.ИмяФормыВыборОбъектаМетаданных,
	СтруктураПараметров,
	Элемент, ЭтаФорма); // - Используем старый добрый механизм обработки выбора значения.
	
	ФормаВыбораОбъектаМетаДанных.ЗакрыватьПриЗакрытииВладельца = Истина;
	ФормаВыбораОбъектаМетаДанных.ЗакрыватьПриВыборе = Истина;
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		ВыбранныйОбъектМетаДанных = ФормаВыбораОбъектаМетаДанных.ОткрытьМодально();
		//+yuraos, 10.09.2015
		Если ВыбранныйОбъектМетаДанных <> Неопределено Тогда
			ВызватьВыборОбъектаБД(ВыбранныйОбъектМетаДанных, Элемент);
		КонецЕсли;
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		// - через механизм обработки выбора значения.
		ФормаВыбораОбъектаМетаДанных.Открыть();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(ВыбранноеЗначение) И ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		// "Приехало" полное имя объекта метаданных из "формы выбора типа".
		ВызватьВыборОбъектаБД(ВыбранноеЗначение, Элемент);
	Иначе
		// "Приехала" ссылка, выбранная в форме списка выбора ОбъектаБД или в выпадающем списке.
		Если Объект.ОбъектБД <> Неопределено И ТипЗнч(ВыбранноеЗначение) <> ТипЗнч(Объект.ОбъектБД) Тогда
			ОбъектБДОчистка(Элемент, Истина);
		КонецЕсли;
		ВызватьОбъектБДПриИзменении(ВыбранноеЗначение, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБДОчисткаМеню(Элемент, СтандартнаяОбработка)
	Перем Оповещение;
	Перем ПараметрыОповещенияОЗавершении;
	Перем ВыборНачальный;
	
	СтандартнаяОбработка = Ложь;
	
	Меню = Элемент.СписокВыбора.Скопировать();
	ОбъектТипа_СписокПустыхСсылокУстановитьКартинки(ЭтаФорма.ОбъектТип, Меню);
	Меню.Вставить(0, Неопределено	, "<Очистить значение Объекта БД>"	, , БиблиотекаКартинок.Удалить);
	Меню.Вставить(1, "$ВыбратьТип"	, "<Выбрать тип Объекта БД>"		, , БиблиотекаКартинок.ВыбратьТип);
	Если Элемент.СписокВыбора.Количество() > 0 Тогда
		Меню.Вставить(2, ""			, "......"							, , );
		Меню.Добавить(Null, "<Очистить список пустых значений>"			, , БиблиотекаКартинок.Очистить);
	КонецЕсли; 
	ВыборНачальный = Меню[0];
	
	ПараметрыОповещенияОЗавершении = Элемент;
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Выбор = ЭтаФорма.ВыбратьИзСписка(Меню, Элемент, ВыборНачальный);
		ОбъектБДОчисткаМенюЗавершение(Выбор, ПараметрыОповещенияОЗавершении);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОбъектБДОчисткаМенюЗавершение"", ЭтаФорма, ПараметрыОповещенияОЗавершении)");
		Выполнить("ЭтаФорма.ПоказатьВыборИзСписка(Оповещение, Меню, Элемент, ВыборНачальный)");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБДОчисткаМенюЗавершение(Выбор, ПараметрыОповещения) Экспорт
	
	Если Выбор = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Элемент = ПараметрыОповещения;
	Если Выбор.Значение = "$ВыбратьТип" Тогда
		ВызватьВыборТипаОбъектаБД();
	ИначеЕсли Выбор.Значение = "" Тогда
		ОбъектБДОчисткаМеню(Элемент, Истина);
	ИначеЕсли Выбор.Значение = Null Тогда
		Элемент.СписокВыбора.Очистить();
	ИначеЕсли Выбор.Значение = Неопределено Тогда
		ОбъектБДОчистка(Элемент, Истина);
	Иначе
		Если Объект.ОбъектБД <> Неопределено И ТипЗнч(Выбор.Значение) <> ТипЗнч(Объект.ОбъектБД) Тогда
			ОбъектБДОчистка(Элемент, Истина);
		КонецЕсли; 
		Объект.ОбъектБД = Выбор.Значение;
		ОбъектБДПриИзменении(Элемент);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБДИзменятьТДвижений(Команда)
	
	ИмяРегистра = СтрЗаменить(Команда.Имя, "ОбъектБДИзменятьТДвижений_", "");
	ТаблицаИмя = ФормаПолучитьИмяТаблицыРегистр(ИмяРегистра);
	СтраницаИмя = ФормаПолучитьИмяСтраницыРегистр(ИмяРегистра);
	
	// Включаем редактирование таблицы набора движений.
	Элементы[СтраницаИмя].Картинка = БиблиотекаКартинок.ТабличныйДокументТолькоПросмотр;
	Элементы[ТаблицаИмя].ИзменятьПорядокСтрок = Истина;
	Элементы[ТаблицаИмя].ИзменятьСоставСтрок = Истина;
	Элементы[ТаблицаИмя].ТолькоПросмотр = Ложь;
	// Переключаем доступность кнопок команд контекстного.
	Элементы[Команда.Имя].Доступность = Ложь;
	Элементы[СтрЗаменить(Команда.Имя, "ОбъектБДИзменятьТДвижений_", "ОбъектБДЗаписатьТДвижений_")].Доступность = Истина;
	Элементы["кнОМЗаписатьИзмененныеНаборыДвижений"].Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБДЗаписатьТДвижений(Команда)
	Перем Оповещение;
	Перем ПараметрыОповещенияОЗавершении;
	
	Если Объект.ТипОбъектаБД <> "Документ" Тогда
		Сообщить("Функция не доступна для объектов типа """ + Объект.ТипОбъектаБД + """ !!!", СтатусСообщения.Информация);
		Возврат;
	КонецЕсли; 
	РегистрИмя = СтрЗаменить(Команда.Имя, "ОбъектБДЗаписатьТДвижений_", "");
	
	ТекстЗаголовка = "ЗАПИСЬ НАБОРА ДВИЖЕНИЙ";
	ТекстВопроса = 
	"Записать набор движений текущего документа
	|по регистру  """ + РегистрИмя + """  ???";
	
	ПараметрыОповещенияОЗавершении = Новый Структура("Команда,РегистрИмя", Команда, РегистрИмя);
		
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, 30, КодВозвратаДиалога.Отмена, ТекстЗаголовка, КодВозвратаДиалога.Отмена);
		ОбъектБДЗаписатьТДвиженийЗавершение(Ответ, ПараметрыОповещенияОЗавершении);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОбъектБДЗаписатьТДвиженийЗавершение"", ЭтаФормаЭтотОбъект(), ПараметрыОповещенияОЗавершении)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, 30, КодВозвратаДиалога.Отмена, ТекстЗаголовка, КодВозвратаДиалога.Отмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБДЗаписатьТДвиженийЗавершение(Ответ, ПараметрыОповещения) Экспорт
	Перем Команда, РегистрИмя;
	
	// Считываем и проверяем значения параметров оповещения о выборе в диалоге.
	Если ТипЗнч(ПараметрыОповещения) = Тип("Структура") Тогда
		ПараметрыОповещения.Свойство("Команда", Команда);
		ПараметрыОповещения.Свойство("РегистрИмя", РегистрИмя);
	КонецЕсли;
	Если ТипЗнч(Команда) <> Тип("КомандаФормы") Тогда
		ВызватьИсключение("ОбъектБДЗаписатьТДвиженийЗавершение() >>> Не передана ссылка на команду формы !");
	КонецЕсли; 
	Если ПустаяСтрока(РегистрИмя) Тогда
		ВызватьИсключение("ОбъектБДЗаписатьТДвиженийЗавершение() >>> Не передано имя регистра для записи набора движений !");
	КонецЕсли; 
	
	// Проверяем ответ из диалога вопроса и выполняем действия.
	Если (Ответ <> КодВозвратаДиалога.ОК) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаИмя = ФормаПолучитьИмяТаблицыРегистр(РегистрИмя);
	Еррор = Неопределено;
	ОбъектБДЗаписатьТДвиженийВыполнение(ТаблицаИмя,РегистрИмя,Еррор);
	Если Еррор <> Истина Тогда
		// если успешно - выключам редактирование таблицы набора движений
		СтраницаИмя = ФормаПолучитьИмяСтраницыРегистр(РегистрИмя);
		Элементы[СтраницаИмя].Картинка = Новый Картинка;
		Элементы[ТаблицаИмя].ИзменятьПорядокСтрок = Ложь;
		Элементы[ТаблицаИмя].ИзменятьСоставСтрок = Ложь;
		Элементы[ТаблицаИмя].ТолькоПросмотр = Истина;
		// переключаем доступность кнопок команд контекстного меню
		Элементы[Команда.Имя].Доступность = Ложь;
		Элементы[СтрЗаменить(Команда.Имя, "ОбъектБДЗаписатьТДвижений_", "ОбъектБДИзменятьТДвижений_")].Доступность = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБДЗаписатьИзмененныеНД(Команда)
	Перем Оповещение;
	Перем ПараметрыОповещенияОЗавершении;
	
	Если Объект.ТипОбъектаБД <> "Документ" Тогда
		Сообщить("Функция не доступна для объектов типа """ + Объект.ТипОбъектаБД + """ !!!", СтатусСообщения.Информация);
		Возврат;
	КонецЕсли;
	
	мсИзмененныеНаборы = Новый Массив;
	ИменаРегистров = "";
	ТабИмя = ФормаПолучитьИмяТаблицыРегистр("");
	ТабИмяДлина = СтрДлина(ТабИмя);
	Для Каждого Элемент Из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ТаблицаФормы") И Лев(Элемент.Имя,ТабИмяДлина) = ТабИмя И Элемент.ТолькоПросмотр = Ложь Тогда
			РегистрИмя = Сред(Элемент.Имя, ТабИмяДлина+1);
			мсИзмененныеНаборы.Добавить(Новый Структура("РегистрИмя,ТаблицаИмя,СтраницаИмя", РегистрИмя, Элемент.Имя, Элемент.Родитель.Имя));
			ИменаРегистров = ИменаРегистров + ?(ПустаяСтрока(ИменаРегистров), "", "
			|") + """" + РегистрИмя + """";
		КонецЕсли;
	КонецЦикла;
	
	Кнопка = Элементы["кнОМЗаписатьИзмененныеНаборыДвижений"];
	Если мсИзмененныеНаборы.Количество() = 0 Тогда
		Сообщить("Нет измененных наборов движений документа для записи !", СтатусСообщения.Информация);
		Кнопка.Доступность = Ложь;
		Возврат;
	КонецЕсли; 
	
	ТекстЗаголовка = "ЗАПИСЬ НАБОРОВ ДВИЖЕНИЙ";
	ТекстВопроса = 
	"Записать наборы движений текущего документа по следующим регистрам:
	|---
	|" + ИменаРегистров + "  ???";
	
	ПараметрыОповещенияОЗавершении = Новый Структура("Кнопка,мсИзмененныеНаборы", Кнопка, мсИзмененныеНаборы);
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, 30, КодВозвратаДиалога.Отмена, ТекстЗаголовка, КодВозвратаДиалога.Отмена);
		ОбъектБДЗаписатьИзмененныеНДЗавершение(Ответ, ПараметрыОповещенияОЗавершении);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		//+yuraos, 10.09.2015 - для обратной совместимости лучше использовать атрибут "ЭтаФорма", "ЭтотОбъект" появляется в режиме совместимости "Версия 8.3.3" и выше.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ОбъектБДЗаписатьИзмененныеНДЗавершение"", ЭтаФормаЭтотОбъект(), ПараметрыОповещенияОЗавершении)");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, 30, КодВозвратаДиалога.Отмена, ТекстЗаголовка, КодВозвратаДиалога.Отмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБДЗаписатьИзмененныеНДЗавершение(Ответ, ПараметрыОповещения) Экспорт
	Перем Кнопка, мсИзмененныеНаборы;
	
	// Считываем и проверяем значения параметров оповещения о выборе в диалоге.
	Если ТипЗнч(ПараметрыОповещения) = Тип("Структура") Тогда
		ПараметрыОповещения.Свойство("Кнопка", Кнопка);
		ПараметрыОповещения.Свойство("мсИзмененныеНаборы", мсИзмененныеНаборы);
	КонецЕсли;
	Если ТипЗнч(Кнопка) <> Тип("КнопкаФормы") ИЛИ Кнопка.Вид <> ВидКнопкиФормы.КнопкаКоманднойПанели Тогда
		ВызватьИсключение("ОбъектБДЗаписатьИзмененныеНДЗавершение() >>> Не передана ссылка на кнопку командной панели !");
	КонецЕсли; 
	Если ТипЗнч(мсИзмененныеНаборы) <> Тип("Массив") Тогда
		ВызватьИсключение("ОбъектБДЗаписатьИзмененныеНДЗавершение() >>> Не передан массив данных для записи измененных наборов движений !");
	КонецЕсли; 
	
	// Проверяем ответ из диалога вопроса и выполняем действия.
	Если (Ответ <> КодВозвратаДиалога.ОК) Тогда
		Возврат;
	КонецЕсли;
	
	Еррор = Неопределено;
	ОбъектБДЗаписатьИзмененныеНДВыполнение(мсИзмененныеНаборы, Еррор);
	Если Еррор <> Истина Тогда
		ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ОбъектБД, ВидИзмененияДанных.Изменение);	// Обновление отображения в реквизите ОбъектБД!
		ОбъектБДПриИзменении("ОбъектБДЗаписать"); // Обновим данные ОбъектаБД в форме, так как они могли измениться в модулях наборов записей.
		//Кнопка.Доступность = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбъектБДЗаписатьИзмененныеНДВыполнение(мсИзмененныеНаборы,Еррор)
	
	КартинкаНулл = Новый Картинка;
	Еррор = Ложь;
	Для Каждого Набор ИЗ мсИзмененныеНаборы Цикл
		Еррор1 = Неопределено;
		ОбъектБДЗаписатьТДвиженийВыполнение(Набор.ТаблицаИмя, Набор.РегистрИмя, Еррор1);
		Если Еррор1 = Истина Тогда
			Еррор = Истина;
			Сообщить(
			"Набор движений """ + Набор.РегистрИмя + """ - НЕ ЗАПИСАН!", СтатусСообщения.Важное);
		Иначе
			Сообщить(
			"Набор движений """ + Набор.РегистрИмя + """ - записан.", СтатусСообщения.Информация);
			// Если успешно - выключам редактирование таблицы набора движений.
			Элементы[Набор.СтраницаИмя].Картинка = КартинкаНулл;
			Элементы[Набор.ТаблицаИмя].ИзменятьПорядокСтрок = Ложь;
			Элементы[Набор.ТаблицаИмя].ИзменятьСоставСтрок = Ложь;
			Элементы[Набор.ТаблицаИмя].ТолькоПросмотр = Истина;
			// Переключаем доступность кнопок команд контекстного.
			Элементы["ОбъектБДЗаписатьТДвижений_" + Набор.РегистрИмя].Доступность = Ложь;
			Элементы["ОбъектБДИзменятьТДвижений_" + Набор.РегистрИмя].Доступность = Истина;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ОбъектБДЗаписатьТДвиженийВыполнение(ТаблицаИмя, РегистрИмя, Еррор)
	
	Еррор = Ложь;
	Если Объект.ТипОбъектаБД <> "Документ" Тогда
		Сообщить("Функция все равно не доступна для объектов типа """ + Объект.ТипОбъектаБД + """ !!!", СтатусСообщения.Информация);
		Еррор = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ОбъектБД) Тогда
		Сообщить("Пустая ссылка на документ-регистратор !!!", СтатусСообщения.Информация);
		Еррор = Истина;
		Возврат;
	КонецЕсли; 
	
	ТаблицаДвижений = ЭтаФорма[Элементы[ТаблицаИмя].ПутьКДанным];
	
	ОбъектБД = Объект.ОбъектБД.ПолучитьОбъект();
	НаборЗаписей = ОбъектБД.Движения[РегистрИмя];
	НаборЗаписей.Прочитать();
	
	ОчисткаНабораДвижений = (ТаблицаДвижений.Количество() = 0 И НаборЗаписей.Количество() <> 0);
	
	Если Объект.ОбменДаннымиЗагрузка Тогда
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
	КонецЕсли;
	
	Попытка
		НаборЗаписей.Загрузить(ТаблицаДвижений.Выгрузить());
		НаборЗаписей.Записать(Истина);
		Если ОчисткаНабораДвижений Тогда
			Сообщить(
			"Очищен не пустой набор движений """ + РегистрИмя + """ регистратора !!!", СтатусСообщения.Внимание);
		КонецЕсли; 
	Исключение
		Еррор = Истина;
		Сообщить(
		"Ошибка записи набора движений """ + РегистрИмя + """ регистратора:
		|" + ОписаниеОшибки(),СтатусСообщения.ОченьВажное);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Функция ФормаИмяФормы(Форма)
	
	ФормаИмяФормы = "";
	Если Форма <> Неопределено Тогда
		Попытка
			ФормаИмяФормы = Форма.ИмяФормы;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Возврат ФормаИмяФормы;
	
КонецФункции
 
&НаКлиенте
Функция ЭтоСвязанноеОкноОбработки(Форма = Неопределено)
	
	Если Форма = Неопределено Тогда
		ПроверяемаяФорма = ЭтаФорма;
	Иначе
		ПроверяемаяФорма = Форма;
	КонецЕсли;
	Попытка
		ФормаВладелец = ПроверяемаяФорма.ВладелецФормы;
	Исключение
		ФормаВладелец = Неопределено;
	КонецПопытки;
	
	Возврат (ФормаВладелец <> Неопределено И ФормаИмяФормы(ФормаВладелец) = ЭтаФорма.ИмяФормы);
	
КонецФункции
 
&НаКлиенте
Процедура спСвязанныеОкнаПроверитьДобавить(ФормаОбработки = Неопределено, НеУдалятьЗакрытые = Ложь) Экспорт
	
	Если ТипЗнч(ЭтаФорма.спСвязанныеОкна) <> Тип("СписокЗначений") Тогда
		ЭтаФорма.спСвязанныеОкна = Новый СписокЗначений;
	КонецЕсли;
	
	ЭтоСвязанноеОкно = ЭтоСвязанноеОкноОбработки(Неопределено);
	
	ЭтаФорма.Заголовок = СтрЗаменить(ЭтаФорма.Заголовок, " +",""); 
	Если ЭтоСвязанноеОкно = Ложь Тогда
		мсУдалить = Новый Массив;
		Для Каждого ФормаС ИЗ ЭтаФорма.спСвязанныеОкна Цикл
			Если ТипЗнч(ФормаС.Значение) <> ТипЗнч(ЭтаФорма) Тогда
				// Не форма - удаляем из списка.
				мсУдалить.Добавить(ФормаС);
			ИначеЕсли ФормаС.Значение = ЭтаФорма Тогда
				// Основная форма - удаляем из списка.
				мсУдалить.Добавить(ФормаС);
			ИначеЕсли ФормаИмяФормы(ФормаС.Значение) <> ЭтаФорма.ИмяФормы Тогда
				// Не форма Администратора1С - удаляем из списка.
				мсУдалить.Добавить(ФормаС);
			ИначеЕсли НеУдалятьЗакрытые <> Истина И ФормаС.Значение.Открыта() <> Истина Тогда
				// Закрытая форма - удаляем из списка в зависимости от ситуации.
				мсУдалить.Добавить(ФормаС);
			КонецЕсли; 
		КонецЦикла;
		Для Каждого ФормаС ИЗ мсУдалить Цикл
			ЭтаФорма.спСвязанныеОкна.Удалить(ФормаС);
		КонецЦикла;
		Если ФормаОбработки <> Неопределено И ФормаИмяФормы(ФормаОбработки) = ЭтаФорма.ИмяФормы И ФормаОбработки <> ЭтаФорма Тогда
			ФормаС = спСвязанныеОкна.НайтиПоЗначению(ФормаОбработки);
			Если ФормаС = Неопределено Тогда
				ЭтаФорма.спСвязанныеОкна.Добавить(ФормаОбработки,);
			Иначе
				ЭтаФорма.спСвязанныеОкна.Сдвинуть(ФормаС, 0-спСвязанныеОкна.Индекс(ФормаС));
			КонецЕсли; 
		КонецЕсли;
	Иначе
		ЭтаФорма.Заголовок = ЭтаФорма.Заголовок + " +"; 
	КонецЕсли;
	
	Если ЭтоСвязанноеОкно = Ложь Тогда
		СписокФорм = ЭтаФорма.спСвязанныеОкна;
	Иначе
		СписокФорм = ЭтаФорма.ВладелецФормы.спСвязанныеОкна;
	КонецЕсли;
	
	Элементы.ПерейтиВСвязанноеОкно.Доступность = (СписокФорм.Количество() > 0);
	Элементы.ЗакрытьСвязанныеОкна.Доступность = (СписокФорм.Количество() > 0);
	
КонецПроцедуры

&НаКлиенте
Функция СписокСвязанныхФормПолучить(СписокФорм, ФормаТ, ВключитьОсновное, УдалитьЗакрытые)
	
	ЭтоСвязанноеОкно = ЭтоСвязанноеОкноОбработки(Неопределено);
	ФормаТ = Неопределено;
	СписокФорм = Новый СписокЗначений;
	
	Если ЭтоСвязанноеОкно = Ложь Тогда
		Если ВключитьОсновное = Истина Тогда
			Представление = Строка(ЭтаФорма.Объект.ОбъектБД); 
			Представление = ?(ПустаяСтрока(Представление), "< . . . >", Представление);
			СписокФорм.Добавить(ЭтаФорма, Представление);
		КонецЕсли; 
		спСвязанныеОкнаПроверитьДобавить(Неопределено, УдалитьЗакрытые <> Истина);
		Для Каждого ФормаС ИЗ ЭтаФорма.спСвязанныеОкна Цикл
			Представление = Строка(ФормаС.Значение.Объект.ОбъектБД);
			Представление = ?(ПустаяСтрока(Представление), "< . . . >", Представление);
			СписокФорм.Добавить(ФормаС.Значение, Представление);
		КонецЦикла; 
	Иначе
		Если ВключитьОсновное = Истина Тогда
			Представление = Строка(ЭтаФорма.ВладелецФормы.Объект.ОбъектБД);
			Представление = ?(ПустаяСтрока(Представление), "< . . . >", Представление);
			СписокФорм.Добавить(ЭтаФорма.ВладелецФормы, Представление);
		КонецЕсли; 
		ЭтаФорма.ВладелецФормы.спСвязанныеОкнаПроверитьДобавить(Неопределено, УдалитьЗакрытые <> Истина);
		Для Каждого ФормаС ИЗ ЭтаФорма.ВладелецФормы.спСвязанныеОкна Цикл
			Представление = Строка(ФормаС.Значение.Объект.ОбъектБД);
			Представление = ?(ПустаяСтрока(Представление), "< . . . >", Представление);
			СписокФорм.Добавить(ФормаС.Значение, Представление);
		КонецЦикла; 
	КонецЕсли;
	
	Меню = Новый СписокЗначений;
	КартинкаНулл = Новый Картинка;
	Для Каждого ФормаС ИЗ СписокФорм Цикл
		ФормаМ = Меню.Добавить();
		ФормаМ.Значение = СписокФорм.Индекс(ФормаС);
		ФормаМ.Представление = "" + ФормаМ.Значение + ": " + ФормаС.Представление;
		ФормаМ.Картинка = ФормаС.Картинка;
		Если ФормаС.Значение = ЭтаФорма Тогда
			ФормаМ.Картинка = БиблиотекаКартинок.Вперед;
			ФормаТ = ФормаМ;
		ИначеЕсли ЭтоСвязанноеОкно = Истина И ФормаС.Значение = ЭтаФорма.ВладелецФормы Тогда
			ФормаМ.Картинка = БиблиотекаКартинок.ВводНаОсновании;
		ИначеЕсли ФормаС.Значение.Открыта() = Ложь Тогда
			ФормаМ.Картинка = БиблиотекаКартинок.Удалить;
		Иначе
			ФормаМ.Картинка = КартинкаНулл;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат Меню;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОбъектБД_СписокВыбора_Сохранить(ИмяФормы, Знач сзСписок)
	ХранилищеНастроекДанныхФорм.Сохранить(ИмяФормы, "ОбъектБД.СписокВыбора", сзСписок.ВыгрузитьЗначения());
КонецПроцедуры

&НаКлиенте
Функция НайтиОбъектВСвязанныхОкнах(Знач ОбъектСсылка, ВключаяОсновноеОкно, ЭтоСвязанноеОкно)
	
	Если ЗначениеЗаполнено(ОбъектСсылка) = Ложь Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если ТипЗнч(ЭтоСвязанноеОкно) <> Тип("Булево") Тогда
		ЭтоСвязанноеОкно = ЭтоСвязанноеОкноОбработки(Неопределено);
	КонецЕсли; 
	
	ФормаОбработки = Неопределено;
	Если ЭтоСвязанноеОкно = Ложь Тогда
		ОсновноеОкно = ЭтаФорма;
	Иначе
		ОсновноеОкно = ЭтаФорма.ВладелецФормы;
	КонецЕсли;
	Если ВключаяОсновноеОкно = Истина И ОсновноеОкно.Объект.ОбъектБД = ОбъектСсылка Тогда
		Возврат ОсновноеОкно;
	КонецЕсли; 
	
	СписокФорм = ОсновноеОкно.спСвязанныеОкна;
	Если ТипЗнч(СписокФорм) = Тип("СписокЗначений") Тогда
		Для Каждого ФормаС ИЗ СписокФорм Цикл
			Если ТипЗнч(ФормаС.Значение) = ТипЗнч(ЭтаФорма) И ФормаС.Значение.Объект.ОбъектБД = ОбъектСсылка Тогда
				ФормаОбработки = ФормаС.Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли; 
	
	Возврат ФормаОбработки;
	
КонецФункции
 
&НаКлиенте
Процедура НовоеОкноАдминистатора1С(ОткрытьСвязанноеОкно, ОбъектСсылка) Экспорт
	
	ИмяФормыОбработки = Объект.Настройки.ИмяФормыОбработки;
	
	Если ИмяФормыОбработки = Неопределено Тогда
		ПредупреждениеСообщение(,"Форма обработки ""Управляемая"" не найдена.");
		Возврат;
	КонецЕсли;
	
	ОбъектБД_СписокВыбора_Сохранить(ЭтаФорма.ИмяФормы, Элементы.ОбъектБД.СписокВыбора);
	
	ЭтоСвязанноеОкно = ЭтоСвязанноеОкноОбработки(Неопределено);
	
	ФормаОбработки = Неопределено;
	Если ОткрытьСвязанноеОкно = Истина И ЗначениеЗаполнено(ОбъектСсылка) Тогда
		// Не дадим один и тот же объект открыть несколько раз в разных связанных окнах.
		ФормаОбработки = НайтиОбъектВСвязанныхОкнах(ОбъектСсылка,Ложь,ЭтоСвязанноеОкно);
	КонецЕсли; 
	
	Если ФормаОбработки = Неопределено Тогда
		Если ЗначениеЗаполнено(ОбъектСсылка) Тогда
			стПараметры = Новый Структура("ОбъектБД", ОбъектСсылка);
		Иначе
			стПараметры = Новый Структура("ОбъектБД", Объект.ОбъектБД);
		КонецЕсли;
		Если ОткрытьСвязанноеОкно = Истина Тогда
			ФормаОбработкиВладелец = ?(ЭтоСвязанноеОкно = Ложь, ЭтаФорма, ЭтаФорма.ВладелецФормы);
		Иначе
			ФормаОбработкиВладелец = Неопределено;
		КонецЕсли; 
		Попытка
			ФормаОбработки = ПолучитьФорму(ИмяФормыОбработки, стПараметры, ФормаОбработкиВладелец, Новый УникальныйИдентификатор);
		Исключение
			Сообщить(ОписаниеОшибки(), СтатусСообщения.Информация);
			Возврат;
		КонецПопытки;
	КонецЕсли; 
	
	Если ФормаОбработки = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Если ОткрытьСвязанноеОкно = Истина Тогда
		Если ЭтоСвязанноеОкно = Ложь Тогда
			ЭтаФорма.спСвязанныеОкнаПроверитьДобавить(ФормаОбработки, Истина);
		Иначе
			ЭтаФорма.ВладелецФормы.спСвязанныеОкнаПроверитьДобавить(ФормаОбработки, Истина);
			ЭтаФорма.спСвязанныеОкнаПроверитьДобавить(Неопределено, Истина);
		КонецЕсли; 
	КонецЕсли;
	
	ФормаОбработки.ЗакрыватьПриЗакрытииВладельца = Истина;
	ФормаОбработки.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура НовоеОкно(Команда)	
	
	Попытка
		КнопкаИмя = Команда.Имя;
	Исключение
		КнопкаИмя = "";
	КонецПопытки;
	ОткрытьСвязанноеОкно = (КнопкаИмя = "НовоеСвязанноеОкно");
	
	НовоеОкноАдминистатора1С(ОткрытьСвязанноеОкно,Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВСвязанноеОкно(Команда)
	Перем Оповещение;
	Перем ПараметрыОповещенияОЗавершении;
	Перем СписокФорм, ФормаТ;
	
	Меню = СписокСвязанныхФормПолучить(СписокФорм, ФормаТ, Истина, Ложь);
	Если Меню.Количество() <= 0 Тогда
		Элементы.ПерейтиВСвязанноеОкно.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗаголовка = "ПЕРЕХОД В СВЯЗАННОЕ ОКНО ОБРАБОТКИ";
	
	ПараметрыОповещенияОЗавершении = Новый Структура("Команда,СписокФорм,ФормаТ",Команда,СписокФорм,ФормаТ);
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		ФормаМ = Меню.ВыбратьЭлемент(ТекстЗаголовка,ФормаТ);
		ПерейтиВСвязанноеОкноЗавершение(ФормаМ, ПараметрыОповещенияОЗавершении);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ПерейтиВСвязанноеОкноЗавершение"", ЭтаФорма, ПараметрыОповещенияОЗавершении)");
		Выполнить("Меню.ПоказатьВыборЭлемента(Оповещение, ТекстЗаголовка, ФормаТ)");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВСвязанноеОкноЗавершение(ФормаМ,ПараметрыОповещения) Экспорт
	Перем Команда, СписокФорм, ФормаТ;
	
	// Считываем и проверяем значения параметров оповещения о выборе в диалоге.
	Если ТипЗнч(ПараметрыОповещения) = Тип("Структура") Тогда
		ПараметрыОповещения.Свойство("Команда", Команда);
		ПараметрыОповещения.Свойство("СписокФорм", СписокФорм);
		ПараметрыОповещения.Свойство("ФормаТ", ФормаТ);
	КонецЕсли;
	Если ТипЗнч(Команда) <> Тип("КомандаФормы") Тогда
		ВызватьИсключение("ПерейтиВСвязанноеОкноЗавершение() >>> Не передана ссылка на команду формы !");
	КонецЕсли; 
	Если ТипЗнч(СписокФорм) <> Тип("СписокЗначений") Тогда
		ВызватьИсключение("ПерейтиВСвязанноеОкноЗавершение() >>> Не передан список связанных окон обработки !");
	КонецЕсли; 
	
	// Проверяем ответ диалога выбора значения списка и выполняем действия.
	Если ФормаМ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ФормаТ = Неопределено ИЛИ ФормаТ.Значение <> ФормаМ.Значение Тогда
		СписокФорм[ФормаМ.Значение].Значение.Открыть(); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьСвязанныеОкна(Команда)
	Перем Оповещение;
	Перем ПараметрыОповещенияОЗавершении;
	Перем СписокФорм;
	
	Меню = СписокСвязанныхФормПолучить(СписокФорм, , Ложь, Ложь);
	Если Меню.Количество() <= 0 Тогда
		Элементы.ЗакрытьСвязанныеОкна.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	Меню.ЗаполнитьПометки(Ложь);
	
	ТекстЗаголовка = "ЗАКРЫТИЕ СВЯЗАННЫХ ОКОН ОБРАБОТКИ";
	
	ПараметрыОповещенияОЗавершении = Новый Структура("Команда,СписокФорм,Меню", Команда, СписокФорм, Меню);
	
	Если ИспользоватьРежимМодальности() Тогда
		// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
		ВыборОК = Меню.ОтметитьЭлементы(ТекстЗаголовка);
		ЗакрытьСвязанныеОкнаЗавершение(ВыборОК, ПараметрыОповещенияОЗавершении);
	Иначе
		// Стандартно в немодальном режиме (8.3) с обработкой результата.
		Оповещение = Вычислить("Новый ОписаниеОповещения(""ЗакрытьСвязанныеОкнаЗавершение"", ЭтаФорма, ПараметрыОповещенияОЗавершении)");
		Выполнить("Меню.ПоказатьОтметкуЭлементов(Оповещение, ТекстЗаголовка)");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьСвязанныеОкнаЗавершение(Ответ,ПараметрыОповещения) Экспорт
	Перем Команда, СписокФорм, Меню;
	
	// Считываем и проверяем значения параметров оповещения о выборе в диалоге.
	Если ТипЗнч(ПараметрыОповещения) = Тип("Структура") Тогда
		ПараметрыОповещения.Свойство("Команда", Команда);
		ПараметрыОповещения.Свойство("СписокФорм", СписокФорм);
		ПараметрыОповещения.Свойство("Меню", Меню);
	КонецЕсли;
	Если ТипЗнч(Команда) <> Тип("КомандаФормы") Тогда
		ВызватьИсключение("ЗакрытьСвязанныеОкнаЗавершение() >>> Не передана ссылка на кнопку командной панели !");
	КонецЕсли; 
	Если ТипЗнч(СписокФорм) <> Тип("СписокЗначений") Тогда
		ВызватьИсключение("ЗакрытьСвязанныеОкнаЗавершение() >>> Не передан список связанных окон обработки !");
	КонецЕсли; 
	Если ТипЗнч(Меню) <> Тип("СписокЗначений") Тогда
		ВызватьИсключение("ЗакрытьСвязанныеОкнаЗавершение() >>> Не передан список меню выбора связанных окон обработки !");
	КонецЕсли;
	
	// Проверяем ответ диалога отметки значений списка и выполняем действия.
	Если Ответ = Истина Тогда
		// Выбор в модальном диалоге.
		МенюОтметки = Меню;
	ИначеЕсли Ответ = Ложь Тогда
		// Отказ в модальном диалоге.
		Возврат;
	ИначеЕсли Ответ = Неопределено Тогда
		// Отказ в асинхронном диалоге.
		Возврат;
	Иначе
		// Выбор в асинхронном диалоге.
		МенюОтметки = Ответ;
	КонецЕсли;
	
	Для Каждого ФормаМ ИЗ МенюОтметки Цикл
		Если ФормаМ.Пометка Тогда
			Если СписокФорм[ФормаМ.Значение].Значение.Открыта() Тогда
				СписокФорм[ФормаМ.Значение].Значение.Закрыть(); 
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	Если ЭтоСвязанноеОкноОбработки(Неопределено) = Ложь Тогда
		ЭтаФорма.спСвязанныеОкнаПроверитьДобавить(Неопределено, Ложь);
	Иначе
		ЭтаФорма.ВладелецФормы.спСвязанныеОкнаПроверитьДобавить(Неопределено, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицыТабличныхЧастейКонтрольВидимости()
	
	ТабИмя = ФормаПолучитьИмяТаблицыТабличнаяЧасть("");
	ТабИмяДлина = СтрДлина(ТабИмя);
	Для Каждого Элемент ИЗ Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ТаблицаФормы") И Лев(Элемент.Имя, ТабИмяДлина) = ТабИмя Тогда
			Если Объект.СкрыватьПустыеТабличныеЧасти = Истина Тогда
				ТабЧастьИмя = Сред(Элемент.Имя, ТабИмяДлина+1);
				Если Элемент.Родитель.Видимость = Истина И ЭтаФорма[ОбъектТипа_ФормаПолучитьИмяРеквизитаТабличнаяЧасть(ЭтаФорма.ОбъектТип, ТабЧастьИмя)].Количество() = 0 Тогда
					Элемент.Родитель.Видимость = Ложь;
				КонецЕсли; 
			Иначе
				Если Элемент.Родитель.Видимость = Ложь Тогда
					Элемент.Родитель.Видимость = Истина;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицыДвиженийРегистровКонтрольВидимости()
	
	ТабИмя = ФормаПолучитьИмяТаблицыРегистр("");
	ТабИмяДлина = СтрДлина(ТабИмя);
	Для Каждого Элемент ИЗ Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ТаблицаФормы") И Лев(Элемент.Имя, ТабИмяДлина) = ТабИмя Тогда
			Если Объект.СкрыватьПустыеТаблицыДвиженийРегистров = Истина Тогда
				РегистрИмя = Сред(Элемент.Имя, ТабИмяДлина+1);
				Если Элемент.Родитель.Видимость = Истина И ЭтаФорма[ОбъектТипа_ФормаПолучитьИмяРеквизитаРегистр(ЭтаФорма.ОбъектТип, РегистрИмя)].Количество() = 0 Тогда
					Элемент.Родитель.Видимость = Ложь;
				КонецЕсли; 
			Иначе
				Если Элемент.Родитель.Видимость = Ложь Тогда
					Элемент.Родитель.Видимость = Истина;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрыватьПустыеТабличныеЧастиПриИзменении(Элемент)
	ТаблицыТабличныхЧастейКонтрольВидимости();
КонецПроцедуры

&НаКлиенте
Процедура СкрыватьПустыеТаблицыДвиженийРегистровПриИзменении(Элемент)
	ТаблицыДвиженийРегистровКонтрольВидимости();
КонецПроцедуры

&НаКлиенте
Функция ИмяРеквизитаКолонкиТаблицы(Элемент, ТаблицаИмя)
	
	ТаблицаИмя = "";
	КолонкаИмя = "";
	ЭлеменИмя = Элемент.Имя;
	Если ТипЗнч(Элемент.Родитель) = Тип("ТаблицаФормы") Тогда
		ТаблицаИмя = Элемент.Родитель.Имя;
	КонецЕсли; 
	ТабИмяДлина = СтрДлина(ТаблицаИмя);
	Если НЕ ПустаяСтрока(ТаблицаИмя) И Лев(ЭлеменИмя, ТабИмяДлина) = ТаблицаИмя Тогда
		// Вагуем имя колонки реквизита по гуще ... 
		// т.е. по имени элемента колонки, считая его стандартно составленным.
		// Корректно имя находися по свойству "ПутьКДанным" элемента поля колонки,
		// но для его чтения требуется серверный контекстный вызов - не наш метод!
		КолонкаИмя = Сред(ЭлеменИмя, ТабИмяДлина+1);
	КонецЕсли;
	
	Возврат КолонкаИмя;
	
КонецФункции

&НаКлиенте
Процедура ТФормаКолонкаОткрытьЗначение(Элемент, СтандартнаяОбработка, ТаблицаИмя, КолонкаИмя, ТекущДанные, ТекЗначение)
	Перем Оповещение;
	Перем ПараметрыОповещенияОЗавершении;
	
	Если ЗначениеЗаполнено(ТекЗначение) И ОбъектТипа_ЗначениеСсылочногоТипа(ЭтаФорма.ОбъектТип, ТекЗначение, Истина) Тогда
		СтандартнаяОбработка = Ложь;
		
		Меню = Новый СписокЗначений;
		Меню.Добавить(0, "Открыть в форме объекта"		, , БиблиотекаКартинок.Лупа);
		Меню.Добавить(1, "Открыть в 1С:Администраторе"	, , БиблиотекаКартинок.Изменить);
		
		ПараметрыОповещенияОЗавершении = Новый Структура("Элемент,ТаблицаИмя,КолонкаИмя,ТекущДанные,ТекЗначение", Элемент, ТаблицаИмя, КолонкаИмя, ТекущДанные, ТекЗначение);
		
		Если ИспользоватьРежимМодальности() Тогда
			// Стандартно в модальном режиме (8.2/8.3) с обработкой результата.
			Выбор = ЭтаФорма.ВыбратьИзМеню(Меню, Элемент);
			ТФормаКолонкаОткрытьЗначениеЗавершение(Выбор, ПараметрыОповещенияОЗавершении);
		Иначе
			// Стандартно в немодальном режиме (8.3) с обработкой результата.
			Оповещение = Вычислить("Новый ОписаниеОповещения(""ТФормаКолонкаОткрытьЗначениеЗавершение"", ЭтаФорма, ПараметрыОповещенияОЗавершении)");
			Выполнить("ЭтаФорма.ПоказатьВыборИзМеню(Оповещение, Меню, Элемент)");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТФормаКолонкаОткрытьЗначениеЗавершение(Выбор, ПараметрыОповещения) Экспорт
	Перем Элемент, ТаблицаИмя, КолонкаИмя, ТекущДанные, ТекЗначение;
	
	// Считываем и проверяем значения параметров оповещения о выборе в диалоге.
	Если ТипЗнч(ПараметрыОповещения) = Тип("Структура") Тогда
		ПараметрыОповещения.Свойство("Элемент"		, Элемент);
		ПараметрыОповещения.Свойство("ТаблицаИмя"	, ТаблицаИмя);
		ПараметрыОповещения.Свойство("КолонкаИмя"	, КолонкаИмя);
		ПараметрыОповещения.Свойство("ТекущДанные"	, ТекущДанные);
		ПараметрыОповещения.Свойство("ТекЗначение"	, ТекЗначение);
	КонецЕсли;
	Если Элемент = Неопределено Тогда
		ВызватьИсключение("ТФормаКолонкаОткрытиеЗавершение() >>> Не передана ссылка на элемент колонки табличного поля формы !");
	КонецЕсли; 
	Если ПустаяСтрока(ТаблицаИмя) Тогда
		ВызватьИсключение("ТФормаКолонкаОткрытиеЗавершение() >>> Не передано имя табличного поля формы !");
	КонецЕсли; 
	Если ПустаяСтрока(КолонкаИмя) Тогда
		ВызватьИсключение("ТФормаКолонкаОткрытиеЗавершение() >>> Не передано имя реквизита колонки табличного поля формы !");
	КонецЕсли; 
	Если ТекущДанные = Неопределено Тогда
		ВызватьИсключение("ТФормаКолонкаОткрытиеЗавершение() >>> Не переданы текущие данные табличного поля формы !");
	КонецЕсли;
	
	// Проверяем ответ диалога выбра значений списка и выполняем действия.
	Если Выбор = Неопределено Тогда
		Возврат;
	ИначеЕсли Выбор.Значение = 1 Тогда
		НовоеОкноАдминистатора1С(Истина,ТекЗначение);
	Иначе
		Попытка
			// По-возможности используем асинхронный метод.
			Выполнить("ПоказатьЗначение(, ТекЗначение);");
		Исключение
			ОткрытьЗначение(ТекЗначение);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТФормаТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)  
	Перем ТаблицаИмя, КолонкаИмя, ТекущДанные, ТекЗначение;
	
	СтандартнаяОбработка = Истина;
	Если Элемент.ТолькоПросмотр = Ложь Тогда
		Возврат; // Стандартная обработка выбора.
	КонецЕсли;
	
	КолонкаИмя = ИмяРеквизитаКолонкиТаблицы(Поле, ТаблицаИмя);
	Если ПустаяСтрока(КолонкаИмя) Тогда
		Возврат; // Стандартная обработка выбора.
	КонецЕсли;
	
	ТабИмяРГ = ФормаПолучитьИмяТаблицыРегистр("");
	ТабИмяРГДлина = СтрДлина(ТабИмяРГ);
	ТабИмяТЧ = ФормаПолучитьИмяТаблицыТабличнаяЧасть("");
	ТабИмяТЧДлина = СтрДлина(ТабИмяТЧ);
	
	Отказ = Истина;
	Если Лев(Элемент.Имя, ТабИмяРГДлина) = ТабИмяРГ Тогда
		// Таблица движений регистра.
		Отказ = Ложь;
	ИначеЕсли Лев(Элемент.Имя, ТабИмяТЧДлина) = ТабИмяТЧ Тогда
		// Таблица табличной части объекта.
		Отказ = Ложь;
	Иначе
	КонецЕсли;
	
	Если Отказ = Истина Тогда
		Возврат; // Стандартная обработка выбора.
	КонецЕсли;
	
	Попытка
		ТекущДанные = Элемент.ТекущиеДанные;
		ТекЗначение = ТекущДанные[КолонкаИмя];
	Исключение
		Возврат; // Стандартная обработка выбора.
	КонецПопытки;
	
	ТФормаКолонкаОткрытьЗначение(Элемент, СтандартнаяОбработка, ТаблицаИмя, КолонкаИмя, ТекущДанные, ТекЗначение);
		
КонецПроцедуры

&НаКлиенте
Процедура ТФормаКолонкаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; // Всегда нестандартная обработка.
	
	ТаблицаИмя = "";
	КолонкаИмя = ИмяРеквизитаКолонкиТаблицы(Элемент, ТаблицаИмя); 
	Если ПустаяСтрока(КолонкаИмя) Тогда
		Возврат;
	КонецЕсли;
	
	ТабИмяТЧ = ФормаПолучитьИмяТаблицыТабличнаяЧасть("");
	ТабИмяТЧДлина = СтрДлина(ТабИмяТЧ);
	ТабИмяРГ = ФормаПолучитьИмяТаблицыРегистр("");
	ТабИмяРГДлина = СтрДлина(ТабИмяРГ);
	
	Отказ = Истина;
	Если ТаблицаИмя = "ТРеквизиты" Тогда
		Если КолонкаИмя = "Значение" Тогда
			Отказ = Ложь;
		КонецЕсли; 
	ИначеЕсли Лев(Элемент.Имя, ТабИмяТЧДлина) = ТабИмяТЧ Тогда
		// Таблица табличной части объекта.
		Отказ = Ложь;
	ИначеЕсли Лев(Элемент.Имя, ТабИмяРГДлина) = ТабИмяРГ Тогда
		// Таблица движений регистра.
		Отказ = Ложь;
	Иначе
	КонецЕсли;
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ТекущДанные = Элементы[ТаблицаИмя].ТекущиеДанные;
		ТекЗначение = ТекущДанные[КолонкаИмя];
	Исключение
		Возврат;
	КонецПопытки;
	
	ТФормаКолонкаОткрытьЗначение(Элемент, СтандартнаяОбработка, ТаблицаИмя, КолонкаИмя, ТекущДанные, ТекЗначение);
	
КонецПроцедуры

&НаКлиенте
Функция ЭтаФормаЭтотОбъект()
	Попытка
		Возврат Вычислить("ЭтаФорма");
	Исключение
		Возврат Вычислить("ЭтотОбъект");
	КонецПопытки;
КонецФункции

&НаСервере
// инициализирует серверную переменную This
// "облегченным" ("пустым") объектом обработки
// для вызова "статических" (не зависящих от данных объекта) методов
Процедура ThisInitiate()
	Если This = Null ИЛИ This = Неопределено Тогда
		This = Новый(ЗначениеИзСтрокиВнутр(ЭтаФорма.ОбъектТип));
		This.УправляемаяФорма = Истина;
	КонецЕсли; 
КонецПроцедуры
 
&НаСервере
Процедура yuraosИнициализация(ОбъектОбработка)
	
	// Добавим служебные реквизиты формы.
	мсДобавить = Новый Массив;
	
	// 1. Тип объекта обработки.
	мсДобавить.Добавить(Новый РеквизитФормы("ОбъектТип", Новый ОписаниеТипов("Строка", ,
														 Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)), "", "Тип объекта обработки", Ложь));
	// 2. Значение ОбъектБД, переданное через параметр формы, для установки в обработчие события ПриОткрытии.
	МетаОбъектБД = ОбъектОбработка.Метаданные().Реквизиты["ОбъектБД"];
	мсДобавить.Добавить(Новый РеквизитФормы("ПараметрыОбъектБД", МетаОбъектБД.Тип, "", "Объект БД (из параметров)", Ложь));
	
	ЭтаФорма.ИзменитьРеквизиты(мсДобавить);
	
	// Запомним тип объекта обработки для внеконтекстного вызова его методов.
	// http://infostart.ru/public/241811/
	ЭтаФорма.ОбъектТип = ЗначениеВСтрокуВнутр(ТипЗнч(ОбъектОбработка));
	
	// Наведем небольшой фэншуй (не принципиально).
	Элементы.пмОМУдаление.Картинка = БиблиотекаКартинок.Удалить;
	Элементы.кнОМНастройка.Картинка = БиблиотекаКартинок.НастройкаСписка;
	
	// Установим/переустановим действия для некоторых элементов формы.
	// 1. "ОбработкаВыбора", "Очистка" для поля ввода "ОбъектБД".
	Поле = Элементы.ОбъектБД;
	Попытка
		// Чтобы в интерфейсе "Такси" кнопка выбора была видна в самом поле, а не только в его выпадающем списке.
		Выполнить("
		|Поле.ОтображениеКнопкиВыбора = ОтображениеКнопкиВыбора.ОтображатьВВыпадающемСпискеИВПолеВвода;
		|");
	Исключение
	КонецПопытки;
	Поле.КнопкаОчистки = Истина;
	Поле.УстановитьДействие("ОбработкаВыбора", "ОбъектБДОбработкаВыбора");
	Поле.УстановитьДействие("Очистка", "ОбъектБДОчисткаМеню");
	// 2. "ПриИзменении" для флагов скрытия пустых табличных полей.
	Флаг = Элементы.СкрыватьПустыеТабличныеЧасти;
	Флаг.УстановитьДействие("ПриИзменении", "СкрыватьПустыеТабличныеЧастиПриИзменении");
	Флаг = Элементы.СкрыватьПустыеТаблицыДвиженийРегистров;
	Флаг.УстановитьДействие("ПриИзменении", "СкрыватьПустыеТаблицыДвиженийРегистровПриИзменении");
	
	// Добавим команды для расширения функциональности.
	Команда = Команды.Добавить("ОбъектБДИзменятьТДвижений");
	Команда.Действие = "ОбъектБДИзменятьТДвижений";
	Команда.Заголовок = "Включить изменение набора движений";
	Команда.Подсказка = "Включить изменение таблицы набора движений документа";
	Команда.Картинка = БиблиотекаКартинок.ТабличныйДокументТолькоПросмотр;
	
	Команда = Команды.Добавить("ОбъектБДЗаписатьТДвижений");
	Команда.Действие = "ОбъектБДЗаписатьТДвижений";
	Команда.Заголовок = "Записать набор движений документа";
	Команда.Подсказка = "Записать таблицу набора движений документа в базу";
	Команда.Картинка = БиблиотекаКартинок.Провести;
	
	Команда0 = Команды.Добавить("ОбъектБДЗаписатьИзмененныеНД");
	Команда0.Действие = "ОбъектБДЗаписатьИзмененныеНД";
	Команда0.Заголовок = "Записать измененные наборы движений";
	Команда0.Подсказка = "Записать измененные наборы движений документа в базу";
	Команда0.Картинка = БиблиотекаКартинок.СохранитьЗначения;
	
	Команда1 = Команды.Добавить("НовоеОкно");
	Команда1.Действие = "НовоеОкно";
	Команда1.Заголовок = "Открыть новое независимое окно";
	Команда1.Подсказка = "Открыть новое независимое окно обработки";
	Команда1.Картинка = БиблиотекаКартинок.НовоеОкно;
	
	Команда2 = Команды.Добавить("НовоеСвязанноеОкно");
	Команда2.Действие = "НовоеОкно";
	Команда2.Заголовок = "Открыть новое связанное окно";
	Команда2.Подсказка = "Открыть новое связанное окно обработки";
	Команда2.Картинка = БиблиотекаКартинок.ВводНаОсновании;
	
	Команда3 = Команды.Добавить("ПерейтиВСвязанноеОкно");
	Команда3.Действие = "ПерейтиВСвязанноеОкно";
	Команда3.Заголовок = "Перейти в связанное окно";
	Команда3.Подсказка = "Перейти в связанное окно обработки";
	Команда3.Картинка = БиблиотекаКартинок.Вперед;
	
	Команда4 = Команды.Добавить("ЗакрытьСвязанныеОкна");
	Команда4.Действие = "ЗакрытьСвязанныеОкна";
	Команда4.Заголовок = "Закрыть связанные окна";
	Команда4.Подсказка = "Закрыть связанные окна обработки";
	Команда4.Картинка = БиблиотекаКартинок.Закрыть;
	
	// Добавим кнопки для доступа к новым командам.
	Кнопка = Элементы.Добавить("кнОМЗаписатьИзмененныеНаборыДвижений", Тип("КнопкаФормы"), Элементы.пмПроведение);
	Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Кнопка.ИмяКоманды = Команда0.Имя;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.ТолькоВоВсехДействиях = Ложь;
		
	ГруппаКнопок1 = Элементы.Вставить("МенюОкнаОбработки", Тип("ГруппаФормы"), ЭтаФорма.КоманднаяПанель, Элементы.кнОМДомашняяСтраницаОбработки);
	ГруппаКнопок1.Вид = ВидГруппыФормы.Подменю;
	ГруппаКнопок1.Заголовок = "Окна обработки";
	ГруппаКнопок1.Подсказка = "Действия с окнами обработки";
	ГруппаКнопок1.Картинка = БиблиотекаКартинок.Вперед;
	
	ГруппаКнопок2 = Элементы.Добавить("ГруппаСвязанныеОкна", Тип("ГруппаФормы"), ГруппаКнопок1);
	ГруппаКнопок2.Вид = ВидГруппыФормы.ГруппаКнопок;
	ГруппаКнопок2.Заголовок = "";
	ГруппаКнопок2.Подсказка = "";
	
	Кнопка = Элементы.Добавить(Команда2.Имя, Тип("КнопкаФормы"), ГруппаКнопок2);
	Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Кнопка.ИмяКоманды = Команда2.Имя;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.ТолькоВоВсехДействиях = Ложь;
		
	Кнопка = Элементы.Добавить(Команда3.Имя, Тип("КнопкаФормы"), ГруппаКнопок2);
	Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Кнопка.ИмяКоманды = Команда3.Имя;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.ТолькоВоВсехДействиях = Ложь;
	
	Кнопка = Элементы.Добавить(Команда4.Имя, Тип("КнопкаФормы"), ГруппаКнопок2);
	Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Кнопка.ИмяКоманды = Команда4.Имя;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.ТолькоВоВсехДействиях = Ложь;
	
	Кнопка = Элементы.Добавить(Команда1.Имя, Тип("КнопкаФормы"), ГруппаКнопок1);
	Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Кнопка.ИмяКоманды = Команда1.Имя;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.ТолькоВоВсехДействиях = Ложь;
	
КонецПроцедуры
//+yuraos, 10.09.2015

/////////////////////////////////////////////////////////////////////////////////////////////
// НЕДОПУСТИМЫЕ СИМВОЛЫ XML. ДЕРЕВО XML.
//
// Ошибка при вызове метода контекста (ПрочитатьИзменения): Ошибка преобразования данных XML.
//

// НЕДОПУСТИМЫЕ СИМВОЛЫ XML. ($001A, $001F, $FFFF)

&НаКлиенте
Процедура ПомощьДеревоXMLНедопустимыеСимволыXML(Команда)
	ПредупреждениеСообщение(, """Недопустимые символы XML""
	|
	|Файл XML рассматривается как обычный текстовый файл,
	|в котором производится поиск:
	|
	|- Недопустимых символов XML спецификаций 1.0, 1.1;
	|- Поиск специальных символов;
	|- Поиск прочих символов (указываются их коды).
	|
	|Возможно задать интервал кодов символов, в этом случае
	|будет произведена проверка наличия символов, соответствующих
	|кодам этого интервала.
	|
	|Результат - таблица, содержащая текст XML-файла ""как есть"".
	|Строки с ошибками выделены цветом.
	|
	|=======================================================
	|
	|""Ошибка преобразования данных XML"" (""Дерево XML"")
	|
	|Файл XML рассматривается как структурированный файл,
	|содержание которого преобразуется в древовидную структуру.
	|
	|В процессе прочтения файла производится поиск 1 (одной)
	|ошибки, приводящей к нарушению обмена данными в РИБ:
	|
	|""Ошибка преобразования данных XML""
	|Ошибка - следствие незаполненого значения ""НастройкиКомпоновкиДанных"".
	|(с другими ошибками автор не сталкивался)
	|
	|Результат - дерево, структура, содержащая текст XML-файла.
	|Строки с ошибками выделены цветом.
	|
	|");
КонецПроцедуры

&НаКлиенте
Процедура ФайлXMLНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Перем ДиалогВыбораФайла;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Заголовок = "Выберите файл XML";
	ДиалогВыбораФайла.Фильтр = "Файл (*.xml)|*.xml";
	
	Если НЕ ДиалогВыбораФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли; 
	
	ФайлXMLОчистка(Элемент, Истина);
	
	Объект.СпецификацияXML11 = СпецификацияXML11Доступна();
	
	Объект.ФайлXML = ДиалогВыбораФайла.ВыбранныеФайлы[0];
	Возврат;

КонецПроцедуры

&НаКлиенте
Функция СпецификацияXML11Доступна()
	Перем ПозXML11, ТекстXML;
	
	ТекстXML = "Текст, содержащий недопустимый символ ПФ: " + Символы.ПФ + ".";
	Попытка
		ПозXML11 = НайтиНедопустимыеСимволыXML(ТекстXML, 1, "1.1");	// ТекстXML, Начало, Спецификация XML.
	Исключение
		ПозXML11 = Неопределено;
	КонецПопытки;
	
	Возврат НЕ ПозXML11 = Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ФайлXMLОчистка(Элемент, СтандартнаяОбработка)
	
	ЭтаФорма.СимволXMLТаб 	= Истина;	// ОБЯЗАТЕЛЬНО.
	ЭтаФорма.СимволXMLВК 	= Истина;
	ЭтаФорма.СимволXMLПС	= Истина;
	ЭтаФорма.СимволXMLНПП	= Истина;
	ЭтаФорма.СимволXMLВТаб	= Ложь;		// Недопустимый символ XML.
	ЭтаФорма.СимволXMLПФ	= Ложь;		// Недопустимый символ XML.
	
	Элементы.НедопустимыеСимволыXML.Заголовок = "Недопустимые символы XML";
	Объект.НедопустимыеСимволыXML.Очистить();
	Элементы.НедопустимыеСимволыXML.Обновить();
	ЭтаФорма.КВоСтрокXMLСОшибкамиВСимволах = 0;
	Элементы.СледующаяСтрокаСНедопустимымиСимволамиXML.Доступность = Ложь;
	Элементы.ПредыдущаяСтрокаСНедопустимымиСимволамиXML.Доступность= Ложь;
	
	Элементы.ДеревоXML.Заголовок = "Дерево XML";
	ЭтаФорма.ДеревоXML.ПолучитьЭлементы().Очистить();
	Элементы.ДеревоXML.Обновить();
	ЭтаФорма.КВоСтрокXMLСОшибкамиВСтруктуре = 0;
	Элементы.СледующаяСтрокаОшибкиДереваXML.Доступность = Ложь;
	Элементы.ПредыдущаяСтрокаОшибкиДереваXML.Доступность= Ложь;
	
	Объект.ФайлXML = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлXMLОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение(Объект.ФайлXML);
КонецПроцедуры

&НаКлиенте
Процедура НайтиНедопустимыеСимволыXMLВФайле(Команда)
	Перем ТекстовыйДокумент, НомерСтроки, ТекстXML;
	Перем НедопустимыеСимволы, Позиции, СтрокаТЧ, НомерОшибки;
	Перем ИдентификаторПервойНайденной, МассивИдентификаторов;

	Элементы.СледующаяСтрокаСНедопустимымиСимволамиXML.Доступность = Ложь;
	Элементы.ПредыдущаяСтрокаСНедопустимымиСимволамиXML.Доступность= Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.ФайлXML) Тогда
		ПредупреждениеСообщение(, "Не указан файл XML.");
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	Состояние("Поиск недопустимых символов XML ...");
	
	Объект.НедопустимыеСимволыXML.Очистить();
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент; 
	ТекстовыйДокумент.Прочитать(Объект.ФайлXML, КодировкаТекста.UTF8);
	
	НомерОшибки = 0;
	МассивИдентификаторов = Новый Массив;
	ЭтаФорма.КВоСтрокXMLСОшибкамиВСимволах = 0;
	ИдентификаторПервойНайденной = Неопределено;
	
	Для НомерСтроки = 1 ПО ТекстовыйДокумент.КоличествоСтрок() Цикл 
		
		ТекстXML = ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки); 
		НедопустимыеСимволы = НайтиНедопустимыеСимволыXMLВЗначении(ТекстовыйДокумент, НомерСтроки, ТекстXML);
		
		СтрокаТЧ = Объект.НедопустимыеСимволыXML.Добавить();
		СтрокаТЧ.ТекстXML = ТекстXML;
		Если ЗначениеЗаполнено(НедопустимыеСимволы) Тогда
			НомерОшибки = НомерОшибки + 1;
			СтрокаТЧ.Ошибка = Истина;
			СтрокаТЧ.НомерОшибки = НомерОшибки;
			СтрокаТЧ.НедопустимыеСимволы = СокрЛП(НедопустимыеСимволы);
			Если ИдентификаторПервойНайденной = Неопределено Тогда
				ИдентификаторПервойНайденной= СтрокаТЧ.ПолучитьИдентификатор();
				МассивИдентификаторов.Добавить(СтрокаТЧ.ПолучитьИдентификатор());
			Иначе
				МассивИдентификаторов.Добавить(СтрокаТЧ.ПолучитьИдентификатор());
			КонецЕсли;
			Сообщить("стр " + СтрокаТЧ.НомерСтроки + " Недопустимые: " + СтрокаТЧ.НедопустимыеСимволы + ".");
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивИдентификаторов.Количество() > 0 Тогда
		
		ЭтаФорма.КВоСтрокXMLСОшибкамиВСимволах = МассивИдентификаторов.Количество();
		Элементы.НедопустимыеСимволыXML.Заголовок = "Недопустимые символы XML (строк ошибками " + ЭтаФорма.КВоСтрокXMLСОшибкамиВСимволах + ")";
		
		ЭтаФорма.ТекущийЭлемент = Элементы.НедопустимыеСимволыXML;
		
		ЭтаФорма.ТекущийИдентификаторСтрокиXML = ИдентификаторПервойНайденной;
		Элементы.НедопустимыеСимволыXML.ТекущаяСтрока = ИдентификаторПервойНайденной;
		Элементы.НедопустимыеСимволыXML.Обновить();
		
		ЭтаФорма.СписокИдентификаторовСтрокНедопустимыеСимволыXML.ЗагрузитьЗначения(МассивИдентификаторов);
		Если МассивИдентификаторов.Количество() > 1 Тогда
			Элементы.СледующаяСтрокаСНедопустимымиСимволамиXML.Доступность = Истина;
			Элементы.ПредыдущаяСтрокаСНедопустимымиСимволамиXML.Доступность= Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстовыйДокумент = Неопределено;
	
	ПредупреждениеСообщение(, "Поиск недопустимых символов XML завершен.
	|Найдено " + ЭтаФорма.КВоСтрокXMLСОшибкамиВСимволах + " строк с ошибками.");
	
КонецПроцедуры

&НаКлиенте
Функция НайтиНедопустимыеСимволыXMLВЗначении(Знач ТекстовыйДокумент, Знач НомерСтроки, Знач ТекстXML)
	Перем СимволПроизвольный;
	Перем НедопустимыеСимволы;
	
	НедопустимыеСимволы = "";
	
	НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, "XML", "1.0");				// (Код символа < 32).
	Если Объект.СпецификацияXML11 Тогда
		НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, "XML", "1.1");			// (Код символа < 32).
	КонецЕсли;
	
	// Символы, не относящиеся к недопустимым символам XML, но влияющие на содержимое/структуру файла XML.
	Если ЭтаФорма.СимволXMLТаб Тогда
		НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, Символы.Таб	, "Таб");	// (Код символа = 9). Встречается в файлах-обменах РИБ 1С:7.7/8.Х. Нарушает загрузку в РИБ.
	КонецЕсли;
	Если ЭтаФорма.СимволXMLВК Тогда
		НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, Символы.ВК	, "ВК");	// (Код символа = 13).
	КонецЕсли;
	Если ЭтаФорма.СимволXMLПС Тогда
		НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, Символы.ПС	, "ПС");	// (Код символа = 10).
	КонецЕсли;
	Если ЭтаФорма.СимволXMLНПП Тогда
		НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, Символы.НПП	, "НПП");	// (Код символа = 160).
	КонецЕсли;
	
	// ИЗБЫТОЧНОСТЬ.
	// Недопустимые символы XML. Должны отлавливаться функцией НайтиНедопустимыеСимволыXML().
	Если ЭтаФорма.СимволXMLВТаб Тогда
		НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, Символы.ВТаб	, "ВТаб");	// (Код символа = 11). Недопустимый символ XML.
	КонецЕсли;
	Если ЭтаФорма.СимволXMLПФ Тогда
		НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, Символы.ПФ	, "ПФ");	// (Код символа = 12). Недопустимый символ XML.
	КонецЕсли;
	
	// Произвольный символ, указанный на форме.
	
	// 0 - тоже проверить/
	СимволПроизвольный = ПолучитьСимвол(ЭтаФорма.СимволXMLПроизвольный);
	Если НЕ СимволПроизвольный = Неопределено Тогда
		НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, ЭтаФорма.СимволXMLПроизвольный, ("?"+ЭтаФорма.СимволXMLПроизвольный));
	КонецЕсли;
	
	// Сиволы в цикле по Кодам.
	
	Если НЕ ЭтаФорма.СимволXMLПроизвольныйНачальный = 0 И НЕ ЭтаФорма.СимволXMLПроизвольныйКонечный = 0 Тогда
		Для ит = ЭтаФорма.СимволXMLПроизвольныйНачальный ПО ЭтаФорма.СимволXMLПроизвольныйКонечный Цикл
			СимволПроизвольный = ПолучитьСимвол(ит);
			Если НЕ СимволПроизвольный = Неопределено Тогда
				НайтиПозицииНедопустимыхСимволовXMLВСтроке(ТекстовыйДокумент, НомерСтроки, ТекстXML, НедопустимыеСимволы, ит, ("?"+ит));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат НедопустимыеСимволы;
	
КонецФункции

&НаКлиенте
Процедура НайтиПозицииНедопустимыхСимволовXMLВСтроке(Знач ТекстовыйДокумент, Знач НомерСтроки, Знач ТекстXML, НедопустимыеСимволы, Знач СимволПроверяемый, Знач СпецификацияXML)
	Перем ПозН1, ПозН2, ПозЛевСк, ТекущийСимвол;
	Перем ЛевыхСкобок, ПравыхСкобок;
	Перем КомбинацияС;
	
	Если СимволПроверяемый = "XML" Тогда
		
		Если СпецификацияXML = "1.0" Тогда
			
			// Количество открывающих и закрывающих кавычек (прямых).
			
			ЛевыхСкобок = СтрЧислоВхождений(ТекстXML, """");
			Если НЕ (ЛевыхСкобок % 2)=0 Тогда
				Сообщить("стр " + НомерСтроки + ". Нечетное количество двойных кавычек " + ЛевыхСкобок + ".");
			КонецЕсли;
			
			// Количество открывающих и закрывающих кавычек.
			
			ЛевыхСкобок = СтрЧислоВхождений(ТекстXML, "«");
			ПравыхСкобок = СтрЧислоВхождений(ТекстXML, "»");
			Если НЕ ЛевыхСкобок = ПравыхСкобок Тогда
				Сообщить("стр " + НомерСтроки + ". Левых кавычек «" + ЛевыхСкобок + "#" + ПравыхСкобок + "» Правых кавычек.");
			КонецЕсли;
			
			// Количество открывающих и закрывающих скобок.
			
			ЛевыхСкобок = СтрЧислоВхождений(ТекстXML, "<");
			ПравыхСкобок = СтрЧислоВхождений(ТекстXML, ">");
			Если НЕ ЛевыхСкобок = ПравыхСкобок Тогда
				Сообщить("стр " + НомерСтроки + ". Левых скобок <" + ЛевыхСкобок + "#" + ПравыхСкобок + "> Правых скобок.");
			КонецЕсли;
			
			// Комбинации символов.
			
			КомбинацияС = Символы.Таб + "<";
			Если Найти(ТекстXML, КомбинацияС) = 0 И НЕ (НомерСтроки = 1 ИЛИ НомерСтроки = ТекстовыйДокумент.КоличествоСтрок()) Тогда
				Сообщить("стр " + НомерСтроки + ". Комбинация [ТАБ<] не найдена.");
			КонецЕсли;
			
		КонецЕсли;
		
		// Спецификации XML "1.0", "1.1".
		
		ПозН1 = НайтиНедопустимыеСимволыXML(ТекстXML, 1, СпецификацияXML);
		Пока ПозН1 > 0 Цикл
			НедопустимыеСимволы = НедопустимыеСимволы + " " + СимволПроверяемый + "(" + ПозН1 + ")";
			ТекущийСимвол = Сред(ТекстXML, ПозН1, 1);
			ТекстXML = СтрЗаменить(ТекстXML, ТекущийСимвол, " ");
			ПозН1 = НайтиНедопустимыеСимволыXML(ТекстXML, 1, СпецификацияXML);
		КонецЦикла;
		
	Иначе
		
		// Символы "ВК", "ПФ", "Таб", "ВТаб", и др.
		
		Если ТипЗнч(СимволПроверяемый) = Тип("Число") Тогда
			СимволПроверяемый = Символ(СимволПроверяемый)
		КонецЕсли;
		
		ПозН1 = Найти(ТекстXML, СимволПроверяемый);
		Если ПозН1 > 0 Тогда
			
			ПозЛевСк = Найти(ТекстXML, "<");
			
			Для ит = ПозН1 ПО СтрДлина(ТекстXML) Цикл
				ТекущийСимвол = Сред(ТекстXML, ит, 1);
				Если (СимволПроверяемый = Символы.Таб И НЕ ПозЛевСк = 0 И ит < ПозЛевСк) Тогда
					Продолжить;
				КонецЕсли;
				Если ТекущийСимвол = СимволПроверяемый Тогда
					НедопустимыеСимволы = НедопустимыеСимволы + " " + СпецификацияXML + "(" + ит + ")";
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СимволXMLПроизвольныйПриИзменении(Элемент)
	Перем СимволПроизвольный;
	
	СимволПроизвольный = ПолучитьСимвол(ЭтаФорма.СимволXMLПроизвольный);
	Если НЕ СимволПроизвольный = Неопределено Тогда
		Если ЭтаФорма.СимволXMLПроизвольный < 32 Тогда
			ЭтаФорма.СимволXMLПроизвольныйОтображаемый = "<32";
		Иначе
			ЭтаФорма.СимволXMLПроизвольныйОтображаемый = СимволПроизвольный;
		КонецЕсли;
	Иначе
		ЭтаФорма.СимволXMLПроизвольныйОтображаемый = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСимвол(КодСимвола)
	Перем СимволПроизвольный;
	
	Попытка
		СимволПроизвольный = Символ(КодСимвола);
	Исключение
		СимволПроизвольный = Неопределено;
	КонецПопытки;
	
	Возврат СимволПроизвольный;
	
КонецФункции

&НаКлиенте
Процедура СимволXMLПроизвольныйОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭтаФорма.СимволXMLПроизвольный = 0;
	ЭтаФорма.СимволXMLПроизвольныйОтображаемый = "";
КонецПроцедуры

&НаКлиенте
Процедура СимволXMLПроизвольныйНачальныйПриИзменении(Элемент)
	ЭтаФорма.СимволXMLПроизвольныйКонечный = ЭтаФорма.СимволXMLПроизвольныйНачальный;
	ЭтаФорма.СимволXMLПроизвольныйНиКОтображаемый = "";
КонецПроцедуры

&НаКлиенте
Процедура СимволXMLПроизвольныйКонечныйПриИзменении(Элемент)
	Если ЭтаФорма.СимволXMLПроизвольныйКонечный < ЭтаФорма.СимволXMLПроизвольныйНачальный Тогда
		ЭтаФорма.СимволXMLПроизвольныйНачальный = ЭтаФорма.СимволXMLПроизвольныйКонечный;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СимволXMLПроизвольныйКонечныйОчистка(Элемент, СтандартнаяОбработка)
	ЭтаФорма.СимволXMLПроизвольныйНачальный = 0;
	ЭтаФорма.СимволXMLПроизвольныйКонечный = 0;
КонецПроцедуры

&НаКлиенте
Процедура СледующаяСтрокаСНедопустимымиСимволамиXML(Команда)
	Перем ИдентификаторТекущий, ИдентификаторСледующий;
	
	ИдентификаторТекущий = ЭтаФорма.СписокИдентификаторовСтрокНедопустимыеСимволыXML.НайтиПоЗначению(ЭтаФорма.ТекущийИдентификаторСтрокиXML);
	ИдентификаторТекущий = ЭтаФорма.СписокИдентификаторовСтрокНедопустимыеСимволыXML.Индекс(ИдентификаторТекущий);
	Если ЭтаФорма.СписокИдентификаторовСтрокНедопустимыеСимволыXML.Количество() > ИдентификаторТекущий + 1 Тогда
		ИдентификаторСледующий = ЭтаФорма.СписокИдентификаторовСтрокНедопустимыеСимволыXML.Получить(ИдентификаторТекущий + 1);
		Элементы.НедопустимыеСимволыXML.ТекущаяСтрока = ИдентификаторСледующий.Значение;
		ЭтаФорма.ТекущийИдентификаторСтрокиXML = ИдентификаторСледующий.Значение;
		Элементы.НедопустимыеСимволыXML.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущаяСтрокаСНедопустимымиСимволамиXML(Команда)
	Перем ИдентификаторТекущий, ИдентификаторПредыдущий;
	
	ИдентификаторТекущий = ЭтаФорма.СписокИдентификаторовСтрокНедопустимыеСимволыXML.НайтиПоЗначению(ЭтаФорма.ТекущийИдентификаторСтрокиXML);
	ИдентификаторТекущий = ЭтаФорма.СписокИдентификаторовСтрокНедопустимыеСимволыXML.Индекс(ИдентификаторТекущий);
	Если ИдентификаторТекущий > 0 Тогда
		ИдентификаторПредыдущий = ЭтаФорма.СписокИдентификаторовСтрокНедопустимыеСимволыXML.Получить(ИдентификаторТекущий - 1);
		Элементы.НедопустимыеСимволыXML.ТекущаяСтрока = ИдентификаторПредыдущий.Значение;
		ЭтаФорма.ТекущийИдентификаторСтрокиXML = ИдентификаторПредыдущий.Значение;
		Элементы.НедопустимыеСимволыXML.Обновить();
	КонецЕсли;
	
КонецПроцедуры

// ДЕРЕВО XML.

&НаКлиенте
Процедура ПостроитьДеревоXML(Команда)
	Перем ДеревоДОМ, ДокументДОМ, ДочерниеУзлыДОМ, ДочернийУзелДОМ, СтрокаД;
	Перем ИдентификаторПервойДереваXML;
	Перем ИдентификаторПервойНайденной, МассивИдентификаторов;
	
	Если НЕ ЗначениеЗаполнено(Объект.ФайлXML) Тогда
		ПредупреждениеСообщение(, "Не указан файл XML.");
		Возврат;
	КонецЕсли;
	
	Состояние("Построение дерева XML ...");
	
	ДеревоДОМ = ЭтаФорма.ДеревоXML;
	ДеревоДОМ.ПолучитьЭлементы().Очистить();
	
	Элементы.СледующаяСтрокаОшибкиДереваXML.Доступность = Ложь;
	Элементы.ПредыдущаяСтрокаОшибкиДереваXML.Доступность= Ложь;
	
	НомерОшибки = 0;
	МассивИдентификаторов = Новый Массив;
	ЭтаФорма.КВоСтрокXMLСОшибкамиВСтруктуре = 0;
	
	ДокументДОМ = ПолучитьДокументДОМ(Объект.ФайлXML);
	ДочерниеУзлыДОМ = ПолучитьДочерниеУзлыДОМ(ДокументДОМ);
	Если ДочерниеУзлыДОМ = Неопределено ИЛИ ДочерниеУзлыДОМ.Количество() = 0 Тогда
		// Завершение работы.
		// Закрытие Объектов.
		ДокументДОМ = Неопределено;
		Возврат;
	КонецЕсли;
	
	ДобавитьДочерниеУзлыДОМВДеревоXML(ДеревоДОМ, ДеревоДОМ.ПолучитьЭлементы(), ДочерниеУзлыДОМ, МассивИдентификаторов, НомерОшибки);
	
	ИдентификаторПервойДереваXML = ЭтаФорма.ДеревоXML.ПолучитьЭлементы().Получить(0).ПолучитьИдентификатор();
	Элементы.ДеревоXML.Развернуть(ИдентификаторПервойДереваXML, Истина);
	
	Если МассивИдентификаторов.Количество() > 0 Тогда
		
		ЭтаФорма.КВоСтрокXMLСОшибкамиВСтруктуре = МассивИдентификаторов.Количество();
		Элементы.ДеревоXML.Заголовок = "Дерево XML (строк ошибками " + ЭтаФорма.КВоСтрокXMLСОшибкамиВСтруктуре + ")";
		
		ЭтаФорма.ТекущийЭлемент = Элементы.ДеревоXML;
		
		ЭтаФорма.ТекущийИдентификаторСтрокиДереваXML = МассивИдентификаторов[0];
		Элементы.ДеревоXML.ТекущаяСтрока = ЭтаФорма.ТекущийИдентификаторСтрокиДереваXML;
		Элементы.ДеревоXML.Обновить();
		
		ЭтаФорма.СписокИдентификаторовСтрокОшибокСтруктурыXML.ЗагрузитьЗначения(МассивИдентификаторов);
		Если МассивИдентификаторов.Количество() > 1 Тогда
			Элементы.СледующаяСтрокаОшибкиДереваXML.Доступность = Истина;
			Элементы.ПредыдущаяСтрокаОшибкиДереваXML.Доступность= Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ПредупреждениеСообщение(, "Поиск недопустимых символов XML завершен.
	|Найдено " + ЭтаФорма.КВоСтрокXMLСОшибкамиВСтруктуре + " строк с ошибками.");
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьДокументДОМ(ФайлXML)
	Перем ЧтениеXML, ПостроительДОМ, ДокументДОМ;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ФайлXML);
	
	ПостроительДОМ = Новый ПостроительDOM; 
	Попытка
		ДокументДОМ = ПостроительДОМ.Прочитать(ЧтениеXML);
		//Сообщить("Версия XML: " + ДокументДОМ.ВерсияXML);
	Исключение
		Сообщить(ОписаниеОшибки());
		ДокументДОМ = Неопределено;
	КонецПопытки;
	
	// Завершение работы.
	// Закрытие Объектов.
	ЧтениеXML.Закрыть();
	
	Возврат ДокументДОМ;
	
КонецФункции

&НаКлиенте
Функция ПолучитьДочерниеУзлыДОМ(Знач ДОМ)
	Перем ДочерниеУзлыДОМ;
	
	Попытка
		ДочерниеУзлыДОМ = ДОМ.ДочерниеУзлы;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Неопределено;
	КонецПопытки;
	
	// Если строки пусты, то они не включаются в список элементов.
	// Если строки пусты и отдельные ячейки содержат оформление (например задан тип "Текст"), 
	// то эти строки включаются в список (содержание: пустые).
	Возврат ДочерниеУзлыДОМ;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьДочерниеУзлыДОМВДеревоXML(ДеревоДОМ, СтрокиДОМ, ДочерниеУзлыДОМ, МассивИдентификаторов, НомерСтроки)
	Перем СтрокаДОМ, ИдентификаторСтрокиДОМ, СтрокаДерева, ИндексСтроки;
	
	Если ДочерниеУзлыДОМ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого УзелДОМ ИЗ ДочерниеУзлыДОМ Цикл
		
		СтрокаДОМ = СтрокиДОМ.Добавить();
		НомерСтроки				= НомерСтроки + 1;
		СтрокаДОМ.НомерСтроки	= НомерСтроки;
		СтрокаДОМ.Ошибка		= Ложь;
		СтрокаДОМ.УзелXML		= УзелДОМ.ИмяУзла;
		СтрокаДОМ.ТекстXML		= УзелДОМ.ЗначениеУзла;
		СтрокаДОМ.Картинка		= БиблиотекаКартинок.СтартБизнесПроцесса;
		ИдентификаторСтрокиДОМ	= СтрокаДОМ.ПолучитьИдентификатор();
		
		Если (УзелДОМ.ЗначениеУзла = Неопределено И ТипЗнч(УзелДОМ.ПервыйДочерний) = Тип("ТекстDOM") И УзелДОМ.ПервыйДочерний = УзелДОМ.ПоследнийДочерний) Тогда
			СтрокаДОМ.ТекстXML = УзелДОМ.ТекстовоеСодержимое;
			Продолжить;
		ИначеЕсли (УзелДОМ.ЗначениеУзла = Неопределено И УзелДОМ.ПервыйДочерний = Неопределено И УзелДОМ.ПервыйДочерний = УзелДОМ.ПоследнийДочерний) Тогда
			Если СтрокаДОМ.УзелXML = "НастройкиКомпоновкиДанных" И НЕ ЗначениеЗаполнено(УзелДОМ.ТекстовоеСодержимое) Тогда
				МассивИдентификаторов.Добавить(ИдентификаторСтрокиДОМ);
				СтрокаДОМ.Ошибка		= Истина;
				СтрокаДОМ.НомерОшибки	= МассивИдентификаторов.Количество();
				СтрокаДОМ.ТекстОшибки	= "<Неопределено>";
				Сообщить("" + СтрокаДОМ.НомерСтроки + ". Неопределенное значение ""НастройкиКомпоновкиДанных"" возможно это
				|нарушение структуры XML-данных, приводящих к ""Ошибке преобразования данных XML"".");
			КонецЕсли;
			СтрокаДОМ.ТекстXML = УзелДОМ.ТекстовоеСодержимое;
			Продолжить;
		Иначе
			ПрочитатьАтрибутыУзлаДОМ(ДеревоДОМ, СтрокаДОМ.ПолучитьЭлементы(), УзелДОМ.Атрибуты, МассивИдентификаторов, НомерСтроки);
		КонецЕсли;
		
		ДобавитьДочерниеУзлыДОМВДеревоXML(ДеревоДОМ, СтрокаДОМ.ПолучитьЭлементы(), УзелДОМ.ДочерниеУзлы, МассивИдентификаторов, НомерСтроки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьАтрибутыУзлаДОМ(ДеревоДОМ, СтрокиДОМ, Атрибуты, МассивИдентификаторов, НомерСтроки)
	Перем Атрибут, СтрокаДОМ;
	
	Если Атрибуты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Атрибут ИЗ Атрибуты Цикл
		СтрокаДОМ = СтрокиДОМ.Добавить();
		НомерСтроки				= НомерСтроки + 1;
		СтрокаДОМ.НомерСтроки	= НомерСтроки;
		СтрокаДОМ.Ошибка		= Ложь;
		СтрокаДОМ.УзелXML		= Атрибут.ИмяУзла; 
		СтрокаДОМ.ТекстXML		= Атрибут.ЗначениеУзла;
		СтрокаДОМ.Картинка		= БиблиотекаКартинок.Свойства;	
	КонецЦикла;
	
КонецПроцедуры	

&НаКлиенте
Процедура СледующаяСтрокаОшибкиДереваXML(Команда)
	Перем ИдентификаторТекущий, ИдентификаторСледующий;
	
	ИдентификаторТекущий = ЭтаФорма.СписокИдентификаторовСтрокОшибокСтруктурыXML.НайтиПоЗначению(ЭтаФорма.ТекущийИдентификаторСтрокиДереваXML);
	ИдентификаторТекущий = ЭтаФорма.СписокИдентификаторовСтрокОшибокСтруктурыXML.Индекс(ИдентификаторТекущий);
	Если ЭтаФорма.СписокИдентификаторовСтрокОшибокСтруктурыXML.Количество() > ИдентификаторТекущий + 1 Тогда
		ИдентификаторСледующий = ЭтаФорма.СписокИдентификаторовСтрокОшибокСтруктурыXML.Получить(ИдентификаторТекущий + 1);
		Элементы.ДеревоXML.ТекущаяСтрока = ИдентификаторСледующий.Значение;
		ЭтаФорма.ТекущийИдентификаторСтрокиДереваXML = ИдентификаторСледующий.Значение;
		Элементы.ДеревоXML.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущаяСтрокаОшибкиДереваXML(Команда)
	Перем ИдентификаторТекущий, ИдентификаторПредыдущий;
	
	ИдентификаторТекущий = ЭтаФорма.СписокИдентификаторовСтрокОшибокСтруктурыXML.НайтиПоЗначению(ЭтаФорма.ТекущийИдентификаторСтрокиДереваXML);
	ИдентификаторТекущий = ЭтаФорма.СписокИдентификаторовСтрокОшибокСтруктурыXML.Индекс(ИдентификаторТекущий);
	Если ИдентификаторТекущий > 0 Тогда
		ИдентификаторПредыдущий = ЭтаФорма.СписокИдентификаторовСтрокОшибокСтруктурыXML.Получить(ИдентификаторТекущий - 1);
		Элементы.ДеревоXML.ТекущаяСтрока = ИдентификаторПредыдущий.Значение;
		ЭтаФорма.ТекущийИдентификаторСтрокиДереваXML = ИдентификаторПредыдущий.Значение;
		Элементы.ДеревоXML.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатистикаПоСсылкамМинимум(Элемент)
	Перем СтруктураВозврата;
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипОбъектаБД) Тогда
		ПредупреждениеСообщение(, "Тип объекта не выбран.");
		Возврат;
	КонецЕсли;
	
	Состояние("Подождите. Подсчет ссылок ...");
	СтруктураВозврата = СтатистикаПоСсылкамМинимумНаСервере();
	
	Элементы.ВсегоСсылок.Заголовок = "∑:" + СтруктураВозврата.ВсегоСсылок + " (" + СтруктураВозврата.ПомеченныхСсылок + ")";
	
КонецПроцедуры

&НаСервере
Функция СтатистикаПоСсылкамМинимумНаСервере()
	Перем Запрос, РезультатЗапроса, Результат;
	Перем СтруктураВозврата, ВсегоСсылок, ПомеченныхСсылок;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ТаблицаБД.Ссылка) КАК ВсегоСсылок
		|ИЗ
		|	" + Объект.ТипОбъектаБД + "." + Объект.ИмяОбъектаБД + " КАК ТаблицаБД";
	
	РезультатЗапроса = Запрос.Выполнить();
	Результат = РезультатЗапроса.Выгрузить();
	
	ВсегоСсылок = Результат[0].ВсегоСсылок;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ТаблицаБД.Ссылка) КАК ПомеченныхСсылок
		|ИЗ
		|	" + Объект.ТипОбъектаБД + "." + Объект.ИмяОбъектаБД + " КАК ТаблицаБД
		|ГДЕ
		|	ТаблицаБД.ПометкаУдаления = Истина";
	
	РезультатЗапроса = Запрос.Выполнить();
	Результат = РезультатЗапроса.Выгрузить();
	
	ПомеченныхСсылок = Результат[0].ПомеченныхСсылок;
	
	СтруктураВозврата = Новый Структура("ВсегоСсылок,ПомеченныхСсылок", ВсегоСсылок, ПомеченныхСсылок);
	
	Возврат СтруктураВозврата;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// YURAOS.
//

//+yuraos, 10.09.2015
#Если НаСервере Тогда
	// Сообщить("Инициализация объекта формы на сервере.");
	// Серверная переменная с "облегченным" ("пустым") объектом обработки
	// для вызова "статических" (не зависящих от данных объекта) методов
	// в начале серверного вызова пусть будет Null.
	This = Null; // а дальше при необходимости 
	// Его можно будет инициализировать так: ThisInitiate();
#КонецЕсли
//+yuraos, 10.09.2015
