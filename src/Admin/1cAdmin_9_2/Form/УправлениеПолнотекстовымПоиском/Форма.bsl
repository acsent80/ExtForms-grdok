
// Обработчики событий формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновитьСтатус();
	
КонецПроцедуры

// Обработчики команд формы

&НаКлиенте
Процедура ОбновитьИндекс(Команда)
	Состояние(
		НСтр("ru = 'Идет обновление полнотекстового индекса...
		|Пожалуйста, подождите.'"));
	
	ОбновитьИндексСервер();
	
	Состояние(НСтр("ru = 'Обновление полнотекстового индекса завершено.'"));
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИндекс(Команда)
	Состояние(
		НСтр("ru = 'Идет очистка полнотекстового индекса...
		|Пожалуйста, подождите.'"));
	
	ОчиститьИндексСервер();
	
	Состояние(НСтр("ru = 'Очистка полнотекстового индекса завершена.'"));
КонецПроцедуры

// Служебные процедуры и функции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтатусИндекса.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИндексАктуален");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	//Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(WindowsШрифты.ШрифтДиалоговИМеню, , , Истина, Ложь, Ложь, Ложь, ));

КонецПроцедуры

&НаСервере
Процедура ОбновитьИндексСервер()
	ПолнотекстовыйПоиск.ОбновитьИндекс(Ложь, Ложь);
	ОбновитьСтатус();
КонецПроцедуры

&НаСервере
Процедура ОчиститьИндексСервер()
	ПолнотекстовыйПоиск.ОчиститьИндекс();
	ОбновитьСтатус();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатус()
	// Доступность кнопок, дата актуальности индекса.
	
	//+yuraos, 20.10.2015
	МодульПТПС = МодульПТПСерверПолучить();
	//+yuraos, 20.10.2015
	
	РазрешитьПолнотекстовыйПоиск = (ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить);
	Если РазрешитьПолнотекстовыйПоиск Тогда
		ДатаАктуальностиИндекса = ПолнотекстовыйПоиск.ДатаАктуальности();
		//+yuraos, 20.10.2015
		ИндексАктуален = МодульПТПС.ИндексПоискаАктуален();
		//+yuraos, 20.10.2015
		////-yuraos, 20.10.2015
		//ИндексАктуален = ПолнотекстовыйПоискСервер.ИндексПоискаАктуален();
		////-yuraos, 20.10.2015
		Элементы.ФормаОбновитьИндекс.Доступность = НЕ ИндексАктуален;
		Если ИндексАктуален Тогда
			СтатусИндекса = НСтр("ru = 'Обновление не требуется'");
		Иначе
			СтатусИндекса = НСтр("ru = 'Требуется обновление'");
		КонецЕсли;
	Иначе
		ДатаАктуальностиИндекса = '00010101';
		ИндексАктуален = Ложь;
		Элементы.ФормаОбновитьИндекс.Доступность = Ложь;
		СтатусИндекса = НСтр("ru = 'Полнотекстовый поиск отключен'");
	КонецЕсли;
	
КонецПроцедуры

//+yuraos, 20.10.2015
&НаСервере
Функция МодульПТПСерверПолучить()
	Попытка
		Возврат Вычислить("ПолнотекстовыйПоискСервер");
	Исключение
		Попытка 
			Возврат Вычислить("ЭтотОбъект");
		Исключение
			Возврат Вычислить("ЭтаФорма");
		КонецПопытки;
	КонецПопытки;
	Возврат Неопределено;
КонецФункции
 
//+yuraos, 20.10.2015
&НаСервере
Функция ИндексПоискаАктуален() Экспорт
	
	ОперацииРазрешены = (ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить);
	Возврат (
		// Операции не разрешены,
		// или индекс полностью соответствует текущему состоянию информационной базы,
		// или индекс обновлялся менее 5 минут назад.
		НЕ ОперацииРазрешены
		ИЛИ ПолнотекстовыйПоиск.ИндексАктуален()
		ИЛИ ТекущаяДата() < ПолнотекстовыйПоиск.ДатаАктуальности() + 300); // Исключение из правила ТекущаяДатаСеанса().
	
КонецФункции
